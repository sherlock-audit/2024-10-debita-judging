Lucky Tan Cod

High

# Lenders could lose fees if a loan gets extended

### Summary

If a lender doesn't collect interest between the loan getting extended and the borrower paying it off, the lender loses the fees owed to them.

### Root Cause

`DebitaV3Loan.sol:238-240` does not add new interest but writes over existing value.
```solidity
    function payDebt(uint[] memory indexes) public nonReentrant {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

        require(
            ownershipContract.ownerOf(loanData.borrowerID) == msg.sender,
            "Not borrower"
        );
        // check next deadline
        require(
            nextDeadline() >= block.timestamp,
            "Deadline passed to pay Debt"
        );

        for (uint i; i < indexes.length; i++) {
            uint index = indexes[i];
            // get offer data on memory
            infoOfOffers memory offer = loanData._acceptedOffers[index];

            // change the offer to paid on storage
            loanData._acceptedOffers[index].paid = true;

            // check if it has been already paid
            require(offer.paid == false, "Already paid");

           
            // if the lender is the owner of the offer and the offer is perpetual, then add the funds to the offer
            if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                loanData._acceptedOffers[index].debtClaimed = true;
                IERC20(offer.principle).approve(address(lendOffer), total);
                lendOffer.addFunds(total);
            } else {
238             loanData._acceptedOffers[index].interestToClaim =
239                 interest -
240                 feeOnInterest;
            }

            SafeERC20.safeTransferFrom(
                IERC20(offer.principle),
                msg.sender,
                feeAddress,
                feeOnInterest
            );

            loanData._acceptedOffers[index].interestPaid += interest;
        }
       ...
    }
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L237-L241

```solidity
    function extendLoan() public {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
        LoanData memory m_loan = loanData;
        require(
            ownershipContract.ownerOf(loanData.borrowerID) == msg.sender,
            "Not borrower"
        );
       ...
        for (uint i; i < m_loan._acceptedOffers.length; i++) {
            infoOfOffers memory offer = m_loan._acceptedOffers[i];
            // if paid, skip
            // if not paid, calculate interest to pay
            if (!offer.paid) {
                uint alreadyUsedTime = block.timestamp - m_loan.startedAt;

                uint extendedTime = offer.maxDeadline -
                    alreadyUsedTime -
                    block.timestamp;
                uint interestOfUsedTime = calculateInterestToPay(i);
                uint interestToPayToDebita = (interestOfUsedTime * feeLender) /
                    10000;

                ...
              
                if (
                    lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer
                ) {
                    IERC20(offer.principle).approve(
                        address(lendOffer),
                        interestOfUsedTime - interestToPayToDebita
                    );
                    lendOffer.addFunds(
                        interestOfUsedTime - interestToPayToDebita
                    );
                } else {
                    loanData._acceptedOffers[i].interestToClaim +=
                        interestOfUsedTime -
                        interestToPayToDebita;
                }
                loanData._acceptedOffers[i].interestPaid += interestOfUsedTime;
            }
        }
    }
```
```solidity
    function calculateInterestToPay(uint index) public view returns (uint) {
        ...

        uint interest = (anualInterest * activeTime) / 31536000;

        // subtract already paid interest
        return interest - offer.interestPaid;
    }
```

### Internal pre-conditions

1. Loan gets extended
2. Lender doesn't collect interest
3. Borrower pays the debt

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Some of the fee owed to lender gets lost.

### PoC

_No response_

### Mitigation

In `DebitaV3Loan.sol:238-240`, add value instead of writing it.
```solidity
                loanData._acceptedOffers[index].interestToClaim +=
                    interest -
                    feeOnInterest;
```