Furry Cloud Cod

Medium

# The `DebitaV3Loan::_claimDebt` function does not update state variables correctly

## Impact
### Summary 
The `DebitaV3Loan::_claimDebt` function is designed to enable a lender claim the amount the loan out and the interest it has acrued provided the borrower has repaid the debt. The transfers the required amount back to the lender but fails to update some state variable correctly leaving the chain in an inconsistent state.

### Vulnerability Details
This vulnerability exists because the function attempts to reset the cached variable in memory instead of the state variable itself. To see this more closely, the function caches a copy of `loanData` in memory, using it to make checks and obtain the required amount to send to the lender. Instead of resetting `interestToClaim` to zero in storage, the function resets `interestToClaim` to zero in memory which does not persist after the function call. Therefore, though the lender has claim their interest for the particular loan, the `interestToClaim` in storage still holds a value greater than zero. 

Thus, the `loanData._acceptedOffers[index].interestToClaim` is not updated correctly.

Here is a link to the function in question https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L288-L311 and can be viewed in the snippet below

```javascript
    function _claimDebt(uint index) internal {
        LoanData memory m_loan = loanData;
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);


        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        require(
            ownershipContract.ownerOf(offer.lenderID) == msg.sender,
            "Not lender"
        );
        require(offer.paid == true, "Not paid");
        require(offer.debtClaimed == false, "Already claimed");
        loanData._acceptedOffers[index].debtClaimed = true;
        ownershipContract.burn(offer.lenderID);
        uint interest = offer.interestToClaim;
@>      offer.interestToClaim = 0; // @audit-note this resets in memory not storage


        SafeERC20.safeTransfer(
            IERC20(offer.principle),
            msg.sender,
            interest + offer.principleAmount
        );


        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```


### Impact
Since the `DebitaV3Loan::_claimDebt` fails to update the `loanData._acceptedOffers[index].interestToClaim` correctly, the chain is left in an inconsistent state and could mislead any one who relies on such metrics for any decision making.

## Proof of Concept
1. Prank a lender whose loan has been repaid to claim their debt by calling the `DebitaV3Loan::claimDebt` function.
2. check the `interestToClaim` of the loan with the `index` corresponding to that of lender 1 above and see that `interestToClaim` is greater than zero when logically speaking, should be zero.


<details>
<summary>PoC</summary>
Place the following code into `MultiplePrinciples.t.sol`.

```javascript
 function test_SpomariaPoC_ClaimDebtDoesNotUpdateLoanDataCorrectly() public {

        matchOffers();
        // get loan info
        DebitaV3Loan.LoanData memory loanData = DebitaV3LoanContract
            .getLoanData();
        uint[] memory indexes = allDynamicData.getDynamicUintArray(3);
        indexes[0] = 0;
        indexes[1] = 1;
        indexes[2] = 2;
        vm.startPrank(borrower);
        deal(wETH, borrower, 10e18, false);
        AEROContract.approve(address(DebitaV3LoanContract), 10e18);
        wETHContract.approve(address(DebitaV3LoanContract), 10e18);

        vm.warp(block.timestamp + 86400); 
        vm.roll(10);
        DebitaV3LoanContract.payDebt(indexes);
        vm.stopPrank();

        // first lender calls the claimDebt function
        DebitaV3LoanContract.claimDebt(0);

        DebitaV3Loan.infoOfOffers memory offer_0 = DebitaV3LoanContract.getLoanData()._acceptedOffers[0];
        DebitaV3Loan.infoOfOffers memory offer_1 = DebitaV3LoanContract.getLoanData()._acceptedOffers[1];

        // assert that interestToClaim is not updated after lender has claimed interest
        assertGt(offer_0.interestToClaim, 0);
        assertGt(offer_1.interestToClaim, 0);
    } 
```

Now run `forge test --match-test test_SpomariaPoC_ClaimDebtDoesNotUpdateLoanDataCorrectly -vvvv`

Output:
```javascript
.
.
.
    ├─ [0] VM::assertGt(7859589041095891 [7.859e15], 0) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertGt(8849315068493151 [8.849e15], 0) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Return] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 50.95ms (13.62ms CPU time)

Ran 1 test suite in 6.39s (50.95ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)


```

</details>

## Tools Used

Manual Review and Foundry


## Recommended Mitigation Steps
Consider editing the function in question to reset the `interestToClaim` in storage not memory as below

```diff
    function _claimDebt(uint index) internal {
        LoanData memory m_loan = loanData;
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);


        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        require(
            ownershipContract.ownerOf(offer.lenderID) == msg.sender,
            "Not lender"
        );
        require(offer.paid == true, "Not paid");
        require(offer.debtClaimed == false, "Already claimed");
        loanData._acceptedOffers[index].debtClaimed = true;
        ownershipContract.burn(offer.lenderID);
        uint interest = offer.interestToClaim;
-       offer.interestToClaim = 0;
+       loanData._acceptedOffers[index].interestToClaim = 0;


        SafeERC20.safeTransfer(
            IERC20(offer.principle),
            msg.sender,
            interest + offer.principleAmount
        );


        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```
