Ripe Cotton Mink

Medium

# Lender will Unable to Claim Their NFT Collateralized Loan

### Summary

The lender couldn't claim the collateral anymore if the lender call `DebitaV3Loan::claimCollateralAsLender` before the auction has been initialized even though the lender hasn't claim the collateral yet.


### Root Cause

In `DebitaV3Loan::claimCollateralAsLender`, if the lender wants to claim the NFT collateral then `DebitaV3Loan::claimCollateralAsNFTLender` is called. The function returning boolean value, it check 2 scenario where if the auction has been initialized or the acceptedOffers.length has to be 1 and auction not initialized. If the scenario is fulfilled then `true` will be returned, otherwise if the scenario isn't fulfilled then `false` will be returned. However, the boolean value being returned is useless as it not being used anywhere even `DebitaV3Loan::claimCollateralAsLender`.

The issue is in `loanData._acceptedOffers[index].collateralClaimed = true;` line. The lender has been updated to claimed, doesn't matter if the lender has really claimed or not. If one of the lender of NFT collateralized loan call `DebitaV3Loan::claimCollateralAsLender` before the auction initialized for the particular NFT, then the lender simply can't claim the collateral anymore even after the auction has been initialized and the NFT has already sold.

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L377

```solidity
    function claimCollateralAsNFTLender(uint index) internal returns (bool) {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
@>      loanData._acceptedOffers[index].collateralClaimed = true;

        if (m_loan.auctionInitialized) {
            // if the auction has been initialized
            // check if the auction has been sold
            require(auctionData.alreadySold, "Not sold on auction");

            uint decimalsCollateral = IveNFTEqualizer(loanData.collateral)
                .getDataByReceipt(loanData.NftID)
                .decimals;

            uint payment = (auctionData.tokenPerCollateralUsed *
                offer.collateralUsed) / (10 ** decimalsCollateral);

            SafeERC20.safeTransfer(
                IERC20(auctionData.liquidationAddress),
                msg.sender,
                payment
            );

            return true;
        } else if (
            m_loan._acceptedOffers.length == 1 && !m_loan.auctionInitialized
        ) {
            // if there is only one offer and the auction has not been initialized
            // send the NFT to the lender
            IERC721(m_loan.collateral).transferFrom(
                address(this),
                msg.sender,
                m_loan.NftID
            );
            return true;
        }
        return false;
    }
```

Example Scenario :

1. The particular loan has passed the deadline.
2. One of the lender calls `DebitaV3Loan::claimCollateralAsLender`, which then calls `DebitaV3Loan::claimCollateralAsNFTLender`.
3. The auction hasn't been initialized, so the function return false.
4. The lender will unable to claim the partial collateral anymore because the state says the collateral has been claimed at the first place.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

No Attack Required

### Impact

Lender will unable to claim the NFT collateral even if the auction has been initialized and sold.


### PoC

_No response_

### Mitigation

```diff
    function claimCollateralAsNFTLender(uint index) internal returns (bool) {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
-       loanData._acceptedOffers[index].collateralClaimed = true;

        if (m_loan.auctionInitialized) {
            // if the auction has been initialized
            // check if the auction has been sold
            require(auctionData.alreadySold, "Not sold on auction");

            uint decimalsCollateral = IveNFTEqualizer(loanData.collateral)
                .getDataByReceipt(loanData.NftID)
                .decimals;

            uint payment = (auctionData.tokenPerCollateralUsed *
                offer.collateralUsed) / (10 ** decimalsCollateral);

            SafeERC20.safeTransfer(
                IERC20(auctionData.liquidationAddress),
                msg.sender,
                payment
            );

+           loanData._acceptedOffers[index].collateralClaimed = true;
            return true;
        } else if (
            m_loan._acceptedOffers.length == 1 && !m_loan.auctionInitialized
        ) {
            // if there is only one offer and the auction has not been initialized
            // send the NFT to the lender
            IERC721(m_loan.collateral).transferFrom(
                address(this),
                msg.sender,
                m_loan.NftID
            );
+           loanData._acceptedOffers[index].collateralClaimed = true;
            return true;
        }
+       loanData._acceptedOffers[index].collateralClaimed = false;
        return false;
    }
```
