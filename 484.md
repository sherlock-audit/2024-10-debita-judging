Shallow Cerulean Iguana

High

# Wrong interestToClaim logic in `DebitaV3Loan::claimDebt` function

### Summary

There is no functionality for the borrower to pay only the interest. Using [`DebitaV3Loan::payDebt`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L186C1-L257C6) function, borrower can pay debt (principal) along with the interest. When the debt is paid, for that loan, that specific offer's `paid` prop is set to `true`

```solidity
loanData._acceptedOffers[index].paid = true;
```

If the loan is not perpetual, the amount of `interest - feeOnInterest` is added to offer's `interestToClaim` prop

```solidity
            if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                loanData._acceptedOffers[index].debtClaimed = true;
                IERC20(offer.principle).approve(address(lendOffer), total);
                lendOffer.addFunds(total);
            } else {
                loanData._acceptedOffers[index].interestToClaim =
                    interest -
                    feeOnInterest;
            }
```

From the above implementation, it is evident that for any debt the lender will claim `offer.paid` will always be true. As a result, in [`DebitaV3Loan::claimDebt`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L271C1-L286C6) function, the else block will never be executed.

```solidity
        if (offer.paid) {
            _claimDebt(index);
@>      } else {
            // if not already full paid, claim interest
            claimInterest(index);
        }
```

[`DebitaV3Loan::claimInterest`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L259C1-L269C6) function is implemented as `internal` and it is only called in `DebitaV3Loan::claimDebt`. This implementation makes this function redundant.

Now there are two possible scenarios, firstly, protocol team says "we don't want to implement the functionality for lender to claim interest only". Furthermore, if we look into [`DebitaV3Loan::calculateInterestToPay`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L721C1-L738C6) function, the return value assumes that there is some interest amount that is previously paid.

```solidity
 return interest - offer.interestPaid;
```

Now in the second scenario, protocol team rectifies the implementation of `DebitaV3Loan::claimInterest` function.

### Root Cause

In consideration to above scenarios and code implementation, let's look into [`DebitaV3Loan::_claimDebt`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L288C1-L311C6) function's implementation. When lender wants to claim debt, he calls `DebitaV3Loan::claimDebt` function, which internally calls `DebitaV3Loan::_claimDebt` function and in this function before transferring tokens, `offer.interestToClaim` is set to `0'.

```solidity
    function _claimDebt(uint index) internal {
        LoanData memory m_loan = loanData;
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        require(
            ownershipContract.ownerOf(offer.lenderID) == msg.sender,
            "Not lender"
        );
        require(offer.paid == true, "Not paid");
        require(offer.debtClaimed == false, "Already claimed");
        loanData._acceptedOffers[index].debtClaimed = true;
        ownershipContract.burn(offer.lenderID);
        uint interest = offer.interestToClaim;
@>      offer.interestToClaim = 0;

        SafeERC20.safeTransfer(
            IERC20(offer.principle),
            msg.sender,
            interest + offer.principleAmount
        );

        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```

Here lies the actual problem. The `offer` in the above code is a cached local variable. As a result, the offer's state is not updated in the storage. Which means, even if the tokens are transferred, the storage will remain unchanged and there will always be a balance in books for `interestToClaim` against a particular offer. For this particular scenario, in current code implementation the `PoC` is provided in below relevant section.

Now another problem is that, if the protocol team provides the functionality for the lender to claim the interest, which is more likely because `DebitaV3Loan::claimInterest` function updates the state correctly.

```solidilty
    function claimInterest(uint index) internal {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
        infoOfOffers memory offer = loanData._acceptedOffers[index];
        uint interest = offer.interestToClaim;

@>      require(interest > 0, "No interest to claim");

@>      loanData._acceptedOffers[index].interestToClaim = 0;
        SafeERC20.safeTransfer(IERC20(offer.principle), msg.sender, interest);
        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```

Because in `DebitaV3Loan::_claimDebt` function, the state is not updated correctly, the lender will be able to receive the interest twice, once by claiming debt and then by claiming interest, because `require(interest > 0, "No interest to claim");` will be by passed first time because of lack of state update in `DebitaV3Loan::_claimDebt` function.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Even if the tokens are transferred, the storage will remain unchanged and there will always be a balance in books for `interestToClaim` against a particular offer.

Second impact, if `DebitaV3Loan::claimInterest` function is implemented correctly:
Because in `DebitaV3Loan::_claimDebt` function, the state is not updated correctly, the lender will be able to receive the interest twice because in `DebitaV3Loan::claimInterest` function `require(interest > 0, "No interest to claim");` will be by passed first time.

### PoC

Create a new file `test/WaqasPoC.t.sol` and add below code in this file.

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";

contract PoC is Test {
    
    DynamicData public allDynamicData;
    DLOFactory public dloFactory;
    DBOFactory public dboFactory;
    DLOImplementation public LendOrder;
    DLOImplementation public LendOrder2;
    DLOImplementation public LendOrder3;
    DLOImplementation public dloImplementation;
    DBOImplementation public BorrowOrder;
    DBOImplementation public dboImplementation;
    veNFTAerodrome public receiptContract;
    ERC20Mock public AEROContract;
    ERC20Mock public DAI;
    ERC20Mock public BUSD;
    VotingEscrow public ABIERC721Contract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DebitaV3Loan public DebitaV3LoanContract;

    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;

    address public debitaChainlink;
    address public debitaPythOracle;
    address public lender;
    address public lender2;
    address public lender3;
    address public borrower;
    address public borrower2;
    address public borrower3;
    address public incentivizer;

    uint public receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        dloImplementation = new DLOImplementation();
        dloFactory = new DLOFactory(address(dloImplementation));
        dboImplementation = new DBOImplementation();
        dboFactory = new DBOFactory(address(dboImplementation));
        receiptContract = new veNFTAerodrome(veAERO, AERO);
        AEROContract = ERC20Mock(AERO);
        DAI = new ERC20Mock();
        BUSD = new ERC20Mock();
        ABIERC721Contract = VotingEscrow(veAERO);
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(dloFactory),
            address(dboFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        dloFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        dboFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
        
        lender = makeAddr("lender");
        lender2 = makeAddr("lender2");
        lender3 = makeAddr("lender3");
        borrower = makeAddr("borrower");
        borrower2 = makeAddr("borrower2");
        borrower3 = makeAddr("borrower3");
        incentivizer = makeAddr("incentivizer");
        
        deal(AERO, lender, 1000e18, false);
        deal(address(DAI), lender, 1000e18, false);
        deal(address(BUSD), lender, 1000e18, false);

        deal(AERO, lender2, 1000e18, false);
        deal(address(DAI), lender2, 1000e18, false);
        deal(address(BUSD), lender2, 1000e18, false);

        deal(AERO, lender3, 1000e18, false);
        deal(address(DAI), lender3, 1000e18, false);
        deal(address(BUSD), lender3, 1000e18, false);

        deal(AERO, incentivizer, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);

        setOracles();

    }

    function test_poc_wrongClaimInterestLogicInClaimDebt() external {
        
        //# CREATE LEND ORDER WITH AERO AS PRINCIPLE
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = AERO;
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 5e18;

            vm.startPrank(lender);

            AEROContract.approve(address(dloFactory), 5e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder = DLOImplementation(lendOrder);
        }

        //# CREATE BORROW ORDER
        {
            vm.startPrank(borrower);
            
            //# Mint veNFT and get NFR
            AEROContract.approve(address(ABIERC721Contract), 100e18);
            uint id = ABIERC721Contract.createLock(10e18, 365 * 4 * 86400);
            ABIERC721Contract.approve(address(receiptContract), id);
            uint[] memory nftID = allDynamicData.getDynamicUintArray(1);
            nftID[0] = id;
            receiptContract.deposit(nftID);
            receiptID = receiptContract.lastReceiptID();
            
            //# Params
            bool[] memory borrowOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory borrowLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
            address[] memory _oracleIDs_Principles = allDynamicData.getDynamicAddressArray(1);
            uint[] memory borrowRatios = allDynamicData.getDynamicUintArray(1);
            
            uint maxInterestRate = 1400;
            uint duration = 864000; // 10 days
            address collateral = address(receiptContract);
            bool isNFT = true;
            
            borrowOraclesActivated[0] = true;
            borrowLTVs[0] = 5000;
            acceptedPrinciples[0] = AERO;
            _oracleIDs_Principles[0] = debitaChainlink;
            borrowRatios[0] = 0;

            uint collateralAmount = 1;

            receiptContract.approve(address(dboFactory), receiptID);
            address borrowOrder = dboFactory.createBorrowOrder(
                borrowOraclesActivated,
                borrowLTVs,
                maxInterestRate,
                duration,
                acceptedPrinciples,
                collateral,
                isNFT,
                receiptID,
                _oracleIDs_Principles,
                borrowRatios,
                debitaChainlink,
                collateralAmount
            );

            vm.stopPrank();
            
            BorrowOrder = DBOImplementation(borrowOrder);
        }

        //# LAPSE SOME TIME TO CHANGE EPOCH
        vm.warp(block.timestamp + 15 days);

        //# MATCH OFFERS
        {
            address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(1);
            uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(1);
            address[] memory principles = allDynamicData.getDynamicAddressArray(1);
            uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
            uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(1);
            uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(1);

            lendOrders[0] = address(LendOrder);
            lendAmountPerOrder[0] = 5e18;
            porcentageOfRatioPerLendOrder[0] = 10000;
            principles[0] = AERO;
            indexForPrinciple_BorrowOrder[0] = 0;
            indexForCollateral_LendOrder[0] = 0;
            indexPrinciple_LendOrder[0] = 0;

            address loan = DebitaV3AggregatorContract.matchOffersV3(
                lendOrders,
                lendAmountPerOrder,
                porcentageOfRatioPerLendOrder,
                address(BorrowOrder),
                principles,
                indexForPrinciple_BorrowOrder,
                indexForCollateral_LendOrder,
                indexPrinciple_LendOrder
            );

            DebitaV3LoanContract = DebitaV3Loan(loan);
        }

        //# LAPSE 5 DAYS
        vm.warp(block.timestamp + 5 days);

        //# BORROWER PAYS DEBT FOR LEND ORDER 1
        {
            uint principalToRepay = DebitaV3LoanContract.getLoanData().principlesAmount[0];
            uint interestToPay = DebitaV3LoanContract.calculateInterestToPay(0);
            uint feeOnInterest = interestToPay * 1500 / 10000;
            uint totalPayment = principalToRepay + interestToPay;

            uint256 loanContractBalanceBeforeRepayment = AEROContract.balanceOf(address(DebitaV3LoanContract));

            //# Params
            uint[] memory payDebtIndexes = new uint[](1);
            payDebtIndexes[0] = 0;
            
            vm.startPrank(borrower);

            AEROContract.approve(address(DebitaV3LoanContract), totalPayment);
            DebitaV3LoanContract.payDebt(payDebtIndexes);

            vm.stopPrank();

            uint256 loanContractBalanceAfterRepayment = AEROContract.balanceOf(address(DebitaV3LoanContract));

            assertEq(loanContractBalanceAfterRepayment - loanContractBalanceBeforeRepayment, totalPayment - feeOnInterest);

        }

        //# LENDER 1 CLAIMS THE DEBT
        {
            uint principalToClaim = DebitaV3LoanContract.getLoanData().principlesAmount[0];
            uint interest = DebitaV3LoanContract.getLoanData()._acceptedOffers[0].interestPaid;
            uint feeOnInterest = interest * 1500 / 10000;
            uint totalClaimDebtAmount = principalToClaim + interest - feeOnInterest;

            uint256 lenderBalanceBeforeClaim = AEROContract.balanceOf(lender);

            vm.prank(lender);
            DebitaV3LoanContract.claimDebt(0);

            uint256 lenderBalanceAfterClaim = AEROContract.balanceOf(lender);

            assertEq(lenderBalanceAfterClaim - lenderBalanceBeforeClaim, totalClaimDebtAmount);

            uint interestToClaimAfterClaim = DebitaV3LoanContract.getLoanData()._acceptedOffers[0].interestToClaim;
            console.log("Interest to claim in books even interest is claimed along with principal: ", interestToClaimAfterClaim);

        }

    }

    function setOracles() internal {
        DebitaChainlink oracle = new DebitaChainlink(
            0xBCF85224fc0756B9Fa45aA7892530B47e10b6433,
            address(this)
        );
        DebitaPyth oracle2 = new DebitaPyth(address(0x0), address(0x0));
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle), true);
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle2), true);

        oracle.setPriceFeeds(AERO, 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0);

        debitaChainlink = address(oracle);
        debitaPythOracle = address(oracle2);
    }
```

Run the test using below command

`forge test --mp WaqasPoC.t.sol --mt test_poc_wrongClaimInterestLogicInClaimDebt --fork-url https://mainnet.base.org --fork-block-number 21151256 -vv`

```bash
Ran 1 test for test/WaqasPoC.t.sol:PoC
[PASS] test_poc_wrongClaimInterestLogicInClaimDebt() (gas: 6472742)
Logs:
  1727286839
  1727286839
  1727286839
  1727286839
  Interest to claim in books even interest is claimed along with principal:  5821917808219178

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 24.87ms (5.97ms CPU time)

Ran 1 test suite in 640.01ms (24.87ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

`DebitaV3Loan::_claimDebt` should be changed as below

```diff
function _claimDebt(uint index) internal {
    LoanData memory m_loan = loanData;
    IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

    infoOfOffers memory offer = m_loan._acceptedOffers[index];
    require(
        ownershipContract.ownerOf(offer.lenderID) == msg.sender,
        "Not lender"
    );
    require(offer.paid == true, "Not paid");
    require(offer.debtClaimed == false, "Already claimed");
    loanData._acceptedOffers[index].debtClaimed = true;
    ownershipContract.burn(offer.lenderID);
    uint interest = offer.interestToClaim;
--  offer.interestToClaim = 0;
++  loanData._acceptedOffers[index].interestToClaim = 0;

    SafeERC20.safeTransfer(
        IERC20(offer.principle),
        msg.sender,
        interest + offer.principleAmount
    );

    Aggregator(AggregatorContract).emitLoanUpdated(address(this));
}
```