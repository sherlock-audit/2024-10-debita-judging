Silly Taffy Hawk

Medium

# [M-2] Confidence interval of Pyth price is not validated in `DebitaPyth::getThePrice`, leading to potentially inaccurate price usage.

### Summary

The `DebitaPyth::getThePrice` function retrieves the price of a token from the Pyth oracle using `pyth.getPriceNoOlderThan` function. However, it does not validate the confidence interval (priceData.conf) of the price returned by the oracle. This omission introduces a risk of relying on imprecise or unreliable prices, especially during periods of market volatility or low liquidity, leading to potential inaccurate evaluations of collateral values.

### Root Cause

The root cause is the absence of validation checks on the `confidence interval` (priceData.conf) of the price returned by the Pyth oracle. While the function [`pyth.getPriceNoOlderThan`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/oracles/DebitaPyth.sol#L32-L35) ensures the freshness of the price by using a maximum age (600 seconds), it completely disregards the confidence interval (priceData.conf).

```solidity
    function getThePrice(address tokenAddress) public view returns (int) {
        // falta hacer un chequeo para las l2
        bytes32 _priceFeed = priceIdPerToken[tokenAddress];
        require(_priceFeed != bytes32(0), "Price feed not set");
        require(!isPaused, "Contract is paused");

        // Get the price from the pyth contract, no older than 90 seconds
@>        PythStructs.Price memory priceData = pyth.getPriceNoOlderThan(
@>            _priceFeed,
@>            600
        );

        // Check if the price feed is available and the price is valid
        require(isFeedAvailable[_priceFeed], "Price feed not available");
        require(priceData.price > 0, "Invalid price");
        return priceData.price;
    }
```

Here is the logic for the `PythStructs.Price` struct:

```solidity
struct Price {
        // Price
        int64 price;
        // Confidence interval around the price
        uint64 conf;
        // Price exponent
        int32 expo;
        // Unix timestamp describing when the price was published
        uint publishTime;
    }
```

As stated in the [Pyth documentation on confidence intervals](https://docs.pyth.network/price-feeds/best-practices#confidence-intervals), it is important to check the `confidence interval` values to prevent the contract from accepting untrusted prices. Ignoring this critical metric may have a real impact on calculations like LTV and collateralization ratios during high volatility or low market liquidity. 

Although the contest documentation states, "We will have a bot constantly monitoring the price of pairs. If there is a difference greater than 5%, the oracle will be paused until it stabilizes again," without checking the confidence level, the function risks using a price that may already be unreliable or inaccurate, especially during volatile market conditions. 

### Internal pre-conditions

The `DebitaPyth::getThePrice` function is called to retrieve a price for a token.
The Pyth oracle provides a price with a confidence interval in its response, however the protocol ignores the confidence interval (priceData.conf) and uses only the price (priceData.price).

### External pre-conditions

Market conditions or anomalies result in a wide confidence interval due to e.g. high volatility, low liquidity, unusual market events or disparities across price publishers in the Pyth network.

### Attack Path

_No response_

### Impact

A lack of validation for the confidence interval in a Pyth oracle price can introduce significant risks especially in the `DebitaV3Aggregator::matchOffersV3` function. This function heavily relies on the `getPriceFrom` method to fetch oracle prices for collateral and principles, which are then used to calculate critical metrics like Loan-to-Value (LTV) ratios. If the fetched price has a high confidence interval - indicating significant uncertainty - but this is not validated in `DebitaPyth::getThePrice`, the `matchOffersV3` function may accept an unreliable price. 

This issue becomes particularly dangerous during periods of low liquidity, high volatility, or price manipulation attacks, where prices may appear stable but have large confidence intervals. Without validation of the confidence interval, the protocol risks under-collateralizing loans during periods of price uncertainty or manipulation. This could result in borrowers securing loans exceeding the true value of their collateral.

### PoC

_No response_

### Mitigation

Validate the confidence interval returned by the Pyth oracle in `DebitaPyth::getThePrice`:

```diff
    function getThePrice(address tokenAddress) public view returns (int) {
        // falta hacer un chequeo para las l2
        bytes32 _priceFeed = priceIdPerToken[tokenAddress];
        require(_priceFeed != bytes32(0), "Price feed not set");
        require(!isPaused, "Contract is paused");

        // Get the price from the pyth contract, no older than 90 seconds
        PythStructs.Price memory priceData = pyth.getPriceNoOlderThan(
            _priceFeed,
            600
        );

        // Check if the price feed is available and the price is valid
        require(isFeedAvailable[_priceFeed], "Price feed not available");
        require(priceData.price > 0, "Invalid price");
+       require(priceData.conf == 0 || (priceData.conf > 0 && (priceData.price / int64(priceData.conf)) >= MIN_CONF_RATIO), "Price confidence is too low");
        return priceData.price;
    }
```
The `MIN_CONF_RATIO` value could be an additional parameter for Pyth nodes, a global configuration or a constant in the contract.
Note that a confidence interval of 0 means no spread in price, so should be considered as a valid price.