Obedient Green Bee

Medium

# Borrowers will have to pay overhead fee for extending loans

### Summary

The incorrect calculation in function `DebitaLoanV3::extendLoan()` will cause borrowers to pay more fee than expected for extending loans.

### Root Cause

- In extending loan logic, the borrower has to pay maximum fee for the unpaid offers if the already paid fee is not enough. The [missing fee is calculated incorrectly](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L600-L610) as `feeOfMaxDeadline = ((offer.maxDeadline * feePerDay) / 86400)`, which uses `offer.maxDeadline`,in term of timestamp, but not using the duration. It should be max duration of the offer, not max deadline of the offer. 
By using max deadline, the value of `feeOfMaxDeadline` will be very high compared to the actual value needed. As a result, the `feeOfMaxDeadline` will be adjusted to be `maxFee` and the `missingBorrowFee` would almost reach maximum `missingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid = maxFee - PorcentageOfFeePaid`
```solidity
                if (PorcentageOfFeePaid != maxFee) {
                    // calculate difference from fee paid for the initialDuration vs the extra fee they should pay because of the extras days of extending the loan.  MAXFEE shouldnt be higher than extra fee + PorcentageOfFeePaid
@>                    uint feeOfMaxDeadline = ((offer.maxDeadline * feePerDay) /
                        86400);
                    if (feeOfMaxDeadline > maxFee) {
                        feeOfMaxDeadline = maxFee;
                    } else if (feeOfMaxDeadline < feePerDay) {
                        feeOfMaxDeadline = feePerDay;
                    }

@>                    misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
```

- [The missing fee is then needed to be paid by the borrower](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L613-L627)
```solidity
                    misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
                uint principleAmount = offer.principleAmount;
@.                uint feeAmount = (principleAmount * misingBorrowFee) / 10000;

                SafeERC20.safeTransferFrom(
                    IERC20(offer.principle),
                    msg.sender,
                    address(this),
                    interestOfUsedTime - interestToPayToDebita
                );

@>                SafeERC20.safeTransferFrom(
                    IERC20(offer.principle),
                    msg.sender,
                    feeAddress,
                    interestToPayToDebita + feeAmount
                );
```


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A borrow offer is created with 10 days duration
2. A lend offer is created with min durations = 3 days, and max duration = 15 days
3. 2 above offers are matched, which creates a Loan with initial duration = 10 days
4. After 5 days, the borrower extends loan. Here, the fee paid is calculated based on initial duration `PorcentageOfFeePaid = ((m_loan.initialDuration * feePerDay) / 86400)` = `10 days * feePerDay` = `0.4%` < `maxFEE`. So the borrower has to pay for the missing, which is `5 days * feePerDay = 0.2%` missing. However, as `offer.maxDeadline` is very likely to much higher than `15 days` duration, then the borrower has to pay `max fee = 0.8%`, instead of `0.4% + 0.2% = 0.6%` as expected

### Impact

- Borrowers will likely have to pay overhead fee for extending loans. The issue happens with the loan that is not yet paid maximum fee combined with the offers that have maximum duration less than duration needed to be worth of maximum fee (~ 20 days duration with feePerDay = 0.04% and max fee = 0.8%)

### PoC
Modify the test file `test/fork/Loan/ltv/OracleOneLenderLoanReceipt.t.sol` as below:

```diff
    function setUp() public {
        ...
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
-             864000,
+            10 days,
            acceptedPrinciples,
            address(receiptContract),
            true,
            receiptID,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            1
        );
        vm.stopPrank();

        AEROContract.approve(address(DLOFactoryContract), 5e18);
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
-            8640000,
-            86400,
+            15 days,
+            5 days,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            5e18
        );

        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }


+    function test_extend_loan() public {
+      MatchOffers();
+
+      vm.startPrank(borrower);
+
+      AEROContract.approve(address(DebitaV3LoanContract), 100e18);
+
+      DebitaV3Loan.LoanData memory _loanData = DebitaV3LoanContract
+            .getLoanData();
+
+      uint feePerDay = DebitaV3AggregatorContract.feePerDay();
+      
+      // initial duration = 10 days
+      // fee paid is less than max fee
+      uint feePaid_percentage = feePerDay * 10 days / 86400;
+
+      // max duraion of lend offer is 15 days
+      uint feeOfMaxDuration_percentage = feePerDay * 15 days / 86400;
+
+      // expected missing fee in amount
+      uint expectedMissingFee_amount =  
+        _loanData._acceptedOffers[0].principleAmount * (feeOfMaxDuration_percentage - feePaid_percentage) / 10000;
+      
+      // cache balance
+      uint balanceBefore = AEROContract.balanceOf(borrower);
+      
+      // interest to be paid for the used time
+      uint interestOfUsedTime = DebitaV3LoanContract.calculateInterestToPay(0);
+
+      vm.warp(block.timestamp + 5 days);
+
+      // call extend loan
+      DebitaV3LoanContract.extendLoan();
+
+      uint balanceAfter = AEROContract.balanceOf(borrower);
+
+      // the actual fee paid
+      uint extendFee = balanceBefore - balanceAfter - interestOfUsedTime;
+
+      assertEq(extendFee, expectedMissingFee_amount);
+    }
```
Run the test with command
```bash
$ forge t --mt test_extend_loan -vvvv --fork-url https://mainnet.base.org --fork-block-number 22435362
```
and the test will fail
```bash
[FAIL: assertion failed: 25479452054794520 != 10000000000000000] test_extend_loan()
```

### Mitigation
Consider updating the logic to something like below
```diff
-uint feeOfMaxDeadline = ((offer.maxDeadline * feePerDay)
+uint feeOfMaxDeadline = ((offerMaxDuration * feePerDay)
```