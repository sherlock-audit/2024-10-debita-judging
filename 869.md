Sneaky Grape Goat

High

# State Variable Shadowing in changeOwner Function

### Summary

A bug was discovered in the `DebitaV3Aggregator` and `auctionFactoryDebita` contract, where the `changeOwner` function fails to update the state variable `owner`. This issue arises due to shadowing of the state variable `owner` by a function parameter with the same name. As a result, the intended update to the contract's ownership does not occur, leading to potential operational issues and security concerns as owner can never be changed after deployment

### Root Cause

The problematic code resides in the `changeOwner` function
1. In `DebitaV3Aggregator`, in lines [682-686](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L682-L686):
```solidity
function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```
2. In `auctionFactoryDebita` , in lines [218-222](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L218-L222):
```solidity
function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```

In both of these cases, the function parameter `owner` shadows the state variable `owner`. Inside the function, all references to `owner` refer to the parameter instead of the state variable. This line:
```solidity
owner = owner;
```
performs a self-assignment of the parameter `owner` and does not update the state variable `owner`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

1. Owner cannot be changed after deployment and deployer becomes the permanent owner. 
2. If a new owner is intended to assume control, their inability to do so can result in failure to implement updates to key contract parameters and the existing owner retaining control even when they should no longer have authority


### PoC

1. Create a new file in the test folder: `Poc.t.sol`
2. Paste the following code:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {DBOFactory} from "../src/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "../src/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "../src/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "../src/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "../src/DebitaV3Aggregator.sol";
import {Ownerships} from "../src/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "../src/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "../src/DebitaV3Loan.sol";
import {DebitaIncentives} from "../src/DebitaIncentives.sol";

contract TwoLendersERC20Loan is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;

    address owner = makeAddr("owner");
    address newOwner = makeAddr("newOwner");

    function setUp() public {
        vm.startPrank(owner);
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        vm.stopPrank();
    }

    function testAggregatorChangeOwner() public {
       // anyone can call changeOwner with their address as argument
        vm.prank(newOwner);
        DebitaV3AggregatorContract.changeOwner(newOwner);

        // owner should be the same even if owner himself calls changeOwner to change owner
        assertEq(DebitaV3AggregatorContract.owner(), owner);
    }

    function testAuctionChangeOwner() public {
        vm.prank(newOwner);
        auctionFactoryDebitaContract.changeOwner(newOwner);

        // owner should be the same even if owner himself calls changeOwner to change owner
        assertEq(auctionFactoryDebitaContract.owner(), owner);
    }
}
```
3. Run `forge test --mt testAggregatorChangeOwner` and `forge test --mt testAuctionChangeOwner`

### Mitigation

Change the argument name to eliminate shadowing:
```diff
-  function changeOwner(address owner) public {
+  function changeOwner(address newOwner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
-       owner = owner;
+       owner = newOwner;
    }
```