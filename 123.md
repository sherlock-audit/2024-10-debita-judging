Helpful Frost Huskie

High

# Borrowers need to pay more interest than expected because of precision loss

### Summary

The [newWeightedAPR's](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L490) calculation will be round down. This will cause precision loss. Borrowers need to pay more borrow interest than expected.

### Root Cause

In matchOffersV3, users can match multiple lend order with one borrow order. We need to meet the lend order's apr requirement and at the same time, we need to meet the borrow order's apr requirement. So we need to calculate averaged weighted apr.
The problem is that there is some precision loss in the `newWeightedAPR`'s calculation. This will cause that the `weightedAverageAPR` is less than the actual apr for the borrower.
For example:
1. First lend order, apr = 100(1%), lendAmount = 1e18, weightedAverageAPR = 100
2. Second lend order, apr = 9000(90%), lendAmount = 1e14, `newWeightedAPR` will be round down to 0, and weightedAverageAPR = 100.
3. We can repeat add similar lend order from step 2.
4. The result is that weightedAverageAPR = 72(0.72%), but the actual borrow interest will be higher than 1%.
Actual apr for this borrower:
(1e18 * 1% + 1e14*28 * 90%)/*(1e18 + 28 * 1e14) = 1.25%
Actual apr increase 25% compared with the borrower's expected apr.

```solidity
        amountPerPrinciple[principleIndex] += lendAmountPerOrder[i];
@>        uint newWeightedAPR = (lendInfo.apr * lendAmountPerOrder[i]) /
            amountPerPrinciple[principleIndex];
        weightedAverageAPR[principleIndex] =
                newWeightedAPR +
                updatedLastApr;
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

Borrowers have to pay more interest than expected.

### PoC

```solidity
    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), address(this), 1000e18, true);
        AERO = address(AEROContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        deal(AERO, address(this), 1000e18, false);
        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            100, // apr 1%
            864000,
            acceptedPrinciples,
            AERO,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            10e18
        );

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            100,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio, // ratio is 1e18
            address(0x0),
            5e18
        );
        address lendOrderAddress1 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            9000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio, // ratio is 1e18
            address(0x0),
            5e18
        );
        LendOrder = DLOImplementation(lendOrderAddress);
        LendOrder1 = DLOImplementation(lendOrderAddress1);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }
    function testPocMatchOffers() public {
        uint256 index = 0;
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(29);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(29);
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(29);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(29);
        uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(29);
        // Actual apr is :
        // (1e18 * 1% + 1e14*28 * 90%)/*(1e18 + 28 * 1e14) = 
        // (100 * 1e14 +  25.2 * 1e14) / (1e14 * (1e4 + 28)) 
        // = 125.2/(10028) = 1.25%
        indexForPrinciple_BorrowOrder[0] = 0;
        for (index = 0; index < 29; index++) {
            indexForCollateral_LendOrder[index] = 0;
            indexPrinciple_LendOrder[index] = 0;
            if (index == 0) {
                lendOrders[index] = address(LendOrder);
                lendAmountPerOrder[0] = 1e18;
            } else {
                lendOrders[index] = address(LendOrder1);
                lendAmountPerOrder[index] = 1e14;
            }
            porcentageOfRatioPerLendOrder[index] = 10000;
        }

        principles[0] = AERO;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
    }
```

### Mitigation

Add one precision decimal 1e18 to mitigate the precision loss
```diff
+            uint newWeightedAPR = (lendInfo.apr * lendAmountPerOrder[i] * 1e18) /
                 amountPerPrinciple[principleIndex];
```
Based on this diff, final weightedAverageAPR is:  124850418827283605873 in the above test case. After we remove the 1e18 decimal, the final apr is 124.  This is quite similar with the manual calculated result 1.25%.