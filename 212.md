Sharp Parchment Chipmunk

Medium

# The Incentivizing System is Rendered Useless Due to Logical Error

### Summary

The `DebitaIncentives::incentivizePair()` function has a logical error that makes the entire incentivizing system useless.


### Root Cause

- The logical error lies in the [DebitaIncentives::incentivizePair()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L264) function. The issue arises in the following block:
```solidity
            // if bribe token has been indexed into array of the epoch
            if (!hasBeenIndexedBribe[epoch][incentivizeToken]) {
                uint lastAmount = bribeCountPerPrincipleOnEpoch[epoch][
@>                  principle
                ];
                SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, lastAmount)
                ] = incentivizeToken;
@>              bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
                hasBeenIndexedBribe[epoch][incentivizeToken] = true;
            }
```
The function mistakenly increments `bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]` instead of `bribeCountPerPrincipleOnEpoch[epoch][principle]`. This prevents `bribeCountPerPrincipleOnEpoch[epoch][principle]` from being updated, leading to incorrect behavior.


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This bug causes the [DebitaIncentives.getBribesPerEpoch()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L367) function to always return an empty array of bribes. Consequently:
1. Borrowers and lenders cannot retrieve information about bribes.
2. The lack of bribe information prevents users from engaging in borrowing or lending activities for a principle.
3. Ultimately, the vulnerability renders the entire incentivizing system useless.


### PoC

Add the following code into [MultipleLoansDuringIncentives.t.sol](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/test/fork/Incentives/MultipleLoansDuringIncentives.t.sol).
```solidity
    function testGetBribesPerEpoch() public {
        incentivize(AERO, AERO, USDC, true, 1e18, 2);

        DebitaIncentives.InfoOfBribePerPrinciple[] memory bribes = incentivesContract.getBribesPerEpoch(2, 0, 10);

        emit log_named_uint("bribes length", bribes.length);
        for (uint i = 0; i < bribes.length; i++) {
            emit log("------------------------------");
            emit log_named_uint("index", i);
            DebitaIncentives.InfoOfBribePerPrinciple memory bribe = bribes[i];
            emit log_named_address("principle", bribe.principle);
            emit log_named_array("bribes", bribe.bribeToken);
            emit log_named_array("amountPerLent", bribe.amountPerLent);
            emit log_named_array("amountPerBorrow", bribe.amountPerBorrow);
            emit log_named_uint("epoch", bribe.epoch);
        }
    }
```
Run the following command:
```bash
forge test -vvv --fork-url https://mainnet.base.org --fork-block-number 21151256 --mt testGetBribesPerEpoch
```
The test output is:
```bash
Ran 1 test for test/fork/Incentives/MultipleLoansDuringIncentives.t.sol:testIncentivesAmongMultipleLoans
[PASS] testGetBribesPerEpoch() (gas: 416810)
Logs:
  bribes length: 1
  ------------------------------
  index: 0
  principle: 0x940181a94A35A4569E4529A3CDfB74e38FD98631
  bribes: []
  amountPerLent: []
  amountPerBorrow: []
  epoch: 2

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.19ms (1.42ms CPU time)
```
The output shows that even though bribes are deposited, no bribe information is returned.


### Mitigation

It is recommended to replace the `incentivizeToken` with the `principle` in the function:
```diff
-               bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
+               bribeCountPerPrincipleOnEpoch[epoch][principle]++;
```
After the fix, the output of the test code changes to:
```bash
Ran 1 test for test/fork/Incentives/MultipleLoansDuringIncentives.t.sol:testIncentivesAmongMultipleLoans
[PASS] testGetBribesPerEpoch() (gas: 420157)
Logs:
  bribes length: 1
  ------------------------------
  index: 0
  principle: 0x940181a94A35A4569E4529A3CDfB74e38FD98631
  bribes: [0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913]
  amountPerLent: [1000000000000000000]
  amountPerBorrow: [0]
  epoch: 2

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.06ms (2.23ms CPU time)
```
This confirms the fix resolves the issue and restores the incentivizing system.
