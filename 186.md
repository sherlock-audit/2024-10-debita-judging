Atomic Butter Bison

High

# [H-8] Deleted buy orders remain marked as legitimate after deletion

### Summary

**Note, this issue is present in all the FACTORY contracts. `DBOFactory::deleteBorrowOrder`, `DLOFactory::deleteOrder`, `auctionFactoryDebita::_deleteAuctionOrder` and `buyOrderFactory::_deleteBuyOrder`**

After deleting a buy order, the `isBuyOrderLegit` mapping in the `buyOrderFactory` contract is not updated to reflect the deletion. This results in deleted buy orders still appearing as legitimate within the system. Functions that rely on this mapping to verify the legitimacy of buy orders will incorrectly treat deleted orders as valid, leading to potential security vulnerabilities, DoS and unintended behavior.__

### Root Cause

The issue lies in the `buyOrderFactory::_deleteBuyOrder` [function](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol#L127). When a buy order is deleted, the function does not update the `isBuyOrderLegit` mapping to mark the order as illegitimate. Specifically, the mapping isBuyOrderLegit[_buyOrder] remains true even after the order has been deleted.

```javascript
    function _deleteBuyOrder(address _buyOrder) public onlyBuyOrder {
        uint index = BuyOrderIndex[_buyOrder];
        BuyOrderIndex[_buyOrder] = 0;

        allActiveBuyOrders[index] = allActiveBuyOrders[activeOrdersCount - 1];
        allActiveBuyOrders[activeOrdersCount - 1] = address(0);

        BuyOrderIndex[allActiveBuyOrders[index]] = index;

        activeOrdersCount--;
    }
```

The above function should also have this line of code `isBuyOrderLegit[_buyOrder] = false`.
The system continues to recognize the deleted buy order as legitimate.

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

There are multiple issues such as:
1. Attackers could exploit deleted buy orders that are still marked as legitimate to perform unauthorized operations, such as reactivating or manipulating orders.
2. The system might attempt to process transactions involving deleted buy orders, potentially leading to DoS, reverting transactions, fund misappropriation, or loss.
3. The protocol's state becomes inconsistent with deleted orders appearing as active, leading to confusion and errors in order management.
4. Functions that match buy and sell orders may include deleted buy orders, causing transaction failures or unintended matches.

### PoC

Adjust the `BuyOrder.t.sol` file setup as follows

```diff
contract BuyOrderTest is Test {
//..
//..

+   // Array to hold buy order addresses for testing
+   address[] public buyOrderAddresses;

    function setUp() public {
-       deal(AERO, seller, 100e18, false);
-       deal(AERO, buyer, 100e18, false);
+       deal(AERO, seller, 1000e18, false);
+       deal(AERO, buyer, 1000e18, false);

//..
//..

-       // vm.startPrank(buyer);
-       // AEROContract.approve(address(factory), 1000e18);
-       // address _buyOrderAddress = factory.createBuyOrder(
-       //     AERO,
-       //     address(receiptContract),
-       //     100e18,
-       //     7e17
-       // );
-       // buyOrderContract = BuyOrder(_buyOrderAddress);
-       // vm.stopPrank();

+       // Create 5 buy orders
+      vm.startPrank(buyer);
+     AEROContract.approve(address(factory), 1000e18);
+    for (uint i = 0; i < 5; i++) {
+           address _buyOrderAddress = factory.createBuyOrder(
+              AERO,
+              address(receiptContract),
+              100e18,
+               7e17
+          );
+         buyOrderAddresses.push(_buyOrderAddress);
+       }
+       vm.stopPrank();
+   }
```

Now add the following test inside the test file    

```javascript
function testOrderStillValidAfterDeletion() public {
        // Assert initial state
        uint activeOrdersCount = factory.activeOrdersCount();
        assertEq(activeOrdersCount, 5, "Active orders count should be 5");

        //assert that last order is legit before deletion
        address buyOrderAddress = buyOrderAddresses[4];
        bool isBuyOrderLegit = factory.isBuyOrderLegit(buyOrderAddress);
        assertEq(isBuyOrderLegit, true);
        console.log("Buy order status before deletion is ", isBuyOrderLegit);

        // Delete the last buy order
        address lastBuyOrderAddress = buyOrderAddresses[activeOrdersCount - 1];
        assertEq(lastBuyOrderAddress, buyOrderAddress);
        vm.prank(buyer);
        BuyOrder(lastBuyOrderAddress).deleteBuyOrder();

        //Assert order is still legit after deletion
        bool isBuyOrderStillLegit = factory.isBuyOrderLegit(lastBuyOrderAddress);
        assertEq(isBuyOrderStillLegit, true);
        console.log("Buy order status after deletion is ", isBuyOrderStillLegit);
    }
```

Test output

```javascript
Ran 1 test for test/fork/BuyOrders/BuyOrder.t.sol:BuyOrderTest
[PASS] testOrderStillValidAfterDeletion() (gas: 99430)
Logs:
  Buy order status before deletion is  true
  Buy order status after deletion is  true

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 14.56ms (677.40Âµs CPU time)

Ran 1 test suite in 304.32ms (14.56ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

```diff
    function _deleteBuyOrder(address _buyOrder) public onlyBuyOrder {
        uint index = BuyOrderIndex[_buyOrder];
        BuyOrderIndex[_buyOrder] = 0;
+     isBuyOrderLegit[_buyOrder] = false

        allActiveBuyOrders[index] = allActiveBuyOrders[activeOrdersCount - 1];
        allActiveBuyOrders[activeOrdersCount - 1] = address(0);

        BuyOrderIndex[allActiveBuyOrders[index]] = index;

        activeOrdersCount--;
    }
```

**Important:** The order of operations inside the `BuyOrder::deleteBuyOrder` function needs to be changed as well once the fix is implemented, otherwise, it will be impossible to delete orders because the `emitDelete` function has an `onlyBuyOrder` modifier on it. As it stands, the function calls `_deleteBuyOrder` before emitting the event. If the `_deleteBuyOrder` will set `isBuyOrderLegit[_buyOrder] = false` then the  `onlyBuyOrder` modifier on the `emitDelete` function will always revert.

```diff
    function deleteBuyOrder() public onlyOwner {
//..
//..
-       IBuyOrderFactory(buyOrderFactory)._deleteBuyOrder(address(this));
        IBuyOrderFactory(buyOrderFactory).emitDelete(address(this));
+       IBuyOrderFactory(buyOrderFactory)._deleteBuyOrder(address(this));
    }
```