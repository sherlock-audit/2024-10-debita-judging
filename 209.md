Old Obsidian Nuthatch

Medium

# Users won't get bribes information for epochs.

### Summary

`DebitaIncentives.getBribesPerEpoch()` function will always return the empty bribes information for epochs.


### Root Cause

- The [DebitaIncentives.incentivizePair()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L225-L294) function updates `bribeCountPerPrincipleOnEpoch[epoch]` for `incentivizeToken` instead of `principle`:
```solidity
    function incentivizePair(
        address[] memory principles,
        address[] memory incentiveToken,
        bool[] memory lendIncentivize,
        uint[] memory amounts,
        uint[] memory epochs
    ) public {
        ... SKIP ...
            // if bribe token has been indexed into array of the epoch
            if (!hasBeenIndexedBribe[epoch][incentivizeToken]) {
                uint lastAmount = bribeCountPerPrincipleOnEpoch[epoch][
                    principle
                ];
                SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, lastAmount)
                ] = incentivizeToken;
264:            bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
                hasBeenIndexedBribe[epoch][incentivizeToken] = true;
            }
        ... SKIP ...
    }
```
As can be seen, the function increases mistakenly `bribeCountPerPrincipleOnEpoch[epoch]` for `incentivizeToken` instead of `principle` in `L264`. As a result, `bribeCountPerPrincipleOnEpoch[epoch][principle]` will never increase and will always be zero.
- The `bribeCountPerPrincipleOnEpoch[epoch][principle]` is used in [DebitaIncentives.getBribesPerEpoch()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L344-L397) function:
```solidity
    function getBribesPerEpoch(
        uint epoch,
        uint offset,
        uint limit
    ) public view returns (InfoOfBribePerPrinciple[] memory) {
        // get the amount of principles incentivized
        uint totalPrinciples = principlesIncentivizedPerEpoch[epoch];
        if (totalPrinciples == 0) {
            return new InfoOfBribePerPrinciple[](0);
        }
        if (offset > totalPrinciples) {
            return new InfoOfBribePerPrinciple[](0);
        }
        if (limit > totalPrinciples) {
            limit = totalPrinciples;
        }
        uint length = limit - offset;
        InfoOfBribePerPrinciple[] memory bribes = new InfoOfBribePerPrinciple[](
            length
        );

        for (uint i = 0; i < length; i++) {
            address principle = epochIndexToPrinciple[epoch][i + offset];
367:        uint totalBribes = bribeCountPerPrincipleOnEpoch[epoch][principle];
            address[] memory bribeToken = new address[](totalBribes);
            uint[] memory amountPerLent = new uint[](totalBribes);
            uint[] memory amountPerBorrow = new uint[](totalBribes);

            for (uint j = 0; j < totalBribes; j++) {
                address token = SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, j)
                ];
                uint lentIncentive = lentIncentivesPerTokenPerEpoch[principle][
                    hashVariables(token, epoch)
                ];
                uint borrowIncentive = borrowedIncentivesPerTokenPerEpoch[
                    principle
                ][hashVariables(token, epoch)];

                bribeToken[j] = token;
                amountPerLent[j] = lentIncentive;
                amountPerBorrow[j] = borrowIncentive;
            }

            bribes[i] = InfoOfBribePerPrinciple(
                principle,
                bribeToken,
                amountPerLent,
                amountPerBorrow,
                epoch
            );
        }
        return bribes;
    }
```
Since `totalBribes` is always zero in `L367`, the returned item `bribes[i]` of the function contains empty arrays for `bribeToken`, `amountPerLent` and `amountPerBorrow` for all `i`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Break of core functionality of the incentive system because the users can't get any bribes information for epochs.


### PoC

_No response_

### Mitigation

Replace `incentivizeToken` with `principle` as follows:
```diff
    function incentivizePair(
        address[] memory principles,
        address[] memory incentiveToken,
        bool[] memory lendIncentivize,
        uint[] memory amounts,
        uint[] memory epochs
    ) public {
        ... SKIP ...
            // if bribe token has been indexed into array of the epoch
            if (!hasBeenIndexedBribe[epoch][incentivizeToken]) {
                uint lastAmount = bribeCountPerPrincipleOnEpoch[epoch][
                    principle
                ];
                SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, lastAmount)
                ] = incentivizeToken;
-               bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
+               bribeCountPerPrincipleOnEpoch[epoch][principle]++;
                hasBeenIndexedBribe[epoch][incentivizeToken] = true;
            }
        ... SKIP ...
    }
```
