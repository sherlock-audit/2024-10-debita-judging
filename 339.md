Mysterious Vanilla Toad

Medium

# DebitaV3Aggregator::changeOwner() doesn't update owner storage variable

### Summary

`changeOwner()` is supposed to allow the Debita team to change the `owner` of the `DebitaV3Aggregator` contract. The problem is that the input param (`owner`) has the name as the storage variable `owner`. When the current owner calls changeOwner(), the function will always revert because the new owner address will be different than msg.sender (the current owner).

### Root Cause

The owner input parameter has the same name as the owner storage variable:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L682-L686

### Internal pre-conditions

n/a

### External pre-conditions

n/a

### Attack Path

1. `owner` of `DebitaAggregatorV3` calls `changeOwner(newOwner)` passing in the address of the new owner
2. `changeOwner(newOwner)` reverts at this line, `require(msg.sender == owner, "Only owner");`, because `owner` here is being interpreted by Solidity as the local variable `owner`, not the storage variable `owner`.
3. `changeOwner` can be called by anyone, but nothing occurs other than the input param setting it's value to itself.

### Impact

The protocol can't update owner in DebitaV3Aggregator.

### PoC

Add to any Foundry `/test/` file and run:
`forge test --mt testChangeOwner`

```solidity
//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";

contract Aggregator {

    uint deployedTime = 99999999999;

    address public owner;

    constructor() {
        owner = msg.sender;
    }

    function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
}

contract BaseTest is Test {
    address owner = makeAddr("owner");
    Aggregator aggregator;

    function setUp() public {
        vm.startPrank(owner);
        aggregator = new Aggregator();
        vm.stopPrank();
    }
 
    function testChangeOwner() public {
        address newOwner = makeAddr("newOwner");

        vm.startPrank(owner);
        assertEq(aggregator.owner(), owner);
        vm.expectRevert();
        aggregator.changeOwner(newOwner);
        vm.stopPrank();
    }
}
```

### Mitigation

Update changeOwner to the following:
```solidity

function changeOwner(address _owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = _owner;
    }
```