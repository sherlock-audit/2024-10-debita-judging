Helpful Frost Huskie

High

# Lend offer can be deleted multiple times

### Summary

The missing check in `addFunds` will cause that lend offers can be deleted twice.

### Root Cause

In [another issue](https://github.com/sherlock-audit/2024-11-debita-finance-v3-0x37-web3/issues/4), we have similar impact with this finding. But they are different findings because their root cause and attack vector are different. 

The problem is that we miss active check in [DLOImplementation:addFunds](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162), this will cause that lend offer owner can add funds even after the lend offer is deleted and inactive.
Once the lend owner add some funds into the inactive lend order, the lend owner can cancel this lend offer again. Because the limitation condition for function cancelOffer() is `availableAmount > 0`.
When we can delete the same lend order multiple times, we can decrease `activeOrdersCount` to 0. This will impact other lend orders. Other lend orders cannot be deleted.
```solidity
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                // When the borrow repay the lend offer, and our mode is perpetual, the funds will be return back via this interface.
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```
```solidity
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        // When we cancel one lend order, we will set the perpetual to false.
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;
        // Return the left ERC20 token back to the lender.
        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }

```
```solidity
    function deleteOrder(address _lendOrder) external onlyLendOrder {
        uint index = LendOrderIndex[_lendOrder];
        LendOrderIndex[_lendOrder] = 0;
        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;
        // take out last borrow order
        allActiveLendOrders[activeOrdersCount - 1] = address(0);
        activeOrdersCount--;
    }

```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Alice creates one lend order
2. Alice cancel this lend order. Now this order is inactive and `availableAmount` = 0.
3. Alice adds funds via `addFunds` to let `availableAmount` larger than 0.
4. Alice cancel this lend order again. We can cancel this lend order again because `availableAmount` > 0.
5. repeat step 3 & 4, `activeOrdersCount` will become to 0.

### Impact

Other lend orders cannot be deleted. This will impact all operations which need `deleteOrder`.
For example, lend orders cannot be cancelled, or we cannot accept this lend offer completely in non-perpetual mode.

### PoC

N/A

### Mitigation

Add one check to make sure that the owner cannot add funds when the lend order is inactive.
```diff
     function addFunds(uint amount) public nonReentrant {
         require(
             msg.sender == lendInformation.owner ||
                 IAggregator(aggregatorContract).isSenderALoan(msg.sender),
             "Only owner or loan"
         );
+        require(isActive, "Offer is not active");
         SafeERC20.safeTransferFrom(
             IERC20(lendInformation.principle),
             msg.sender,
@@ -174,12 +180,20 @@ contract DLOImplementation is ReentrancyGuard, Initializable {
         lendInformation.availableAmount += amount;
         IDLOFactory(factoryContract).emitUpdate(address(this));
     }
```