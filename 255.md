Curly Cyan Eel

Medium

# Owner cannot update ownership in `auctionFactoryDebita::changeOwner`

### Summary

The `auctionFactoryDebita` contract has shadowed variables and as a result the contract't owner storage variable cannot be upgraded to a new owner.


### Root Cause

When passing the parameter to the `changeOwner` function the function's local variable `owner` shadows the contracts storage variable of the `owner`.
 Here is where the storage variable is declared [AuctionFactory.sol:37](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L37)
Here is where the function shadows the storage variable at [AuctionFactory:218-222](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L218-L222)

Here is the declared storage variable:
```solidity
address owner; // owner of the contract
```
Here is the function to change the owner:
```solidity
function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner; 
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The owner of the contract cannot update the owner of the contract. As a result, the `changeOwner` function does not update the owner and anyone can call it since the  owner in this check `require(msg.sender == owner, "Only owner");`  is the owner address provided as a function parameter.

### PoC
```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";

//auctionFactory
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";


contract ShadowedVariablesAuctionFactory is Test {
    auctionFactoryDebita s_auctionFactory;
    address owner= makeAddr("owner");


    function setUp() public {
        vm.startPrank(owner);
        s_auctionFactory = new auctionFactoryDebita();
        vm.stopPrank();

    }

    function test_auctionFactory_cannot_change_owner() public {
        //Verify the owner was initaily set correctly
        //since the variable is private we will read it from its storage slot
        uint256 ownerSlot = 8;
        console.log("The owner slot is" ,ownerSlot);
        bytes32 ownerRaw = vm.load(address(s_auctionFactory), bytes32(ownerSlot));
        address actualOwner = address(uint160(uint256(ownerRaw)));
        vm.assertEq(actualOwner, owner, "Verify during deployment the owner was set");

        //attempt to change the owner
        address newOwner = makeAddr("newOwner");
        vm.startPrank(owner);
        vm.expectRevert();
        //This will revert because the varaible is shadowed when trying to access the owner storage variable
        s_auctionFactory.changeOwner(newOwner);
        vm.stopPrank();


        //because the variable is shadowed the current owner and the new owner have to match
        //so anyone can call the changeOwner function but it won't update the owner
        vm.prank(newOwner);
        s_auctionFactory.changeOwner(newOwner);
      

        //However after everything is done, the owner is still the address that deployed the contract
        //read the owner again to verify it was not updated
        ownerRaw = vm.load(address(s_auctionFactory), bytes32(ownerSlot));
        actualOwner = address(uint160(uint256(ownerRaw)));
        vm.assertEq(actualOwner, owner, "Verify owner never changed");
        console.log(actualOwner);


        vm.assertFalse(actualOwner == newOwner);
    }
}
```
### Mitigation

Make the following changes:
```diff
-    function changeOwner(address owner) public {
+   function changeOwner(address _owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
-        owner = owner; 
+       owner = _owner; 
    }
```