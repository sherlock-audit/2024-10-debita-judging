Attractive Teal Raven

Medium

# Borrower can not `extendLoan` if the `maxDuration` is under 5 days due to underflow

### Summary

If the loan data `maxDuration` is less than `5 days` the logic of `DebitaV3Loan::extendLoan` would underflow when calculating `misingBorrowFee`.


### Root Cause

If we look at the instance where `missingBorrowFee` are calculated inside `extendLoan`, there are 2 crucial variable `feeOfMaxDeadline` and `PorcentageOfFeePaid`.

for `PorcentageOfFeePaid` the calculation is:

[DebitaV3Loan.sol#L575-L579](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L575-L579)

```js
if (PorcentageOfFeePaid > maxFee) {
  PorcentageOfFeePaid = maxFee;
} else if (PorcentageOfFeePaid < minFEE) {
  PorcentageOfFeePaid = minFEE;
}
```

where `PorcentageOfFeePaid` value would be between `minFEE` or `maxFee` which is between range of `20 to 80`.

for `feeOfMaxDeadline`:

[DebitaV3Loan.sol#L600-L611](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L600-L611)

```js
                if (PorcentageOfFeePaid != maxFee) {
                    // calculate difference from fee paid for the initialDuration vs the extra fee they should pay because of the extras days of extending the loan.  MAXFEE shouldnt be higher than extra fee + PorcentageOfFeePaid
@>                  uint feeOfMaxDeadline = ((offer.maxDeadline * feePerDay) /
                        86400);
                    if (feeOfMaxDeadline > maxFee) {
                        feeOfMaxDeadline = maxFee;
                    } else if (feeOfMaxDeadline < feePerDay) {
                        feeOfMaxDeadline = feePerDay;
                    }

@>                  misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
```

`feeOfMaxDeadline` should be expected to be greater than or equal to `PorcentageOfFeePaid`, but there are times when this line underflow.
by the calculation above we should note that `feeOfMaxDeadline` can be as low as `feePerDay` which is `4`, this can happen if the `offer.maxDeadline` equal to `1 days`.

the overflow would happen as long as `feeOfMaxDeadline` is less than `20`, this can be achieved when `offer.maxDeadline` at `5 days -1` or less.

### Internal pre-conditions

1. lender create lend offer where the `maxDeadline` at `5 days -1`
2. borrower create borrow offer
3. match offer

### External pre-conditions

_No response_

### Attack Path

1. after 10% of duration, borrower call `extendLoan`
2. calls revert

### Impact

Borrower can not extend their loan


### PoC

add the following code to `Debita-V3-Contracts/test/local/Loan/TwoLendersERC20Loan.t.sol`:

```js
import { stdError } from "forge-std/Test.sol";
```

```js
    function test_PoC_extendLoanWithMaxDurationUnder5DaysWouldUnderFlow(
        uint256 loan_duration,
        uint256 minDuration,
        uint256 maxDuration
    ) public {
        // lender: set min and max duration so the duration of the loan is less than 5 days
        minDuration = bound(minDuration, 1 days, 5 days - 1);
        maxDuration = bound(maxDuration, minDuration, 5 days - 1);

        // borrower: set loan_duration for borrower between min and max duration
        loan_duration = bound(loan_duration, minDuration, maxDuration);

        // set timestamp
        vm.warp(0);
        address borrower_1 = makeAddr("borrower_1");
        address lender_1 = makeAddr("lender_1");

        deal(AERO, lender_1, 1000e18, false);
        deal(AERO, borrower_1, 1000e18, false);
        deal(USDC, borrower_1, 1000e18, false);

        // Borrower 1 creating borrow order
        vm.startPrank(borrower_1);

        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = USDC;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        USDCContract.approve(address(DBOFactoryContract), 15e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            2200,
            loan_duration,
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            15e18
        );
        vm.stopPrank();

        vm.startPrank(lender_1);
        AEROContract.approve(address(DLOFactoryContract), 10e18);
        ratio[0] = 5e17;

        address lendOrderAddress_0 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            maxDuration, // max duration
            minDuration, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        // match orders
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint256[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint256[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress_0;
        lendAmountPerOrder[0] = 25e17;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(borrowOrderAddress),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);

        // set timestamp to 10% off loan_duration to call extendLoan
        uint256 tenPercentOfLoan_duration = (loan_duration * 1000) / 10000;
        vm.warp(tenPercentOfLoan_duration + 1000); // add 1000 just to make sure we are past the 10% of initial duration

        vm.startPrank(borrower_1);
        IERC20(AERO).approve(address(DebitaV3LoanContract), 100e18);

        // expect revert from underflow
        vm.expectRevert(stdError.arithmeticError);
        DebitaV3LoanContract.extendLoan();
    }
```

then call `forge test --mt test_PoC_extendLoanWithMaxDurationUnder5DaysWouldUnderFlow`, the result should pass

```bash
Ran 1 test for test/local/Loan/TwoLendersERC20Loan.t.sol:TwoLendersERC20Loan
[PASS] test_PoC_extendLoanWithMaxDurationUnder5DaysWouldUnderFlow(uint256,uint256,uint256) (runs: 257, Î¼: 4904539, ~: 4904554)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 625.16ms (555.54ms CPU time)

Ran 1 test suite in 727.45ms (625.16ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

there are few option I would suggest:

1. when lender create lend offer, add check so the value of maxDuration should be greater than `5 days`
2. add proper check when calculating `feeOfMaxDeadline`, if its below `minFEE` then set the value of `feeOfMaxDeadline` to `minFEE` instead `feePerDay`
