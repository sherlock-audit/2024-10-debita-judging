Proud Blue Wren

High

# auction doesn't support TaxTokenReceipts, leading to the lender losing funds

### Summary

DebitaV3 support two NFT as collateral: TaxTokensReceipts and veNFTReceipts. And borrowers can use their collateral to accept liquidity from multiple lenders.If borrower doesn't pay the loan before deadline, then lender can call auction to sell the collateral NFT. But the auction doesn't support TaxTokenReceipts. The auction will revert in function `buyNFT` due to the limit of ` from` and `to`. 
So the lender can not claim collateral in this situation, leads to loss fund. 

### Root Cause

In `Auction.sol`, function `buyNFT` https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/Auction.sol#L149
It will transfer the NFT token to buyer.
```solidity
        IERC721 Token = IERC721(s_CurrentAuction.nftAddress);
        Token.safeTransferFrom(
            address(this),
            msg.sender,
            s_CurrentAuction.nftCollateralID
        );
``` 
But this code doesn't support `TaxTokenReceipt`, because the limit of `from` and `to` in `transferFrom`. 
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L93

```solidity
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override(ERC721, IERC721) {
        bool isReceiverAddressDebita = IBorrowOrderFactory(borrowOrderFactory)
            .isBorrowOrderLegit(to) ||
            ILendOrderFactory(lendOrderFactory).isLendOrderLegit(to) ||
            IAggregator(Aggregator).isSenderALoan(to);
        bool isSenderAddressDebita = IBorrowOrderFactory(borrowOrderFactory)
            .isBorrowOrderLegit(from) ||
            ILendOrderFactory(lendOrderFactory).isLendOrderLegit(from) ||
            IAggregator(Aggregator).isSenderALoan(from);
        // Debita not involved --> revert
        require(
            isReceiverAddressDebita || isSenderAddressDebita,
            "TaxTokensReceipts: Debita not involved"
        );
       // ... 
}
```
The `TaxTokenReceipts` can not successfully transfer to buyer,so the auction about the collateral will always fail. 

Assume the situation: 
1. borrower choose TaxTokenReceipt as collateral 
2. The borrowOffer matches with 2 lendOffer 
3. borrower doesn't pay the loan before deadline 
4. The lenders want to claim collateral, they need to create auction for collateral first. 
5. The auction will always fail and the lender can not claim collateral. 


### Internal pre-conditions

1. Borrower choose TaxTokenReceipts as collateral 
2. The BorrowOffer match with more than 2  LendOffer 

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The lender can not claim collateral, leads to loss fund. 

### PoC

This poc will revert in `buyNFT`. 
```solidity
    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();

        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = ERC20Mock(AERO);
        USDCContract = ERC20Mock(USDC);
        FOTContract = ERC20Mock(fBomb);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );
        receiptContract = new TaxTokensReceipts(
            fBomb,
            address(DBOFactoryContract),
            address(DLOFactoryContract),
            address(DebitaV3AggregatorContract)
        );
        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );

        deal(AERO, firstLender, 1000e18, false);
        deal(AERO, secondLender, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);
        deal(fBomb, buyer, 100e18, true);

        vm.startPrank(borrower);
        
        FOTContract.approve(address(receiptContract), 1000e18);
        uint receiptID = receiptContract.deposit(10e18);
        assertEq(FOTContract.balanceOf(address(receiptContract)), 10e18);
        assertEq(receiptContract.balanceOf(borrower), 1);
        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = address(receiptContract);
        oraclesActivated[0] = false;
        ltvs[0] = 0;
        receiptContract.approve(address(DBOFactoryContract), receiptID);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            address(receiptContract),
            true,
            receiptID,
            oraclesPrinciples,
            ratio,
            address(0x0),
            1
        );
        vm.stopPrank();

        AEROContract.approve(address(DLOFactoryContract), 5e18);
        ratio[0] = 65e16;

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            8640000,
            86400,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );

        vm.startPrank(secondLender);
        AEROContract.approve(address(DLOFactoryContract), 5e18);
        ratio[0] = 4e17;
        address SecondlendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            500,
            9640000,
            86400,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();
        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
        SecondLendOrder = DLOImplementation(SecondlendOrderAddress);
    }

    function testAuctionPoc() public {
        MatchOffers();
        vm.warp(block.timestamp + 8640010);
        DebitaV3LoanContract.createAuctionForCollateral(0);

        DutchAuction_veNFT auction = DutchAuction_veNFT(
            DebitaV3LoanContract.getAuctionData().auctionAddress
        );
        DutchAuction_veNFT.dutchAuction_INFO memory auctionData = auction
            .getAuctionData();

        vm.warp(block.timestamp + (86400 * 10) + 1);

        deal(fBomb, buyer, 100e18);
        vm.startPrank(buyer);

        FOTContract.approve(address(auction), 100e18);
        auction.buyNFT();
    }
```
```solidity
Ran 1 test for test/fork/Loan/ratio/TwoLenderLoanReceipt.t.sol:DebitaAggregatorTest
[FAIL: revert: TaxTokensReceipts: Debita not involved] testAuctionPoc() (gas: 3782575)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 6.54ms (1.90ms CPU time)
```


### Mitigation

_No response_