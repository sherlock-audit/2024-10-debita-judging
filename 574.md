Brave Glossy Duck

High

# veNFT Becomes Stuck in `buyOrder` Contract Due to Incorrect `ERC721.transferFrom` Address in the `buyOrder::sellNFT` function

### Summary

An incorrect address in the `buyOrder::sellNFT` function causes NFTs to be sent to the `buyOrder` contract instead of the intended buyer(owner). This results in the NFT being permanently stuck in the contract as it lacks a function for buyers to claim them.

### Root Cause

In the [buyOrder::sellNFT](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrder.sol#L99-L103) function:

```solidity
function sellNFT(uint receiptID) public {
	// ...
	IERC721(buyInformation.wantedToken).transferFrom(
		msg.sender,
@>		address(this), 
		receiptID
	);
	// ...
}
```

The `IERC721.transferFrom` call incorrectly transfers the NFT to the `buyOrder` contract's `address (address(this))` instead of directly to the buyer (the buyOrder contract's `owner`). This results in the NFT being permanently stuck in the contract since it doesn't have any other mechanism for the buyer to claim it.

### Internal pre-conditions

1. A seller must call `sellNFT()` with a valid receiptID.
2. buyInformation.isActive must be `true`.
3. `buyInformation.availableAmount` must be greater than `0`

### External pre-conditions

1. A buyer has placed a buy order with a valid NFT receipt Id and has sufficient funds.

### Attack Path

1. Seller calls `buyerOrder::sellNFT()` with a valid receipt ID.
2. The contract transfers the NFT to its address `(address(this))` instead of the `buyer(owner)`.
3. The NFT remains locked in the contract as it lacks a function for the buyer to claim it.

### Impact

The buyer's NFT permanently loses their NFT as it is stuck in the `buyerOrder` contract. Buyers are unable to receive the NFT despite transferring their token causing financial loss to them.

### PoC

1. Deploys a mock veNFT:

```solidity
contract MockVeNFT is ERC721Enumerable {
    struct LockedBalance {
        int128 amount;
        uint256 end;
        bool isPermanent;
    }

    mapping(uint256 => LockedBalance) public lock;
    address public voter;

    constructor() ERC721("MockVeNFT", "vNFT") {}

    function mint(address to) external returns (uint256) {
        uint256 tokenId = totalSupply() + 1;
        _mint(to, tokenId);

        // Mock lock data needed by receipt system
        lock[tokenId] = LockedBalance({
            amount: int128(100e18), // 100 token locked
            end: block.timestamp + 365 days,
            isPermanent: false
        });

        return tokenId;
    }

    // Required by receipt system
    function locked(
        uint256 tokenId
    ) external view returns (LockedBalance memory) {
        return lock[tokenId];
    }
}
```

2. Deploy all necessary contracts.
3. Create a `buyOrder` contract and call `sellNFT`
```solidity
contract BuyOrderTest is Test {
    buyOrderFactory factory;
    BuyOrder buyOrderImplementation;
    MockVeNFT public veNFT;
    veNFTAerodrome public receiptContract;
    ERC20Mock underlyingToken;
    address buyer = makeAddr("buyer");
    address nftHolder = makeAddr("holder");

    DynamicData public allDynamicData;
    
    // Receipt-veNFT receiptID
    uint256 receiptId = 1;

    function setUp() public {
        // deploy a mock veNFT
        veNFT = new MockVeNFT();
        // deploy a mock ERC20 underlying token;
        underlyingToken = new ERC20Mock();
        // deploy veNFT receipt contract
        receiptContract = new veNFTAerodrome(
            address(veNFT),
            address(underlyingToken)
        );

        // deploy buy order implementation contract
        buyOrderImplementation = new BuyOrder();

        // deploy buy order factory contract
        factory = new buyOrderFactory(address(buyOrderImplementation));

        // mint buyer some erc20 token
        underlyingToken.mint(buyer, 1000e18);

        vm.startPrank(nftHolder);
        // mint veNFT to user
        uint256 veTokenId = veNFT.mint(nftHolder);

        // approve and deposit into receipt system
        veNFT.approve(address(receiptContract), veTokenId);

        uint256[] memory nftIds = new uint256[](1);
        nftIds[0] = veTokenId;

        // deposit into receipt contract
        receiptContract.deposit(nftIds);

        // verify receipt ownership
        assertEq(receiptContract.ownerOf(receiptId), nftHolder);

        vm.stopPrank();
    }

    function test_BuyerDidnotReceiveNFT() public {
        vm.startPrank(buyer);
        // buyer approve factory to spend token
        underlyingToken.approve(address(factory), 1000e18);

        // Create buy order
        // Parameters: buyToken, wantedToken, amount, ratio
        address buyOrderAddr = factory.createBuyOrder(
            address(underlyingToken),  // paying with underlying token
            address(receiptContract),  // want to buy receipt NFT
            100e18,                    // willing to spend 100 tokens
            1e18                       // 1:1 ratio
        );
        vm.stopPrank();

        vm.startPrank(nftHolder);
        // approve receipt for buy order factory
        receiptContract.approve(address(buyOrderAddr), receiptId);
        // Nft holder sellNFT
        BuyOrder(buyOrderAddr).sellNFT(receiptId);

        // NFT stuck in buy order contract
        assertEq(receiptContract.ownerOf(receiptId), address(buyOrderAddr));
        // buyer did not receive his NFT token
        assertNotEq(receiptContract.ownerOf(receiptId), buyer);
    }
}
```

4. Test log shows the NFT's owner is the `buyOrder` contract instead of the owner(buyer)
```solidity
    ├─ [764] veNFTAerodrome::ownerOf(1) [staticcall]
    │   └─ ← [Return] DebitaProxyContract: [0xa38D17ef017A314cCD72b8F199C0e108EF7Ca04c]
    ├─ [0] VM::assertEq(DebitaProxyContract: [0xa38D17ef017A314cCD72b8F199C0e108EF7Ca04c], DebitaProxyContract: [0xa38D17ef017A314cCD72b8F199C0e108EF7Ca04c]) [staticcall]
    │   └─ ← [Return] 
    ├─ [764] veNFTAerodrome::ownerOf(1) [staticcall]
    │   └─ ← [Return] DebitaProxyContract: [0xa38D17ef017A314cCD72b8F199C0e108EF7Ca04c]
    ├─ [0] VM::assertNotEq(DebitaProxyContract: [0xa38D17ef017A314cCD72b8F199C0e108EF7Ca04c], buyer: [0x0fF93eDfa7FB7Ad5E962E4C0EdB9207C03a0fe02]) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Return] 
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.45ms (997.67µs CPU time)
```

### Mitigation

Correct the `to` address in the `buyerOrder::sellNFT` function to transfer the NFT directly to the buyer’s address instead of the contract address.

```diff
IERC721(buyInformation.wantedToken).transferFrom(  
    msg.sender,  // NFT holder 
-  address(this),
+  buyInformation.owner,  // Buyer address  
    receiptID  
); 
```