Magic Vinyl Aardvark

Medium

# claimCollateralAsLender will block lender share of NFT collateral if called before the auction is created

### Summary

Letâ€™s consider claimCollateralAsLender - the case when [collateral is NFT](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L361)
```solidity
function claimCollateralAsLender(uint index) external nonReentrant {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
        require(
            ownershipContract.ownerOf(offer.lenderID) == msg.sender,
            "Not lender"
        );
        // burn ownership
        ownershipContract.burn(offer.lenderID);
        uint _nextDeadline = nextDeadline();

        require(offer.paid == false, "Already paid");
        require(
            _nextDeadline < block.timestamp && _nextDeadline != 0,
            "Deadline not passed"
        );
        require(offer.collateralClaimed == false, "Already executed");

        // claim collateral
        if (m_loan.isCollateralNFT) {
            claimCollateralAsNFTLender(index);
        } else {
            loanData._acceptedOffers[index].collateralClaimed = true;
            uint decimals = ERC20(loanData.collateral).decimals();
            SafeERC20.safeTransfer(
                IERC20(loanData.collateral),
                msg.sender,
                (offer.principleAmount * (10 ** decimals)) / offer.ratio
            );
        }
        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }

function claimCollateralAsNFTLender(uint index) internal returns (bool) {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        loanData._acceptedOffers[index].collateralClaimed = true;

        if (m_loan.auctionInitialized) {
            // if the auction has been initialized
            // check if the auction has been sold
            require(auctionData.alreadySold, "Not sold on auction");

            uint decimalsCollateral = IveNFTEqualizer(loanData.collateral)
                .getDataByReceipt(loanData.NftID)
                .decimals;

            uint payment = (auctionData.tokenPerCollateralUsed *
                offer.collateralUsed) / (10 ** decimalsCollateral);

            SafeERC20.safeTransfer(
                IERC20(auctionData.liquidationAddress),
                msg.sender,
                payment
            );

            return true;
        } else if (
            m_loan._acceptedOffers.length == 1 && !m_loan.auctionInitialized
        ) {
            // if there is only one offer and the auction has not been initialized
            // send the NFT to the lender
            IERC721(m_loan.collateral).transferFrom(
                address(this),
                msg.sender,
                m_loan.NftID
            );
            return true;
        }
        return false;
    }
```
So we see that the internal function claimCollateralAsNFTLender 
first change collateralClaimed field to true
Then check the cases
```solidity
loanData._acceptedOffers[index].collateralClaimed = true;
```
However, the cases are not fully checked.

Specifically, the case when the auction has not yet been initialized and the number of offers > 1 => simply does nothing for lender, but mark its collateralClaimed field as true.

Thus, if this function is called before the auction is created - after the auction has taken place and the lender claims its collateral - it will not be able to issue it.

### Root Cause

The claimCollateralAsNFTLender function converts the collateralClaimed state to true, although in some cases it does not pay collateral to lender

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

First, I want to say that this is obviously not a user error but an error in the protocol code. 

The user should not lose all collateral because one function was called before another.

Moreover, the order of transactions of a user can be incorrect and for reasons independent from him.

1. If the auction initialization transaction, NFT purchases from the auction and collateral withdrawal were sent three different tx but in one single block - the validator can intentionally switch them in different order and then the lender will lose access to its collateral portion

2. Potential chain re org can simply lead to the lender losing its collateral even if the actions are correct initially, but whose order was confused after reorg

### Impact

Impact - High
Lender loss collateral
Likelihood: low

Severity: medium

### PoC

_No response_

### Mitigation

Fix claimCollateralAsNFTLender function