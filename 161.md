Helpful Frost Huskie

High

# buyOrder can be deleted twice

### Summary

deleteBuyOrder() function lacks reentrance protection. This will cause one buyOrder can be deleted twice.

### Root Cause

In buyOrder, buy order owner will transfer some buy token. Sellers can sell their NFT to get some buy tokens. In [buyOrderFactory:createBuyOrder](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol#L75), there is not any limitation about the buy token and wanted tokens.
This means that we can place one buy order with one malicious buy token and one malicious wanted NFT token.
The problem is that [deleteBuyOrder()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrder.sol#L75) does not have one reentrancy protection. We can make one reentrancy attack via the malicious buy tokens.

Attack vector:
1. Alice creates one buy order with the malicious buy token and wanted token.
2. Alice sell NFT to let `availableAmount` decreased to 0.
3. In the process of `sellNFT`, our malicious buy token's transfer function will be triggered. We reenter the `deleteBuyOrder` to delete this buy order at the first time.
4. After the transfer function, we will delete this buy order at the second time in [here](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrder.sol#L137).
```solidity
    function deleteBuyOrder() public onlyOwner {
        require(buyInformation.isActive, "Buy order is not active");
        // save amount on memory
        uint amount = buyInformation.availableAmount;
        buyInformation.isActive = false;
        buyInformation.availableAmount = 0;

        SafeERC20.safeTransfer(
            IERC20(buyInformation.buyToken),
            buyInformation.owner,
            amount
        );

        IBuyOrderFactory(buyOrderFactory)._deleteBuyOrder(address(this));
        IBuyOrderFactory(buyOrderFactory).emitDelete(address(this));
    }
```
```solidity
    function sellNFT(uint receiptID) public {
        require(buyInformation.isActive, "Buy order is not active");
       ...
@> In this transfer, reenter `deleteBuyOrder` to trigger `_deleteBuyOrder` at the first time.
        SafeERC20.safeTransfer(
            IERC20(buyInformation.buyToken),
            msg.sender,
            amount - feeAmount
        );

        SafeERC20.safeTransfer(
            IERC20(buyInformation.buyToken),
            IBuyOrderFactory(buyOrderFactory).feeAddress(),
            feeAmount
        );
        // If there are some left funds, we can buy another NFT receipt here.
        if (buyInformation.availableAmount == 0) {
            buyInformation.isActive = false;
            IBuyOrderFactory(buyOrderFactory).emitDelete(address(this));
@> Here, we trigger `_deleteBuyOrder` at the second time.
            IBuyOrderFactory(buyOrderFactory)._deleteBuyOrder(address(this));
        } else {
            IBuyOrderFactory(buyOrderFactory).emitUpdate(address(this));
        }
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Alice creates one buy order with the malicious buy token and wanted token.
2. Alice sell NFT to let `availableAmount` decreased to 0.
3. In the process of `sellNFT`, our malicious buy token's transfer function will be triggered. We reenter the `deleteBuyOrder` to delete this buy order at the first time.
4. After the transfer function, we will delete this buy order at the second time in [here](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrder.sol#L137).

### Impact

Malicious users can create some malicious buy orders to delete buy order twice repeatedly. This will cause `activeOrdersCount` decreased abnormally and some other normal buy order cannot be deleted. For example, if the buy order's owner wants to delete this buy order, this will fail because `activeOrdersCount` may have been decreased to 0 incorrectly.
```solidity
    function _deleteBuyOrder(address _buyOrder) public onlyBuyOrder {
        uint index = BuyOrderIndex[_buyOrder];
        BuyOrderIndex[_buyOrder] = 0;

        allActiveBuyOrders[index] = allActiveBuyOrders[activeOrdersCount - 1];
        allActiveBuyOrders[activeOrdersCount - 1] = address(0);

        BuyOrderIndex[allActiveBuyOrders[index]] = index;

        activeOrdersCount--;
    }
```

### PoC

N/A

### Mitigation

Add reentrancy protection for function sellNFT() and deleteBuyOrder().