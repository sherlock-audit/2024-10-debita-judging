Acrobatic Turquoise Vulture

Medium

# Deleted borrow order is still considered valid/legit within the Debita protocol

### Summary

_No response_

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

When matching an offer, it will check if the borrowing order passed in is legit.

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L293

```solidity
File: DebitaV3Aggregator.sol
274:     function matchOffersV3(
..SNIP..
292:         require(
293:             DBOFactory(s_DBOFactory).isBorrowOrderLegit(borrowOrder),
294:             "Invalid borrow order"
295:         );
```

Within the `acceptBorrowOffer` function, it will call the `deleteBorrowOrder` function at Line 179 when the order is marked as inactive.

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Implementation.sol#L179

```solidity
File: DebitaBorrowOffer-Implementation.sol
137:     function acceptBorrowOffer(
138:         uint amount
139:     ) public onlyAggregator nonReentrant onlyAfterTimeOut {
..SNIP..
166:         // if available amount is less than 0.1% of the start amount, the order is no longer active and will count as completed.
167:         if (percentageOfAvailableCollateral <= 10) {
168:             isActive = false;
..SNIP..
177:             borrowInformation.availableAmount = 0;
178:             IDBOFactory(factoryContract).emitDelete(address(this));
179:             IDBOFactory(factoryContract).deleteBorrowOrder(address(this));
180:         } else {
181:             IDBOFactory(factoryContract).emitUpdate(address(this));
182:         }
183:     }
```
Similarly, after the `cancelOffer()`  function is executed, it will call the `deleteBorrowOrder` function at Line 216 below when the order is marked as inactive. 

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Implementation.sol#L216

```solidity
File: DebitaBorrowOffer-Implementation.sol
188:     function cancelOffer() public onlyOwner nonReentrant {
189:         BorrowInfo memory m_borrowInformation = getBorrowInfo();
190:         uint availableAmount = m_borrowInformation.availableAmount;
191:         require(availableAmount > 0, "No available amount");
192:         // set available amount to 0
193:         // set isActive to false
194:         borrowInformation.availableAmount = 0;
195:         isActive = false;
196: 
197:         // transfer collateral back to owner
198:         if (m_borrowInformation.isNFT) {
199:             if (m_borrowInformation.availableAmount > 0) {
200:                 IERC721(m_borrowInformation.collateral).transferFrom(
201:                     address(this),
202:                     msg.sender,
203:                     m_borrowInformation.receiptID
204:                 );
205:             }
206:         } else {
207:             SafeERC20.safeTransfer(
208:                 IERC20(m_borrowInformation.collateral),
209:                 msg.sender,
210:                 availableAmount
211:             );
212:         }
213: 
214:         // emit canceled event on factory
215: 
216:         IDBOFactory(factoryContract).deleteBorrowOrder(address(this));
217:         IDBOFactory(factoryContract).emitDelete(address(this));
218:     }
```

However, the issue is that within the `deleteBorrowOrder` function, it does not set `isBorrowOrderLegit[address(borrowOffer)] = false;` after deleting

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Factory.sol#L162

```solidity
File: DebitaBorrowOffer-Factory.sol
162:     function deleteBorrowOrder(address _borrowOrder) external onlyBorrowOrder {
163:         // get index of the borrow order
164:         uint index = borrowOrderIndex[_borrowOrder];
165:         borrowOrderIndex[_borrowOrder] = 0;
166: 
167:         // get last borrow order
168:         allActiveBorrowOrders[index] = allActiveBorrowOrders[
169:             activeOrdersCount - 1
170:         ];
171:         // take out last borrow order
172:         allActiveBorrowOrders[activeOrdersCount - 1] = address(0);
173: 
174:         // switch index of the last borrow order to the deleted borrow order
175:         borrowOrderIndex[allActiveBorrowOrders[index]] = index;
176:         activeOrdersCount--;
177:     }
```

As a result, within the Debita contracts, they will assume that the deleted borrow order is still valid.

### Impact

Within the Debita contracts, they will assume that the deleted borrow order is still valid, which might lead to unexpected errors. For instance, the borrow order is still considered as valid within the `DebitaV3Aggregator`'s matching feature, and buy order feature.

### PoC

_No response_

### Mitigation

Consider the following change:

```diff
function deleteBorrowOrder(address _borrowOrder) external onlyBorrowOrder {
    // get index of the borrow order
    uint index = borrowOrderIndex[_borrowOrder];
    borrowOrderIndex[_borrowOrder] = 0;

    // get last borrow order
    allActiveBorrowOrders[index] = allActiveBorrowOrders[
        activeOrdersCount - 1
    ];
    // take out last borrow order
    allActiveBorrowOrders[activeOrdersCount - 1] = address(0);

    // switch index of the last borrow order to the deleted borrow order
    borrowOrderIndex[allActiveBorrowOrders[index]] = index;
    activeOrdersCount--;
    
+		isBorrowOrderLegit[address(_borrowOrder)] = false;`    
}
```