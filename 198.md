Brave Glossy Duck

Medium

# DOS in `DebitaV3Aggregator::matchOffersV3` for Token Pairs with Large Price Disparity due to insufficient precision when calculating ratio

### Summary

Insufficient price ratio scaling `10**8` when calculating collateral/principle ratios. For token pairs with large price disparities (e.g., SHIB/BTC), this causes the ratio calculation to round to `0`, resulting in a `**division by zero error**` in subsequent calculations. This prevents the protocol from supporting collateral/principle pairs of large price differences.

### Root Cause

There are 2 areas in [`DebitaV3Aggregator.sol::L350`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L350) and [`DebitaV3Aggregator.sol::L451`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L451-#L468) when calculating the ratio between collateral and principle using oracle prices, multiple division operations occur:

1. First, calculate the initial ratio with insufficient scaling:

```solidity
uint fullRatioPerLending = (priceCollateral_LendOrder * 10**8) / pricePrinciple;
// Rounds to 0 due to insufficient scaling
```
2. This zero ratio propagates through subsequent calculations:

```solidity
// maxValue becomes 0
uint maxValue = (0 * lendInfo.maxLTVs[collateralIndex]) / 10000;

// maxRatio becomes 0
maxRatio = (0 * (10**principleDecimals)) / (10**8);

// ratio becomes 0
ratio = (0 * porcentageOfRatioPerLendOrder[i]) / 10000;
```

3. Finally, when calculating userUsedCollateral, division by zero occurs:
```solidity
// Attempt to divide by zero
userUsedCollateral = (lendAmountPerOrder[i] * (10**decimalsCollateral)) / 0;
// Reverts with "division by zero"
```

### Internal pre-conditions
1. Oracle must be enabled for both tokens in the pair

2. Token pairs with:
Price disparity > 10**8 (e.g., SHIB $0.00002477 vs BTC $91,266.62) causing the ratio to round to 0

### External pre-conditions

1. Chainlink oracle returns prices with 8 decimal places (standard)

2. Either:
Price disparity between tokens > 10**8 must be large enough that the ratio calculation rounds to 0

### Attack Path

1. Borrowers create borrow orders with `SHIB` as collateral and accept `WBTC` as principle
2. `Aggregator` try to match borrow order with `SHIB` as collateral and lend order with `BTC` as principle.
3. `matchOffersV3` will revert because of `division or modulo by zero` error.

### Impact

The protocol can't support any token pairs with significant price differences because the transaction will always revert when trying to match these orders. This prevents lend/borrow pairs like `SHIB/BTC` from being functional on the protocol.

### PoC

Add this into the `BasicDebitaAggregatorTest.t.sol`. Using SHIB token as collateral and WBTC as principle

```solidity
    function testMatchOffersWithLargeTokenPrices() public {
        // deploy mock tokens
        // shib as collateral
        ERC20Mock shib = new ERC20Mock();
        // wbtc as principle
        ERC20Mock wbtc = new ERC20Mock();

        // deal token
        deal(address(shib), address(this), 1000000 * 10 ** 18);
        deal(address(wbtc), address(this), 100 * 10 ** 8);

        // approve token
        IERC20(address(shib)).approve(
            address(DBOFactoryContract),
            type(uint256).max
        );
        IERC20(address(wbtc)).approve(
            address(DLOFactoryContract),
            type(uint256).max
        );

        // deploy mock chainlink price feed (8 decimals)
        MockChainlinkAggregator shibUsdFeed = new MockChainlinkAggregator(8);
        MockChainlinkAggregator btcUsdFeed = new MockChainlinkAggregator(8);

        // Set mock Chainlink prices
        shibUsdFeed.setPrice(2477); // SHIB/USD price from actual chainlink price feed
        btcUsdFeed.setPrice(9126662275309); // BTC/USD price from actual chainlink price feed

        // deploy DebitaChainLink
        DebitaChainlink oracle = new DebitaChainlink(
            address(0x0), // set as 0 for testing
            address(this)
        );

        // set oracle in aggregator with owner
        address owner = DebitaV3AggregatorContract.owner();
        vm.prank(owner);
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle), true);

        // Set price feeds
        oracle.setPriceFeeds(address(shib), address(shibUsdFeed));
        oracle.setPriceFeeds(address(wbtc), address(btcUsdFeed));

        // setup for lend order and borrow order creation
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oracles_Collaterals = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;

        // principle
        oraclesPrinciples[0] = address(oracle);
        acceptedPrinciples[0] = address(wbtc);

        // collateral
        acceptedCollaterals[0] = address(shib);
        oracles_Collaterals[0] = address(oracle);

        oraclesActivated[0] = true;
        ltvs[0] = 7500;

        // Create borrow order
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1000, // maxApr 10%
            864000, // duration 10 days
            acceptedPrinciples,
            address(shib), // SHIB as collateral
            false, // not NFT
            0, // receiptID
            oraclesPrinciples,
            ratio,
            address(oracle), // Use oracle
            1000000 * 10 ** 18 // 1M SHIB
        );

        // Create lend order
        address lendOrderAddress = DLOFactoryContract.createLendOrder({
            _perpetual: false, // not perpetual
            _oraclesActivated: oraclesActivated,
            _lonelyLender: false, // not lonely lender
            _LTVs: ltvs,
            _apr: 1000, // apr 10%
            _maxDuration: 8640000, // max duration
            _minDuration: 86400, // min duration
            _acceptedCollaterals: acceptedCollaterals,
            _principle: address(wbtc), // BTC as principle
            _oracles_Collateral: oracles_Collaterals,
            _ratio: ratio,
            _oracleID_Principle: address(oracle),
            _startedLendingAmount: 1 * 10 ** 8 // 1 BTC
        });

        // Matching arrays for `matchOffer` call
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        // Setup matching parameters
        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 1 * 10 ** 8; // 1 BTC
        porcentageOfRatioPerLendOrder[0] = 10000; // 100%
        principles[0] = address(wbtc); // wbtc as principle

        // index for principle and collateral
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        // failed because of division by ratio which is 0
        vm.expectRevert();
        DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
    }
```

Test log:
```solidity
  │   └─ ← [Revert] panic: division or modulo by zero (0x12)
```

Test shows `matchOfferV3` function reverted due to `division or module by zero`

### Mitigation

Use higher precision `10**18` instead of `10**8`, this should safely covers all the ERC20 token

[Line 350](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L350)
```diff
- uint ValuePrincipleFullLTVPerCollateral = (priceCollateral_BorrowOrder *
                        10 ** 8) / pricePrinciple;

+  uint ValuePrincipleFullLTVPerCollateral = (priceCollateral_BorrowOrder *
                        10 ** 18) / pricePrinciple;
```

[Line 451](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L451)
```diff
- uint fullRatioPerLending = (priceCollateral_LendOrder *
                    10 ** 8) / pricePrinciple;
+ uint fullRatioPerLending = (priceCollateral_LendOrder *
                    10 ** 18) / pricePrinciple;
```
