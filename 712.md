Curly Cyan Eel

High

# The purchased ERC721 token is locked in the `BuyOrder` contract

### Summary

After calling the `BuyOrder::sellNFT` function the ERC721 is transferred to the contract, but there is no way for the buyer/owner to withdraw the token.

### Root Cause

There is missing logic in the `BuyOrder::sellNFT` to make the purchased token withdrawable from the contract.
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrder.sol#L92

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

The buyOrder is not for a normal erc721 instead it is a deposit receipt from veNFTAerodrome or veNFTEqualizer,
but in the case of this bug/finding it can considered to be a normal ERC721.
Process to create a buyOrder
NFT seller:
1. Deposit NFT into the `Receipt-veNFT.sol`
2. A vault will be deployed and the NFT will be transferred to the vault.
3. The depositor will receive a receipt which is an ERC721 token itself

NFT Buyer:
1. They will call `buyOrderFactory::createBuyOrder` (note: the `wantedToken` is not restricted to a receipt or even a ERC721 which
cause problems later on since the contract only support ERC721, but that is another issue/user error)


NFT Seller:
1. The seller will then call `buyOrder::sellNFT` and input the `receiptId` of there previously deposited NFT.
2. The NFT will be transferred to the `buyOrder` contract and the payment token ERC20 will be transferred to the seller.

ISSUE:
The problem is the ERC721 receipt token is stuck in the contract and there is no way to transfer it to the buyer so he can withdraw the underlying receipt's assets.

### Impact

The ERC721 token which is a receipt in this case will be locked in the contract. The buyer will purchase the token and never receive it.

### PoC

Run the test with `forge test --mt test_purchased_NFT_is_stuck -vvvv`

```solidity
//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";

import {ERC721} from "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

import "@contracts/buyOrders/buyOrderFactory.sol";
import "@contracts/buyOrders/buyOrder.sol";

/// @title MockERC721 for the receipt generating contract
contract MockERC721 is ERC721 {
    struct receiptInstance {
        uint256 receiptID; // ID of the receipt
        uint256 attachedNFT; // ID of the NFT attached to the receipt
        uint256 lockedAmount; // Amount of the NFT locked
        uint256 lockedDate; // Unlock date of the NFT locked
        uint256 decimals; // Decimals of the underlying token
        address vault; // Address of the vault
        address underlying; // Address of the underlying token
        bool OwnerIsManager; // If holder is the manager of the attached NFT
    }

    constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) {}

    function mint(address to, uint256 tokenId) public {
        _mint(to, tokenId);
    }

    function getDataByReceipt(uint256 _receiptId) public returns (receiptInstance memory) {
        receiptInstance memory receiptData = receiptInstance({
            receiptID: _receiptId,
            attachedNFT: _receiptId,
            lockedAmount: uint256(1),
            lockedDate: block.timestamp + 1 days,
            decimals: 0,
            vault: address(0),
            underlying: address(0),
            OwnerIsManager: true
        });
        return receiptData;
    }
}


contract NFTStuckBuyOrder is Test {
    MockERC721 s_mockERC721;
    ERC20Mock s_mockToken;
    buyOrderFactory s_buyOrderFactory;
    BuyOrder s_buyOrder;

    //users
    address buyer = makeAddr("buyer"); //the person who wants to buy the NFT
    address seller = makeAddr("seller"); // the person who will sell the NFT for ERC20Token

    //tokenId
    uint256 tokenId = 1;

    //receiptId
    uint256 receiptId = 1;

    function setUp() public {
        s_mockERC721 = new MockERC721("Mock NFT", "MFT");
        s_mockToken = new ERC20Mock();
        s_buyOrder = new BuyOrder();
        address impl = address(s_buyOrder);
        s_buyOrderFactory = new buyOrderFactory(impl);
        s_mockToken.mint(buyer, 100 ether);

        s_mockERC721.mint(seller, tokenId);
    }

    function test_purchased_NFT_is_stuck() public {
        //first the buyer will make a buy order for an NFT
        vm.startPrank(buyer);
        uint256 tokenAmount = 10e18;
        uint256 ratio = 10e18; //note 5e17 is 0.5 ratio
        s_mockToken.approve(address(s_buyOrderFactory), tokenAmount);
        address buyOrderAddress =
            s_buyOrderFactory.createBuyOrder(address(s_mockToken), address(s_mockERC721), tokenAmount, ratio);
        vm.stopPrank();

        //NFT seller will sell the nft to the buyOrder contract
        vm.startPrank(seller);
        s_mockERC721.approve(buyOrderAddress, tokenId);
        BuyOrder(buyOrderAddress).sellNFT(receiptId);

        //After the order has been finalized there is no way to transfer the NFT out of the contract
    }
}
```

### Mitigation

Add a function for the owner of the `buyOrder` contract to withdraw the funds once the `buyOrder` is no longer active.
For example add this function to `buyOrder.sol`.
```solidity
function withdraw(uint256 tokenId) onlyOwner {
    require(!buyInformation.isActive, "Buy order is active");
    IERC721(buyInformation.wantedToken).transferFrom(address(this), msg.sender, tokenId);
    buyInformation.capturedAmount--;
}
```