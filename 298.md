Micro Ginger Tarantula

High

# Last lenders to a defaulted loans which utilizes TaxTokensReceipt NFT as collateral, won't be able to withdraw their collateral.

### Summary

The ``TaxTokenReceipt.sol`` contract purpose is to allow people to deposit Fee on transfer tokens, and in exchange mints them an NFT that they can use to create borrow orders, with the specified FOT token representing the underlying asset. As we can see from the [TaxTokenReceipt::deposit()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L58-L75) function, and the comment above it:
```solidity
    // expect that owners of the token will excempt from tax this contract
    function deposit(uint amount) public nonReentrant returns (uint) {
        uint balanceBefore = ERC20(tokenAddress).balanceOf(address(this));
        SafeERC20.safeTransferFrom(
            ERC20(tokenAddress),
            msg.sender,
            address(this),
            amount
        );
        uint balanceAfter = ERC20(tokenAddress).balanceOf(address(this));
        uint difference = balanceAfter - balanceBefore;
        require(difference >= amount, "TaxTokensReceipts: deposit failed");
        tokenID++;
        tokenAmountPerID[tokenID] = amount;
        _mint(msg.sender, tokenID);
        emit Deposited(msg.sender, amount);
        return tokenID;
    }
``` 
There will be some kind of whitelist which is expected to allow a certain FOT token not to charge any fee when deposited into the ``TaxTokensReceipt.sol`` contract. If the NFT is used as a collateral in a borrow order, and that borrower fails to repay his debt, then an auction for said NFT will be created. The ``sellingToken`` of the auction will be the underlying FOT token of the ``TaxTokensReceipt.sol`` contract. When somebody decides to buy the NFT on auction he has to call the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function:
```solidity
    function buyNFT() public onlyActiveAuction {
        // get memory data
        dutchAuction_INFO memory m_currentAuction = s_CurrentAuction;
        // get current price of the auction
        uint currentPrice = getCurrentPrice();
        // desactivate auction from storage
        s_CurrentAuction.isActive = false;
        uint fee;
        if (m_currentAuction.isLiquidation) {
            fee = auctionFactory(factory).auctionFee();
        } else {
            fee = auctionFactory(factory).publicAuctionFee();
        }

        // calculate fee
        uint feeAmount = (currentPrice * fee) / 10000;
        // get fee address
        address feeAddress = auctionFactory(factory).feeAddress();
        // Transfer liquidation token from the buyer to the owner of the auction
        SafeERC20.safeTransferFrom(
            IERC20(m_currentAuction.sellingToken),
            msg.sender,
            s_ownerOfAuction,
            currentPrice - feeAmount
        );

        SafeERC20.safeTransferFrom(
            IERC20(m_currentAuction.sellingToken),
            msg.sender,
            feeAddress,
            feeAmount
        );

        // If it's a liquidation, handle it properly
        if (m_currentAuction.isLiquidation) {
            debitaLoan(s_ownerOfAuction).handleAuctionSell(
                currentPrice - feeAmount
            );
        }
        IERC721 Token = IERC721(s_CurrentAuction.nftAddress);
        Token.safeTransferFrom(
            address(this),
            msg.sender,
            s_CurrentAuction.nftCollateralID
        );

        auctionFactory(factory)._deleteAuctionOrder(address(this));
        auctionFactory(factory).emitAuctionDeleted(
            address(this),
            s_ownerOfAuction
        );
        // event offerBought
    }
```
 There is a separate problem in the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function, where the function will always revert, because in the overridden [TaxTokensReceipt::transferFrom()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L93-L120) function neither the ``to`` nor the ``from`` addresses are whitelisted. However this is a separate issue, and fixing it doesn't prevent the issue described in this report. As can be seen from the above code snippet, some price calculations are performed, then the FOT token is transferred to the feeAddress and to the appropriate Loan contract. Then the [DebitaV3Loan::handleAuctionSell()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L318-L334) function is called: 
```solidity
    function handleAuctionSell(uint amount) external nonReentrant {
        require(
            msg.sender == auctionData.auctionAddress,
            "Not auction contract"
        );
        require(auctionData.alreadySold == false, "Already sold");
        LoanData memory m_loan = loanData;
        IveNFTEqualizer.receiptInstance memory nftData = IveNFTEqualizer(
            m_loan.collateral
        ).getDataByReceipt(m_loan.NftID);
        uint PRECISION = 10 ** nftData.decimals;
        auctionData.soldAmount = amount;
        auctionData.alreadySold = true;
        auctionData.tokenPerCollateralUsed = ((amount * PRECISION) /
            (loanData.valuableCollateralUsed));
        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```

As can be seen from the code snippet above there are no checks that check what is the actual amount of tokens that was transferred to the contract. The transferred amount is assumed to be the currentPrice - feeAmount from the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function:
```solidity
            debitaLoan(s_ownerOfAuction).handleAuctionSell(
                currentPrice - feeAmount
            );
```
Let's consider the following scenario:
 - The collateral used for the borrow order is a TaxTokensReceipt NFT which has **2e18** locked amount, the decimals are **18**. 
 - The requested principal is USDC, and the borrower has requested **2_700e6 USDC** for **1e18** of the underlying token in the TaxTokensReceipt NFT, this is a ratio of **2_700e6**.
 - The borrow order is matched against 2 separate lend orders, each providing **2_700e6 USDC** as the principal token. And each infoOfOffers for the lenders has ``collateralUsed`` = **1e18**
 - When the loan is created the ``valuableCollateralUsed`` parameter of the loan will be equal to **2e18**
 - Consider the underlaying FOT token has **5%** fee on transfer
 - The loan defaults, and the [createAuctionForCollateral()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L417-L489) function is called, which creates an auction for the NFT.
 - Consider there is a buyer who buys the NFT immediately via the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function. Consider the Debita protocol doesn't charge fees for easier calculations. 
 - The user who calls the the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function will have to transfer 2e18 of the NFT underlying token, however there is 5% fee on transfer to addresses that are not the ``TaxTokensReceipt.sol`` contract, thus the actual amount that will be transferred to the ``DebitaV3Loan.sol`` instance will be **2e18 - (2e18 \* 5%) = 1.9e18**, but the ``amount`` parameter of the [DebitaV3Loan::handleAuctionSell()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L318-L334) function will be **2e18**
 - Then the tokenPerCollateralUsed will calculated in the following way
```solidity
auctionData.tokenPerCollateralUsed = ((amount * PRECISION) / (loanData.valuableCollateralUsed));
```
 - We get **(2e18 \* 1e18) / 2e18 = 1e18**
 - When the first lender call the [claimCollateralAsLender()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L340-L372) function, it will internally call the [claimCollateralAsNFTLender()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L374-L411) function, and the amount that is transferred to the lender will be calculated in the following way:
```solidity
uint payment = (auctionData.tokenPerCollateralUsed * offer.collateralUsed) / (10 ** decimalsCollateral);
```
 - payment will be equal to **(1e18 \* 1e18) / 1e18 = 1e18**
 - The ``DebitaV3Loan.sol`` instance will transfer 1e18 of the underlying tokens to the first lender (the first lender will receive 0.95e18, considering that there is 5% fee on transfer)
 - However when the second lender decides to claim his collateral, his payment will be calculated to be equal to **1e18** as well, however the ``DebitaV3Loan.sol`` instance only has **0.9e18** tokens. As only **1.9e18** underlying tokens were transferred to it from the auction. The call to the [claimCollateralAsLender()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L340-L372) function will revert as there are not enough funds in the contract. 
 
This results in second lender essentially loosing his claim to the underlying tokens which act as a collateral, and those underlying tokens will be locked in the contract forever. There are no partial withdraws or any sweeping functions. The last user to withdraw won't be able to, essentially loosing the initial principal he provided to the borrower. Depending on the amount of the loan, the way orders are matched, the price at which the underlying collateral is bough, and the fee charged on transfer by the underlying FOT token, it is possible that several of the last users that wish to claim their share of the collateral won't be able to. 

### Root Cause
The [DebitaV3Loan::handleAuctionSell()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L318-L334) function expects that the amount parameter provided to it from the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function will be equal to the actual amount of tokens that is transferred to the ``DebitaV3Loan.sol`` instance, however that won't be the case when the collateral is a TaxTokensReceipt NFT, and the underlying asset is a FOT token. 

### Internal pre-conditions

1. Borrower creates a Borrow Order utilizing a ``TaxTokenReceipt.sol`` NFT, with the underlying collateral being a FOT token.
2. The Borrower defaults on his loan, and the loan is liquidated. 
3. The bug regarding the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function, reverting due to the NFT not being able to be transferred is fixed. 
### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact
The DebitaFinance team claims to support FOT tokens via the ``TaxTokensReceipt.sol`` contract, and that the underlying FOT token won't charge fees when it is being deposited/transferred only to the ``TaxTokensReceipt.sol`` contract. However when a TaxTokensReceipt NFT is used as a collateral by a borrower, and that borrower defaults on his loan, due to the nature of FOT tokens, the last lender won't be able to withdraw his share of the underlying token of the collateral. 

### PoC

_No response_

### Mitigation

Consider whether you want to support FOT tokens, as the current implementation is not supporting all FOT tokens as deposits in the ``TaxTokensReceipt.sol`` contract can happen only for certain tokens that don't charge fees when depositing into certain contracts. Otherwise the whole flow has to be changed, the [DebitaV3Loan::handleAuctionSell()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L318-L334) function, has to be called lets say by the borrower, or by lenders so it can check the balance before and after the FOT tokens are transferred from the Auction, and use that amount to calculate the ``auctionData.tokenPerCollateralUsed``.