Jumpy Mocha Flamingo

Medium

# `claimCollateralAsNFTLender` does not check the return value.

### Summary

`claimCollateralAsNFTLender` does not check the return value, which may result in the lender's funds being unintentionally lost.

### Root Cause

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L361
```solidity
        // claim collateral
        if (m_loan.isCollateralNFT) {
            claimCollateralAsNFTLender(index);
        } else {
            loanData._acceptedOffers[index].collateralClaimed = true;
            uint decimals = ERC20(loanData.collateral).decimals();
            SafeERC20.safeTransfer(
                IERC20(loanData.collateral),
                msg.sender,
                (offer.principleAmount * (10 ** decimals)) / offer.ratio
            );
        }
```
```solidity
    function claimCollateralAsNFTLender(uint index) internal returns (bool) {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        loanData._acceptedOffers[index].collateralClaimed = true;

        if (m_loan.auctionInitialized) {
            // if the auction has been initialized
            // check if the auction has been sold
            require(auctionData.alreadySold, "Not sold on auction");

            uint decimalsCollateral = IveNFTEqualizer(loanData.collateral)
                .getDataByReceipt(loanData.NftID)
                .decimals;

            uint payment = (auctionData.tokenPerCollateralUsed *
                offer.collateralUsed) / (10 ** decimalsCollateral);

            SafeERC20.safeTransfer(
                IERC20(auctionData.liquidationAddress),
                msg.sender,
                payment
            );

            return true;
        } else if (
            m_loan._acceptedOffers.length == 1 && !m_loan.auctionInitialized
        ) {
            // if there is only one offer and the auction has not been initialized
            // send the NFT to the lender
            IERC721(m_loan.collateral).transferFrom(
                address(this),
                msg.sender,
                m_loan.NftID
            );
            return true;
        }
        return false;
    }
```
When `claimCollateralAsNFTLender` returns `false`, the lender fails to successfully claim the collateral. However, `loanData._acceptedOffers[index].collateralClaimed` is already set to `true`, meaning they will lose those funds.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The lender may lose funds if `claimCollateralAsNFTLender` returns `false`.

### PoC

_No response_

### Mitigation

```diff
        // claim collateral
        if (m_loan.isCollateralNFT) {
-           claimCollateralAsNFTLender(index);
+           require(claimCollateralAsNFTLender(index));

        } else {
            loanData._acceptedOffers[index].collateralClaimed = true;
            uint decimals = ERC20(loanData.collateral).decimals();
            SafeERC20.safeTransfer(
                IERC20(loanData.collateral),
                msg.sender,
                (offer.principleAmount * (10 ** decimals)) / offer.ratio
            );
        }
```