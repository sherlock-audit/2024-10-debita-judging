Atomic Butter Bison

High

# [H-9] Deleted auctions remain marked as legitimate after deletion

### Summary

**Note, this issue is present in all the FACTORY contracts. `DBOFactory::deleteBorrowOrder`, `DLOFactory::deleteOrder`, `auctionFactoryDebita::_deleteAuctionOrder` and `buyOrderFactory::_deleteBuyOrder`**

After deleting an auction, the `isAuction` mapping in the `auctionFactoryDebita` contract is not updated to reflect the deletion. This results in deleted auctions still appearing as legitimate within the system. Functions that rely on this mapping to verify the legitimacy of auctions will incorrectly treat deleted auctions as valid, leading to potential security vulnerabilities, DoS and unintended behavior.

### Root Cause

The issue lies in the `auctionFactoryDebita::_deleteAuctionOrder` [function](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L145). When an auction is deleted, the function does not update the `isAuction` mapping to reflect its new status. Specifically, the mapping isAuction[_AuctionOrder] remains true even after the auction has been deleted.

```javascript
    function _deleteAuctionOrder(address _AuctionOrder) external onlyAuctions {
        // get index of the Auction order
        uint index = AuctionOrderIndex[_AuctionOrder];
        AuctionOrderIndex[_AuctionOrder] = 0;

        // get last Auction order
        allActiveAuctionOrders[index] = allActiveAuctionOrders[
            activeOrdersCount - 1
        ];
        // take out last Auction order
        allActiveAuctionOrders[activeOrdersCount - 1] = address(0);

        // switch index of the last Auction order to the deleted Auction order
        AuctionOrderIndex[allActiveAuctionOrders[index]] = index;
        activeOrdersCount--;
    }
```
The above function should also have this line of code `isAuction[_AuctionOrder] = false`.
The system continues to recognize the deleted auction as legitimate even after deletion.

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

There are multiple issues such as:

1. Attackers could exploit deleted auctions that are still marked as legitimate to perform unauthorized operations, such as reactivating or manipulating orders.
2. The system might attempt to process transactions involving deleted auctions, potentially leading to DoS, reverting transactions, fund misappropriation, or loss.
3. The protocol's state becomes inconsistent with deleted auctions appearing as active, leading to confusion and errors in order management.
4. Functions that match buy orders and auctions may include deleted auctions, causing transaction failures or unintended matches.

### PoC

Adjust the `Auction.t.sol` file setup as follows

```diff
contract BuyOrderTest is Test {
//..
//..

+   // Array to hold buy order addresses for testing
+   address[] public buyOrderAddresses;

    function setUp() public {
-       deal(AERO, seller, 100e18, false);
-       deal(AERO, buyer, 100e18, false);
+       deal(AERO, seller, 1000e18, false);
+       deal(AERO, buyer, 1000e18, false);

//..
//..

-       // vm.startPrank(buyer);
-       // AEROContract.approve(address(factory), 1000e18);
-       // address _buyOrderAddress = factory.createBuyOrder(
-       //     AERO,
-       //     address(receiptContract),
-       //     100e18,
-       //     7e17
-       // );
-       // buyOrderContract = BuyOrder(_buyOrderAddress);
-       // vm.stopPrank();

+       // Create 5 buy orders
+      vm.startPrank(buyer);
+     AEROContract.approve(address(factory), 1000e18);
+    for (uint i = 0; i < 5; i++) {
+           address _buyOrderAddress = factory.createBuyOrder(
+              AERO,
+              address(receiptContract),
+              100e18,
+               7e17
+          );
+         buyOrderAddresses.push(_buyOrderAddress);
+       }
+       vm.stopPrank();
+   }
```

Now add the following test inside the test file

```javascript
    function testAuctionStillValidAfterDeletion() public {
        // Assert initial state
        uint activeOrdersCount = factory.activeOrdersCount();
        assertEq(activeOrdersCount, 5, "Active orders count should be 5");

        // Assert AuctionOrderIndex and allActiveAuctionOrders before deletion
        address auctionAddress = auctionAddresses[4];
        bool isAuctionLegit = factory.isAuction(auctionAddress);
        assertEq(isAuctionLegit, true);
        console.log("Auction status before deletion is ", isAuctionLegit);

        // Delete the last auction order
        address lastAuctionAddress = auctionAddresses[activeOrdersCount - 1];
        assertEq(lastAuctionAddress, auctionAddress);
        DutchAuction_veNFT lastAuction = DutchAuction_veNFT(lastAuctionAddress);
        vm.prank(signer);
        lastAuction.cancelAuction();

        // Assert state after deletion
        bool isAuctionStillLegit = factory.isAuction(auctionAddress);
        assertEq(isAuctionStillLegit, true);
        console.log("Auction status after deletion is ", isAuctionStillLegit);
    }
```

Test output

```javascript
Ran 1 test for test/fork/Auctions/Auction.t.sol:Auction
[PASS] testAuctionStillValidAfterDeletion() (gas: 188644)
Logs:
  Auction status before deletion is  true
  Auction status after deletion is  true

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 21.33ms (2.62ms CPU time)

Ran 1 test suite in 281.82ms (21.33ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

```diff
    function _deleteAuctionOrder(address _AuctionOrder) external onlyAuctions {
        // get index of the Auction order
        uint index = AuctionOrderIndex[_AuctionOrder];
        AuctionOrderIndex[_AuctionOrder] = 0;
+       isAuction[_AuctionOrder] = false;

//..
//..
    }
```

Important: The order of operations inside the `DutchAuction_veNFT::cancelAuction` function needs to be changed as well once the fix is implemented, otherwise, it will be impossible to delete auctions because the `emitAuctionDeleted` function has an `onlyAuctions` modifier on it. As it stands, the function calls `_deleteAuctionOrder` before emitting the event. If the `_deleteAuctionOrder` will set `isAuction[_AuctionOrder] = false` then the `onlyAuctions` modifier on the `emitAuctionDeleted` function will always revert. The same thing applies to `DutchAuction_veNFT::buyNFT` function.

```diff
    function cancelAuction() public onlyActiveAuction onlyOwner {
//..
//..
-       auctionFactory(factory)._deleteAuctionOrder(address(this));
        auctionFactory(factory).emitAuctionDeleted(
            address(this),
            s_ownerOfAuction
        );
+       auctionFactory(factory)._deleteAuctionOrder(address(this));
    }
```