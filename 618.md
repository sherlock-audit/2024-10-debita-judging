Dry Ebony Hyena

Medium

# [M-3]: `changeOwner` function will not actually change the owner due to shadowing

### Summary

In three of the contracts:

- [buyOrderFactory.sol:186](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol?plain=1#L186)
- [AuctionFactory.sol: 218](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol?plain=1#L218)
- [DebitaV3Aggregator.sol:682](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol?plain=1#L682)

there are `changeOwner` functions which with the current implementations will not actually change the owner if being called within the first 6 hours of deployment.

### Root Cause

In the following contracts:

- [buyOrderFactory.sol:186](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol?plain=1#L186)
- [AuctionFactory.sol: 218](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol?plain=1#L218)
- [DebitaV3Aggregator.sol:682](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol?plain=1#L682)

the `changeOwner` function:

```solidity
    function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```

is using the same naming for the parameter `address owner` as for the state variable `owner`.

Since the function parameter `owner` has the same name as a state variable, the local variable (parameter) will overshadow the state variable within the function's scope. The reference to the variable inside the function will refer to the local variable rather than the state variable which will lead to unexpected behavior and will break the protocol's intention for the `owner` to be able to update the `owner` state variable within those 6 hours after deployment.

### Internal pre-conditions

1. `owner` needs to call the `changeOwner` function within 6 hours of deployment.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The three protocols using the same implementation of the `changeOwner` function are not actually able to change the existing `owner` when the function is being called, thus breaking the intended protocol functionality and behavior.

### PoC

With the current implementation the following test will fail and revert:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {Audit} from "../src/Audit.sol";

contract ShadowingTest is Test {
    Audit auditContract;
    address public initialOwner;
    address public newOwner;

    function setUp() external {
        auditContract = new Audit();
        initialOwner = address(this); // The deployer is the initial owner
        newOwner = address(0x123);
    }

    function testOwnerUpdated() public {
        assertEq(auditContract.owner(), initialOwner);

        vm.prank(initialOwner);
        auditContract.changeOwner(newOwner);

        assertEq(auditContract.owner(), newOwner);
    }
}

```

When using one of the mitigation proposed below, the test will pass.

Mitigation:

```solidity
    function changeOwner(address newOwner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = newOwner;
    }

```

### Mitigation

1. Changing the state variable names, following the convention with an `s_` suffix: `s_owner` and rewrite the setting of the `owner` line:

```solidity
    function changeOwner(address owner) public {
        require(msg.sender == s_owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        s_owner = owner;
    }
```

2. Rename just the function parameter such as `newOwner` and rewrite the setting of the `owner` line:

```solidity
    function changeOwner(address newOwner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = newOwner;
    }
```

