Acrobatic Wool Cricket

Medium

# Claiming Incentives becomes harder as Bribes tokens aren't indexed which causes incentive related read functions to be wrong

### Summary
Claiming incentives is done through a parameter `tokensIncentives` which captures the tokens to claim for a given principle. The read function that returns the tokens to claim does not return the address of the bribe tokens, making it tedious for the user to claim his/her rewards.

Indexing bribe is done through mappings `SpecificBribePerPrincipleOnEpoch`, `hasBeenIndexedBribe` and `bribeCountPerPrincipleOnEpoch`.
The former is a Boolean that captures epoch and incentivized token to true or false.
The latter counts the bribe count for principle per epoch ~ epoch, principle -> value

However the `bribeCountPerPrincipleOnEpoch` is updated incorrectly causing the `SpecificBribePerPrincipleOnEpoch` to not store the incentivizeToken for the epoch and principle.


### Root Cause

In [DebitaIncentives.sol](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L264)

The count for the principle on epoch is updated incorrectly.
```solidity

    function incentivizePair(
        address[] memory principles,
        address[] memory incentiveToken,
        bool[] memory lendIncentivize,
        uint[] memory amounts,
        uint[] memory epochs
    ) public {
        for (uint i; i < principles.length; i++) {
.
.
            // if bribe token has been indexed into array of the epoch
            if (!hasBeenIndexedBribe[epoch][incentivizeToken]) {
1@>                uint lastAmount = bribeCountPerPrincipleOnEpoch[epoch][
                    principle
                ];
                SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, lastAmount)
                ] = incentivizeToken;
2@>                bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
                hasBeenIndexedBribe[epoch][incentivizeToken] = true;
            }
.
.
}
```

We see that the lastAmount to store the `SpecificBribePerPrincipleOnEpoch` uses the value from `bribeCountPerPrincipleOnEpoch[epoch][principle]` however once it makes the entries, it updates `bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++`

This causes the incentivizeToken to always be stored at 0 last amount
i.e: `SpecificBribePerPrincipleOnEpoch[epoch][hashVariables(principle, 0)]`

This means that the `bribeCountPerPrincipleOnEpoch` is not captured properly for the principle and always return 0. This affects the `getBribesPerEpoch` making `totalBribes` always set to 0 as its not updated properly. 

`getBribesPerEpoch` will return with missing values for `bribeToken`, `amountPerLent` and `amountPerBorrow`.

This makes `claimIncentives` function's parameter much harder to fill as the getter function doesn't work as expected as its supposed to capture information related to `lentIncentivesPerTokenPerEpoch` and `borrowedIncentivesPerTokenPerEpoch` that is directly used as paramters to the `claimIncentives` method.

```solidity
    function getBribesPerEpoch(
        uint epoch,
        uint offset,
        uint limit
    ) public view returns (InfoOfBribePerPrinciple[] memory) {
        // get the amount of principles incentivized
.
.
        for (uint i = 0; i < length; i++) {
            address principle = epochIndexToPrinciple[epoch][i + offset];
            uint totalBribes = bribeCountPerPrincipleOnEpoch[epoch][principle];
            address[] memory bribeToken = new address[](totalBribes);
            uint[] memory amountPerLent = new uint[](totalBribes);
            uint[] memory amountPerBorrow = new uint[](totalBribes);

@>            for (uint j = 0; j < totalBribes; j++) {  //@audit will be zero
                address token = SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, j)
                ];
                uint lentIncentive = lentIncentivesPerTokenPerEpoch[principle][
                    hashVariables(token, epoch)
                ];
                uint borrowIncentive = borrowedIncentivesPerTokenPerEpoch[
                    principle
                ][hashVariables(token, epoch)];

                bribeToken[j] = token;
                amountPerLent[j] = lentIncentive;
                amountPerBorrow[j] = borrowIncentive;
            }

            bribes[i] = InfoOfBribePerPrinciple(
                principle,
                bribeToken,  //@audit will be unfilled
                amountPerLent,
                amountPerBorrow,
                epoch
            );
        } 

      return bribes;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Two different incentiveToken come, A1, A2, with principle P1
2. A1 is registered on `bribeCountPerPrincipleOnEpoch[epoch][P1]` ~ 0 , `SpecificBribePerPrincipleOnEpoch(epoch, hashVariables(principle, 0)`
3. `bribeCountPerPrincipleOnEpoch[epoch][A1]` is incremented
4. when A2 comes, `bribeCountPerPrincipleOnEpoch[epoch][P1]` is still 0
5. so A2 is registered on `bribeCountPerPrincipleOnEpoch[epoch][P1]` ~ which is still 0 overwriting A1 in `SpecificBribePerPrincipleOnEpoch`
6. so this doesn't capture every incentivizeToken A1,A2 in this method.

### Impact

`incentivizePair` doesn't capture the incentivized pairs properly leading `claimIncentives` to be harder for users to call as the `tokensIncentives` that have to passed in the parameters are much harder to read, compared to using the dedicated read function.

### PoC

_No response_

### Mitigation

In DebitaIncentives.sol, `incentivizePair` function

```diff
              if (!hasBeenIndexedBribe[epoch][incentivizeToken]) {
                uint lastAmount = bribeCountPerPrincipleOnEpoch[epoch][
                    principle
                ];
                SpecificBribePerPrincipleOnEpoch[epoch][
                    hashVariables(principle, lastAmount)
                ] = incentivizeToken;
-               bribeCountPerPrincipleOnEpoch[epoch][incentivizeToken]++;
+               bribeCountPerPrincipleOnEpoch[epoch][principle]++;
                hasBeenIndexedBribe[epoch][incentivizeToken] = true;
            }
```