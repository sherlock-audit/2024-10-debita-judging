Spare Brick Mockingbird

Medium

# `changeOwner` function argument shadows `owner` variable, preventing authorized owner from updating ownership in contracts

### Summary

The `changeOwner` function is implemented to allow the contract deployer to update the contract owner within six hours of deployment. However, the function's parameter `address owner` shadows the `owner` state variable, leading to the following unintended behaviour:

- The `msg.sender` is incorrectly compared to the function parameter instead of the state variable.
- This causes the function to revert unless `msg.sender` matches the argument passed, preventing the current owner from updating ownership.

As a result, ownership transfer functionality is entirely broken. This issue affects the `DebitaV3Aggregator`, `auctionFactoryDebita`, and `buyOrderFactory` contracts.

### Root Cause

In the `changeOwner` function implemented in the [DebitaV3Aggregator](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L682-L686), [auctionFactoryDebita](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L218-L222) and [buyOrderFactory](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol#L186-L190) contracts the argument shadows the `owner` state variable and the `msg.sender` is compared with the argument instead of the `owner` state variable.

```solidity
    function changeOwner(address owner) public {
@>        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```

### Internal pre-conditions

In the `DebitaV3Aggregator`, `auctionFactoryDebita`, and `buyOrderFactory` contracts, the `block.timestamp` must be less than `deployedTime + 6 hours`

### External pre-conditions

_No response_

### Attack Path

1. The contract owner attempts to transfer ownership by calling the `changeOwner` function and passing the desired new owner address as the argument.
2. Inside the `changeOwner` function, due to the `owner` variable shadowing, the `require` statement compares `msg.sender` (the current contract owner's address) with the argument provided, instead of the `owner` state variable:

```solidity
require(msg.sender == owner, "Only owner");
```
3. Since `msg.sender` does not match the argument (the desired new owner's address), the function reverts, preventing the ownership transfer and leaving the owner unable to assign a new owner.

### Impact

The affected contracts (`DebitaV3Aggregator`, `auctionFactoryDebita`, `buyOrderFactory`) lose their ability to transfer ownership.

### PoC

```solidity

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {buyOrderFactory} from "@contracts/buyOrders/buyOrderFactory.sol";
import {BuyOrder} from "@contracts/buyOrders/buyOrder.sol";

contract ChangeOwnerTest is Test {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DebitaV3Loan public DebitaV3LoanContract;
    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    buyOrderFactory public buyOrderFactoryContract;
    BuyOrder public buyOrderImplementation;

    // Owner address is address(this), which deploys the Debita contracts.
    address originalOwner = address(this);

    address newOwner = address(0x02);
    address feeAddress = address(this);

    function setUp() public {
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        buyOrderImplementation = new BuyOrder();
        buyOrderFactoryContract = new buyOrderFactory(
            address(buyOrderImplementation)
        );

        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
    }

    function testChangeOwner() public {
        // Try to change owner from "originalOwner" to "newOwner"
        // The transactions will revert because: 
        // 1. `changeOwner` function argument shadow the contract's "owner" state variable.
        // 2. as a result, `changeOwner` function compares the "msg.sender" (originalOwner) with the address passed as an argument (newOwner).

        // function changeOwner(address owner) public {
        //@>     require(msg.sender == owner, "Only owner");
        //     require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        //     owner = owner;
        // }

        // `auctionFactoryDebita` contract `owner` variable visibility is internal and it is stored in the storage slot 8
        // the content of the storage slot is read by the vm.load function which returns bytes32 and then converted to an address
        address auctionFactoryDebitaContractOwner = address(uint160(uint256(vm.load(address(auctionFactoryDebitaContract),bytes32(uint256(8))))));

        // "originalOwner" is the owner of the contracts
        assertEq(DebitaV3AggregatorContract.owner(), originalOwner);
        assertEq(auctionFactoryDebitaContractOwner, originalOwner);
        assertEq(buyOrderFactoryContract.owner(), originalOwner);

        // "originalOwner" being the owner of the contracts, is authorized to call the `changeOwner` functions
        vm.startPrank(originalOwner);

        // DebitaV3Aggregator
        vm.expectRevert("Only owner");
        DebitaV3AggregatorContract.changeOwner(newOwner);

        // AuctionFactory
        vm.expectRevert("Only owner");
        auctionFactoryDebitaContract.changeOwner(newOwner);

        // buyOrderFactory
        vm.expectRevert("Only owner");
        buyOrderFactoryContract.changeOwner(newOwner);

        // Any user can call the `changeOwner` function
        // However, the owner won't change because the updated variable is the local "owner" variable, not the contract's "owner" state variable.
        
        // function changeOwner(address owner) public {
        //     require(msg.sender == owner, "Only owner");
        //     require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        //@>     owner = owner;
        // }

        // "newOwner" calls the `changeOwner` functions
        vm.startPrank(newOwner);

        // DebitaV3Aggregator
        DebitaV3AggregatorContract.changeOwner(newOwner);

        // AuctionFactory
        auctionFactoryDebitaContract.changeOwner(newOwner);

        // buyOrderFactory
        buyOrderFactoryContract.changeOwner(newOwner);

        // The function calls did not revert because `msg.sender` is equal to the argument passed.

        // check that the owner has not changed for the DebitaV3Aggregator contract
        assertEq(DebitaV3AggregatorContract.owner(), originalOwner);

        // check that the owner has not changed for the auctionFactoryDebita contract
        auctionFactoryDebitaContractOwner = address(uint160(uint256(vm.load(address(auctionFactoryDebitaContract),bytes32(uint256(8))))));
        assertEq(auctionFactoryDebitaContractOwner, originalOwner);

        // check that the owner has not changed for the buyOrderFactory contract
        assertEq(buyOrderFactoryContract.owner(), originalOwner);
    }
}
```


Steps to reproduce:

1. Create a file `ChangeOwnerTest.t.sol` inside `Debita-V3-Contracts/test/local/` and paste the PoC code.

2. Run the test in the terminal with the following command:

`forge test --mt testChangeOwner`

### Mitigation

In the `DebitaV3Aggregator`, `auctionFactoryDebita`, and `buyOrderFactory` contracts, modify the `changeOwner` function to avoid shadowing the `owner` state variable and to ensure the `owner` state variable is updated correctly:

```diff
-    function changeOwner(address owner) public {
+    function changeOwner(address _owner) public {
         require(msg.sender == owner, "Only owner");
         require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
-        owner = owner;
+        owner = _owner;
     }
```

This change allows the authorized owner to modify the contract ownership as intended.