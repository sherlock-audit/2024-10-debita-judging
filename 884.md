Nice Indigo Squid

Medium

# Lender will loss his collateral share permanently when collateral in NFT

### Summary

Lender will loss his collateral share permanently when collateral in NFT

### Root Cause

Lenders can claim the collateral in default using [claimCollateralAsLender().](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L361) If collateral is NFT, it calls [claimCollateralAsNFTLender()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L374C5-L411C6) which returns bool on the basis of collateral is claimed or not. 

The problem is, its return value is not checked. As result, if claimCollateralAsNFTLender() returns false(indicating collateral is not claimed), it doesn't revert trx but burns the lenderID of the lender, preventing him to claim again
```solidity
 function claimCollateralAsLender(uint256 index) external nonReentrant {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
        require(ownershipContract.ownerOf(offer.lenderID) == msg.sender, "Not lender");
        // burn ownership
@>      ownershipContract.burn(offer.lenderID);
        uint256 _nextDeadline = nextDeadline();

        require(offer.paid == false, "Already paid");
        require(_nextDeadline < block.timestamp && _nextDeadline != 0, "Deadline not passed");
        require(offer.collateralClaimed == false, "Already executed");

        // claim collateral
        if (m_loan.isCollateralNFT) {
@>          claimCollateralAsNFTLender(index);
        } else {
            loanData._acceptedOffers[index].collateralClaimed = true;
            uint256 decimals = ERC20(loanData.collateral).decimals();
            SafeERC20.safeTransfer(
                IERC20(loanData.collateral), msg.sender, (offer.principleAmount * (10 ** decimals)) / offer.ratio
            );
        }
        Aggregator(AggregatorContract).emitLoanUpdated(address(this));
    }
```

### Internal pre-conditions

1. Collateral should be NFT
2. Borrower should default on the debt

### External pre-conditions

None

### Attack Path

1. Suppose there are 4 lenders[L1, L2, L3, L4] and collateral is a NFT
2. Borrower repaid the debt of L1 & L2 but defaulted on L3 & L4
3. In order to claim collateral, NFT should be auctioned and then L3/4 can claim using claimCollateralAsLender()
4. But, L3 called claimCollateralAsLender() before NFT is auctioned, which burns his lenderID(nft) and calls claimCollateralAsNFTLender()
```solidity
 function claimCollateralAsLender(uint256 index) external nonReentrant {
...
        // burn ownership
        ownershipContract.burn(offer.lenderID);
...

        // claim collateral
        if (m_loan.isCollateralNFT) {
            claimCollateralAsNFTLender(index);
        } else {
...
    }
```
5. Now, claimCollateralAsNFTLender() will return false because both if & else-if statement will not run because auction is not started(if-statement) & m_loan._acceptedOffers.length is > 1(else-if statement)
```solidity
 function claimCollateralAsNFTLender(uint256 index) internal returns (bool) {
...
        if (m_loan.auctionInitialized) {
            // if the auction has been initialized
            // check if the auction has been sold
            require(auctionData.alreadySold, "Not sold on auction");

            uint256 decimalsCollateral = IveNFTEqualizer(loanData.collateral).getDataByReceipt(loanData.NftID).decimals;

            uint256 payment = (auctionData.tokenPerCollateralUsed * offer.collateralUsed) / (10 ** decimalsCollateral);

            SafeERC20.safeTransfer(IERC20(auctionData.liquidationAddress), msg.sender, payment);

            return true;
        } else if (m_loan._acceptedOffers.length == 1 && !m_loan.auctionInitialized) {
            // if there is only one offer and the auction has not been initialized
            // send the NFT to the lender
            IERC721(m_loan.collateral).transferFrom(address(this), msg.sender, m_loan.NftID);
            return true;
        }
        return false;
    }
```
6. Return value of claimCollateralAsNFTLender(), which is false is not checked in claimCollateralAsLender() and trx is executed successfully, burning lender's lenderID(nft) but without claiming his collateral

### Impact

Lender will lose his whole collateral if collateral NFT is not auctioned

### PoC

_No response_

### Mitigation

Check the return value of claimCollateralAsNFTLender() and execute the trx accordingly 
```diff
   function claimCollateralAsLender(uint256 index) external nonReentrant {
...
        // claim collateral
        if (m_loan.isCollateralNFT) {
-           claimCollateralAsNFTLender(index);
+           bool isClaimed = claimCollateralAsNFTLender(index);
+           require(isClaimed, "Not claimed");
        } else {
...
    }
```