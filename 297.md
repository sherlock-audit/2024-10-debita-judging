Shallow Cerulean Iguana

Medium

# Entitled lenders/borrowers not able to claim incentives

### Summary

When a borrower order is matched with multiple lend offers and there is a middle order principle token that is not added as whitelisted pair in `DebitaIncentives`, then all the following principal token lenders will not be able to claim incentives.

### Root Cause

[Source](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L316C13-L318C14)
In `DebitaIncentives::updateFunds` function, it actually returns, if any intermediary single pair in the loop is not whitelisted.

```solidity
    function updateFunds(
        infoOfOffers[] memory informationOffers,
        address collateral,
        address[] memory lenders,
        address borrower
    ) public onlyAggregator {
        for (uint i = 0; i < lenders.length; i++) {
            bool validPair = isPairWhitelisted[informationOffers[i].principle][
                collateral
            ];
@>          if (!validPair) {
                return;
            }
    ....
```

Because of this issue, incentives are not updated for the following whitelisted pairs in the loop.

### Internal pre-conditions

- a particular principal token pair is not whitelisted by owner in the `DebitaIncentives`

### External pre-conditions

- Connector matches a borrow order (accepting multiple principles) with multiple lend offers and within these lend offers there is a lend offer that contains that particular principal which is not whitelisted as a pair in `DebitaIncentives`

### Attack Path

1. Borrower creates a borrow order, accepting AERO, DAI and BUSD as principles and offers NFR as collateral
2. Lender1 creates lend order with AERO as principle
3. Lender2 creates lend order with DAI as principle
4. Lender3 creates lend order with BUSD as principle
5. Owner of `DebitaIncentives` whitelists AERO/NFR and BUSD/NFR pairs (note that DAI is not whitelisted)
6. Incentivizer incentivizes above two pairs
7. Time lapses 15 days epoch is changed
8. Connector matches the borrow order with above three lend offers in same sequence
9. Time lapses 15 days epoch is changed
10. Lender3 with the lend offer having BUSD as principle (that is whitelisted and incentivized), tries to claim incentives, but fails and transaction reverts.

### Impact

Entitled lenders/borrowers will not be able to claim the incentives because funds will not be updated in above mentioned scenario.

### PoC

Create a new file `WaqasPoC.t.sol` in `test/` and place below code in it.

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";
// import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";
// import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";

contract PoC is Test {
    
    DynamicData public allDynamicData;
    DLOFactory public dloFactory;
    DBOFactory public dboFactory;
    DLOImplementation public LendOrder;
    DLOImplementation public LendOrder2;
    DLOImplementation public LendOrder3;
    DLOImplementation public dloImplementation;
    DBOImplementation public BorrowOrder;
    DBOImplementation public dboImplementation;
    veNFTAerodrome public receiptContract;
    ERC20Mock public AEROContract;
    ERC20Mock public DAI;
    ERC20Mock public BUSD;
    VotingEscrow public ABIERC721Contract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DebitaV3Loan public DebitaV3LoanContract;

    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;

    address public debitaChainlink;
    address public debitaPythOracle;
    address public lender;
    address public lender2;
    address public lender3;
    address public borrower;
    address public borrower2;
    address public borrower3;
    address public incentivizer;

    uint public receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        dloImplementation = new DLOImplementation();
        dloFactory = new DLOFactory(address(dloImplementation));
        dboImplementation = new DBOImplementation();
        dboFactory = new DBOFactory(address(dboImplementation));
        receiptContract = new veNFTAerodrome(veAERO, AERO);
        AEROContract = ERC20Mock(AERO);
        DAI = new ERC20Mock();
        BUSD = new ERC20Mock();
        ABIERC721Contract = VotingEscrow(veAERO);
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(dloFactory),
            address(dboFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        dloFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        dboFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
        
        lender = makeAddr("lender");
        lender2 = makeAddr("lender2");
        lender3 = makeAddr("lender3");
        borrower = makeAddr("borrower");
        borrower2 = makeAddr("borrower2");
        borrower3 = makeAddr("borrower3");
        incentivizer = makeAddr("incentivizer");
        
        deal(AERO, lender, 1000e18, false);
        deal(address(DAI), lender, 1000e18, false);
        deal(address(BUSD), lender, 1000e18, false);

        deal(AERO, lender2, 1000e18, false);
        deal(address(DAI), lender2, 1000e18, false);
        deal(address(BUSD), lender2, 1000e18, false);

        deal(AERO, lender3, 1000e18, false);
        deal(address(DAI), lender3, 1000e18, false);
        deal(address(BUSD), lender3, 1000e18, false);

        deal(AERO, incentivizer, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);

        setOracles();

    }

    function test_poc_lendersAndBorrowersCanLooseIncentives() external {
        
        //# CREATE BORROW ORDER WITH THREE PRINCIPLES
        {
            vm.startPrank(borrower);
            
            //# Mint veNFT and get NFR
            AEROContract.approve(address(ABIERC721Contract), 100e18);
            uint id = ABIERC721Contract.createLock(10e18, 365 * 4 * 86400);
            ABIERC721Contract.approve(address(receiptContract), id);
            uint[] memory nftID = allDynamicData.getDynamicUintArray(1);
            nftID[0] = id;
            receiptContract.deposit(nftID);
            receiptID = receiptContract.lastReceiptID();
            
            //# Params
            bool[] memory borrowOraclesActivated = allDynamicData.getDynamicBoolArray(3);
            uint[] memory borrowLTVs = allDynamicData.getDynamicUintArray(3);
            address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(3);
            address[] memory _oracleIDs_Principles = allDynamicData.getDynamicAddressArray(3);
            uint[] memory borrowRatios = allDynamicData.getDynamicUintArray(3);
            
            borrowOraclesActivated[0] = true;
            borrowOraclesActivated[1] = true;
            borrowOraclesActivated[2] = true;
            
            borrowLTVs[0] = 5000;
            borrowLTVs[1] = 5000;
            borrowLTVs[2] = 5000;

            uint maxInterestRate = 1400;
            uint duration = 864000; // 10 days
            
            acceptedPrinciples[0] = AERO;
            acceptedPrinciples[1] = address(DAI);
            acceptedPrinciples[2] = address(BUSD);
            
            address collateral = address(receiptContract);
            bool isNFT = true;
            
            _oracleIDs_Principles[0] = debitaChainlink;
            _oracleIDs_Principles[1] = debitaChainlink;
            _oracleIDs_Principles[2] = debitaChainlink;
            
            borrowRatios[0] = 0;
            borrowRatios[1] = 0;
            borrowRatios[2] = 0;

            uint collateralAmount = 1;

            receiptContract.approve(address(dboFactory), receiptID);
            address borrowOrder = dboFactory.createBorrowOrder(
                borrowOraclesActivated,
                borrowLTVs,
                maxInterestRate,
                duration,
                acceptedPrinciples,
                collateral,
                isNFT,
                receiptID,
                _oracleIDs_Principles,
                borrowRatios,
                debitaChainlink,
                collateralAmount
            );

            vm.stopPrank();
            
            BorrowOrder = DBOImplementation(borrowOrder);
        }

        //# CREATE LEND ORDER 1 WITH AERO AS PRINCIPLE
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = AERO;
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 2e18;

            vm.startPrank(lender);

            AEROContract.approve(address(dloFactory), 2e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder = DLOImplementation(lendOrder);
        }

        //# CREATE LEND ORDER 2 WITH DAI AS PRINCIPLE
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = address(DAI);
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 2e18;

            vm.startPrank(lender2);

            DAI.approve(address(dloFactory), 2e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder2 = DLOImplementation(lendOrder);
        }

        //# CREATE LEND ORDER 3 WITH BUSD AS PRINCIPLE
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = address(BUSD);
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 1e18;

            vm.startPrank(lender3);

            BUSD.approve(address(dloFactory), 1e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder3 = DLOImplementation(lendOrder);
        }

        //# OWNER WHITELISTS PAIRS
        {
            incentivesContract.whitelListCollateral(
                AERO,
                address(receiptContract),
                true
            );

            incentivesContract.whitelListCollateral(
                address(BUSD),
                address(receiptContract),
                true
            );
        }

        //# INCENTIVIZE PAIRS
        {
            
            //# Params
            address[] memory principles = allDynamicData.getDynamicAddressArray(2);
            address[] memory incentiveTokens = allDynamicData.getDynamicAddressArray(2);
            bool[] memory lendIncentivizes = allDynamicData.getDynamicBoolArray(2);
            uint[] memory amounts = allDynamicData.getDynamicUintArray(2);
            uint[] memory epochs = allDynamicData.getDynamicUintArray(2);
            
            principles[0] = AERO;
            principles[1] = address(BUSD);

            incentiveTokens[0] = AERO;
            incentiveTokens[1] = AERO;
            
            lendIncentivizes[0] = true;
            lendIncentivizes[1] = true;

            amounts[0] = 100e18;
            amounts[1] = 100e18;

            epochs[0] = 2;
            epochs[1] = 2;

            vm.startPrank(incentivizer);
            IERC20(AERO).approve(address(incentivesContract), 200e18);
            incentivesContract.incentivizePair(
                principles,
                incentiveTokens,
                lendIncentivizes,
                amounts,
                epochs
            );

            vm.stopPrank();

        }

        //# LAPSE SOME TIME TO CHANGE EPOCH
        vm.warp(block.timestamp + 15 days);

        //# MATCH OFFERS --> will not update incentives for BUSD/NFR pair
        {
            address[] memory lendOrders = allDynamicData.getDynamicAddressArray(3);
            uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(3);
            uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(3);
            address[] memory principles = allDynamicData.getDynamicAddressArray(3);
            uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(3);
            uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(3);
            uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(3);

            lendOrders[0] = address(LendOrder);
            lendOrders[1] = address(LendOrder2);
            lendOrders[2] = address(LendOrder3);
            
            lendAmountPerOrder[0] = 2e18;
            lendAmountPerOrder[1] = 2e18;
            lendAmountPerOrder[2] = 1e18;

            porcentageOfRatioPerLendOrder[0] = 10000;
            porcentageOfRatioPerLendOrder[1] = 10000;
            porcentageOfRatioPerLendOrder[2] = 10000;
            
            principles[0] = AERO;
            principles[1] = address(DAI);
            principles[2] = address(BUSD);
            
            indexForPrinciple_BorrowOrder[0] = 0;
            indexForPrinciple_BorrowOrder[1] = 1;
            indexForPrinciple_BorrowOrder[2] = 2;
            
            indexForCollateral_LendOrder[0] = 0;
            indexForCollateral_LendOrder[1] = 0;
            indexForCollateral_LendOrder[2] = 0;
            
            indexPrinciple_LendOrder[0] = 0;
            indexPrinciple_LendOrder[1] = 1;
            indexPrinciple_LendOrder[2] = 2;

            address loan = DebitaV3AggregatorContract.matchOffersV3(
                lendOrders,
                lendAmountPerOrder,
                porcentageOfRatioPerLendOrder,
                address(BorrowOrder),
                principles,
                indexForPrinciple_BorrowOrder,
                indexForCollateral_LendOrder,
                indexPrinciple_LendOrder
            );

            DebitaV3LoanContract = DebitaV3Loan(loan);
        }

        //# LAPSE SOME TIME TO CHANGE EPOCH
        vm.warp(block.timestamp + 15 days);

        //# LENDER 3 CLAIMS HIS INCENTIVES --> but fails
        {

            //# Params
            address[] memory principles = allDynamicData.getDynamicAddressArray(1);
            address[] memory tokenUsed = allDynamicData.getDynamicAddressArray(1);
            address[][] memory incentiveTokens = new address[][](1);

            principles[0] = address(BUSD);
            tokenUsed[0] = AERO;
            incentiveTokens[0] = tokenUsed;

            vm.startPrank(lender3);
            incentivesContract.claimIncentives(principles, incentiveTokens, 2);
            vm.stopPrank();

        }

    }

    function setOracles() internal {
        DebitaChainlink oracle = new DebitaChainlink(
            0xBCF85224fc0756B9Fa45aA7892530B47e10b6433,
            address(this)
        );
        DebitaPyth oracle2 = new DebitaPyth(address(0x0), address(0x0));
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle), true);
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle2), true);

        oracle.setPriceFeeds(AERO, 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0);
        // for simplicity keeping oracles same as AERO
        oracle.setPriceFeeds(address(DAI), 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0);
        oracle.setPriceFeeds(address(BUSD), 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0);

        debitaChainlink = address(oracle);
        debitaPythOracle = address(oracle2);
    }

```

Run the test using below command

`forge test --mp WaqasPoC.t.sol --mt test_poc_lendersAndBorrowersCanLooseIncentives --fork-url https://mainnet.base.org --fork-block-number 21151256 -vv`

```bash
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 16.27ms (4.36ms CPU time)

Ran 1 test suite in 667.22ms (16.27ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/WaqasPoC.t.sol:PoC
[FAIL: revert: No borrowed or lent amount] test_poc_lendersAndBorrowersCanLooseIncentives() (gas: 10050095)

Encountered a total of 1 failing tests, 0 tests succeeded
```

### Mitigation

[DebitaIncentives::updateFunds](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L306) function should be updated as below

```diff
function updateFunds(
        infoOfOffers[] memory informationOffers,
        address collateral,
        address[] memory lenders,
        address borrower
    ) public onlyAggregator {
        for (uint i = 0; i < lenders.length; i++) {
          ....
          if (!validPair) {
--           return;
++           continue;
          }
          ....
        }
}
```