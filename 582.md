Cheery Powder Boa

Medium

# DebitaIncentives.updateFunds(..) does not update funds correctly

### Summary

`DebitaIncentives.updateFunds(infoOfOffers[] memory informationOffers, address collateral, address[] memory lenders, address borrower)` is called by the aggregator whenever orders are matched to update funds for incentivized pairs. However, the `updateFunds()` function will return prematurely when a non-whitelisted pair is encountered and any subsequent whitelisted pairs will not be accounted for, leading to funds loss for the users involved.

### Root Cause

In `DebitaIncentives.updateFunds(..)`, incentivized pairs are checked:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L316
However, when a non-whitelisted pair is encountered, the function is exited prematurely, as subsequent pairs are skipped completely.

### Internal pre-conditions

N/A

### External pre-conditions

- User incentivizes token pair A/B
- Aggregator matches order where non-incentivized pair is ordered before the incentivized pair, e.g. pair[0] = A/C, pair[1] = A/B

### Attack Path

1. User incentivizes token pair
2. Borrowers and lenders create multiple orders respectively, aggregator matches orders where an incentivized pair comes after a non-incentivized pair (note: a malicious aggregator can reorder orders to intentionally block users from acquiring funds, if the malicious aggregator has incentives assigned he'll receive more rewards as other users are left out)
3. Incentives are not assigned correctly, leading to loss of funds

### Impact

- Users lose rewards permanently
- Malicious aggregator receives more rewards

### PoC

Note: to see why the PoC works, execute it with the verbose flag (-vvvv) and notice that no `UpdatedFunds(lenders[i], principle, collateral, borrower, _currentEpoch)` event is emitted. Inverting the pair orders will cause the event to be emitted correctly.

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";
import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";

contract testIncentivesAmongMultipleLoans is Test {
    veNFTEqualizer public receiptContract;
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    ERC20Mock public AEROContract;
    ERC20Mock public USDCContract;
    ERC20Mock public wETHContract;
    DLOImplementation public LendOrder;
    DLOImplementation public SecondLendOrder;
    DLOImplementation public ThirdLendOrder;

    address DebitaChainlinkOracle;
    address DebitaPythOracle;

    DLOImplementation public LendOrder1;
    DLOImplementation public LendOrder2;
    DBOImplementation public BorrowOrder;

    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;
    address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address wETH = 0x4200000000000000000000000000000000000006;
    address AEROFEED = 0x4EC5970fC728C5f65ba413992CD5fF6FD70fcfF0;
    address USDCFEED = 0x7e860098F58bBFC8648a4311b374B1D669a2bc6B;
    address WETHFEED = 0x71041dddad3595F9CEd3DcCFBe3D1F4b0a16Bb70;
    address borrower = address(0x02);
    address secondBorrower = address(0x03);
    address firstLender = address(this);
    address secondLender = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
    address thirdLender = 0x25ABd53Ea07dc7762DE910f155B6cfbF3B99B296;
    address buyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
    address connector = 0x81B2c95353d69580875a7aFF5E8f018F1761b7D1;

    address feeAddress = address(this);

    uint receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = ERC20Mock(AERO);
        USDCContract = ERC20Mock(USDC);
        wETHContract = ERC20Mock(wETH);

        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(address(DebitaV3AggregatorContract));
        auctionFactoryDebitaContract.setAggregator(address(DebitaV3AggregatorContract));
        DLOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        DBOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));

        incentivesContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
        setOracles();
        incentivesContract.whitelListCollateral(AERO, AERO, true);
        incentivesContract.whitelListCollateral(AERO, USDC, true);
    }

    function incentivize(address _principle, address _collateral, address _incentiveToken, bool _isLend, uint _amount, uint epoch) internal {
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        address[] memory collateral = allDynamicData.getDynamicAddressArray(1);
        address[] memory incentiveToken = allDynamicData.getDynamicAddressArray(1);

        bool[] memory isLend = allDynamicData.getDynamicBoolArray(1);
        uint[] memory amount = allDynamicData.getDynamicUintArray(1);
        uint[] memory epochs = allDynamicData.getDynamicUintArray(1);

        principles[0] = _principle;
        collateral[0] = _collateral;
        incentiveToken[0] = _incentiveToken;
        isLend[0] = _isLend;
        amount[0] = _amount;
        epochs[0] = epoch;

        IERC20(_incentiveToken).approve(address(incentivesContract), 1000e18);
        deal(_incentiveToken, address(this), _amount, false);
        incentivesContract.incentivizePair(
            principles,
            incentiveToken,
            isLend,
            amount,
            epochs
        );
    }

    function setOracles() internal {
        DebitaChainlink oracle = new DebitaChainlink(0xBCF85224fc0756B9Fa45aA7892530B47e10b6433, address(this));
        DebitaPyth oracle2 = new DebitaPyth(address(0x0), address(0x0));
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle), true);
        DebitaV3AggregatorContract.setOracleEnabled(address(oracle2), true);

        oracle.setPriceFeeds(AERO, AEROFEED);
        oracle.setPriceFeeds(USDC, USDCFEED);
        oracle.setPriceFeeds(wETH, WETHFEED);

        DebitaChainlinkOracle = address(oracle);
        DebitaPythOracle = address(oracle2);
    }

    function testIncentivesNotCounted() public {
        incentivize(AERO, AERO, USDC, false, 1e18, 2);
        vm.warp(block.timestamp + 15 days);

        address alice = makeAddr("alice");
        address bob = makeAddr("bob");

        deal(AERO, alice, 1000e18, false);
        deal(AERO, bob, 1000e18, false);
        deal(USDC, alice, 1000e18, false);
        deal(USDC, bob, 1000e18, false);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(2);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(2);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(2);
        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(2);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(2);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        ratio[1] = 1e18;
        oraclesPrinciples[1] = address(0x0);
        acceptedPrinciples[1] = USDC;
        oraclesActivated[1] = false;
        ltvs[1] = 0;

        vm.startPrank(alice);
        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1000,
            86400, // duration
            acceptedPrinciples, // accepted principles / <-- index for principle / borrow order
            AERO,
            false, // is NFT
            0, // receipt ID
            oraclesPrinciples,
            ratio,
            address(0x0), // oracle collateral
            200e18
        );

        vm.stopPrank();

        vm.startPrank(bob);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
        IERC20(USDC).approve(address(DLOFactoryContract), 1000e18);

        address lendOrderAddress1 = DLOFactoryContract.createLendOrder(
            false, // perpetual
            oraclesActivated,
            false, // lonely
            ltvs,
            1000, // APR
            86400, // max duration (1 day)
            0,
            acceptedPrinciples, // accepted collaterals
            USDC, // principle
            oraclesPrinciples,
            ratio,
            address(0x0),
            100e18 // lend amount
        );

        address lendOrderAddress2 = DLOFactoryContract.createLendOrder(
            false, // perpetual
            oraclesActivated,
            false, // lonely
            ltvs,
            1000, // APR
            86400, // max duration (1 day)
            0,
            acceptedPrinciples, // accepted collaterals
            AERO, // principle
            oraclesPrinciples,
            ratio,
            address(0x0),
            100e18 // lend amount
        );

        vm.stopPrank();

        // match orders
        BorrowOrder = DBOImplementation(borrowOrderAddress);
        LendOrder1 = DLOImplementation(lendOrderAddress1);
        LendOrder2 = DLOImplementation(lendOrderAddress2);
        
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(2);
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(2);
        address[] memory principles = allDynamicData.getDynamicAddressArray(2);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(2);
        uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(2);
        uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(2);

        lendOrders[0] = lendOrderAddress1;
        lendAmountPerOrder[0] = 100e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = USDC;
        indexForPrinciple_BorrowOrder[0] = 1;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[1] = lendOrderAddress2;
        lendAmountPerOrder[1] = 100e18;
        porcentageOfRatioPerLendOrder[1] = 10000;
        principles[1] = AERO;
        indexForPrinciple_BorrowOrder[1] = 0;
        indexForCollateral_LendOrder[1] = 0;
        indexPrinciple_LendOrder[1] = 1;

        address loanAddress = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
        
    }

}
```

### Mitigation

Consider updating the `updateFunds(infoOfOffers[] memory informationOffers, address collateral, address[] memory lenders, address borrower)` function to not return completely, just skip the current iteration.