Curly Cyan Eel

High

# Lender calls `DebitaV3Loan::claimCollateralAsNFTLender` they will lose their collateral

### Summary

Due to incorrect checks in the contract, a lender can call `DebitaV3Loan::claimCollateralAsNFTLender` when a borrower has defaulted and the lender will be marked as `collateral paid` without transferring or crediting any collateral  to the lender.
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L340

### Root Cause

The root cause is the return value from the following function is ignored:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L361


### Internal pre-conditions

1. The borrower's collateral must be an NFT
2. There must  at least two lenders in the loan.
3. The borrower defaults on its loan

### External pre-conditions

_No response_

### Attack Path

Once the internal pre-conditions have been met the lender will call `DebitaV3Loan::claimCollateralAsNFTLender`. The lender will not receive the NFT since there are multiple lenders, but the lender will have their offer as `collateralClaimed == true` even though they have been credited/received nothing.

### Impact

The lender will not be able to claim the borrower's collateral after defaulting on the loan.

### PoC

One of the import locations might have to be fixed.
Run the test with `forge test --mt test_lender_collateral_lost --fork-url https://mainnet.base.org --fork-block-number 21151256 -vvv`

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";

contract DebitaAggregatorTest is Test, DynamicData {
    VotingEscrow public ABIERC721Contract;
    veNFTEqualizer public receiptContract;
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    ERC20Mock public AEROContract;
    ERC20Mock public USDCContract;
    DLOImplementation public LendOrder;
    DLOImplementation public SecondLendOrder;

    DBOImplementation public BorrowOrder;

    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;
    address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address borrower = address(0x02);
    address firstLender = address(this);
    address secondLender = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
    address buyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;

    address feeAddress = address(this);

    uint256 receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        receiptContract = new veNFTEqualizer(veAERO, AERO);
        ABIERC721Contract = VotingEscrow(veAERO);
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = ERC20Mock(AERO);
        USDCContract = ERC20Mock(USDC);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(address(DebitaV3AggregatorContract));
        auctionFactoryDebitaContract.setAggregator(address(DebitaV3AggregatorContract));
        DLOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        DBOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));

        incentivesContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        DebitaV3AggregatorContract.setValidNFTCollateral(address(receiptContract), true);

        deal(AERO, firstLender, 1000e18, false);
        deal(AERO, secondLender, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);

        vm.startPrank(borrower);
        IERC20(AERO).approve(address(ABIERC721Contract), 100e18);
        uint256 id = ABIERC721Contract.createLock(10e18, 365 * 4 * 86400);
        ABIERC721Contract.approve(address(receiptContract), id);
        uint256[] memory nftID = allDynamicData.getDynamicUintArray(1);
        nftID[0] = id;
        receiptContract.deposit(nftID);

        receiptID = receiptContract.lastReceiptID();

        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = address(receiptContract);
        oraclesActivated[0] = false;
        ltvs[0] = 0;
        receiptContract.approve(address(DBOFactoryContract), receiptID);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            address(receiptContract),
            true,
            receiptID,
            oraclesPrinciples,
            ratio,
            address(0x0),
            1
        );
        vm.stopPrank();

        AEROContract.approve(address(DLOFactoryContract), 5e18);
        ratio[0] = 65e16;

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            8640000,
            86400,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );

        vm.startPrank(secondLender);
        AEROContract.approve(address(DLOFactoryContract), 5e18);
        ratio[0] = 4e17;
        address SecondlendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            500,
            9640000,
            86400,
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();
        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
        SecondLendOrder = DLOImplementation(SecondlendOrderAddress);
    }
    //forge test --mt test_lender_collateral_lost --fork-url https://mainnet.base.org --fork-block-number 21151256 -vvv
    ///////////////////////////////////////////////
    //          MAIN TEST                        //
    ///////////////////////////////////////////////

    function test_lender_collateral_lost() public {
        MatchOffers();
        uint256[] memory indexes = allDynamicData.getDynamicUintArray(2);
        indexes[0] = 0;
        indexes[1] = 1;

        vm.warp(block.timestamp + 8640000);

        vm.startPrank(secondLender);
        //the secondLender will be index 1 in the accepted offers
        DebitaV3LoanContract.claimCollateralAsLender(1);
        vm.stopPrank();

        //access the storage data where it says whether a lender has already claimed their collateral
        //bool isItClaimed = DebitaV3LoanContract.loanData(1).collateralClaimed;
        DebitaV3Loan.LoanData memory m_loadData = DebitaV3LoanContract.getLoanData();

        //the second lender will be index 1
        console.log(
            "The collateral was marked as claimed but not transferred", m_loadData._acceptedOffers[1].collateralClaimed
        );
    }

    function MatchOffers() internal {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
        uint256[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(2);
        uint256[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(2);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint256[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(2);
        uint256[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(2);
        uint256[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(2);

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 25e17;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        lendOrders[1] = address(SecondLendOrder);
        lendAmountPerOrder[1] = 25e17;
        porcentageOfRatioPerLendOrder[1] = 10000;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
    }

    function testIncentivesTwoLendersReceipt() public {
        address[] memory principles = allDynamicData.getDynamicAddressArray(2);
        address[] memory collateral = allDynamicData.getDynamicAddressArray(2);
        address[] memory incentiveToken = allDynamicData.getDynamicAddressArray(2);

        bool[] memory isLend = allDynamicData.getDynamicBoolArray(2);
        uint256[] memory amount = allDynamicData.getDynamicUintArray(2);
        uint256[] memory epochs = allDynamicData.getDynamicUintArray(2);

        principles[0] = AERO;
        collateral[0] = address(receiptContract);
        incentiveToken[0] = USDC;
        isLend[0] = true;
        amount[0] = 100e6;
        epochs[0] = 2;

        principles[1] = AERO;
        collateral[1] = address(receiptContract);
        incentiveToken[1] = USDC;
        isLend[1] = false;
        amount[1] = 100e6;
        epochs[1] = 2;

        address[] memory tokensUsedAsBribes = allDynamicData.getDynamicAddressArray(1);
        tokensUsedAsBribes[0] = USDC;

        incentivesContract.whitelListCollateral(AERO, address(receiptContract), true);
        deal(USDC, address(this), 1000e18, false);
        IERC20(USDC).approve(address(incentivesContract), 1000e18);
        incentivesContract.incentivizePair(principles, incentiveToken, isLend, amount, epochs);

        vm.warp(block.timestamp + 15 days);
        uint256 currentEpoch = incentivesContract.currentEpoch();
        address[][] memory tokensIncentives = new address[][](incentiveToken.length);

        tokensIncentives[0] = tokensUsedAsBribes;
        MatchOffers();
        vm.prank(firstLender);
        vm.expectRevert();
        incentivesContract.claimIncentives(principles, tokensIncentives, 2);
        vm.warp(block.timestamp + 30 days);
        DebitaV3Loan.LoanData memory loanData = DebitaV3LoanContract.getLoanData();

        uint256 porcentageOfLending = (loanData._acceptedOffers[0].principleAmount * 10000) / 50e17;
        uint256 porcentageOfLendingSecond = (loanData._acceptedOffers[1].principleAmount * 10000) / 50e17;

        uint256 incentivesFirstLender = (100e6 * porcentageOfLending) / 10000;
        uint256 incentivesSecondLender = (100e6 * porcentageOfLendingSecond) / 10000;

        uint256 balanceBeforeFirstLender = IERC20(USDC).balanceOf(firstLender);
        vm.prank(firstLender);
        incentivesContract.claimIncentives(principles, tokensIncentives, 2);
        uint256 balanceAfterFirstLender = IERC20(USDC).balanceOf(firstLender);

        uint256 balanceBeforeSecondLender = IERC20(USDC).balanceOf(secondLender);
        vm.prank(secondLender);
        incentivesContract.claimIncentives(principles, tokensIncentives, 2);
        uint256 balanceAfterSecondLender = IERC20(USDC).balanceOf(secondLender);

        uint256 balanceBeforeBorrower = IERC20(USDC).balanceOf(borrower);
        vm.prank(borrower);
        incentivesContract.claimIncentives(principles, tokensIncentives, 2);
        uint256 balanceAfterBorrower = IERC20(USDC).balanceOf(borrower);

        assertEq(balanceBeforeSecondLender + incentivesSecondLender, balanceAfterSecondLender);

        assertEq(balanceBeforeFirstLender + incentivesFirstLender, balanceAfterFirstLender);

        assertEq(balanceBeforeBorrower + 100e6, balanceAfterBorrower);
    }
}

```

### Mitigation

Make the following change here:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L361

```diff
-    claimCollateralAsNFTLender(index);
+   bool isClaimed = claimCollateralAsNFTLender(index);
+   require(isClaimed, "Collateral not claimed successfully");
```