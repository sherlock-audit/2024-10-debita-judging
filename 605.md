Expert Smoke Capybara

Medium

# Improper ownership minting will show the ownership of lender as borrower and vice versa.

### Summary

The `DebitaLoanOwnerships` contract is used for minting ownerships to lenders and borrowers respectively in order to show their ownership.
The contract uses an odd-even logic for the same, where odd `tokenId` will return a `tokenURI` specifically for lenders and even would return for borrowers.
```solidity
    function tokenURI(
        uint256 tokenId
    ) public view override returns (string memory) {
        require(tokenId <= id, "Token Id does not exist");
        IDebitaAggregator _debita = IDebitaAggregator(DebitaContract);
        address loanAddress = _debita.getAddressById(tokenId);
        string memory _type = tokenId % 2 == 0 ? "Borrower" : "Lender";    <@ - Odd for lender and even for borrower.
```
The issue lies with the way we use the [`DebitaLoanOwnerships::mint`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLoanOwnerships.sol#L34) function in the [`DebitaV3Aggregator::matchOffersV3`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L274)
```solidity
function matchOffersV3(
        address[] memory lendOrders,    <@ - // Multiple orders can be passed here
        uint[] memory lendAmountPerOrder,
        uint[] memory porcentageOfRatioPerLendOrder,
        address borrowOrder,
        address[] memory principles,
        uint[] memory indexForPrinciple_BorrowOrder,
        uint[] memory indexForCollateral_LendOrder,
        uint[] memory indexPrinciple_LendOrder
    ) external nonReentrant returns (address) {
// ... Rest of the code...
         for (uint i = 0; i < lendOrders.length; i++) {
            
// ... Rest of the code...

            // mint ownership for the lender
            uint lendID = IOwnerships(s_OwnershipContract).mint(lendInfo.owner);  <@ - // Lender minting happens in a for loop without considering `tokenId % 2 == 1` has to be sufficed.
            offers[i] = DebitaV3Loan.infoOfOffers({
                principle: lendInfo.principle,
                lendOffer: lendOrders[i],
                principleAmount: lendAmountPerOrder[i],
                lenderID: lendID,
                apr: lendInfo.apr,
                ratio: ratio,
                collateralUsed: userUsedCollateral,
                maxDeadline: lendInfo.maxDuration + block.timestamp,
                paid: false,
                collateralClaimed: false,
                debtClaimed: false,
                interestToClaim: 0,
                interestPaid: 0
            });
            getLoanIdByOwnershipID[lendID] = loanID;
            lenders[i] = lendInfo.owner;
            DLOImplementation(lendOrders[i]).acceptLendingOffer(
                lendAmountPerOrder[i]
            );
        }  // loop exited
        // ...Rest of the code...
       uint borrowID = IOwnerships(s_OwnershipContract).mint(borrowInfo.owner);  <@ - // Minting happens without considering `tokenId % 2 == 0` has to be sufficed.
       // ...Rest of the code...
```
Due to the loop, when the number of lend orders are greater than 1, it would mint incorrect ownerships to the even numbered order.
Similarly, in case of even number of lend orders (greater than 1), would mint incorrect ownership for borrower.
This is clearly not an intended flow, hence would affect the user's ownership and off-chain data.

### Root Cause

In [`DebitaAggregatorV3.sol:502`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L502) where minting happens inside a for loop without considering the logic mentioned on [`DebitaLoanOwnerships.sol:84`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLoanOwnerships.sol#L84)
```string memory _type = tokenId % 2 == 0 ? "Borrower" : "Lender";```

Similarly, in [`DebitaAggregatorV3.sol:577`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L577) minting takes place without considering the above logic.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Anyone willing to match orders for borrowers and lenders need to call the [`matchOffersV3`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L274) with **more than 1** lend order.

### Impact

1. Incorrect minting of token would happen as the tokenURI will return metadata as a borrower to a lender and vice versa.
2. The function [`buildImage`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLoanOwnerships.sol#L53) will return borrower's image url to a lender and vice versa.
3. This would confuse users and the off-chain mechanism which would show this data to the users. 

### PoC

The below test case was added in `MultiplePrinciples.t.sol`
```solidity
    function testIncorrectOwnershipMint() public {
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        address[] memory collateral = allDynamicData.getDynamicAddressArray(1);
        address[] memory incentiveToken = allDynamicData.getDynamicAddressArray(
            1
        );

        bool[] memory isLend = allDynamicData.getDynamicBoolArray(1);
        uint[] memory amount = allDynamicData.getDynamicUintArray(1);
        uint[] memory epochs = allDynamicData.getDynamicUintArray(1);

        principles[0] = AERO;
        collateral[0] = USDC;
        incentiveToken[0] = AERO;
        isLend[0] = true;
        amount[0] = 100e18;
        epochs[0] = 2;
        deal(AERO, address(this), 10000e18);
        incentivesContract.whitelListCollateral(AERO, USDC, true);

        IERC20(AERO).approve(address(incentivesContract), 1000e18);
        incentivesContract.incentivizePair(
            principles,
            incentiveToken,
            isLend,
            amount,
            epochs
        );
        vm.warp(block.timestamp + 15 days);
        matchOffers(); // Matches 3 lenders with a borrow order

        // Starts from 1
        address ownerFirst = ownershipsContract.ownerOf(1);
        address ownerSecond = ownershipsContract.ownerOf(2);
        address ownerThird = ownershipsContract.ownerOf(3);
        address ownerFourth = ownershipsContract.ownerOf(4);

        // The DebitaLoanOwnerships.sol mentions that the owner of the token is the borrower if the index is even and the lender if the index is odd
        assertEq(ownerFirst, firstLender); // 1 % 2 == 0 ? "Borrower" : "Lender" == "Lender"
        assertEq(ownerSecond, secondLender); // 2 % 2 == 0 ? "Borrower" : "Lender" == "Borrower" <- Incorrectly assigned borrower's tokenURI (type and buildImage will be incorrect as well) 
        assertEq(ownerThird, thirdLender); // 3 % 2 == 0 ? "Borrower" : "Lender" == "Lender"
        assertEq(ownerFourth, borrower); // 4 % 2 == 0 ? "Borrower" : "Lender" == "Borrower"

}
```

This clearly shows a lender getting borrower's  NFT minted where tokenURI would return borrower's metadata.

### Mitigation

It is recommended to pass `_tokenType` directly in the parameter of mint and maintain a mapping for the same.