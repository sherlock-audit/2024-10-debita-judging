Expert Smoke Capybara

Medium

# Auction creation with same `initAmount` and `floorAmount` denies user from editing floor.

### Summary

The [`AuctionFactory::createAuction`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L68) function can be used by anyone to create an auction.
The issue is this function allows users to pass same `_initAmount` and `_floorAmount`, even though the code documentation suggests "initAmount should be more than floorAmount".
```solidity
function createAuction(
        uint _veNFTID,
        address _veNFTAddress,
        address liquidationToken,
        uint _initAmount,
        uint _floorAmount,
        uint _duration
    ) public returns (address) {
        // check if aggregator is set
        require(aggregator != address(0), "Aggregator not set");
initAmount should be more than floorAmount
        // initAmount should be more than floorAmount   <@ - // Mentioned comment
        require(_initAmount >= _floorAmount, "Invalid amount");  <@ - // Allows passing `_initAmount == _floorAmount`
        DutchAuction_veNFT _createdAuction = new DutchAuction_veNFT(
            _veNFTID,
            _veNFTAddress,
    // ... rest of the code ...
```

Furthermore, this will initialise the `tickPerBlock` as 0.
```solidity
    constructor(
        uint _veNFTID,
        address _veNFTAddress,
        address sellingToken,
        address owner,
        uint _initAmount,
        uint _floorAmount,
        uint _duration,
        bool _isLiquidation
    ) {
        // ... Rest of code ...
        s_CurrentAuction = dutchAuction_INFO({
            auctionAddress: address(this),
            nftAddress: _veNFTAddress,
            nftCollateralID: _veNFTID,
            sellingToken: sellingToken,
            owner: owner,
            initAmount: curedInitAmount,
            floorAmount: curedFloorAmount,
            duration: _duration, //@audit - can we pass duration as 0
            endBlock: block.timestamp + _duration,
            tickPerBlock: (curedInitAmount - curedFloorAmount) / _duration, <@ - // (curedInitAmount - curedFloorAmount) == 0
            isActive: true,
            initialBlock: block.timestamp,
            isLiquidation: _isLiquidation,
            differenceDecimals: difference
        });
      // .... Rest of code ....
    }
```

Now, if the user tries to edit the floor price using [`Auction::editFloorPrice`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/Auction.sol#L192) function, it will revert due to division by 0.
```solidity
    function editFloorPrice(
        uint newFloorAmount
    ) public onlyActiveAuction onlyOwner {
        uint curedNewFloorAmount = newFloorAmount *
            (10 ** s_CurrentAuction.differenceDecimals);
        require(
            s_CurrentAuction.floorAmount > curedNewFloorAmount,
            "New floor lower"
        );

        dutchAuction_INFO memory m_currentAuction = s_CurrentAuction;
        uint newDuration = (m_currentAuction.initAmount - curedNewFloorAmount) /
            m_currentAuction.tickPerBlock;    <@ - // This will panic revert due to division by 0

        uint discountedTime = (m_currentAuction.initAmount -
            m_currentAuction.floorAmount) / m_currentAuction.tickPerBlock;   <@ - // This will panic revert due to division by 0

```
Hence, the mentioned comment in the doc is actually correct way to implement in this case as even if this would've not reverted due to division by zero, the meaning of `dutch auction` would've lost anyways as the floor would've dropped directly to the new floor instead due to `tickPerBlock` being 0 even after editing.


### Root Cause

In [`AuctionFactory.sol:80`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L80), the require check allows `_initAmount == _floorAmount` to pass through, which is contradictory to the code documentation.


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User comes and creates an auction using the [`AuctionFactory::createAuction`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L68) function where params `_initAmount` and `_floorAmount` are equal.
2. Same user tries to edit the floor using [`Auction::editFloorPrice`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/Auction.sol#L192) which will revert due to division by 0.

### Impact

1. User will be denied from editing the floor price.
2. Code documentation, which is correct in this case is not followed.

### PoC

The below test was added to a new file created by name `AuctionTest.t.sol`, added in the `Auctions` folder
```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";
import {auctionFactoryDebita, DutchAuction_veNFT} from "@contracts/auctions/AuctionFactory.sol";

// DutchAuction_veNFT
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";

contract AuctionTest is Test {
    VotingEscrow public ABIERC721Contract;
    auctionFactoryDebita public factory;
    DutchAuction_veNFT public auction;
    DebitaV3Aggregator public DebitaV3AggregatorContract;

    address signer = 0x5F35576Ae82553209224d85Bbe9657565ab16a4f;
    address secondSigner = 0x81B2c95353d69580875a7aFF5E8f018F1761b7D1;
    address buyer = address(0x02);
    address veAERO = 0xeBf418Fe2512e7E6bd9b87a8F0f294aCDC67e6B4;
    address AERO = 0x940181a94A35A4569E4529A3CDfB74e38FD98631;
    address USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;

    function setUp() public {
        deal(AERO, signer, 100e18, false);
        deal(AERO, buyer, 100e18, false);
        factory = new auctionFactoryDebita();
        ABIERC721Contract = VotingEscrow(veAERO);

        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(0x0),
            address(0x0),
            address(0x0),
            address(0x0),
            address(0x0),
            address(0x0)
        );
        factory.setAggregator(address(DebitaV3AggregatorContract));
        vm.startPrank(signer);

        ERC20Mock(AERO).approve(address(ABIERC721Contract), 1000e18);
        uint id = ABIERC721Contract.createLock(100e18, 365 * 4 * 86400);
        ABIERC721Contract.approve(address(factory), id);
        address _auction = factory.createAuction(
            id,
            veAERO,
            AERO,
            10e18,
            10e18,
            86400
        );
        auction = DutchAuction_veNFT(_auction);
        vm.stopPrank();
    }


    function testEditFloorPriceNow() public {
        // edit floor price
        vm.startPrank(signer);
        vm.expectRevert(); // [FAIL: panic: division or modulo by zero (0x12)]
        auction.editFloorPrice(5e18);
        vm.stopPrank();
    }

}
```
This showcases the revert due to division by zero.

### Mitigation

It is recommended to replace `>=` with `>` in `createAuction`:
```diff
    function createAuction(
        uint _veNFTID,
        address _veNFTAddress,
        address liquidationToken,
        uint _initAmount,
        uint _floorAmount,
        uint _duration
    ) public returns (address) {
        // check if aggregator is set
        require(aggregator != address(0), "Aggregator not set");

        // initAmount should be more than floorAmount
-       require(_initAmount >= _floorAmount, "Invalid amount");
+       require(_initAmount > _floorAmount, "Invalid amount");
        
        DutchAuction_veNFT _createdAuction = new DutchAuction_veNFT(
      // .... Rest of the code .....  
 }
```