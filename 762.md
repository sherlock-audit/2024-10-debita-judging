Brisk Cobalt Skunk

High

# `TaxTokensReceipt` used as collateral always leads to loss of funds due to custom `transferFrom()` limitations

### Summary

The `TaxTokensReceipt` ERC721 overrides the default `transferFrom()` function to ensure that the NFT can be transferred only between Debita's contracts:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L98-L106
It fails to cover all possibilities of valid `to` and `from` addresses still inside Debita's ecosystem.

### Root Cause

Custom validation of the `to` and `from` addresses fails to cover the transfer from `Auction` contract to NFT seller, which happens after full or partial liquidation.
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L148-L153
That's the receipt NFT transfer, where `nftAddress` is the `TaxTokensContract` address.


### Internal pre-conditions

- loan is created where `TaxTokensReceipt` NFT is used as collateral


### External pre-conditions

--

### Attack Path

--

### Impact

Loss of funds for either borrower or lender depending on conditions due to collateral NFT being stuck in an auction. 

1. One lend order:
For this scenario this has an impact when the loan is not repaid and the borrower maliciously initializes an auction making it impossible for the lender to withdraw ERC721 itself. Borrower *can* withdraw the NFT after an honest loan ends.

2. Multiple lend orders:
In that case the issue will lock both parties unless the loan is repaid in full - because then the borrower can also withdraw the NFT without utilising the auction.


### PoC

To create a PoC entire setup for `TaxTokensContract`-based loan that was initially made for a more complex finding was utilized. This helps to simulate the actual transfer failure in practice during the `claimCollateralAsLender()` call rather than PoCing simple transfer to an unsupported address. The code from the dropdown below shows the case with one lend order where the borrower does not repay the loan.

<details>
<summary>PoC</summary>
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/veNFTAerodrome.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../../interfaces/getDynamicData.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

// import ERC20
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract TestTaxTokensReceipts is Test {
    DBOFactory public dbFactory;
    DLOFactory public dlFactory;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public aggregator;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    // simulating a taxable token --> address(token)
    // taxable token has to extent TaxTokensReceipts interface

    address lender = 0x1000000000000000000000000000000000000001;
    address borrower = 0x2000000000000000000000000000000000000002;
    FeeOnTransfer token;
    ERC20Mock principle;

    TaxTokensReceipts public receiptContract;
    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        dbFactory = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        dlFactory = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();

        token = new FeeOnTransfer();
        principle = new ERC20Mock();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
  
        aggregator = new DebitaV3Aggregator(
            address(dlFactory),
            address(dbFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );
receiptContract = new TaxTokensReceipts(
            address(token),
            address(dbFactory),
            address(dlFactory),
            address(aggregator)
        );
         ownershipsContract.setDebitaContract(
            address(aggregator)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(aggregator)
        );
        dlFactory.setAggregatorContract(
            address(aggregator)
        );
        dbFactory.setAggregatorContract(
            address(aggregator)
        );

        incentivesContract.setAggregatorContract(
            address(aggregator)
        );
        aggregator.setValidNFTCollateral(
            address(receiptContract),
            true
        );
      
        token.exemptFromTax(address(receiptContract));
    
        deal(address(token), borrower, 100e18, true);
    }

    function test_taxTokensTransferFailure() public {
        vm.startPrank(borrower);
        token.approve(address(receiptContract), 1000e18);
        uint tokenID = receiptContract.deposit(100e18);
        assertEq(receiptContract.balanceOf(borrower), 1);
        createBorrowOrder(
            1e18,
            4000,
            tokenID,
            864000,
            1,
            address(principle),
            address(receiptContract),
            borrower
        );
        vm.stopPrank();

        vm.startPrank(lender);
        createLendOrder(
            1e18,
            4000,
            864000,
            864000,
            100e18,
            address(principle),
            address(receiptContract),
            lender
        );
        vm.stopPrank();

        vm.startPrank(borrower);
        matchOffers();
        vm.stopPrank();

        vm.warp(block.timestamp + 12 days);
        vm.prank(lender); // lender liquidates
        DebitaV3LoanContract.createAuctionForCollateral(0);
            DutchAuction_veNFT auction = DutchAuction_veNFT(
            DebitaV3LoanContract.getAuctionData().auctionAddress
        );
        DutchAuction_veNFT.dutchAuction_INFO memory auctionData = auction
            .getAuctionData();

        address nftBuyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;

        deal(address(token), nftBuyer, 100e18);
        vm.startPrank(nftBuyer);

        FeeOnTransfer(address(token)).approve(address(auction), 100e18);
        vm.expectRevert(bytes("TaxTokensReceipts: Debita not involved"));
        auction.buyNFT();
        vm.stopPrank();

        vm.prank(lender); 
        vm.expectRevert(bytes("Not sold on auction"));
        DebitaV3LoanContract.claimCollateralAsLender(0);

    }

    function createLendOrder(
        uint _ratio,
        uint maxInterest,
        uint minTime,
        uint maxTime,
        uint amountPrinciple,
        address principle,
        address collateral,
        address lender
    ) internal returns (address) {
        deal(principle, lender, amountPrinciple, false);
        IERC20(principle).approve(address(dlFactory), 1000e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address lendOrderAddress = dlFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            maxInterest,
            maxTime,
            minTime,
            acceptedCollaterals,
            principle,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountPrinciple
        );
        LendOrder = DLOImplementation(lendOrderAddress);
        return lendOrderAddress;
    }
    function createBorrowOrder(
        uint _ratio,
        uint maxInterest,
        uint tokenId,
        uint time,
        uint amountCollateral,
        address principle,
        address collateral,
        address borrower
    ) internal {
        IERC721(collateral).approve(address(dbFactory), tokenId);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = principle;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address borrowOrderAddress = dbFactory.createBorrowOrder(
            oraclesActivated,
            ltvs,
            maxInterest,
            time,
            acceptedPrinciples,
            collateral,
            true,
            tokenId,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountCollateral
        );
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function matchOffers() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 100e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = address(principle);

        // 0.1e18 --> 1e18 collateral

        address loan = aggregator.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
    }
}


contract FeeOnTransfer is ERC20{
    uint feeBp = 1000; // 10%
    mapping (address=>bool) public isNotTaxed;

      constructor() ERC20("ERC20FoT", "E20FoT") {}

    function mint(address account, uint256 amount) external {
        _mint(account, amount);
    }

    function burn(address account, uint256 amount) external {
        _burn(account, amount);
    }

    function exemptFromTax(address toExempt) external {
        isNotTaxed[toExempt] = true;
    }

    function transferFrom(address from, address to, uint256 value) public override returns (bool) {
        address spender = _msgSender();
        uint amount = value * (10000-feeBp) / 10000; 
        if (isNotTaxed[to]) {
            amount = value;
        }
        super._spendAllowance(from, spender, amount);
        super._transfer(from, to, amount);
        return true;
    }
}
</details>

Run it with:
```shell
forge test --fork-url https://mainnet.base.org --fork-block-number 21151256  --mt test_taxTokensTransferFailure -vvvvv
```


### Mitigation

Adding support to `Auction` contract similarly to other Debita's contracts is not an option because a new one is deployed for each liquidation. Consider adding some sort of mapping indicating the transfer is from the `Auction` contract and add a permissioned function to flag the new auction as allowed after deployment in `AuctionFactory`.
