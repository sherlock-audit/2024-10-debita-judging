Digital Hazelnut Kangaroo

Medium

# A lending offer can be canceled repeatedly.

### Summary

A lender can cancel his lending offer multiple times by repeatedly invoking `cancelOffer` and `addFunds`. In `cancelOffer`, if the `availableAmount > 0`, the lender can cancel the offer (`DebitaLendOffer-Implementation.sol:148`). It does not check the `isActive` flag.
```solidity
    function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
148:    require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
157:    IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144-L159

However, the lender can call `addFunds` to increase the `availableAmount`. So, the lender can re-cancel a canceled offer by first calling `addFunds`.
```solidity
    function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
174:    lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162-L176

In `DebitaLendOfferFactory.sol`, the index of orders starts from 0. `deleteOrder` sets the deleted order's index to 0 and moves the last index in the order array to the position of the deleted order. If the same `_lendOrder` is reused for `deleteOrder`, it will delete some other order.
```solidity
    function deleteOrder(address _lendOrder) external onlyLendOrder {
        uint index = LendOrderIndex[_lendOrder];
209:    LendOrderIndex[_lendOrder] = 0;

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);

        activeOrdersCount--;
    }
```

Therefore, a malicious lender can repeatedly cancel his lending offer to remove all other lending orders.

### Root Cause

The `isActive` flag is not checked in `cancelOffer`, making a lender offer can be repeatedly canceled.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. We assume that 5 lending orders are created in `DebitaLendOfferFactory.sol`.
2. Alice creates a lending offer.
3. Alice cancels her lending offer (`cancelOffer`).
4. Alice add fund to her lending offer (`addFunds`).
5. Alice re-cancel her lending offer (`cancelOffer`).
6. Now there are 4 lending orders in `DebitaLendOfferFactory.sol`.

### Impact

A malicious lender can repeatedly cancel his lending offer to remove all other lending orders.

### PoC

_No response_

### Mitigation

In function `cancelOffer`, check the `isActive` flag, and cancel the offer only when `isActive` is set to `true`.