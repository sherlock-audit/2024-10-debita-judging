Happy Rouge Coyote

High

# Combination of two protocol issues will result a lender to lose all his funds in certain situations

### Summary

Debita protocol has two logic mistakes that if malicious user wants to freeze all the lenders funds and prevent them from withdrawing will be able to do it by combining these mistakes. This vulnerability is only for those lenders who lend their ERC20 tokens that lacks `.decimals()` funciton.

### Root Cause

In [`DebitaV3Aggregator`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L167) There are three places where `.decimals()` is used:


[DebitaV3Aggregator::L348](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L348):

```solidity
uint principleDecimals = ERC20(principles[i]).decimals();
```

[DebitaV3Aggregator::L371](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L371):

```solidity
uint decimalsCollateral = ERC20(borrowInfo.valuableAsset).decimals();
```

[DebitaV3Aggreagor::L453](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L455-L456):

```solidity
uint principleDecimals = ERC20(principles[principleIndex]).decimals();
```

This issue leads to problem where the Borrow offers or Lend offers that uses ERC20 without `.decimals()` function will not be able to be matched by anyone since the aggregator contracts expects both of them to implement it. 

This means that the only way for lender to withdraw his funds is to call [`cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) function which transfers all the available amount back to the lender. BUT there is another problem with the factory contract where this will be impossible for any lender to cancel and withdraw his funds.

The second bug:

Debita has factory contract for lend offer, implementation of this contract contains a function for canceling the offer. A malicious user can halt the process of cancelation for everyone such that no lender can cancel his offer.

In [`DebitaLendOffer-Implementation::144`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144) The function is to be called whenever the lender wants to cancel his order. This function also calls the `DebitaLendOfferFactory` contract's [`deleteOrder`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207) function that indexes the changes on mappings and decrements the `activeOrdersCount`.

Since the protocol is using solidity version `^0.8.0` thus it protects against Integer Underflow/Overflow. If the `activeOrdersCount` is `0` and there is attempt to `cancel` an order it will revert.

The problem is there that the implementation's `cancelOffer` can be called as many times as the malicious user wants, leading to decrementing the `activeOrdersCount` to `0` and every next attempt to cancel will revert resulting to DOS.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

Assume there are 100 Lender orders already created and Alice comes:
*(which are using tokens without `.decimals()` function, for example [CloutContracts](https://etherscan.io/address/0x1da4858ad385cc377165a298cc2ce3fce0c5fd31#readContract), [DigixDAO](https://etherscan.io/address/0xe0b7927c4af23765cb51314a0e0521a9645f0e2a#readContract), etc)*

1. Alice creates her own Lend order.
2. Alice calls `cancelOffer` of her Lend order.
3. Alice calls `addFunds` in order to increment the `availableAmount` and pass the requirement check of `cancelOffer`
4. Alice repeats `step 2` and `step 3` 101 times decrementing the `activeOrdersCount` to `0`


Now no one can cancel his lend order, unless there is new created one and the `activeOrdersCount` is greater than `0`, eliminating this, the only way to process the order is through the aggregator contract if someone matches the orders, but we discussed that for every token that lacks `.decimals()` this will revert.

### Impact

Lenders who lend their ERC20 tokens, which does not contain `.decimals()` function will loose their tokens forever, if the malicious user tracks the fired events and repeats the attack path for every new lend order.

### PoC

According to the [Official EIP20 Documentation](https://eips.ethereum.org/EIPS/eip-20#decimals) the function `.decimals()` is OPTIONAL. And its not a part from the standard.

The second bug's POC:

```solidity
    function testDeleteLendOrder() public {
        vm.startPrank(lender);
        IERC20(AERO).approve(lendersOrder, type(uint256).max);

        // There are 10 lend orders, 1 from lender and 9 from other lenders
        // When the lender repeats this 10 times the activeLendOrders will be 0
        for(uint256 i; i < 10; i++) {
            DLOImplementation(lendersOrder).cancelOffer();
            DLOImplementation(lendersOrder).addFunds(1);
        }

        // Expect revert because of integer underflow
        vm.expectRevert();
        DLOImplementation(lendersOrder).cancelOffer();
        vm.stopPrank();

    }
```

```plain
Ran 1 test for test/local/LendOfferFactory/LendOfferFactory.t.sol:LendOfferFactoryTest
[PASS] testDeleteLendOrder() (gas: 679370)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.45ms (2.39ms CPU time)
```

### Mitigation

Recommend using a tryCatch block to query the decimals. If it fails, hardcode it to 18 for scaling and make sure that `isActive` is set to `true` whenever calling the `addFunds` function.

```diff
    function addFunds(uint amount) public nonReentrant {
+       require(isActive, "...");
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```

In the `DebitaLendOfferFactory` contract set `isLendOrderLegit[msg.sender]` to `false` when the order is deleted

```diff
    function deleteOrder(address _lendOrder) external onlyLendOrder {
        uint index = LendOrderIndex[_lendOrder];
        LendOrderIndex[_lendOrder] = 0;

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);
+       isLendOrderLegit[msg.sender] = false;

        activeOrdersCount--;
    }
```