Sneaky Leather Seal

High

# An attacker can delete all lending order leading to a permanent loss of funds and other adverse effects

### Summary

A malicious lender  can delete all lending offers from the `DebitaLendOfferFactory.sol` contract  which will result in permanent loss of funds for all affected users . Although a lender does not neccessarily need to be malicious for this unfortunate occurence to happen. It could also be as a result of a lender making changes to their `perpetual` status.

### Root Cause

This exploit is as a result of two vulnerabilities in two different contracts.
* The first vulnerability can be found in the `DLOFactory::deleteOrder` function.
Let us carefully analyze the [function](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207);

```solidity
    function deleteOrder(address _lendOrder) external onlyLendOrder {
        //@audit >> missing state update isLendOrderLegit[_lendOrder] = false
        uint index = LendOrderIndex[_lendOrder];
        //@audit >> the first lender in the factory has been previously set to 0
        //@audit >> this update below sets another lender to index 0
        LendOrderIndex[_lendOrder] = 0;

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);

        activeOrdersCount--;
    }
```
The function carries a modifier [onlyLendOrder](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L102), which ensures that the lendorderImplementation calling the function is legit. After a successful deletion of the order, the function does not update the mapping for the deleted order, to ensure that it is no longer legit. Consequently, the check will always pass if the caller calls it again. This leaves a chance for a replay.
* The second vulnerability can be found in the `DLOImplementation::changePerpetual` [function](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L178).

It is important to understand how the protocol works to completely understand this vulnerability. In the `DLOImplementation` contract the `DLOFactory::deleteOrder` will only be called for 3 reasons as listed below:

1.  It is called within the [`acceptLendingOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L133) function, when the lend offer is not perpetual, and the  lender's offer has been completely accepted such that there is no amount available to lend to users [here](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L128). In this case, it can only be called once, beacuse a second attempt to call `acceptLendingOffer` will revert due to an attempt to subtract a nonzero amount from the available amout which is currently zero(underflow errors will occur).
2.  It is called within the [`cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144C14-L144C25) function. In this case when the user wishes to cancel their offer, the function sends all the available amount out to the user, `lendInformation.availableAmount` is set to 0, and the [deleteOrder](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L157) is called. There are checks in place that ensures that the function  can't be called multiple times.
3.  It is called within the [`changePerpetual`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L184) function. in this function [there is a requirement that the lend order is currently active](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L179C17-L179C25). However on deleting the lendorder, the bool `isActive` is not updated. This case now leaves a chance for a user to call the `changePerpetual` multiple times(with an argument of `false`), hence accessing the deleteOrder function multiple times.
```solidity
    function changePerpetual(bool _perpetual) public onlyOwner nonReentrant {
        require(isActive, "Offer is not active"); 
        lendInformation.perpetual = _perpetual;
        if (_perpetual == false && lendInformation.availableAmount == 0) {
            IDLOFactory(factoryContract).emitDelete(address(this));
            IDLOFactory(factoryContract).deleteOrder(address(this));
            //@audit>> missing state update
            // isActive = false
        } else {
            IDLOFactory(factoryContract).emitUpdate(address(this));
        }
    }
```



### Internal pre-conditions

1. There must be more than one user with active lend offers in the protocol 

### External pre-conditions

1. The attacker has to have their available amount totally loaned out(This is easily achievable since the attacker can match their offer to any borrow offer that fits it perfectly or create a fake borrow order that will match their malicious lendoffer)
2. The attacker must initially activate perpetual lending on their contract

### Attack Path

Since it has been establish in the root cause section how a malicious user can repeatedly call `DLOFactory::deleteOrder` by calling `changePerpetual` repeatedly.  this section will focus on how repeated calls by a malicious user will delete all the existing orders from list of pending order. Each step demonstrate the state changes as the deleteoffer is called repeatedly by the user.

Step 1: Creation of Orders

* User A creates `OrderA`.
* User B creates `OrderB`.
* User C creates `OrderC.`
* User D creates `OrderD`.
Initial State:

`allActiveLendOrders` = [OrderA, OrderB, OrderC, OrderD]
`LendOrderIndex` = {OrderA: 0, OrderB: 1, OrderC: 2, OrderD: 3}
`activeOrdersCount` = 4

Step 2: User C Calls `deleteOrder(OrderC)`

Index of `OrderC` is 2.
`LendOrderIndex[OrderC]` is set to 0 
The last order `(OrderD)` is moved to index 2
```solidity
allActiveLendOrders[2] = allActiveLendOrders[3]; // allActiveLendOrders[2] = OrderD
LendOrderIndex[OrderD] = 2;
```
The last index (3) is set to address(0):
```solidity
allActiveLendOrders[3] = address(0);
```
`activeOrdersCount` is decremented
```solidity
activeOrdersCount = 3;
```

Updated State:

`allActiveLendOrders` = [OrderA, OrderB, OrderD, address(0)]
`LendOrderIndex` = {OrderA: 0, OrderB: 1, OrderC: 0, OrderD: 2}
`activeOrdersCount` = 3

Step 3: User C Calls `deleteOrder(OrderC)` Again

Index of `OrderC `is `0` (as set previously).
`LendOrderIndex[OrderC]` is set to 0 again
The last active order `(OrderD)` is moved to index 0:
```solidity
allActiveLendOrders[0] = allActiveLendOrders[2]; // allActiveLendOrders[0] = OrderD
LendOrderIndex[OrderD] = 0;
```
The last index (2) is set to address(0):
```solidity
allActiveLendOrders[2] = address(0);
```
`activeOrdersCount` is decremented:
```solidity
activeOrdersCount = 2;
```
Updated State:

`allActiveLendOrders` = [OrderD, OrderB, address(0), address(0)]
`LendOrderIndex` = {OrderA: 0, OrderB: 1, OrderC: 0, OrderD: 0}
`activeOrdersCount` = 2
Step 4: User C Calls deleteOrder(OrderC) Yet Again

Index of `OrderC` is 0 (as set previously).
`LendOrderIndex[OrderC]` is set to 0 again.
The last active order `(OrderB)` is moved to index 0
```solidity
allActiveLendOrders[0] = allActiveLendOrders[1]; // allActiveLendOrders[0] = OrderB
LendOrderIndex[OrderB] = 0;
```
The last index (1) is set to address(0):
```solidity
allActiveLendOrders[1] = address(0);
```
`activeOrdersCount` is decremented:
```solidity
activeOrdersCount = 1;
```
Updated State:

`allActiveLendOrders` = [OrderB, address(0), address(0), address(0)]
`LendOrderIndex` = {OrderA: 0, OrderB: 0, OrderC: 0, OrderD: 0}
`activeOrdersCount` = 1
Step 5: User C Calls deleteOrder(OrderC) Once More

Index of `OrderC` is 0 (as set previously).
`LendOrderIndex[OrderC]` is set to 0 again
There are no other active orders to move, so the index 0 is set to address(0):
```solidity
allActiveLendOrders[0] = address(0);
```
`activeOrdersCount` is decremented
```solidity
activeOrdersCount = 0;
```
Final State:
`allActiveLendOrders` = [address(0), address(0), address(0), address(0)]
`LendOrderIndex` = {OrderA: 0, OrderB: 0, OrderC: 0, OrderD: 0}
`activeOrdersCount` = 0

The attacker can use a bot to carry out this exploit ensuring that activeOrdersCount is always 0.

### Impact

The exploit will result in the following;
1. (In a case where the lender's token is not on high demand and they are not able to find borrowers with matching offer), lenders will not be able cancel their lending order, and they will permanently have their funds stuck in their `DLOImplementation` contract: 
This is possible because on calling [`cancelOffer`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L144C14-L144C25), as explained above, there will be an attempt to delete the user order(which will revert, since the attcker completely decreased activeOrderCount to 0)

2. Lenders will have their funds permanently stucked in the contract since there will be a call to `deleteOffer` when their total available amount has been completely borrowed

### PoC

_No response_

### Mitigation

```diff
    function deleteOrder(address _lendOrder) external onlyLendOrder {
+       isLendOrderLegit[_lendOrder] = false
        uint index = LendOrderIndex[_lendOrder];
        //@audit >> the first lender in the factory has been previously set to 0
        //@audit >> this update below sets another lender to index 0
        LendOrderIndex[_lendOrder] = 0;

        // switch index of the last borrow order to the deleted borrow order
        allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
        LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

        // take out last borrow order

        allActiveLendOrders[activeOrdersCount - 1] = address(0);

        activeOrdersCount--;
    }
```

```diff
    function changePerpetual(bool _perpetual) public onlyOwner nonReentrant {
        require(isActive, "Offer is not active"); 
        lendInformation.perpetual = _perpetual;
        if (_perpetual == false && lendInformation.availableAmount == 0) {
+           isActive = false
            IDLOFactory(factoryContract).emitDelete(address(this));
            IDLOFactory(factoryContract).deleteOrder(address(this));
        } else {
            IDLOFactory(factoryContract).emitUpdate(address(this));
        }
    }
```