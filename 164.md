Dandy Charcoal Bee

Medium

# TaxTokensRecepeit doesn't support FOT tokens

### Summary

The logic of TaxTokensRecepeit doesn't support FOT tokens at all, clearly breaking his intended functionality.

### Root Cause

We can identify the cause in [`TaxTokensRecepeit:deposit()#69`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L69) that checks that the actual amount received is greater or equal than the amount transferred.

```solidity
function deposit(uint amount) public nonReentrant returns (uint) {
    uint balanceBefore = ERC20(tokenAddress).balanceOf(address(this));
    SafeERC20.safeTransferFrom(
        ERC20(tokenAddress),
        msg.sender,
        address(this),
        amount
    );
    uint balanceAfter = ERC20(tokenAddress).balanceOf(address(this));
    uint difference = balanceAfter - balanceBefore;
    require(difference >= amount, "TaxTokensReceipts: deposit failed"); // <@
}
```

This condition will always fail for FOT tokens since they transfer **less than the amount specified**, due to the fee.

### Internal pre-conditions

1. A user wants to use a FOT token in the Debita protocol.

### External pre-conditions

_No response_

### Attack Path

We assume the TaxTokensReceipts for a FOT token with a 10% transfer tax has just been deployed:
1. Bob wants to deposit `1000e18` of such token
2. He then calls `deposit(1000e18)`
3. The contract will actually receive `900e18` tokens due to the 10% tax
    - `balanceBefore = 0`
    - `balanceAfter = 900e19`
    - `difference = 900e18`
4. The call will always revert since `900e18 < 1000e18`, making the `require` statement fail

### Impact

Broken functionality of the contract because it can't support FOT tokens, even if intended to (as stated in the README).

### PoC

Add the following test file in the `test/local` folder, it uses a simple FOT token with a 10% tax applied on his `transferFrom()`:

```solidity
pragma solidity ^0.8.0;

import {Test, console2} from "forge-std/Test.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract FOT is ERC20 {

    address immutable tax_address = 0xC0BB9960A0738eCdf2bfCCA9308E32afA84d8Dbf;
    uint256 immutable tax_bps = 1000;

    constructor() ERC20("FOT Example", "FOT") {
        _mint(msg.sender, 100000e18);
    }

    function transferFrom(address from, address to, uint256 value) public virtual override(ERC20) returns (bool) {
        uint256 tax = (value * tax_bps) / 10_000;
        address spender = _msgSender();
        _spendAllowance(from, spender, value);
        _transfer(from, to, value - tax); // <@ 10% tax applied here
        _transfer(from, tax_address, tax);
        return true;
    }

}

contract TaxTokenRecepeitTest is Test {

    FOT token;
    TaxTokensReceipts recepeit;

    function setUp() public {
        token = new FOT();
        recepeit = new TaxTokensReceipts(
            address(token),
            address(0),
            address(0),
            address(0)
        );
    }

    function test_deposit_recepeit_FOT_fails() public {
        token.approve(address(recepeit), 1000e18);
        vm.expectRevert();
        uint256 id = recepeit.deposit(1000e18);
    }

}
```

### Mitigation

1. remove the `require` statement pointed in the root cause section, since it will always hold false for FOT tokens
2. modify the accounting logic to credit `difference`, instead of `amount`, to the caller