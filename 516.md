Micro Sage Turkey

Medium

# The `TaxTokensReceipt` contract is incompatible with fee-on-transfer tokens

### Summary

Debita intends to interact with fee-on-transfer tokens but only within the `TaxTokensReceipt.sol` contract:

> Fee-on-transfer tokens will be used only in TaxTokensReceipt contract

This contract allows a user to `deposit()` tokens in the contract and get minted an NFT that can be used in the protocol as collateral (the collateral being the deposit amount).

<https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L59-L75>

```solidity
function deposit(uint amount) public nonReentrant returns (uint) {
    uint balanceBefore = ERC20(tokenAddress).balanceOf(address(this));
    SafeERC20.safeTransferFrom(
        ERC20(tokenAddress),
        msg.sender,
        address(this),
        amount
    );
    uint balanceAfter = ERC20(tokenAddress).balanceOf(address(this));
    uint difference = balanceAfter - balanceBefore;
    require(difference >= amount, "TaxTokensReceipts: deposit failed");
    tokenID++;
    tokenAmountPerID[tokenID] = amount;
    _mint(msg.sender, tokenID);
    emit Deposited(msg.sender, amount);
    return tokenID;
}
```

However, attempting to deposit fee-on-transfer tokens will always fail.

### Root Cause

The issue arises from a logical error in the handling of fee-on-transfer tokens.

The function starts by caching the contract token balance in `balanceBefore`.

Then it transfers the `amount` of tokens from the caller to the contract.

After that, it caches the new contract token balance in `balanceAfter` and calculates the exact amount of tokens received by the contract in `difference`.

Lastly, it verifies the `difference` is greater or equal to the `amount` attempted to be deposited.

Due to the nature of fee-on-transfer tokens, the amount received by the contract will always be less than the `amount` attempted to be deposited.

This means the `require(difference >= amount, "TaxTokensReceipts: deposit failed");` will always fail.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

Assume `LOL` token takes 1 token on each transfer and is the token used in `TaxTokensReceipt.sol`.

- User calls `deposit(100)` so `amount == 100`
- Contract `balance == 0` so `balanceBefore == 0`
- The transfer occurs and `LOL` collects 1 token from the `100` being transferred
- Contract receives `100 - 1` token so `balanceAfter == 99`
- `uint difference = balanceAfter - balanceBefore` so `difference == 99`
- The `require()` evaluates `difference >= amount` so `99 >= 100`
- The transaction reverts due to the requirement evaluating to `false` because `99 is not greater or equal to 100`


### Impact

The `TaxTokensReceipt` contract intends to be compatible with fee-on-transfer tokens but fails to do so.

### PoC

None

### Mitigation

The `deposit()` function should simply account for the actual amount of tokens received in its accounting.

One way to do so is the following:

```diff
function deposit(uint amount) public nonReentrant returns (uint) {
    uint balanceBefore = ERC20(tokenAddress).balanceOf(address(this));
    SafeERC20.safeTransferFrom(
        ERC20(tokenAddress),
        msg.sender,
        address(this),
        amount
    );
    uint balanceAfter = ERC20(tokenAddress).balanceOf(address(this));
    uint difference = balanceAfter - balanceBefore;
-   require(difference >= amount, "TaxTokensReceipts: deposit failed");
+   amount = difference;
    tokenID++;
    tokenAmountPerID[tokenID] = amount;
    _mint(msg.sender, tokenID);
    emit Deposited(msg.sender, amount);
    return tokenID;
}
```