Micro Sage Turkey

Medium

# Owner can't be changed due to variable shadowing

### Summary

There are 3 Debita contracts that feature a `changeOwner()` function, allowing the current contract owner to designate a new owner up to 6 hours after the contract has been deployed:
- [`buyOrderFactory.sol`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol#L186-L190)
- [`AuctionFactory.sol`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L218-L222)
- [`DebitaV3Aggregator.sol`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L682-L686)

```solidity
contract DebitaV3Aggregator is ReentrancyGuard {
    // ----- snip -----
    address public owner;

    // ----- snip -----
    function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```

The issue here is that the storage variable `owner` cannot be changed due to variable shadowing.

However, both the storage variable responsible for storing the address of the owner AND the `changeOwner()` parameter have the same name.




### Root Cause

The root cause of the issue arises from the fact that both the storage variable responsible for storing the address of the owner AND the `changeOwner()` parameter have the same name.

This is called shadowing, more can be [read here](https://solstep.gitbook.io/solidity-steps/step-3/27-the-shadowing-effect).

When called, the `changeOwner()` function creates its own scope, integrating the `owner` parameter to it.

All the operations done on `owner` within the `changeOwner()` function will refer to the `owner` local variable (the parameter).

This means `owner` will only be modified in the function and never affect the storage.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

The contracts owner cannot be changed after being deployed.

The `changeOwner()` function acts as a safeguard in case the `owner` has to be modified within 6 hours after the contracts have been deployed. It is safe to assume Debita will set the right `owner` at deploy time.
Likelihood: **LOW**

Since these contracts heavily rely on `owner` privileges to update core functionalities (fees, authorized oracles, authorized NFT collateral, pause the contract...) having the wrong `owner` at deploy time can be a long-term issue.
Impact: **HIGH**

Overall severity: **MEDIUM**

### PoC

The following PoC is using a simplified setup and `changeOwner()` function:

```solidity
pragma solidity ^0.8.20;

import "forge-std/Test.sol";

contract ShadowingTest is Test {
    function testShadowing() public {
        address user = makeAddr("user");

        vm.startPrank(user);
        OwnerShadowed ownerShadowed = new OwnerShadowed();

        assertEq(ownerShadowed.owner(), address(0));
        ownerShadowed.changeOwner(user);
        assertEq(ownerShadowed.owner(), address(0));
        
        vm.stopPrank();
    }
}

contract OwnerShadowed {
    address public owner;

    function changeOwner(address owner) external {
        require(msg.sender == owner, "Not owner");
        owner = owner;
    }
}
```

### Mitigation

Refactor the `changeOwner()` functions like such:

```solidity
function changeOwner(address _owner) public {
    require(msg.sender == owner, "Only owner");
    require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
    owner = _owner;
}
```
