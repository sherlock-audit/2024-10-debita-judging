Original Blonde Barbel

Medium

# Biased incentives mechanism unfairly benefits lonely lenders

### Summary

When lend and borrow offers are matched, incentives for each party are updated by calling `DebitaIncentives::updateFunds`. This function processes rewards for all collateral-principle pairs passed as input. However, if any pair is not whitelisted, the function halts prematurely and exits with an empty return, skipping the processing of remaining pairs. As a result, the corresponding parties do not receive any incentives for their offers.

### Root Cause

At the end of the `DebitaV3Aggregator::matchOffersV3` function, the `DebitaIncentives::updateFunds` function is invoked to update the incentives for borrowers and lenders involved in the loan.

```javascript
DebitaIncentives(s_Incentives).updateFunds(
    offers,
    borrowInfo.collateral,
    lenders,
    borrowInfo.owner
);
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L631-L636

The `updateFunds` function iterates through each offer's principle-collateral pair to validate if it is whitelisted. If any pair fails this check, the function stops and performs an early return:

```javascript
function updateFunds(
    infoOfOffers[] memory informationOffers,
    address collateral,
    address[] memory lenders,
    address borrower
) public onlyAggregator {
    for (uint i = 0; i < lenders.length; i++) {
        bool validPair = isPairWhitelisted[informationOffers[i].principle][
            collateral
        ];
        if (!validPair) {
@>          return;         
}
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L316-L318

If the first pair in the offers array is not whitelisted, no incentives are processed for any of the offers in the array.

### Internal pre-conditions

The lend offer whose principle is not whitelisted is set by the aggregator at the beginning of the array in  `DebitaV3Aggregator::matchOffersV3`.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This biased incentives mechanism grants an unfair competitive advantage to lonely lenders, since mixed offers risk being matched with another lend offer not eligible for rewards. This discourages participation in the platform and creates a skewed incentive structure that may harm the ecosystem's integrity.

### PoC

Add the following test to `MultipleLoansDuringIncentives.t`:

<details>

<summary> See PoC </summary>

```javascript
function testIncentinveMixedLoan() public {
    // Only incentivize AERO
    incentivize(AERO, AERO, USDC, false, 1e18, 2);
    vm.warp(block.timestamp + 15 days);
    address[] memory lenders = allDynamicData.getDynamicAddressArray(2);
    address[] memory principles = allDynamicData.getDynamicAddressArray(2);
    lenders[0] = firstLender;
    lenders[1] = secondLender;
    // First lender will use wETH for the principle (not whitelisted)
    principles[0] = wETH;
    // Second lender uses whitelisted principle
    principles[1] = AERO;
    createMixedLoan(borrower, lenders, principles, AERO);
    vm.warp(block.timestamp + 15 days);
    address[] memory tokenUsedIncentive = allDynamicData
        .getDynamicAddressArray(1);
    address[][] memory tokenIncentives = new address[][](
        tokenUsedIncentive.length
    );
    tokenUsedIncentive[0] = USDC;
    tokenIncentives[0] = tokenUsedIncentive;
    address[] memory _principles = allDynamicData.getDynamicAddressArray(1);
    _principles[0] = principles[1];
    vm.startPrank(borrower);
    vm.expectRevert();
    incentivesContract.claimIncentives(_principles, tokenIncentives, 2);
}
```

</details>

The helper function `createMixedLoan` is as follows:

<details>

<summary> createMixedLoan </summary>

```javascript
function createMixedLoan(
    address _borrower,
    address[] memory lenders,
    address[] memory principles,
    address collateral
) internal returns (address) {
    vm.startPrank(_borrower);
    //deal(principle, lender, 1000e18, false);
    deal(collateral, _borrower, 2e23, false);
    IERC20(collateral).approve(address(DBOFactoryContract), 1e23);
    bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(2);
    uint[] memory ltvs = allDynamicData.getDynamicUintArray(2);
    uint[] memory ratio = allDynamicData.getDynamicUintArray(2);
    uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
    uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
    bool[] memory oraclesActivatedLenders = allDynamicData
        .getDynamicBoolArray(1);

    address[] memory acceptedPrinciples = allDynamicData
        .getDynamicAddressArray(2);
    address[] memory acceptedCollaterals = allDynamicData
        .getDynamicAddressArray(1);
    address[] memory oraclesCollateral = allDynamicData
        .getDynamicAddressArray(1);
    address[] memory oraclesPrinciples = allDynamicData
        .getDynamicAddressArray(2);

    // set the values for the loan
    ltvs[0] = 5000;
    ltvs[1] = 5000;
    acceptedPrinciples[0] = principles[0];
    acceptedPrinciples[1] = principles[1];
    acceptedCollaterals[0] = collateral;
    oraclesActivated[0] = true;
    oraclesActivated[1] = true;
    oraclesPrinciples[0] = DebitaChainlinkOracle;
    oraclesPrinciples[1] = DebitaChainlinkOracle;
    oraclesCollateral[0] = DebitaChainlinkOracle;

    address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
        oraclesActivated,
        ltvs,
        1400,
        864000,
        acceptedPrinciples,
        collateral,
        false,
        0,
        oraclesPrinciples,
        ratio,
        DebitaChainlinkOracle,
        1e23
    );

    vm.stopPrank();

    vm.startPrank(lenders[0]);
    deal(principles[0], lenders[0], 1000e18, false);
    IERC20(principles[0]).approve(address(DLOFactoryContract), 100e18);
    ltvsLenders[0] = 5000;
    ratioLenders[0] = 5e17;
    oraclesActivatedLenders[0] = true;
    address lendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivatedLenders,
        false,
        ltvsLenders,
        1350,
        8640000,
        86400,
        acceptedCollaterals,
        principles[0],
        oraclesCollateral,
        ratioLenders,
        DebitaChainlinkOracle,
        5e18
    );
    vm.stopPrank();

    vm.startPrank(lenders[1]);
    deal(principles[1], lenders[1], 1000e18, false);
    IERC20(principles[1]).approve(address(DLOFactoryContract), 100e18);
    ltvsLenders[0] = 5000;
    ratioLenders[0] = 5e17;
    oraclesActivatedLenders[0] = true;
    address lendOrderAddress2 = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivatedLenders,
        false,
        ltvsLenders,
        1350,
        8640000,
        86400,
        acceptedCollaterals,
        principles[1],
        oraclesCollateral,
        ratioLenders,
        DebitaChainlinkOracle,
        5e18
    );
    vm.stopPrank();

    vm.startPrank(connector);

    address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
    uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
        2
    );
    uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexForCollateral_LendOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexPrinciple_LendOrder = allDynamicData
        .getDynamicUintArray(2);

    lendOrders[0] = lendOrderAddress;
    lendOrders[1] = lendOrderAddress2;
    lendAmountPerOrder[0] = 5e18;
    lendAmountPerOrder[1] = 5e18;
    indexForPrinciple_BorrowOrder[0] = 0;
    indexForPrinciple_BorrowOrder[1] = 1;
    porcentageOfRatioPerLendOrder[0] = 10000;
    porcentageOfRatioPerLendOrder[1] = 10000;
    indexForCollateral_LendOrder[0] = 0;
    indexForCollateral_LendOrder[1] = 0;
    indexPrinciple_LendOrder[0] = 0;
    indexPrinciple_LendOrder[1] = 1;

    // match
    address loan = DebitaV3AggregatorContract.matchOffersV3(
        lendOrders,
        lendAmountPerOrder,
        porcentageOfRatioPerLendOrder,
        borrowOrderAddress,
        principles,
        indexForPrinciple_BorrowOrder,
        indexForCollateral_LendOrder,
        indexPrinciple_LendOrder
    );

    DebitaV3LoanContract = DebitaV3Loan(loan);
    vm.stopPrank();
}
```

</details>

### Mitigation

Modify the loop in the updateFunds function to skip non-whitelisted pairs instead of halting execution entirely. Use the `continue` statement to proceed with processing the remaining pairs.