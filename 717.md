Hollow Violet Pike

Medium

# Incorrect `maxDeadline` checking in `payDebt` will cause the borrower to be unable to repay the loan.

### Summary

Incorrect `maxDeadline` checking in `payDebt` function will cause the borrower to be unable to repay the loan when the `maxDeadline` is equal to `block.timestamp`, leading to default.


### Root Cause

In [DebitaV3Loan.sol:210](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L210) there is a checking for `offer.maxDeadline`, the `block.timestamp` need to less than the `maxDeadline`:

```solidity
        require(offer.maxDeadline > block.timestamp, "Deadline passed");
```

However, it is incorrect. In cases:

1. If the duration of the borrow order is equal to the max duration of lend order, the loan's deadline will be equal to the maxDeadline.

2. If the borrower extends the loan, the next deadline will be set to maxDeadline.

In [DebitaV3Loan.sol:743-764](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L743-L764)

```solidity
    function nextDeadline() public view returns (uint) {
        uint _nextDeadline;
        LoanData memory m_loan = loanData;
        if (m_loan.extended) {
            for (uint i; i < m_loan._acceptedOffers.length; i++) {
                if (
                    _nextDeadline == 0 &&
                    m_loan._acceptedOffers[i].paid == false
                ) {
@>                  _nextDeadline = m_loan._acceptedOffers[i].maxDeadline;
                } else if (
                    m_loan._acceptedOffers[i].paid == false &&
                    _nextDeadline > m_loan._acceptedOffers[i].maxDeadline
                ) {
@>                  _nextDeadline = m_loan._acceptedOffers[i].maxDeadline;
                }
            }
        } else {
            _nextDeadline = m_loan.startedAt + m_loan.initialDuration;
        }
        return _nextDeadline;
    }
```

The borrower can not call payDebt when block.timestamp equal to offer.maxDeadline in these cases.


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The loan will default, the borrower lost his collateral and it will be sold in auction.

### PoC


-   For case 2, place this test into BasicDebitaAggregator.t.sol.

```solidity
    function test_matchOffersAndPayBackAtExtendedDeadline() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 3e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        uint balanceBefore = IERC20(AERO).balanceOf(address(this));
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
        uint balanceAfter = IERC20(AERO).balanceOf(address(this));
        assertEq(balanceAfter, balanceBefore + 3e18);
        DebitaV3Loan loanContract = DebitaV3Loan(loan);
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        indexes[0] = 0;

        IERC20(AERO).approve(loan, 4e18);

        vm.warp(864000);
        loanContract.extendLoan();
        vm.warp(8640001);

        vm.expectRevert("Deadline passed");
        loanContract.payDebt(indexes);
    }
```

-   For case 1, in [BasicDebitaAggregator.t.sol:114](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/test/local/Aggregator/BasicDebitaAggregator.t.sol#L114), please modify the maxDuration of the lendOrder from 8640000 to 864000. Then place this test into BasicDebitaAggregator.t.sol.

```solidity
    function test_matchOffersAndPayBackAtMaxdeadline() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 3e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        uint balanceBefore = IERC20(AERO).balanceOf(address(this));
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );
        uint balanceAfter = IERC20(AERO).balanceOf(address(this));
        assertEq(balanceAfter, balanceBefore + 3e18);
        DebitaV3Loan loanContract = DebitaV3Loan(loan);
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        indexes[0] = 0;

        IERC20(AERO).approve(loan, 4e18);

        vm.warp(864001);

        vm.expectRevert("Deadline passed");
        loanContract.payDebt(indexes);
    }
```


### Mitigation

Change the code in DebitaV3Loan.sol:210 to:

```solidity
        require(offer.maxDeadline >= block.timestamp, "Deadline passed");
```
