Nutty Snowy Robin

High

# Borrower Can Reduce the Interest Owed to Each Accepted Lender

### Summary

Every time a borrower extends their loan, at the time of repayment, the lenders will lose the interest that was accrued before the loan extension.

When `extendLoan()` is called, the interest accrued up until that point will be paid by the borrower. This interest will be added to `loanData._acceptedOffers[index].interestToClaim` for each lender linked to the loan, and it will be marked as already paid in `loanData._acceptedOffers[i].interestPaid`:
```solidity
    function extendLoan() public {

        // Function code ...
            
        for (uint i; i < m_loan._acceptedOffers.length; i++) {
            
                // Function code ...
            
                if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                    IERC20(offer.principle).approve(
                        address(lendOffer),
                        interestOfUsedTime - interestToPayToDebita
                    );
                    lendOffer.addFunds(
                        interestOfUsedTime - interestToPayToDebita
                    );
                } else {
>>                  loanData._acceptedOffers[i].interestToClaim += interestOfUsedTime - interestToPayToDebita;
                }
>>              loanData._acceptedOffers[i].interestPaid += interestOfUsedTime;
        }

        // Function code ...
    }
```
When `calculateInterestToPay()` is called, it computes the total interest accrued from the beginning of the loan until that moment and then subtracts the interest already paid (if any):
```solidity
function calculateInterestToPay(uint index) public view returns (uint) {
        infoOfOffers memory offer = loanData._acceptedOffers[index];
        uint anualInterest = (offer.principleAmount * offer.apr) / 10000;
        // check already duration
        uint activeTime = block.timestamp - loanData.startedAt;
        uint minimalDurationPayment = (loanData.initialDuration * 1000) / 10000;
        uint maxDuration = offer.maxDeadline - loanData.startedAt;
        if (activeTime > maxDuration) {
            activeTime = maxDuration;
        } else if (activeTime < minimalDurationPayment) {
            activeTime = minimalDurationPayment;
        }

        uint interest = (anualInterest * activeTime) / 31536000;

        // subtract already paid interest
>>      return interest - offer.interestPaid;
    }
```
The issue arises after extending a loan: when some interest has been paid, and the borrower repays the debt, the `calculateInterestToPay()` function will calculate the remaining interest to be paid. However, in `repayDebt()`, if the lender's offer is not perpetual, the interest to be paid will be **SET** to `loanData._acceptedOffers[index].interestToClaim`, instead of being added:

```solidity
function payDebt(uint[] memory indexes) public nonReentrant {       

    // Function code ...

    for (uint i; i < indexes.length; i++) {
        
        // Function code ...

>>      uint interest = calculateInterestToPay(index);
        uint feeOnInterest = (interest * feeLender) / 10000;

        // Function code ..
    
        if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                loanData._acceptedOffers[index].debtClaimed = true;
                IERC20(offer.principle).approve(address(lendOffer), total);
                lendOffer.addFunds(total);
            } else {
>>              loanData._acceptedOffers[index].interestToClaim = interest - feeOnInterest;
            }

        // Function code ...
    }

}
```
So, the `interestToClaim` for each lender only reflects the interest accrued after the loan was extended, and any interest accrued before the extension will be lost.

### Root Cause

In [DebitaV3Loan:238](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L238) instead of add the interest accrued, is set.

### Internal pre-conditions

- A loan is extended.
- Lender don't claim any interest after extension.
- The lender claims all the interest after the repayment of the loan.

### External pre-conditions

_No response_

### Attack Path

**Loan Details:**
- **Borrower**: Bob
- **Lender**: Alice
- **Amount Lent**: 100,000 USDC
- **Duration**: 3 weeks
- **APR**: 10%

**Loan Extension (After 2 Weeks):**
- **Interest Accrued**: 384 USDC
- **Fee to Pay (15%)**: 57.6 USDC
- **interestToClaim**: 326.4 USDC
- **interestPaid**: 384 USDC

**Loan Repayment (After 1 Week):**
- **Interest Accrued**: 576 - 384 = 192 USDC
- **Fee to Pay (15%)**: 28 USDC
- **interestToClaim**: 164 USDC  *(Note: This value is set, not added.)*
- **interestPaid**: 576 USDC


### Impact

Lender's loss of interest when borrower extends a loan before repaying the debt.

### PoC
Paste the following functions into `test/fork/Incentives/MultipleLoansDuringIncentives.t.sol`
Once pasted, you can run it with: `forge test --fork-url https://mainnet.base.org --fork-block-number 21151256 --no-match-path '**Fantom**' --mt testLooseOfInterest -vvvv`

<details>

<summary>Test to run</summary>

```solidity
function testLooseOfInterest() public {
        // Create 1 normal loans, with 250 AERO lent
        // Duration: 35 days
        // APR: 50% (for the test purpose)
        createUsualLoan(borrower, firstLender, AERO, AERO);
        
        // Pass 30 days
        // Interest to pay:
        // Annual interest = 250e18 * 5000 / 10000
        // interest =  annualInterest * 30 days / 31536000 = ~10e18
        vm.warp(block.timestamp + 30 days);
        vm.startPrank(borrower);
        IERC20(AERO).approve(address(DebitaV3LoanContract), 15e18);
        DebitaV3LoanContract.extendLoan();
        vm.stopPrank();

        // Pass another 30 days and repay
        vm.warp(block.timestamp + 30 days);
        // repay the debt
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        vm.startPrank(borrower);
        IERC20(AERO).approve(address(DebitaV3LoanContract), 265e18);
        DebitaV3LoanContract.payDebt(indexes);
        vm.stopPrank();

        // claim debt + interest
        uint balanceLenderBeforeClaim = IERC20(AERO).balanceOf(firstLender);
        vm.prank(firstLender);
        DebitaV3LoanContract.claimDebt(0);
        uint balanceLenderAfterClaim = IERC20(AERO).balanceOf(firstLender);
        
        // claim collateral
        vm.prank(borrower);
        DebitaV3LoanContract.claimCollateralAsBorrower(indexes);

        uint annualInterest = (250e18 * 5000) / 10000;
        uint interest = (annualInterest * 30 days) / 31536000;
        uint feeInterestTaken = (interest * 15_00) / 10_000;
        
        // Expected to be equal, but because of the bug is not:
        assertNotEq(
            balanceLenderAfterClaim, balanceLenderBeforeClaim + 250e18 + (interest * 2) - (feeInterestTaken * 2)
        );
        // Expected to be not equal, but because of the bug is:
        assertEq(balanceLenderAfterClaim, balanceLenderBeforeClaim + 250e18 + interest - feeInterestTaken);
    }
```

</details>


<details>

<summary>Function to create loan</summary>

```solidity
function createUsualLoan(
        address _borrower,
        address lender,
        address principle,
        address collateral
    ) internal returns (address) {
        vm.startPrank(_borrower);
        deal(principle, lender, 1000e18, false);
        deal(collateral, _borrower, 1000e18, false);
        IERC20(collateral).approve(address(DBOFactoryContract), 500e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 5000;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = true;

        oraclesPrinciples[0] = DebitaChainlinkOracle;
        oraclesCollateral[0] = DebitaChainlinkOracle;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            5000, // high APR to notice interest
            35 days, // duartion
            acceptedPrinciples,
            collateral,
            false, // nft
            0, // receipt ID
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            500e18
        );

        vm.stopPrank();

        vm.startPrank(lender);
        IERC20(principle).approve(address(DLOFactoryContract), 250e18);
        ltvsLenders[0] = 5000;
        ratioLenders[0] = 5e17;
        oraclesActivatedLenders[0] = true;
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivatedLenders,
            false,
            ltvsLenders,
            5000, // apr
            65 days, // extension deadline
            1 weeks,
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            DebitaChainlinkOracle,
            250e18
        );
        vm.stopPrank();
        vm.startPrank(connector);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 250e18;

        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = principle;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
        vm.stopPrank();


    }
```

</details>

### Mitigation

Add the interest accrued instead of setting it into `interestToClaim`:
```diff
function payDebt(uint[] memory indexes) public nonReentrant {       

    // Function code ...

    for (uint i; i < indexes.length; i++) {
        
        // Function code ...

        uint interest = calculateInterestToPay(index);
        uint feeOnInterest = (interest * feeLender) / 10000;

        // Function code ..
    
        if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                loanData._acceptedOffers[index].debtClaimed = true;
                IERC20(offer.principle).approve(address(lendOffer), total);
                lendOffer.addFunds(total);
            } else {
-               loanData._acceptedOffers[index].interestToClaim = interest - feeOnInterest;
+               loanData._acceptedOffers[index].interestToClaim += interest - feeOnInterest;
            }

        // Function code ...
    }

}
```