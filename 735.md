Formal Purple Pig

Medium

# Off-by-One Error in For Loop Preventing Last Element Access.

### Summary

In [DebitaV3Aggregator.sol::getAllLoans()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L708) the last loan is never returned. 


### Root Cause

In `DebitaV3Aggregator.sol` every time a loan is created with [matchOffersV3()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L275) `loanID` is incremented:
```solidity
    function matchOffersV3(
        address[] memory lendOrders,
        uint[] memory lendAmountPerOrder,
        uint[] memory porcentageOfRatioPerLendOrder,
        address borrowOrder,
        address[] memory principles,
        uint[] memory indexForPrinciple_BorrowOrder,
        uint[] memory indexForCollateral_LendOrder,
        uint[] memory indexPrinciple_LendOrder
    ) external nonReentrant returns (address) {
        // Add count
        loanID++;
        ...
```
Therefore `loanID` is the last value, but in [getAllLoans()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L693) loanID is omitted:

```solidity
    function getAllLoans(
        uint offset,
        uint limit
    ) external view returns (DebitaV3Loan.LoanData[] memory) {
        // return LoanData
        uint _limit = loanID;
        if (limit > _limit) {
            limit = _limit;
        }

        DebitaV3Loan.LoanData[] memory loans = new DebitaV3Loan.LoanData[](
            limit - offset
        );

        for (uint i = 0; i < limit - offset; i++) {
            // @audit loanID is omitted here, as this will break
            if ((i + offset + 1) >= loanID) {
                break;
            }
            address loanAddress = getAddressById[i + offset + 1];

            DebitaV3Loan loan = DebitaV3Loan(loanAddress);
            loans[i] = loan.getLoanData();

            // loanIDs start at 1
        }
        return loans;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The function `getAllLoans()` will never return the last loan. 

### PoC

```solidity
  function test_getAllLoans() public {
    // Simulate loanIDs -> 1-10 are active, when a loan is created it has the value
    // of loanID. In this scenario, last loan will be with loanID = 10. 
    uint loanID = 10;

    uint limit = 10;
    uint offset = 2;

    uint _limit = loanID;
    if (limit > _limit) {
        limit = _limit;
    }

    for (uint i = 0; i < limit - offset; i++) {
      // @audit Last loan is never reached.
      // consider using > instead of >=.
      if ((i + offset + 1) >= loanID) {
        break;
      }
      console.log(i + offset + 1);
    }
  }
```
```foundry
Traces:
  [5764] TestAudit::test_getAllLoans()
    ├─ [0] console::log(3) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(4) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(5) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(6) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(7) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(8) [staticcall]
    │   └─ ← [Stop] 
    ├─ [0] console::log(9) [staticcall]
    │   └─ ← [Stop] 
    └─ ← [Return] 
```


### Mitigation

```solidity
    function getAllLoans(
        uint offset,
        uint limit
    ) external view returns (DebitaV3Loan.LoanData[] memory) {
        // return LoanData
        uint _limit = loanID;
        if (limit > _limit) {
            limit = _limit;
        }

        DebitaV3Loan.LoanData[] memory loans = new DebitaV3Loan.LoanData[](
            limit - offset
        );

        for (uint i = 0; i < limit - offset; i++) {
            //@audit Change >= for > to allow access to last loanID.
            if ((i + offset + 1) > loanID) {
                break;
            }
            address loanAddress = getAddressById[i + offset + 1];

            DebitaV3Loan loan = DebitaV3Loan(loanAddress);
            loans[i] = loan.getLoanData();

            // loanIDs start at 1
        }
        return loans;
    }
```
