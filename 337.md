Dandy Charcoal Bee

Medium

# AuctionFactory/BuyOrderFactory/DebitaV3Aggregator:changeOwner() cannot update the owner due to Solidity shadowing

### Summary

Due to Solidity's shadowing the function [`AuctionFactory:changeOwner()`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/auctions/AuctionFactory.sol#L218) is useless, as it cannot write to storage.

### Root Cause

When a scope has more than 1 variable with the same name Solidity applies shadowing, meaning that such name will resolve to the most local's scope value. In this case the function parameter called `owner`.

```solidity
address owner;    // <- state variable

1 function changeOwner(address owner) public {
2    require(msg.sender == owner, "Only owner");  // <@ shadowed
3    require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
4    owner = owner;  // <@ shadowed
  }
```
For this reason, it is impossible to update the state variable named `owner`, because the function is assigning the function parameter back to its value (4) ... without touching the storage at all.

Also note that the actual owner cannot even call this function as intended, by passing another address, because msg.sender is compared against the parameter (1), not the state variable.

### Internal pre-conditions

1. The current owner of the AuctionFactory contract needs to pass it to someone else, thus he calls `chageOwner()`

### External pre-conditions

_No response_

### Attack Path

1. owner tries to calls changeOwner(newOwner) but the call will always revert

### Impact

Broken functionality, it is impossible to change the owner through the function.

Affected contracts:
- AuctionFactory
- BuyOrderFactory
- DebitaV3Aggregator

### PoC

Add the following test contract in `test/local` and run it to demonstrate the issue:

```solidity
pragma solidity ^0.8.0;

import {Test, console2} from "forge-std/Test.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";

contract ChangeOwnerShadowed is Test {

    auctionFactoryDebita factory;

    function setUp() public {
        factory = new auctionFactoryDebita();
    }

    function test_changeOwnerShadowing() public {
        address new_owner = makeAddr("newOwner");

        assertEq(factory.owner(), address(this));

        // 1) here, calling from the actual owner, the function reverts
        vm.expectRevert();
        factory.changeOwner(new_owner);

        // 2) here, calling from the new owner, the call goes trough but the state remains unchanged
        vm.prank(new_owner);
        factory.changeOwner(new_owner);
        assertEq(factory.owner(), address(this));
    }

}
```

### Mitigation

Change the function parameter's name, for example to `_owner`, in order to avoid shadowing of the storage variable.