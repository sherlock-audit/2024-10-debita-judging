Furry Cloud Cod

Medium

# The `buyOrderFactory::changeOwner` fails to change owner as it should

## Impact
### Summary 
The `buyOrderFactory::changeOwner` is designed to change the owner of the `buyOrderFactory` contract, passing owner privileges from the old owner to the new owner. However, this function will not be able to change the `owner` of the contract due to conflicting variable naming convention. 

### Vulnerability Details
The vulnerability of this function lies in the fact that the name of the input parameter for the `buyOrderFactory::changeOwner` function conflicts with the storage variable `owner` in the sense that the two variables are not differentiated.
Here is the link to the function in question https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/buyOrders/buyOrderFactory.sol#L186-L190 and also shown in the code snippet below

```javascript
@>  function changeOwner(address owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
        owner = owner;
    }
```

### Impact
As a result of this conflicting variable naming convention, the solidity compiler fumbles in when to user `owner` as the parameter input of the `buyOrderFactory::changeOwner` function and when to use `owner` as state varable in the `buyOrderFactory` contract. 
In particular, if the `buyOrderFactory::changeOwner` function is called by the `buyOrderFactory::owner`, the call reverts due to the first require statement. This is because `msg.sender` is compared against `owner`, the parameter input instead of `buyOrderFactory::owner`. On the other hand, if `owner` as in the input parameter calls the `buyOrderFactory::changeOwner` function, the function executes but owner is not changed.
Hence, the owner cannot be changed, breaking the protocol's functionality.

## Proof of Concept
1. Prank the owner of `buyOrderFactory` to call the `buyOrderFactory::changeOwner` function which reverts with `Only owner` message.
2. Prank the address we wish to set as the new owner to call the `buyOrderFactory::changeOwner` function. This executes successfully but using the getter function shows that the `buyOrderFactory::owner` has not changed.


<details>
<summary>PoC</summary>
Place the following code into `BasicDebitaAggregator.t.sol`.

```javascript
import {BuyOrder, buyOrderFactory} from "@contracts/buyOrders/buyOrderFactory.sol";

contract BuyOrderFactoryTest is Test {

    buyOrderFactory public factory;
    BuyOrder public buyOrder;
    
    function setUp() public {
        
        BuyOrder instanceDeployment = new BuyOrder();
        factory = new buyOrderFactory(address(instanceDeployment));

    }

    function test_SpomariaPoC_BuyOrderFactoryCantChangeOwner() public {
        
        address factoryOwner = factory.owner();

        address _newFactoryOwner = makeAddr("new_owner");

        vm.startPrank(factoryOwner);
        vm.expectRevert("Only owner");
        factory.changeOwner(_newFactoryOwner);
        vm.stopPrank();

        vm.startPrank(_newFactoryOwner);
        factory.changeOwner(_newFactoryOwner);
        vm.stopPrank();

        // assert that owner was not changed
        assertEq(factory.owner(), factoryOwner);
    }
}
```

Now run `forge test --match-test test_SpomariaPoC_BuyOrderFactoryCantChangeOwner -vvvv`

Output:
```javascript

.
.
.
├─ [0] VM::expectRevert(Only owner)
    │   └─ ← [Return] 
    ├─ [606] buyOrderFactory::changeOwner(new_owner: [0x8138d5842F59D3ce76a371b64D60b577155EF7E4])
    │   └─ ← [Revert] revert: Only owner
    ├─ [0] VM::stopPrank()
    │   └─ ← [Return] 
    ├─ [0] VM::startPrank(new_owner: [0x8138d5842F59D3ce76a371b64D60b577155EF7E4])
    │   └─ ← [Return] 
    ├─ [2682] buyOrderFactory::changeOwner(new_owner: [0x8138d5842F59D3ce76a371b64D60b577155EF7E4])
    │   └─ ← [Return] 
    ├─ [0] VM::stopPrank()
    │   └─ ← [Return] 
    ├─ [491] buyOrderFactory::owner() [staticcall]
    │   └─ ← [Return] BuyOrderFactoryTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496]
    ├─ [0] VM::assertEq(BuyOrderFactoryTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], BuyOrderFactoryTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496]) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Return] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 22.10ms (10.71ms CPU time)

Ran 1 test suite in 51.95ms (22.10ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

</details>

## Tools Used

Manual Review and Foundry


## Recommended Mitigation Steps
Consider changing the name of the input parameter of the `buyOrderFactory::changeOwner` function in such a way that it does not conflict with any state variables. For instance, we could use `address _owner` instead of `address owner` as shown below:

```diff
-   function changeOwner(address owner) public {
+   function changeOwner(address _owner) public {
        require(msg.sender == owner, "Only owner");
        require(deployedTime + 6 hours > block.timestamp, "6 hours passed");
-       owner = owner;
+       owner = _owner;
    }
```
