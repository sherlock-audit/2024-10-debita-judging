Micro Ginger Tarantula

High

# Loans with TaxTokenReceipts as collateral can't be liquidated

### Summary

The ``TaxTokenReceipt.sol`` contract allows users to deposit fee on transfer tokens to it, and it mints them an NFT, that NFT is supposed to be used across the Debita protocol, when users wants to use FOT tokens as collateral. In order to ensure that the NFT is utilized only within the Debita system, the [TaxTokensReceipt::transferFrom()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L93-L120) function is overridden to the following:
```solidity
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override(ERC721, IERC721) {
        bool isReceiverAddressDebita = IBorrowOrderFactory(borrowOrderFactory)
            .isBorrowOrderLegit(to) ||
            ILendOrderFactory(lendOrderFactory).isLendOrderLegit(to) ||
            IAggregator(Aggregator).isSenderALoan(to);
        bool isSenderAddressDebita = IBorrowOrderFactory(borrowOrderFactory)
            .isBorrowOrderLegit(from) ||
            ILendOrderFactory(lendOrderFactory).isLendOrderLegit(from) ||
            IAggregator(Aggregator).isSenderALoan(from);
        // Debita not involved --> revert
        require(
            isReceiverAddressDebita || isSenderAddressDebita,
            "TaxTokensReceipts: Debita not involved"
        );
        if (to == address(0)) {
            revert ERC721InvalidReceiver(address(0));
        }
        // Setting an "auth" arguments enables the `_isAuthorized` check which verifies that the token exists
        // (from != 0). Therefore, it is not needed to verify that the return value is not 0 here.
        address previousOwner = _update(to, tokenId, _msgSender());
        if (previousOwner != from) {
            revert ERC721IncorrectOwner(from, tokenId, previousOwner);
        }
    }
```
As can be seen from the above code snippet the ``to`` or ``from`` address has to be a borrow order, lend order or a loan. Clearly a  check whether the ``to`` or ``from`` addresses are a valid auction is missing. This will be problematic when a loan which uses a TaxTokensReceipt NFT as collateral defaults and is liquidated. The auction will be successfully created and the NFT will be successfully transferred to the newly created auction contract. However when another user who wishes to buy the NFT and provide the collateral tokens which will be used to cover the lenders debt the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function will revert as neither the ``to`` or ``from`` addresses are a valid lend order, borrow order or a loan. This results in loans which utilize the TaxTokenReceipt NFT as collateral and default, to never be successfully liquidated. The NFT and the underlying collateral associated with the NFT will be locked in the ``Auction.sol`` collateral forever, and the lenders will never receive any underlying tokens back, essentially loosing all the principal they provided to the borrower. 

### Root Cause

The overdriven [TaxTokensReceipt::transferFrom()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol#L93-L120) function, only checks whether the ``to`` or ``from`` addresses are a valid borrow order, lend order or a loan in order for a transfer to be successful. Since auctions are not listed, when a user who wishes to buy the NFT via the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function, won't be able to, as the NFT can't be transferred, and the [Auction::buyNFT()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/auctions/Auction.sol#L109-L161) function will revert. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Loans which utilize the TaxTokenReceipt NFT as collateral and default, can never be successfully liquidated. The NFT and the associated underlying collateral will be locked in the ``Auction.sol`` collateral forever, and the lenders will never receive any underlying tokens back, essentially loosing all the principal they provided to the borrower. 

### PoC

[Gist](https://gist.github.com/AtanasDimulski/365c16f87db9360aaf11937b4d9f4be5)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/365c16f87db9360aaf11937b4d9f4be5) add the following test to the ``AuditorTests.t.sol`` file:
```solidity
    function test_CantLiquidateTaxTokenReceipt() public {
        vm.startPrank(alice);
        FOTToken.mint(alice, 10e18);
        FOTToken.approve(address(taxTokenReceipts), type(uint256).max);
        uint256 tokenID = taxTokenReceipts.deposit(10e18);
        taxTokenReceipts.approve(address(dboFactory), tokenID);

        bool[] memory oraclesActivated = new bool[](1);
        oraclesActivated[0] = false;

        uint256[] memory LTVs = new uint256[](1);
        LTVs[0] = 0;

        address[] memory acceptedPrinciples = new address[](1);
        acceptedPrinciples[0] = address(USDC);

        address[] memory oraclesAddresses = new address[](1);
        oraclesAddresses[0] = address(0);

        uint256[] memory ratio = new uint256[](1);
        ratio[0] = 2_500e6;

        /// @notice alice wants 2_500e6 USDC for 1 WETH
        address aliceBorrowOrder = dboFactory.createBorrowOrder(
            oraclesActivated,
            LTVs,
            500, /// @notice set max interest rate to 5%
            10 days,
            acceptedPrinciples,
            address(taxTokenReceipts),
            true,
            tokenID,
            oraclesAddresses,
            ratio,
            address(0),
            1
        );
        vm.stopPrank();

        vm.startPrank(bob);
        USDC.mint(bob, 25_000e6);
        USDC.approve(address(dloFactory), type(uint256).max);

        address[] memory acceptedCollaterals = new address[](1);
        acceptedCollaterals[0] = address(taxTokenReceipts);

        address bobLendOffer = dloFactory.createLendOrder(
            false,
            oraclesActivated,
            false,
            LTVs,
            500,
            15 days,
            10 days,
            acceptedCollaterals,
            address(USDC),
            oraclesAddresses,
            ratio,
            address(0),
            25_000e6
        );

        /// @notice match orders
        address[] memory lendOrders = new address[](1);
        lendOrders[0] = address(bobLendOffer);

        uint[] memory lendAmountPerOrder = new uint[](1);
        lendAmountPerOrder[0] = 25_000e6;

        uint[] memory porcentageOfRatioPerLendOrder = new uint[](1);
        porcentageOfRatioPerLendOrder[0] = 10_000;

        address[] memory principles = new address[](1);
        principles[0] = address(USDC);

        uint[] memory indexForPrinciple_BorrowOrder = new uint[](1);
        indexForPrinciple_BorrowOrder[0] = 0;

        uint[] memory indexForCollateral_LendOrder = new uint[](1);
        indexForCollateral_LendOrder[0] = 0;

        uint[] memory indexPrinciple_LendOrder = new uint[](1);
        indexPrinciple_LendOrder[0] = 0;

        address loanAddress = debitaV3Aggregator.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            aliceBorrowOrder,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        /// @notice loan expired
        skip(10 days + 1);
        DebitaV3Loan(loanAddress).createAuctionForCollateral(0); 
        DebitaV3Loan.AuctionData memory auctionData = DebitaV3Loan(loanAddress).getAuctionData();
        address auctionAddress = auctionData.auctionAddress;
        vm.stopPrank();

        vm.startPrank(tom);
        FOTToken.mint(tom, 10e18);
        FOTToken.approve(address(auctionAddress), type(uint256).max);
        vm.expectRevert("TaxTokensReceipts: Debita not involved");
        Auction(auctionAddress).buyNFT();
        vm.stopPrank();
    }
```

To run the test use: ``forge test -vvv --mt test_CantLiquidateTaxTokenReceipt``

### Mitigation

_No response_