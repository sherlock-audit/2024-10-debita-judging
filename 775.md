Tame Hemp Pangolin

High

# Reentrancy in `DBOFactory::createBorrowOrder()` will DoS the deleting of the borrow order

### Summary

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaBorrowOffer-Factory.sol#L126-L130

Reentrancy in `transferFrom()` with malicious collateral will prevent the `borrowOrderIndex`, `allActiveBorrowOrders`, and `activeOrdersCount` state variables from being updated. This will cause `activeOrdersCount` to decrement below the actual number of active borrow orders, resulting in the `deleteBorrowOrder()` function always reverting if there are not any new borrow orders.

### Root Cause

Not following the Check-Effects-Interactions Pattern - the state variables are not updated before the external call.

```solidity
DebitaBorrowOffer-Factory

function createBorrowOrder(
    bool[] memory _oraclesActivated,
    uint[] memory _LTVs,
    uint _maxInterestRate,
    uint _duration,
    address[] memory _acceptedPrinciples,
    address _collateral,
    bool _isNFT,
    uint _receiptID,
    address[] memory _oracleIDS_Principles,
    uint[] memory _ratio,
    address _oracleID_Collateral,
    uint _collateralAmount
) external returns (address) {
    ...
    DBOImplementation borrowOffer = new DBOImplementation();

    borrowOffer.initialize(
        aggregatorContract,
        msg.sender,
        _acceptedPrinciples,
        _collateral,
        _oraclesActivated,
        _isNFT,
        _LTVs,
        _maxInterestRate,
        _duration,
        _receiptID,
        _oracleIDS_Principles,
        _ratio,
        _oracleID_Collateral,
        _collateralAmount
    );
    isBorrowOrderLegit[address(borrowOffer)] = true;
    if (_isNFT) {
@>      IERC721(_collateral).transferFrom(
            msg.sender,
            address(borrowOffer),
            _receiptID
        );
    } else {
        SafeERC20.safeTransferFrom(
            IERC20(_collateral),
            msg.sender,
            address(borrowOffer),
            _collateralAmount
        );
    }
@>  borrowOrderIndex[address(borrowOffer)] = activeOrdersCount;
@>  allActiveBorrowOrders[activeOrdersCount] = address(borrowOffer);
@>  activeOrdersCount++;
    ...
}
```

### Internal pre-conditions

There are should be some borrow orders created in order the attacker to block the `deleteBorrowOrder()` function.

### External pre-conditions

The attacker should implement a malicious colleteral contract with custom `transferFrom()` logic.

### Attack Path

1. The attacker creates and deploys a malicious collateral contract with custom `transferFrom()` logic.
2. Other users create X number of borrow orders.
3. The attacker calls `createBorrowOrder()` with the malicious collateral contract as the collateral
4. The malicious contract's `transferFrom()` calls the `createBorrowOrder()` X times in order to create borrow orders. The `isBorrowOrderLegit` mapping is updated with these borrow orders but the remaining state variables are not updated.
5. The attacker calls `DebitaBorrowOffer-Implementation::cancelOffer()` which internally calls `deleteBorrowOrder` from factory contract. It doesn't revert because the `onlyBorrowOrder` check passes.

```solidity
DebitaBorrowOffer-Implementation

function cancelOffer() public onlyOwner nonReentrant {
    BorrowInfo memory m_borrowInformation = getBorrowInfo();
    uint availableAmount = m_borrowInformation.availableAmount;
    require(availableAmount > 0, "No available amount");
    // set available amount to 0
    // set isActive to false
    borrowInformation.availableAmount = 0;
    isActive = false;

    // transfer collateral back to owner
    if (m_borrowInformation.isNFT) {
        if (m_borrowInformation.availableAmount > 0) {
            IERC721(m_borrowInformation.collateral).transferFrom(
                address(this),
                msg.sender,
                m_borrowInformation.receiptID
            );
        }
    } else {
        SafeERC20.safeTransfer(
            IERC20(m_borrowInformation.collateral),
            msg.sender,
            availableAmount
        );
    }

    // emit canceled event on factory

    IDBOFactory(factoryContract).deleteBorrowOrder(address(this));
    IDBOFactory(factoryContract).emitDelete(address(this));
}
```

```solidity
DebitaBorrowOffer-Factory

function deleteBorrowOrder(address _borrowOrder) external onlyBorrowOrder {
    // get index of the borrow order
    uint index = borrowOrderIndex[_borrowOrder];
    borrowOrderIndex[_borrowOrder] = 0;

    // get last borrow order
    allActiveBorrowOrders[index] = allActiveBorrowOrders[
        activeOrdersCount - 1
    ];
    // take out last borrow order
    allActiveBorrowOrders[activeOrdersCount - 1] = address(0);

    // switch index of the last borrow order to the deleted borrow order
    borrowOrderIndex[allActiveBorrowOrders[index]] = index;
    activeOrdersCount--;
}
```

6. The `activeOrdersCount` will be X but the actual number of borrow orders are X*2.
7. The attacker repeats the the `cancelOffer()` process until the `activeOrdersCount` is 0.
8. When the users try to call `cancelOffer()` or when the aggregator calls `acceptBorrowOffer()` (if the order is not active), these will always revert because the `deleteBorrowOrder()` function will try to decrement from the `activeOrdersCount` state variable but it will be 0.

```solidity
DebitaBorrowOffer-Implementation

function acceptBorrowOffer(
    uint amount
) public onlyAggregator nonReentrant onlyAfterTimeOut {
    ...
    uint percentageOfAvailableCollateral = (borrowInformation
        .availableAmount * 10000) / m_borrowInformation.startAmount;

    // if available amount is less than 0.1% of the start amount, the order is no longer active and will count as completed.
    if (percentageOfAvailableCollateral <= 10) {
        isActive = false;
        // transfer remaining collateral back to owner
        if (borrowInformation.availableAmount != 0) {
            SafeERC20.safeTransfer(
                IERC20(m_borrowInformation.collateral),
                m_borrowInformation.owner,
                borrowInformation.availableAmount
            );
        }
        borrowInformation.availableAmount = 0;
        IDBOFactory(factoryContract).emitDelete(address(this));
        IDBOFactory(factoryContract).deleteBorrowOrder(address(this));
    } else {
        IDBOFactory(factoryContract).emitUpdate(address(this));
    }
}
```

### Impact

The affected critical functions are `acceptBorrowOffer` and `cancelOffer`. If the user tries to cancel their borrow order, it will be impossible. If the aggregator calls `acceptBorrowOffer` and the available amount is less than 0.1% of the start amount, the remaining collateral will not be transferred to the owner.

### PoC

_No response_

### Mitigation

Update the state variables before the external call.

```diff
DebitaBorrowOffer-Factory

function createBorrowOrder(
    bool[] memory _oraclesActivated,
    uint[] memory _LTVs,
    uint _maxInterestRate,
    uint _duration,
    address[] memory _acceptedPrinciples,
    address _collateral,
    bool _isNFT,
    uint _receiptID,
    address[] memory _oracleIDS_Principles,
    uint[] memory _ratio,
    address _oracleID_Collateral,
    uint _collateralAmount
) external returns (address) {
    ...
    DBOImplementation borrowOffer = new DBOImplementation();

    borrowOffer.initialize(
        aggregatorContract,
        msg.sender,
        _acceptedPrinciples,
        _collateral,
        _oraclesActivated,
        _isNFT,
        _LTVs,
        _maxInterestRate,
        _duration,
        _receiptID,
        _oracleIDS_Principles,
        _ratio,
        _oracleID_Collateral,
        _collateralAmount
    );
    isBorrowOrderLegit[address(borrowOffer)] = true;
+   borrowOrderIndex[address(borrowOffer)] = activeOrdersCount;
+   allActiveBorrowOrders[activeOrdersCount] = address(borrowOffer);
+   activeOrdersCount++;
    if (_isNFT) {
        IERC721(_collateral).transferFrom(
            msg.sender,
            address(borrowOffer),
            _receiptID
        );
    } else {
        SafeERC20.safeTransferFrom(
            IERC20(_collateral),
            msg.sender,
            address(borrowOffer),
            _collateralAmount
        );
    }
-   borrowOrderIndex[address(borrowOffer)] = activeOrdersCount;
-   allActiveBorrowOrders[activeOrdersCount] = address(borrowOffer);
-   activeOrdersCount++;
    ...
}
```