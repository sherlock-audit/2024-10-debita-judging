Original Blonde Barbel

High

# Overwrite in `interestToClaim` in `DebitaV3Loan::payDebt` causes partial loss of lenders' accrued interest

### Summary

The `DebitaV3Loan::payDebt` function is responsible for facilitating debt repayment by borrowers. During this process, the interest owed is computed and deducted from the amounts already paid. For non-perpetual lending offers, the accrued interest claimable by the lender is stored in the `interestToClaim` field of the corresponding offer struct.

Borrowers can also extend their loans by calling `DebitaV3Loan::extendLoan`, during which they pay the accrued interest up to that point. This interest is added to the `interestToClaim` field. However, when borrowers subsequently repay their debt, the value in `interestToClaim` is overwritten instead of being updated. This results in the interest paid prior to the loan extension being disregarded, effectively locking these funds within the loan contract and making them inaccessible to the lender.


### Root Cause

The root issue lies in how the interestToClaim field is handled in the DebitaV3Loan::payDebt function. Specifically:

1. During loan extension via `DebitaV3Loan::extendLoan`, accrued interest up to that point is added to `interestToClaim`:
```javascript
    loanData._acceptedOffers[i].interestToClaim +=
        interestOfUsedTime -
        interestToPayToDebita;
}
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L656-L659

2. Later, in `DebitaV3Loan::payDebt`, instead of incrementing the `interestToClaim` field, it is overwritten:
```javascript
} else {
    loanData._acceptedOffers[index].interestToClaim =
        interest -
        feeOnInterest;
}
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L237-L241

As a result, only the interest accrued between the loan extension and final debt repayment is claimable, while previously accrued interest paid during the extension period remains inaccessible. The interest can only be claimed using the value stored in `interestToClaim` during the claimInterest function:

```javascript 
function claimInterest(uint index) internal {
    IOwnerships ownershipContract = IOwnerships(s_OwnershipContract);
    infoOfOffers memory offer = loanData._acceptedOffers[index];
    uint interest = offer.interestToClaim;


    require(interest > 0, "No interest to claim");


    loanData._acceptedOffers[index].interestToClaim = 0;
    SafeERC20.safeTransfer(IERC20(offer.principle), msg.sender, interest);
    Aggregator(AggregatorContract).emitLoanUpdated(address(this));
}
```
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L262

### Internal pre-conditions

Lend offer has to be non-perpetual.

### External pre-conditions

- The borrower extends the loan before making the final repayment.
- The loan does not enter default.

### Attack Path

1. Borrower enters into a loan agreement.
2. Borrower extends the loan, paying accrued interest up to that point.
3. Borrower repays the loan in full.
4. The lender claims interest but receives only the portion accrued between the loan extension and repayment. Interest accrued and paid prior to the extension remains locked in the contract.

### Impact

Part of the lenderâ€™s rightful interest remains stuck in the loan contract.

### PoC

Below you can find an adjusted version of `testExtendLoan` in `MixMultiplePrinciples.t.sol`. It is observed that the interest accrued between the first and second timestamp shift are ignored for the lending offer with ID 0.

```javascript
function testExtendLoan() public {
    matchOffers();
    uint[] memory indexes = allDynamicData.getDynamicUintArray(3);
    indexes[0] = 0;
    indexes[1] = 1;
    indexes[2] = 2;
    vm.startPrank(borrower);
    deal(wETH, borrower, 10e18, false);
    AEROContract.approve(address(DebitaV3LoanContract), 10e18);
    wETHContract.approve(address(DebitaV3LoanContract), 10e18);
    vm.warp(block.timestamp + 86401);
    DebitaV3LoanContract.extendLoan();
    DebitaV3Loan.infoOfOffers[] memory offers = DebitaV3LoanContract
        .getLoanData()
        ._acceptedOffers;
    console.log(offers[0].interestToClaim); // 25e17*1350/10000*86401/31536000*85/100 = 785968000856164
    console.log(offers[0].interestPaid); // 25e17*1350/10000*86401/31536000 = 924668236301369
    uint interest = DebitaV3LoanContract.calculateInterestToPay(0);
    console.log(interest); // 0
    vm.warp(block.timestamp + 86501);
    interest = DebitaV3LoanContract.calculateInterestToPay(0);
    console.log(interest); // 25e17*1350/10000*86501/31536000 = 925738441780822
    DebitaV3LoanContract.payDebt(indexes);
    offers = DebitaV3LoanContract.getLoanData()._acceptedOffers;
    console.log(offers[0].interestToClaim); // 25e17*1350/10000*86501/31536000*85/100 = 786877675513699
    console.log(offers[0].interestPaid); // 25e17*1350/10000*(86401+86501)/31536000 = 1850406678082191
    assertEq(offers[0].interestToClaim, 786877675513699);
    vm.stopPrank();
}
```

### Mitigation

Add the interest to the previous amount instead of overwriting the value:

```javascript
loanData._acceptedOffers[index].interestToClaim +=
    interest -
    feeOnInterest;
```