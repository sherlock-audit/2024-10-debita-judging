Nutty Snowy Robin

High

# Inflated Fee when `extendLoan()` is Called

### Summary

When the borrower wants to extend the duration of their loan, the fee calculated for the extra duration of each offer always charges the borrower the maximum fee due to an incorrect value calculation using `maxDeadline`.

In `DebitaV3Aggregator::matchOffersV3` function, when a lend order is accepted to match a borrow offer, a struct (`infoOfOffers`) is created to store all the information related to that accepted lend order. During this process, the `maxDeadline` value is calculated, representing the maximum timestamp by which the lender expects to be repaid:
```solidity
function matchOffersV3(...) external nonReentrant returns (address) {
    
// Function code ...
       
        for (uint i = 0; i < lendOrders.length; i++) {

// Function code ...

            uint lendID = IOwnerships(s_OwnershipContract).mint(lendInfo.owner);
            offers[i] = DebitaV3Loan.infoOfOffers({
                principle: lendInfo.principle,
                lendOffer: lendOrders[i],
                principleAmount: lendAmountPerOrder[i],
                lenderID: lendID,
                apr: lendInfo.apr,
                ratio: ratio,
                collateralUsed: userUsedCollateral,
>>              maxDeadline: lendInfo.maxDuration + block.timestamp,
                paid: false,
                collateralClaimed: false,
                debtClaimed: false,
                interestToClaim: 0,
                interestPaid: 0
            });
            getLoanIdByOwnershipID[lendID] = loanID;
            lenders[i] = lendInfo.owner;
            DLOImplementation(lendOrders[i]).acceptLendingOffer(
                lendAmountPerOrder[i]
            );
        }
    
// Function code ...
}   
```
Later in the same function, when all the lend orders are accepted, it calculates and transfers the fee to the owner of the contract. This fee is taken from each principle lent and is calculated based on the duration of the loan, which, by default, corresponds to the duration specified by the borrower.

```solidity
function matchOffersV3( ... ) external nonReentrant returns (address) {

// Function code ...

      // Total percentage of fee is going to take from the borrower based on the duration of the borrow offer
>>    uint percentage = ((borrowInfo.duration * feePerDay) / 86400);

// Function code ...

        for (uint i = 0; i < principles.length; i++) {

             // Function code ...
            
             // calculate fees --> msg.sender keeps 15% of the fee for connecting the offers
>>        uint feeToPay = (amountPerPrinciple[i] * percentage) / 10000;
            uint feeToConnector = (feeToPay * feeCONNECTOR) / 10000;
            feePerPrinciple[i] = feeToPay;
            // transfer fee to feeAddress
            SafeERC20.safeTransfer(
                IERC20(principles[i]),
                feeAddress,
                feeToPay - feeToConnector
            );
            // Function code ...
    
        }

// Function code ...
}
```
When the loan is created, if the borrower wants to extend it, the duration of the loan is adjusted up to the `maxDeadline` value for each accepted offer, with each offer having its own individual deadline. The issue arises when the protocol fee is calculated based on this extended time for each offer. Instead of using the `maxDuration` value of the lender, the calculation incorrectly uses the `maxDeadline` value (timestamp), inflating the fee to the maximum possible amount (0.8%):

```solidity
        function extendLoan() public {
            
        // Function code ...

            for (uint i; i < m_loan._acceptedOffers.length; i++) {
                
                // Function code ...

                uint misingBorrowFee;

                // if user already paid the max fee, then we dont have to charge them again
                if (PorcentageOfFeePaid != maxFee) {
                    // calculate difference from fee paid for the initialDuration vs the extra fee they should pay because of the extras days of extending the loan.  MAXFEE shouldnt be higher than extra fee + PorcentageOfFeePaid
>>                  uint feeOfMaxDeadline = ((offer.maxDeadline * feePerDay) /
                        86400);
                    if (feeOfMaxDeadline > maxFee) {
>>                      feeOfMaxDeadline = maxFee;
                    } else if (feeOfMaxDeadline < feePerDay) {
                        feeOfMaxDeadline = feePerDay;
                    }

                    misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
                uint principleAmount = offer.principleAmount;
                uint feeAmount = (principleAmount * misingBorrowFee) / 10000;

                // Function code ...
            }
        }
```

### Root Cause

- In [DebitaV3Loan.sol:602](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L602), the wrong timestamp value is used to calculate the fee for the maximum duration of the offer. 
.

### Internal pre-conditions

- Extension of a loan

### External pre-conditions

_No response_

### Attack Path

#### Lend Offer by Alice:
- **Available Amount**: 100_000 USDC  
- **Max Duration**: 2 weeks  
- **Collateral Accepted**: WBTC 

#### Borrow Offer by Bob:
- **Available Amount**: 3 WBTC  
- **Duration**: 1.5 weeks  
- **Principle Accepted**: USDC  

#### Order Matching:
- **Duration of the Loan**: 1.5 weeks (`907200 seconds`)  
- **Current Timestamp**: `1732147200` (`GMT: Thursday, November 21, 2024, 12:00:00 AM`)  
- **Loan Deadline**: `1733054400` (`Current Timestamp + 1.5 weeks`)  
- **Max Deadline of Alice's Offer**: `1733356800` (`Current Timestamp + 2 weeks`)  
- **Percentage Fee**: 0.42% (`(1.5 weeks * 0.04%)/86400`)  
- **Fee to Pay**: 420 USDC (`Percentage Fee * Amount Lent`)  

---

#### **Expected Fee Calculation**  
**When the loan is extended**:  
- **New Duration of the Loan**: 2 weeks  
- **Percentage Fee Paid**: 0.42%  
- **New Total Percentage**: 0.56% (`(2 weeks * 0.04%)/86400`)  
- **Percentage to Pay**: 0.14% (`New Total Percentage - Percentage Fee Paid`)  
- **Fee to Pay**: 140 USDC (`Percentage to Pay * Amount Lent`)  

---

#### **Actual Fee Calculation**  
**When the loan is extended**:  
- **New Duration of the Loan**: 2 weeks  
- **Percentage Fee Paid**: 0.42%  
- **New Total Percentage**: 0.8% (bounded, `(maxDeadline * 0.04%)/86400`)  
- **Percentage to Pay**: 0.38% (`New Total Percentage - Percentage Fee Paid`)  
- **Fee to Pay**: 380 USDC (`Percentage to Pay * Amount Lent`)  

### Impact

Every time a borrower wants to extend a loan the fee will always be inflated to the maximum value (0.8%), charging in some scenarios more than it should

### PoC
You can paste the following code to `test/fork/Incentives/MultipleLoansDuringIncentives.t.sol`.
You can run it with: `forge test --fork-url https://mainnet.base.org --fork-block-number 21151256 --no-match-path '**Fantom**' --mt testInflatedFee -vvvv`

<details>

<summary>Test to run</summary>

```solidity
function testInflatedFee() public {
        // Create a loan with 1.5 weeks duration
        createNormalLoan(borrower, firstLender, AERO, AERO);
        
        // Pass 3 days
        vm.warp(block.timestamp + 3 days);
        // Extend the loan to two and get charged as a fee more than it should
        uint balanceBorrowerBefore = IERC20(AERO).balanceOf(borrower);
        vm.startPrank(borrower);
        // Enough to pay the inflated fee (9.5e17)
        IERC20(AERO).approve(address(DebitaV3LoanContract), 1e18);
        DebitaV3LoanContract.extendLoan();
        vm.stopPrank();
        uint balanceBorrowerAfter = IERC20(AERO).balanceOf(borrower);

        // Calculations
        uint percentageFeePaid = (1.5 weeks * 4) / 86400;
        uint expectedNewPercentage = (2 weeks * 4) / 86400;
        // Bound in case, not going to happen
        if(expectedNewPercentage > 80) expectedNewPercentage = 80;
        uint expectedPercentageNotPaid = expectedNewPercentage - percentageFeePaid;
        uint expectedFeeTaken = (250e18 * expectedPercentageNotPaid) / 10_000;
        
    
        uint maxDeadline = DebitaV3LoanContract.nextDeadline();
        console.log("Max deadline", maxDeadline);
        uint actualNewPercentage = (maxDeadline * 4) / 86400;
        // Bound the fee as in the code
        if(actualNewPercentage > 80) actualNewPercentage = 80;
        uint actualPercentageNotPaid = actualNewPercentage - percentageFeePaid;
        uint actualFeeTaken = (250e18 * actualPercentageNotPaid) / 10_000;

        // Should be equal but is not because of the bug (fee of interest 0 as APR is 0% for simplicity):
        assertNotEq(balanceBorrowerAfter, balanceBorrowerBefore - expectedFeeTaken);
        // Should be not equal, but it is:
        assertEq(balanceBorrowerAfter, balanceBorrowerBefore - actualFeeTaken);
    }
```

</details>


<details>

<summary>Function to create the orders and match them</summary>

```solidity
// Create orders and match them
    // No oracles
    function createNormalLoan(
        address _borrower,
        address lender,
        address principle,
        address collateral
    ) internal returns (address) {
        vm.startPrank(_borrower);
        deal(principle, lender, 2000e18, false);
        deal(collateral, _borrower, 2000e18, false);
        IERC20(collateral).approve(address(DBOFactoryContract), 500e18);
        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratioLenders = allDynamicData.getDynamicUintArray(1);
        uint[] memory ltvsLenders = allDynamicData.getDynamicUintArray(1);
        bool[] memory oraclesActivatedLenders = allDynamicData
            .getDynamicBoolArray(1);

        address[] memory acceptedPrinciples = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesCollateral = allDynamicData
            .getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData
            .getDynamicAddressArray(1);

        // set the values for the loan
        ltvs[0] = 5000;
        acceptedPrinciples[0] = principle;
        acceptedCollaterals[0] = collateral;
        oraclesActivated[0] = false;
        ratio[0] = 5e17;

        oraclesCollateral[0] = DebitaChainlinkOracle;

        address borrowOrderAddress = DBOFactoryContract.createBorrowOrder(
            oraclesActivated,
            ltvs,
            0, // 0% APR for simplicity
            907200, // 1.5 weeks
            acceptedPrinciples,
            collateral,
            false, // nft
            0, // receipt ID
            oraclesPrinciples,
            ratio,
            DebitaChainlinkOracle,
            500e18
        );

        vm.stopPrank();

        vm.startPrank(lender);
        IERC20(principle).approve(address(DLOFactoryContract), 250e18);
        ltvsLenders[0] = 5000;
        ratioLenders[0] = 5e17;
        oraclesActivatedLenders[0] = false;
        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivatedLenders,
            false,
            ltvsLenders,
            0, // apr, for simplicity
            2 weeks, // max duration
            1 weeks, // min duration
            acceptedCollaterals,
            principle,
            oraclesCollateral,
            ratioLenders,
            address(0), // oracle principle, not need it
            250e18
        );
        vm.stopPrank();
        vm.startPrank(connector);

        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
            1
        );
        uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
            .getDynamicUintArray(1);
        address[] memory principles = allDynamicData.getDynamicAddressArray(1);
        uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexForCollateral_LendOrder = allDynamicData
            .getDynamicUintArray(1);
        uint[] memory indexPrinciple_LendOrder = allDynamicData
            .getDynamicUintArray(1);

        lendOrders[0] = lendOrderAddress;
        lendAmountPerOrder[0] = 250e18;

        porcentageOfRatioPerLendOrder[0] = 10000;
        principles[0] = principle;

        // match
        address loan = DebitaV3AggregatorContract.matchOffersV3(
            lendOrders,
            lendAmountPerOrder,
            porcentageOfRatioPerLendOrder,
            borrowOrderAddress,
            principles,
            indexForPrinciple_BorrowOrder,
            indexForCollateral_LendOrder,
            indexPrinciple_LendOrder
        );

        DebitaV3LoanContract = DebitaV3Loan(loan);
        vm.stopPrank();


    }
```

</details>


### Mitigation

The duration timestamp should be used instead of the deadline timestamp.

```diff
        function extendLoan() public {
            
        // Function code ...

            for (uint i; i < m_loan._acceptedOffers.length; i++) {
                
                // Function code ...

                uint misingBorrowFee;

                if (PorcentageOfFeePaid != maxFee) {
+                   uint maxDuration = offer.maxDeadline - m_loan.startedAt;
+                   uint feeOfMaxDeadline = ((maxDuration * feePerDay) / 86400);
-                   uint feeOfMaxDeadline = ((offer.maxDeadline * feePerDay) / 86400);
                    if (feeOfMaxDeadline > maxFee) {
>>                      feeOfMaxDeadline = maxFee;
                    } else if (feeOfMaxDeadline < feePerDay) {
                        feeOfMaxDeadline = feePerDay;
                    }

                    misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
                uint principleAmount = offer.principleAmount;
                uint feeAmount = (principleAmount * misingBorrowFee) / 10000;

                // Function code ...
            }
        }
```
