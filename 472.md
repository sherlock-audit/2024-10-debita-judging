Broad Pineapple Huskie

High

# Malicious actor can delete other protocol users' lend orders

### Summary

A malicious actor can delete other protocol users' lend orders by repeatedly calling DLOImplementation::cancelOffer() on his own lend order.

### Root Cause

Due to a missing validation you can add funds to a cancelled order via DLOImplementation::addFunds().

As the only requirement for cancelling an order is to have funds available ([DLOImplementation.sol:148](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L148)), this would allow you to add funds and cancel a lend order repeatedly, even after it has already been cancelled once.

DLOImplementation::cancelOffer() makes a call to DLOFactory::deleteOrder() ([DLOImplementation.sol:157](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L157)). 
In DLOFactory::deleteOrder() the index of the order to be deleted is looked up from the LendOrderIndex mapping. In the case where that order was already deleted, the index will be 0. This will result in the order at index 0 being deleted, even though it's not the order at the address that was passed as an argument.

### Internal pre-conditions

1. There are existing active lend orders
2. Attacker creates a lend order by calling DLOFactory::createLendOrder()

### External pre-conditions

_No response_

### Attack Path

1. Owner of the lend order needs to cancel the order by calling DLOImplementation::cancelOffer()
2. Owner of the lend order needs to add funds into the cancelled order by calling DLOImplementation::addFunds()
3. Owner of the lend order needs to call DLOImplemenatation::cancelOffer() again
4. Repeat steps 2 and 3, causing the deletion of an order each time

### Impact

This can be viewed as a denial of service attack, as the attacker can continuously delete lend orders at minimum cost.

The attacker doesn't gain anything. The cost to the attacker is only the gas fees, as the amount he deposits in order to perform the attack will be refunded back to him as he cancels his order. 

The protocol will miss out on the fees from said orders being fulfilled, while the users won't be able to use the protocol as intended.

### PoC

Add the following test to `TwoLendersERC20Loan.t.sol`:

```solidity
function testShouldDeleteAnotherUsersOrder() public {
        uint256 index_first = DLOFactoryContract.LendOrderIndex(address(LendOrder));
        address order_address = DLOFactoryContract.allActiveLendOrders(index_first);
        assertTrue(DLOFactoryContract.isLendOrderLegit(order_address));
        assertTrue(LendOrder.isActive());

        uint256 index_second = DLOFactoryContract.LendOrderIndex(address(SecondLendOrder));
        address order_address_second = DLOFactoryContract.allActiveLendOrders(index_second);
        assertTrue(DLOFactoryContract.isLendOrderLegit(order_address_second));
        assertTrue(SecondLendOrder.isActive());

        vm.startPrank(firstLender);

        //cancel lend order
        LendOrder.cancelOffer();

        //add funds into cancelled order
        AEROContract.approve(address(LendOrder), 1);
        LendOrder.addFunds(1);

        //cancel same lend order a second time
        LendOrder.cancelOffer();
        vm.stopPrank();

        //assert second order was deleted, even though we did not call cancel on it
        index_second = DLOFactoryContract.LendOrderIndex(address(SecondLendOrder));
        assertEq(DLOFactoryContract.allActiveLendOrders(index_second), address(0));
        assertEq(DLOFactoryContract.activeOrdersCount(), 0);
    }
```

### Mitigation

The issue can be fixed in several ways:

1. Do not allow adding funds to cancelled lend order (DLOImplementation::addFunds())
```diff
function addFunds(uint amount) public nonReentrant {
        require(
            msg.sender == lendInformation.owner ||
                IAggregator(aggregatorContract).isSenderALoan(msg.sender),
            "Only owner or loan"
        );
+       require(isActive == true, "Order is cancelled");
        SafeERC20.safeTransferFrom(
            IERC20(lendInformation.principle),
            msg.sender,
            address(this),
            amount
        );
        lendInformation.availableAmount += amount;
        IDLOFactory(factoryContract).emitUpdate(address(this));
    }
```
2. Do not allow order to be cancelled if it's not active (DLOImplementation::cancelOffer())
```diff
function cancelOffer() public onlyOwner nonReentrant {
        uint availableAmount = lendInformation.availableAmount;
        lendInformation.perpetual = false;
        lendInformation.availableAmount = 0;
+       require(isActive == true, "Order is already cancelled")
        require(availableAmount > 0, "No funds to cancel");
        isActive = false;

        SafeERC20.safeTransfer(
            IERC20(lendInformation.principle),
            msg.sender,
            availableAmount
        );
        IDLOFactory(factoryContract).emitDelete(address(this));
        IDLOFactory(factoryContract).deleteOrder(address(this));
        // emit canceled event on factory
    }
```

