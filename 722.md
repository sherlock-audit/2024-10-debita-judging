Flaky Indigo Parrot

High

# underflow in the fees calculation of the ExtendLoan function

### Summary

An error in the minimum of fees that the borrower should pay lead to a possible underflow in the extendLoan function.

### Root Cause

in the extendLoan function this bloc of code handle the extra fees that the borrower should pay to Debita. 
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L597-L611

feeOfMaxDeadline represent the fees that the user should pay for the all duration of the offer with the max delay. 

If this percentage is less than feePerDay then it will be set to feePerDay
An underflow can occur because when the loan is created in the matchOffersV3 function of the DebitaV3Aggregator
the percentage of fees that the borrower pay is constraint by the minFEE and maxFEE and it is constraint again here :

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Loan.sol#L568-L579
The minFee is higher than the feePerDay as we can see : 

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaV3Aggregator.sol#L199-L202

So if the feeOfMaxDeadline <= feePerDay the call will revert.


### Internal pre-conditions

1. The duration  of the loan must be less than 20 days because percent of fees paid must be less than the maxFEE
2. One of the offer must have less than a day of max duration.

### External pre-conditions

none

### Attack Path

1. The borrower want to extend the loan and call extendLoan. 
2. One of the offer has less than a day of max duration.

### Impact

The user wil not be able to call extend his loan 

### PoC

In order to run the POC You must fix two other issues in the codebase change the local variable feeOfMaxDeadline and change the extendedTime local variable. The extendedTime is responsible of another DOS and feeOfMaxDeadline represented the percentage payed in all the offer max duration so we must deduct the initial timestamp or the bloc of code that interessed use is unreachable.

```solidty
uint extendedTime = offer.maxDeadline -
                     alreadyUsedTime -
                        m_loan.startedAt;
uint feeOfMaxDeadline = (((offer.maxDeadline-m_loan.startedAt) * feePerDay) /
                        86400);
```
You can copy and paste this code in a file in the test folder and run forge test --mt test_ExtendLoanDOS
```solidity
// SPDX-License-Identifier: GPL-2.0
pragma solidity ^0.8.0;

import {Test, console2} from "forge-std/Test.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
//import {ERC721}
import {buyOrderFactory} from "@contracts/buyOrders/buyOrderFactory.sol";
import {BuyOrder} from "@contracts/BuyOrders/BuyOrder.sol";
import {veNFTAerodrome} from "@contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol";
import {TaxTokensReceipts} from "@contracts/Non-Fungible-Receipts/TaxTokensReceipts/TaxTokensReceipt.sol";
import {DebitaChainlink} from "@contracts/oracles/DebitaChainlink.sol";
import {DebitaPyth} from "@contracts/oracles/DebitaPyth.sol";
import {DynamicData} from "test/interfaces/getDynamicData.sol";
import {VotingEscrow} from "@aerodrome/VotingEscrow.sol";

contract CodedPOC is Test {
    DBOFactory borrowFactory;
    DLOFactory lendFactory;
    Ownerships ownerships;
    DebitaV3Aggregator aggregator;
    DebitaIncentives incentives;
    auctionFactoryDebita auctionFactory;
    ERC20Mock AERO;
    ERC20Mock USDC;
    buyOrderFactory buyFactory;
    DebitaChainlink oracleChainlink;
    DebitaPyth oraclePyth;
    MockV3Aggregator priceFeedAERO;
    MockV3Aggregator priceFeedUSDC;
    MockV3Aggregator priceFeedWBTC;
    TaxTokensReceipts taxTokenReceipts;
    DynamicData allDynamicData;
    veNFTAerodrome veNFT;
    WBTC wbtc;
    address constant BOB = address(0x10000);
    address constant ALICE = address(0x20000);
    address constant CHARLIE = address(0x30000);
    address constant CONNECTOR = address(0x40000);
    address constant forwarder = address(0x50000);
    address constant factoryRegistry = address(0x60000);
    address sender;
    address[] internal users;
    VotingEscrow escrow;
    function setUp() public {
         vm.warp(1524785992);
        allDynamicData = new DynamicData();
        users = [BOB, ALICE, CHARLIE];
        AERO = new ERC20Mock();
        USDC = new ERC20Mock();
        wbtc = new WBTC(8);
        escrow = new VotingEscrow(forwarder,address(AERO),factoryRegistry);
        veNFT = new veNFTAerodrome(address(escrow),address(AERO));
        DBOImplementation dbo = new DBOImplementation();
        DLOImplementation dlo = new DLOImplementation();
        borrowFactory = new DBOFactory(address(dbo));
        lendFactory = new DLOFactory(address(dlo));
        ownerships = new Ownerships();
        incentives = new DebitaIncentives();
        auctionFactory = new auctionFactoryDebita();
        DebitaV3Loan loan = new DebitaV3Loan();
        aggregator = new DebitaV3Aggregator(
            address(lendFactory),
            address(borrowFactory),
            address(incentives),
            address(ownerships),
            address(auctionFactory),
            address(loan)
        );

        ownerships.setDebitaContract(address(aggregator));
        auctionFactory.setAggregator(address(aggregator));
        lendFactory.setAggregatorContract(address(aggregator));
        borrowFactory.setAggregatorContract(address(aggregator));
        incentives.setAggregatorContract(address(aggregator));
        BuyOrder buyOrder;
        buyFactory = new buyOrderFactory(address(buyOrder));
        _setOracles();
        taxTokenReceipts =
            new TaxTokensReceipts(address(USDC), address(borrowFactory), address(lendFactory), address(aggregator));
        aggregator.setValidNFTCollateral(address(taxTokenReceipts), true);
        aggregator.setValidNFTCollateral(address(veNFT), true);
        incentives.whitelListCollateral(address(AERO),address(USDC),true);
        incentives.whitelListCollateral(address(USDC),address(AERO),true);

        vm.label(address(AERO), "AERO");
        vm.label(address(USDC), "USDC");
        vm.label(address(priceFeedAERO), "priceFeedAERO");
        vm.label(address(priceFeedUSDC), "priceFeedUSDC");
        vm.label(BOB, "Bob");
        vm.label(ALICE, "Alice");
        vm.label(CHARLIE, "Charlie");
        vm.label(address(wbtc), "WBTC");
        for (uint256 i = 0; i < users.length; i++) {
            AERO.mint(users[i], 100_000_000e18);
            vm.startPrank(users[i]);
            AERO.approve(address(borrowFactory), type(uint256).max);
            AERO.approve(address(lendFactory), type(uint256).max);
            AERO.approve(address(escrow), type(uint256).max);
            AERO.approve(address(incentives), type(uint256).max);
            USDC.mint(users[i], 100_000_000e18);
            USDC.approve(address(borrowFactory), type(uint256).max);
            USDC.approve(address(lendFactory), type(uint256).max);
            USDC.approve(address(taxTokenReceipts), type(uint256).max);
            USDC.approve(address(incentives), type(uint256).max);
            wbtc.mint(users[i], 100_000e8);
            wbtc.approve(address(borrowFactory), type(uint256).max);
            wbtc.approve(address(lendFactory), type(uint256).max);
            wbtc.approve(address(taxTokenReceipts), type(uint256).max);
            wbtc.approve(address(incentives), type(uint256).max);
            
            vm.stopPrank();
        }
  
    }

    function _setOracles() internal {
        oracleChainlink = new DebitaChainlink(address(0x0), address(this));
        oraclePyth = new DebitaPyth(address(0x0), address(0x0));
        aggregator.setOracleEnabled(address(oracleChainlink), true);
        aggregator.setOracleEnabled(address(oraclePyth), true);
        priceFeedAERO = new MockV3Aggregator(8, 1.28e8);
        priceFeedUSDC = new MockV3Aggregator(8, 1e8);
        priceFeedWBTC = new MockV3Aggregator(8, 90_000e8);
        oracleChainlink.setPriceFeeds(address(AERO), address(priceFeedAERO));
        oracleChainlink.setPriceFeeds(address(USDC), address(priceFeedUSDC));
        oracleChainlink.setPriceFeeds(address(wbtc), address(priceFeedWBTC));

    }
   

    function test_ExtendLoanDOS() public {
          bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint256[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint256[] memory ratio = allDynamicData.getDynamicUintArray(1);
        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory acceptedCollaterals = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);
        ltvs[0] = 7371;
        acceptedCollaterals[0] = address(USDC);
        oraclesActivated[0] = true;
        acceptedPrinciples[0] = address(AERO);
        oraclesPrinciples[0] = address(oracleChainlink);
        ratio[0] = 0;
        vm.prank(BOB);
        address borrowOrder = borrowFactory.createBorrowOrder(oraclesActivated, ltvs, 1, 86400 , acceptedPrinciples, address(USDC), false, 0, oraclesPrinciples, ratio, address(oracleChainlink), 1e18 );
        ltvs[0]= 9584;
        vm.prank(ALICE);
        address lendOrder= lendFactory.createLendOrder(false, oraclesActivated, false, ltvs, 1, 91729+3266 , 86400 , acceptedCollaterals, address(AERO), oraclesPrinciples, ratio, address(oracleChainlink), 1000000000000000000);
         uint256[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory porcentageOfRatioPerLendOrder = allDynamicData.getDynamicUintArray(1);
        address[] memory lendOrders = allDynamicData.getDynamicAddressArray(1);
        uint256[] memory indexForPrinciple_BorrowOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory indexForCollateral_LendOrder = allDynamicData.getDynamicUintArray(1);
        uint256[] memory indexPrinciple_LendOrder = allDynamicData.getDynamicUintArray(1);
        lendOrders[0] = lendOrder;
        lendAmountPerOrder[0] = 10000000;
        porcentageOfRatioPerLendOrder[0] = 7598;
        indexForPrinciple_BorrowOrder[0] = 0;
        indexForCollateral_LendOrder[0] = 0;
        indexPrinciple_LendOrder[0] = 0;
        vm.prank(CONNECTOR);
       address loan = aggregator.matchOffersV3(lendOrders, lendAmountPerOrder, porcentageOfRatioPerLendOrder,borrowOrder, acceptedPrinciples, indexForPrinciple_BorrowOrder, indexForCollateral_LendOrder, indexPrinciple_LendOrder);
        vm.warp(block.timestamp + 47498);
        vm.roll(block.number + 1);
              vm.startPrank(BOB);
      ERC20(address(AERO)).approve(loan, type(uint256).max);
        DebitaV3Loan(loan).extendLoan();
        vm.stopPrank();
    }
}


contract WBTC is ERC20Mock {
    uint8 private _decimals;
    constructor(uint8 decimal) ERC20Mock() {
         _decimals=decimal;
    }

 function decimals() public view override returns (uint8) {
        return _decimals;
    }
    function setDecimals(uint8 decimal) public {
        _decimals=decimal;
    }
}
contract MockV3Aggregator  {
  uint256 public constant  version = 0;

  uint8 public  decimals;
  int256 public  latestAnswer;
  uint256 public  latestTimestamp;
  uint256 public  latestRound;

  mapping(uint256 => int256) public  getAnswer;
  mapping(uint256 => uint256) public  getTimestamp;
  mapping(uint256 => uint256) private getStartedAt;

  constructor(uint8 _decimals, int256 _initialAnswer) {
    decimals = _decimals;
    updateAnswer(_initialAnswer);
  }

  function updateAnswer(int256 _answer) public {
    latestAnswer = _answer;
    latestTimestamp = block.timestamp;
    latestRound++;
    getAnswer[latestRound] = _answer;
    getTimestamp[latestRound] = block.timestamp;
    getStartedAt[latestRound] = block.timestamp;
  }

  function updateRoundData(uint80 _roundId, int256 _answer, uint256 _timestamp, uint256 _startedAt) public {
    latestRound = _roundId;
    latestAnswer = _answer;
    latestTimestamp = _timestamp;
    getAnswer[latestRound] = _answer;
    getTimestamp[latestRound] = _timestamp;
    getStartedAt[latestRound] = _startedAt;
  }

  function getRoundData(
    uint80 _roundId
  )
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
  {
    return (_roundId, getAnswer[_roundId], getStartedAt[_roundId], getTimestamp[_roundId], _roundId);
  }

  function latestRoundData()
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound)
  {
    return (
      uint80(latestRound),
      getAnswer[latestRound],
      getStartedAt[latestRound],
      getTimestamp[latestRound],
      uint80(latestRound)
    );
  }

  function description() external pure  returns (string memory) {
    return "v0.8/tests/MockV3Aggregator.sol";
  }
}
```

### Mitigation

All the code bloc should be refactored like that : 

```solidity 
 if (PorcentageOfFeePaid != maxFee) {
                    // calculate difference from fee paid for the initialDuration vs the extra fee they should pay because of the extras days of extending the loan.  MAXFEE shouldnt be higher than extra fee + PorcentageOfFeePaid
                   uint feeOfMaxDeadline = (((offer.maxDeadline-m_loan.startedAt) * feePerDay) /
                        86400);
                    if (feeOfMaxDeadline > maxFee) {
                        feeOfMaxDeadline = maxFee;
                    } else if (feeOfMaxDeadline < minFEE) {
                        feeOfMaxDeadline = minFEE;
                    }

                    misingBorrowFee = feeOfMaxDeadline - PorcentageOfFeePaid;
                }
```