Obedient Green Bee

Medium

# Borrowers can still do many restricted actions with veNFT

### Summary

The flaw in design of Receipt veNFT contract can allow the borrowers do malicious actions that impacts the NFT auction buyers

### Root Cause

- In function [`veNFTAerodrome::deposit()`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/Receipt-veNFT.sol#L72-L78), the `msg.sender` is set as [`manager` for the newly deployed `veNFTVault`](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/Non-Fungible-Receipts/veNFTS/Aerodrome/veNFTAerodrome.sol#L66-L78)
```solidity
    function deposit(uint[] memory nftsID) external nonReentrant {
        // Add the receipt count & create a memory variable to save gas
        uint m_Receipt = s_ReceiptID;
        s_ReceiptID += nftsID.length;
        // For loop minting receipt tokens
        for (uint i; i < nftsID.length; i++) {
            m_Receipt++;

            // Create Vault for each deposit
            veNFTVault vault = new veNFTVault(
                nftAddress,
                address(this),
                m_Receipt,
                nftsID[i],
@>                msg.sender
            );
...
```
```solidity
    constructor(
        address _veAddress,
        address _factoryAddress,
        uint _receiptID,
        uint _nftID,
        address _managerAddress
    ) {
        veNFTAddress = _veAddress;
        factoryAddress = _factoryAddress;
        receiptID = _receiptID;
        attached_NFTID = _nftID;
@>        managerAddress = _managerAddress;
    }
```

- The vault manager has the power to execute many sensitive actions with the veNFT, including vote, claim bribes, reset, extend, poke. 
```solidity
    function voteMultiple(
        address[] calldata vaults,
        address[] calldata _poolVote,
        uint256[] calldata _weights
    ) external {
        for (uint i; i < vaults.length; i++) {
            require(
@>                msg.sender == veNFTVault(vaults[i]).managerAddress(),
                "not manager"
            );
            require(isVaultValid[vaults[i]], "not vault");
            veNFTVault(vaults[i]).vote(_poolVote, _weights);
        }
    }

    function claimBribesMultiple(
        address[] calldata vaults,
        address[] calldata _bribes,
        address[][] calldata _tokens
    ) external {
        for (uint i; i < vaults.length; i++) {
            require(
@>                msg.sender == veNFTVault(vaults[i]).managerAddress(),
                "not manager"
            );
            require(isVaultValid[vaults[i]], "not vault");
            veNFTVault(vaults[i]).claimBribes(msg.sender, _bribes, _tokens);
            emitInteracted(vaults[i]);
        }
    }

    function resetMultiple(address[] calldata vaults) external {
        for (uint i; i < vaults.length; i++) {
            require(
                msg.sender == veNFTVault(vaults[i]).managerAddress(),
                "not manager"
            );
            require(isVaultValid[vaults[i]], "not vault");
            veNFTVault(vaults[i]).reset();
        }
    }

    function extendMultiple(
        address[] calldata vaults,
        uint[] calldata newEnds
    ) external {
        for (uint i; i < vaults.length; i++) {
            require(
@>                msg.sender == veNFTVault(vaults[i]).managerAddress(),
                "not manager"
            );
            require(isVaultValid[vaults[i]], "not vault");
            veNFTVault(vaults[i]).extendLock(newEnds[i]);
        }
    }

    function pokeMultiple(address[] calldata vaults) external {
        for (uint i; i < vaults.length; i++) {
            require(
@>                msg.sender == veNFTVault(vaults[i]).managerAddress(),
                "not manager"
            );
            require(isVaultValid[vaults[i]], "not vault");
            veNFTVault(vaults[i]).poke();
            emitInteracted(vaults[i]);
        }
    }
``` 

- The manager address can only be changed if function `veNFTVault::changeManager()` is executed, by the current NFT holder or by the current manager. So with this point, the borrower can have the ability to execute these sensitive functions, even if the NFT collateral is **on auction**, or even **after sold**.
```solidity
    function changeManager(address newManager) external {
        IReceipt receiptContract = IReceipt(factoryAddress);
        address holder = receiptContract.ownerOf(receiptID);

        require(attached_NFTID != 0, "NFT not attached");
        require(newManager != managerAddress, "same manager");
        require(
@>            msg.sender == holder || msg.sender == managerAddress,
            "not Allowed"
        );
        receiptContract.decrease(managerAddress, attached_NFTID);
        receiptContract.increase(newManager, attached_NFTID);
        managerAddress = newManager;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A borrower deposits veNFT to receive Receipt veNFT token
2. The borrower creates borrow offer with the NFR above as collateral
3. The borrow offer is matched
4. The deadline passes, and the collateral is auctioned
5. At this time, the borrower is the manager of the veNFTVault and the borrower is free to vote, claim bribes, reset, extend, poke.
6. An user buys the NFT from auction.
7. The borrower can front-run the `buyNFT()` transaction with the functions mentioned in step (5). Note that back-running can also make it successfully. By this, the borrower can 1/ extend NFT lock duration, which affect the buyer afterwards, OR 2/ borrower can claim bribes which should belong to the buyer.

### Impact

- NFT auction buyer unexpectedly suffers loss of bribe rewards
- NFT auction buyer unexpectedly suffers longer lock duration of the veNFT 

### PoC

_No response_

### Mitigation

Consider updating the design/mechanism in `Receipt veNFT` and `veNFTVault` contracts so that the Receipt veNFT holders can completely manage the NFT